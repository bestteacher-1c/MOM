#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПриЗагрузкеВариантаНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	// Дополнительные команды
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("ПланПроизводства", Параметры.ПараметрКоманды);
	КонецЕсли;
	
	Если Параметры.Свойство("Расшифровка")
		И Параметры.Расшифровка <> Неопределено Тогда
		
		ЗаменятьВариант = Ложь;
		
		Если Параметры.Расшифровка.ПрименяемыеНастройки.Структура.Количество() > 0 Тогда
			
			ПолеНоменклатураПродукции = Новый ПолеКомпоновкиДанных("НоменклатураПродукции");
			ПолеХарактеристикаПродукции = Новый ПолеКомпоновкиДанных("ХарактеристикаПродукции");
			ПолеСпецификация = Новый ПолеКомпоновкиДанных("Спецификация");
			ПолеНазначениеПродукции = Новый ПолеКомпоновкиДанных("НазначениеПродукции");
			
			Для каждого Группировка Из Параметры.Расшифровка.ПрименяемыеНастройки.Структура[0].ПоляГруппировки.Элементы Цикл
			
				Если Группировка.Поле = ПолеНоменклатураПродукции 
					ИЛИ Группировка.Поле = ПолеХарактеристикаПродукции
					ИЛИ Группировка.Поле = ПолеСпецификация
					ИЛИ Группировка.Поле = ПолеНазначениеПродукции Тогда
					
					ЗаменятьВариант = Истина;
					Прервать;
				
				КонецЕсли; 
			
			КонецЦикла; 
		
		КонецЕсли; 
		
		// заполняем параметры расшифровки
		Если ЭтоАдресВременногоХранилища(Параметры.Расшифровка.Данные) Тогда
		
			ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Параметры.Расшифровка.Данные);
			
			Если НЕ ДанныеРасшифровки.Настройки.ДополнительныеСвойства.Свойство("КлючТекущегоВарианта")
				ИЛИ ДанныеРасшифровки.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта <> "ПлановаяПотребностьВПолуфабрикатахКонтекст" Тогда
			
				Возврат;
			
			КонецЕсли; 
		КонецЕсли; 
		
		Если НЕ ЗаменятьВариант Тогда
			Возврат;
		КонецЕсли; 
		
		Если Параметры.Свойство("КлючВарианта") Тогда
			Параметры.КлючВарианта = "РасшифровкаКонтекст";
		КонецЕсли; 
		Если Параметры.Свойство("КлючНазначенияИспользования") Тогда
			Параметры.КлючНазначенияИспользования = "РасшифровкаКонтекст";
		КонецЕсли;
		
		ЭтаФорма.КлючТекущегоВарианта = "РасшифровкаКонтекст";
		
		ЭтаФорма.РежимРасшифровки = Ложь;
		ЭтаФорма.НастройкиОтчета.РазрешеноИзменятьВарианты = Ложь;
		ЭтаФорма.НастройкиОтчета.ВариантСсылка = ВариантыОтчетов.ВариантОтчета(ЭтаФорма.НастройкиОтчета.ОтчетСсылка, ЭтаФорма.КлючТекущегоВарианта);
		
	КонецЕсли;
	

КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
Процедура ПриЗагрузкеВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	Если КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Свойство("КлючТекущегоВарианта") 
		И КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.КлючТекущегоВарианта = "ПлановаяПотребностьВПолуфабрикатахКонтекст"
		И ЭтаФорма.КлючТекущегоВарианта = "РасшифровкаКонтекст" Тогда
		
		Сценарий = Неопределено;
		ИспользоватьНазначения = Неопределено;
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Свойство("Сценарий", Сценарий);
		ПланПроизводства = Неопределено;
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Свойство("ПланПроизводства", ПланПроизводства);
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Свойство("ИспользоватьНазначения", ИспользоватьНазначения);
		
		ОтчетОбъект = Отчеты.ПлановаяПотребностьВПолуфабрикатах.Создать();
		Вариант = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек.Найти(ЭтаФорма.КлючТекущегоВарианта);
		
		КомпоновкаДанныхКлиентСервер.ЗаполнитьЭлементы(Вариант.Настройки.ПараметрыДанных,	КомпоновщикНастроекФормы.Настройки.ПараметрыДанных);
		КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(Вариант.Настройки.Отбор,			КомпоновщикНастроекФормы.Настройки.Отбор, Ложь);
		
		КомпоновщикНастроекФормы.ЗагрузитьНастройки(Вариант.Настройки);
		
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить(
			"ПланПроизводства", 
			ПланПроизводства);
		
		Если Сценарий <> Неопределено Тогда
		
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроекФормы, 
				"Сценарий", 
				Сценарий);
		
		КонецЕсли; 
		
		Если ИспользоватьНазначения <> Неопределено Тогда
		
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроекФормы, 
				"ИспользоватьНазначения", 
				ИспользоватьНазначения);
		
		КонецЕсли;
		
	КонецЕсли;
	
	КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить("КлючТекущегоВарианта", ЭтаФорма.КлючТекущегоВарианта);
	
	Если ЭтаФорма.ФормаПараметры.Отбор.Свойство("ПланПроизводства") Тогда
		
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить(
			"ПланПроизводства", 
			ЭтаФорма.ФормаПараметры.Отбор.ПланПроизводства);
		
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить(
			"Сценарий", 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтаФорма.ФормаПараметры.Отбор.ПланПроизводства, "Сценарий"));
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроекФормы, 
			"Сценарий", 
			КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Сценарий);
		
		ПланированиеПоНазначениям = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Сценарий
			, "ПланированиеПоНазначениям");
			
		КомпоновщикНастроекФормы.Настройки.ДополнительныеСвойства.Вставить(
			"ИспользоватьНазначения", 
			ПланированиеПоНазначениям);
			
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроекФормы, 
			"ИспользоватьНазначения", 
			ПланированиеПоНазначениям);
			
	КонецЕсли; 
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрСценарий = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Сценарий");
	
	Если НЕ ЗначениеЗаполнено(ПараметрСценарий.Значение) Тогда
		ВызватьИсключение НСтр("ru= 'Не заполнено значение параметра ""Сценарий"".'");
	КонецЕсли; 
	
	Периодичность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрСценарий.Значение, "Периодичность");
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Периодичность", Периодичность);
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	Если КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ПланПроизводства") Тогда
		
		ПланПроизводства = КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ПланПроизводства;
		ПланироватьПолуфабрикатыАвтоматически = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПланПроизводства, "ВидПлана.ПланироватьПолуфабрикатыАвтоматически");
		
	Иначе
		
		ПланПроизводства = Документы.ПланПроизводства.ПустаяСсылка();
		ПланироватьПолуфабрикатыАвтоматически = Ложь;
	
	КонецЕсли;
	
	Если ПланироватьПолуфабрикатыАвтоматически Тогда
		
		ТаблицаПотребности = ПотребностиЗапланированныеАвтоматически(ПланПроизводства);
		
	Иначе
		
		ТаблицаПотребности = ПотребностиЗапланированныеВручную(ПланПроизводства, Периодичность);
		
	КонецЕсли;
	
	НаборыДанных = Новый Структура;
	НаборыДанных.Вставить("НаборДанных1", ТаблицаПотребности);
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(
		СхемаКомпоновкиДанных, 
		КомпоновщикНастроек.ПолучитьНастройки(), 
		ДанныеРасшифровки,
		, 
		Тип("ГенераторМакетаКомпоновкиДанных"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, НаборыДанных, ДанныеРасшифровки, Истина);
		
	// Получение результата
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Функция ПотребностиЗапланированныеАвтоматически(ПланПроизводства)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР ПланыПроизводстваОбороты.Сценарий.Периодичность
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, ДЕНЬ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, НЕДЕЛЯ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, ДЕКАДА)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, МЕСЯЦ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, КВАРТАЛ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, ПОЛУГОДИЕ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводстваОбороты.Период, ГОД)
		|	КОНЕЦ КАК ДинамическийПериод,
		|	ПланыПроизводстваОбороты.Номенклатура,
		|	ПланыПроизводстваОбороты.Характеристика,
		|	ПланыПроизводстваОбороты.Назначение,
		|	ПланыПроизводстваОбороты.КоличествоОборот КАК КоличествоПолуфабрикатов,
		|	0 КАК КоличествоПродукции,
		|	0 КАК КоличествоПотребления,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаясСылка) КАК Спецификация,
		|	ПланыПроизводстваОбороты.Подразделение КАК ПодразделениеДиспетчер,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаясСылка) КАК ПодразделениеИсполнитель,
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаясСылка) КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаясСылка) КАК НоменклатураПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеПродукции,
		|	ПланыПроизводстваОбороты.ПланПроизводства,
		|	ПланыПроизводстваОбороты.ПланПроизводства.НачалоПериода КАК НачалоПериода
		|ИЗ
		|	РегистрНакопления.ПланыПроизводства.Обороты(
		|			,
		|			,
		|			Регистратор,
		|			ПланПроизводства = &ПланПроизводства
		|				И ЭтоПолуфабрикат = ИСТИНА) КАК ПланыПроизводстваОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР ПланыПотребленияМатериалов.Сценарий.Периодичность
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, ДЕНЬ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, НЕДЕЛЯ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, ДЕКАДА)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, МЕСЯЦ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, КВАРТАЛ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, ПОЛУГОДИЕ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
		|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПотребленияМатериалов.Период, ГОД)
		|	КОНЕЦ,
		|	ПланыПотребленияМатериалов.Номенклатура,
		|	ПланыПотребленияМатериалов.Характеристика,
		|	ПланыПотребленияМатериалов.Назначение,
		|	0,
		|	ЕСТЬNULL(ПланыПроизводстваОбороты.КоличествоОборот, 0),
		|	ПланыПотребленияМатериалов.Количество,
		|	ПланыПотребленияМатериалов.ДатаПроизводства,
		|	ПланыПотребленияМатериалов.Спецификация,
		|	ПланыПотребленияМатериалов.ПодразделениеДиспетчер,
		|	ПланыПотребленияМатериалов.ПодразделениеИсполнитель,
		|	ПланыПотребленияМатериалов.Склад,
		|	ПланыПотребленияМатериалов.НоменклатураПродукции,
		|	ПланыПотребленияМатериалов.ХарактеристикаПродукции,
		|	ПланыПотребленияМатериалов.НазначениеПродукции,
		|	ПланыПотребленияМатериалов.ПланПроизводства,
		|	ПланыПотребленияМатериалов.ПланПроизводства.НачалоПериода
		|ИЗ
		|	РегистрНакопления.ПланыПотребленияМатериалов КАК ПланыПотребленияМатериалов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПланыПроизводства.Обороты(
		|				,
		|				,
		|				Регистратор,
		|				ПланПроизводства = &ПланПроизводства) КАК ПланыПроизводстваОбороты
		|		ПО ПланыПотребленияМатериалов.ДатаПроизводства = ПланыПроизводстваОбороты.Период
		|			И ПланыПотребленияМатериалов.Сценарий = ПланыПроизводстваОбороты.Сценарий
		|			И ПланыПотребленияМатериалов.Статус = ПланыПроизводстваОбороты.Статус
		|			И ПланыПотребленияМатериалов.НоменклатураПродукции = ПланыПроизводстваОбороты.Номенклатура
		|			И ПланыПотребленияМатериалов.ХарактеристикаПродукции = ПланыПроизводстваОбороты.Характеристика
		|			И ПланыПотребленияМатериалов.ПодразделениеДиспетчер = ПланыПроизводстваОбороты.Подразделение
		|			И ПланыПотребленияМатериалов.ПланПроизводства = ПланыПроизводстваОбороты.ПланПроизводства
		|			И ПланыПотребленияМатериалов.Спецификация = ПланыПроизводстваОбороты.Спецификация
		|			И ПланыПотребленияМатериалов.НазначениеПродукции = ПланыПроизводстваОбороты.Назначение
		|ГДЕ
		|	ПланыПотребленияМатериалов.ПланПроизводства = &ПланПроизводства
		|	И ПланыПотребленияМатериалов.ЭтоПолуфабрикат = ИСТИНА";
		
	Запрос.УстановитьПараметр("ПланПроизводства", ПланПроизводства);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПотребностиЗапланированныеВручную(ПланПроизводства, Периодичность)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Полуфабрикаты.ДатаВыпуска ДинамическийПериод,
	|	Полуфабрикаты.Номенклатура,
	|	Полуфабрикаты.Характеристика,
	|	Полуфабрикаты.Назначение,
	|	Полуфабрикаты.Количество КАК КоличествоПолуфабрикатов,
	|	0 КАК КоличествоПродукции,
	|	0 КАК КоличествоПотребления,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПроизводства,
	|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаясСылка) КАК Спецификация,
	|	Полуфабрикаты.Ссылка.Подразделение КАК ПодразделениеДиспетчер,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаясСылка) КАК ПодразделениеИсполнитель,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаясСылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаясСылка) КАК НоменклатураПродукции,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаПродукции,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеПродукции,
	|	Полуфабрикаты.Ссылка КАК ПланПроизводства,
	|	Полуфабрикаты.Ссылка.НачалоПериода КАК НачалоПериода
	|ИЗ
	|	Документ.ПланПроизводства.Продукция КАК Полуфабрикаты
	|ГДЕ
	|	Полуфабрикаты.Полуфабрикат
	|	И Полуфабрикаты.Ссылка = &ПланПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Продукция.ДатаВыпуска КАК ДинамическийПериод,
	|	Продукция.Номенклатура,
	|	Продукция.Характеристика,
	|	Продукция.Назначение,
	|	0 КАК КоличествоПолуфабрикатов,
	|	Продукция.Количество КАК КоличествоПродукции,
	|	0 КАК КоличествоПотребления,
	|	Продукция.ДатаВыпуска КАК ДатаПроизводства,
	|	Продукция.Спецификация,
	|	Продукция.Ссылка.Подразделение КАК ПодразделениеДиспетчер,
	|	Продукция.Номенклатура КАК НоменклатураПродукции,
	|	Продукция.Характеристика КАК ХарактеристикаПродукции,
	|	Продукция.Назначение КАК НазначениеПродукции,
	|	Продукция.Ссылка КАК ПланПроизводства,
	|	Продукция.Ссылка.НачалоПериода КАК НачалоПериода,
	|	Продукция.Ссылка.УправлениеПроизводством2_2 КАК УправлениеПроизводством2_2
	|ИЗ
	|	Документ.ПланПроизводства.Продукция КАК Продукция
	|ГДЕ
	|	НЕ Продукция.Полуфабрикат
	|	И Продукция.Ссылка = &ПланПроизводства";
	
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	Запрос.УстановитьПараметр("ПланПроизводства", ПланПроизводства);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаРезультат = РезультатыЗапроса[0].Выгрузить();
	ТаблицаПродукция = РезультатыЗапроса[1].Выгрузить();
	
	ПереченьДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("МатериалыИУслуги");
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделиях.ПараметрыВыбораСпецификаций(
		ПланПроизводства,
		Документы.ПланПроизводства);
	
	КэшированныеСпецификации = Неопределено;
	ПараметрыГрафика = Неопределено;
	КэшированныеЗначения = Неопределено;
	
	Для каждого СтрокаТЧ Из ТаблицаПродукция Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Спецификация) ИЛИ СтрокаТЧ.КоличествоПродукции = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗнакКоличества = 1;
		
		ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуреРасширенный();
		ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, СтрокаТЧ);
		ДанныеПоНоменклатуре.ПодразделениеДиспетчер = СтрокаТЧ.ПодразделениеДиспетчер;
		
		Если СтрокаТЧ.КоличествоПродукции < 0 Тогда
			ДанныеПоНоменклатуре.Количество = -СтрокаТЧ.КоличествоПродукции;
			ЗнакКоличества = -1;
		Иначе
			ДанныеПоНоменклатуре.Количество = СтрокаТЧ.КоличествоПродукции;
		КонецЕсли;
		
		ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных(
			ПереченьДанных,
			,
			ОбеспечениеПроизводства.ВариантЗаполненияОбеспеченияПоВерсииПроизводства(СтрокаТЧ.УправлениеПроизводством2_2));
		ПараметрыВыборки.ОкруглятьКоличествоШтучныхТоваров = Ложь;
		
		ДанныеСпецификации = Справочники.РесурсныеСпецификации.ДанныеСпецификацииСПолуфабрикатами(
			ДанныеПоНоменклатуре,
			Истина,
			ПараметрыВыборки,
			КэшированныеСпецификации);
		
		МассивСтрок = Новый Массив;
		МассивДанных = Новый Массив;
		
		// Данные по материалам
		Для каждого ЭлементМатериал Из ДанныеСпецификации.МатериалыИУслуги Цикл
			
			Если НЕ ЭлементМатериал.Запланировать Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМатериал,	"Номенклатура, Характеристика, Склад, Спецификация");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ,	"ПодразделениеДиспетчер,НоменклатураПродукции, 
				|ХарактеристикаПродукции,НазначениеПродукции, ПланПроизводства, НачалоПериода, ДатаПроизводства, 
				|КоличествоПродукции, КоличествоПолуфабрикатов");
				
			Если ЭлементМатериал.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Обособленно Тогда
				НоваяСтрока.Назначение = СтрокаТЧ.Назначение;
			Иначе 
				НоваяСтрока.Назначение = Справочники.Назначения.ПустаяСсылка();
			КонецЕсли;
			
			НоваяСтрока.КоличествоПотребления = ЗнакКоличества * ЭлементМатериал.Количество;
			НоваяСтрока.ПодразделениеИсполнитель = ЭлементМатериал.ПодразделениеЭтапа;
			
			НоваяСтрока.ДинамическийПериод = Документы.ПланПроизводства.ПолучитьДатуПоГрафику(
				СтрокаТЧ.ДатаПроизводства,
				ЭлементМатериал.ДнейОтПотребности, 
				ПараметрыГрафика);
			
			
			СтрокаПродукции = ТаблицаПродукция.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПродукции, НоваяСтрока);
			СтрокаПродукции.УправлениеПроизводством2_2 = СтрокаТЧ.УправлениеПроизводством2_2;
			СтрокаПродукции.КоличествоПродукции = НоваяСтрока.КоличествоПотребления;
			
			МассивСтрок.Добавить(СтрокаПродукции);
			
			ДанныеПоНоменклатуре = Новый Структура;
			ДанныеПоНоменклатуре.Вставить("Подразделение",      СтрокаТЧ.ПодразделениеДиспетчер);
			ДанныеПоНоменклатуре.Вставить("Номенклатура",       СтрокаПродукции.Номенклатура);
			ДанныеПоНоменклатуре.Вставить("Характеристика",     СтрокаПродукции.Характеристика);
			ДанныеПоНоменклатуре.Вставить("НачалоПроизводства", Мин(СтрокаТЧ.НачалоПериода, НоваяСтрока.ДинамическийПериод));
			
			МассивДанных.Добавить(ДанныеПоНоменклатуре);
			
		КонецЦикла;
		
		Если МассивСтрок.ВГраница() <> -1 Тогда
			
			УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из ТаблицаРезультат Цикл
	
		СтрокаТЧ.ДинамическийПериод = ПланированиеКлиентСерверПовтИсп.РассчитатьДатуНачалаПериода(СтрокаТЧ.ДинамическийПериод, Периодичность);
	
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
