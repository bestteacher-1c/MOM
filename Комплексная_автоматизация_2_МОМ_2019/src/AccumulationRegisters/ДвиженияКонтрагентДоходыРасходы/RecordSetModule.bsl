#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
		
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("РежимЗаписи") 
		И ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	ТекстыЗапросовДляПолученияТаблицыИзменений = 
		ЗакрытиеМесяцаСервер.ТекстыЗапросовДляПолученияТаблицыИзмененийРегистра(ЭтотОбъект.Метаданные(), ЭтотОбъект.Отбор);
	
	Запрос = Новый Запрос;
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиНачальныхДанных;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("ТекстВыборкиТаблицыИзменений", ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиТаблицыИзменений);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("РежимЗаписи") 
		И ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу для последующей записи в регистрах заданий.
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	МассивЗапросовЗаданийПоОперациям = Новый Массив;
	МассивЗапросовЗаданийПоОперациям.Добавить(
		УчетНДСУП.ТекстЗапросаФормированияЗаданийПриИзмененииВыручки("ТаблицаИзмененийДвиженияКонтрагентДоходыРасходы"));
	//++ НЕ УТ
	ИмяТаблицыДействующиеУчетныеПолитики = "ДействующиеУчетныеПолитики_ДвиженияКонтрагентДоходыРасходы";
	МассивЗапросовЗаданийПоОперациям.Добавить(НачислениеСписаниеРезервовПоСомнительнымДолгамСервер.ТекстЗапросаЗаданийКЗакрытиюМесяцаПриИзмененииРасчетов(
		ИмяТаблицыДействующиеУчетныеПолитики));
	//-- НЕ УТ

	//++ НЕ УТ
	// Дополним временные таблицы данными по организациям на УСН, для последующей актуализации данных, при необходимости:
	УчетУСНСервер.ДополнитьМенеджерВременныхТаблицДаннымиПоОрганизациямНаУСНДоходыМинусРасходы(СтруктураВременныеТаблицы.МенеджерВременныхТаблиц);
	ТекстАктуализацииРасходовПриУСН = УчетУСНСервер.ТекстЗапросаОбновленияЗаданийКЗакрытиюМесяцаПриЗаписиРегистра();
	ТекстАктуализацииРасходовПриУСН = СтрЗаменить(ТекстАктуализацииРасходовПриУСН, "#ТаблицаИзмененийРегистра", "ТаблицаИзмененийДвиженияКонтрагентДоходыРасходы");
	ДопУсловия = "ТаблицаИзменений.СтатьяДоходовРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|	ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Организация) = ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Контрагент)
	|	И ТаблицаИзменений.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетКомиссионераКомиссия)";
	ТекстАктуализацииРасходовПриУСН = СтрЗаменить(ТекстАктуализацииРасходовПриУСН, "&ДополнительныеУсловия", ДопУсловия);
	МассивЗапросовЗаданийПоОперациям.Добавить(ТекстАктуализацииРасходовПриУСН);
	//-- НЕ УТ
	ТекстЗапросаЗаданий = ЗакрытиеМесяцаСервер.ТекстЗапросЗаданийКЗакрытиюМесяца(
	                                         "ДвиженияКонтрагентДоходыРасходы", МассивЗапросовЗаданийПоОперациям);
	                                         
	// Уничтожаем таблицу изменений регистра:
	МассивТекстовЗапроса = Новый Массив;
	МассивТекстовЗапроса.Добавить(ДополнительныеСвойства.ТекстВыборкиТаблицыИзменений);
	//++ НЕ УТ
	МассивТекстовЗапроса.Добавить(НачислениеСписаниеРезервовПоСомнительнымДолгамСервер.ТекстЗапросаДействующихУчетныхПолитик(
		"ТаблицаИзмененийДвиженияКонтрагентДоходыРасходы", ИмяТаблицыДействующиеУчетныеПолитики));
	//-- НЕ УТ
	МассивТекстовЗапроса.Добавить(ТекстЗапросаЗаданий);
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ТаблицаИзмененийДвиженияКонтрагентДоходыРасходы");

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу для последующей записи в регистрах заданий.
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
