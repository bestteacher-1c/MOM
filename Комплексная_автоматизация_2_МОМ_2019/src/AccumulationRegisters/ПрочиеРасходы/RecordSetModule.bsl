#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
	 ИЛИ РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение);
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения   = Истина;
	
	// Текущее состояние остатков помещается во временную таблицу,
	// чтобы при записи получить изменение актуальных остатков регистра.
	
	ТекстыЗапросовДляПолученияТаблицыИзменений = 
		ЗакрытиеМесяцаСервер.ТекстыЗапросовДляПолученияТаблицыИзмененийРегистра(ЭтотОбъект.Метаданные(), ЭтотОбъект.Отбор);
	
	Запрос = Новый Запрос;
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиНачальныхДанных;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("ТекстВыборкиТаблицыИзменений", ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиТаблицыИзменений);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// При формировании заданий к расчету себестоимости выполняется дополнительная проверка:
	//  если в периоде изменений есть движения по резервам, в которых статья расходов является статьей использования.
	ТекстЗапросаПериодов = "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ДД.Период, Месяц)) КАК НачалоПериода,
	|	МАКСИМУМ(КОНЕЦПЕРИОДА(ДД.Период, Месяц)) КАК ОкончаниеПериода
	|ИЗ
	|	ТаблицаИзмененийПрочиеРасходы КАК ДД
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ДополнительныеСвойства.ТекстВыборкиТаблицыИзменений;
	Запрос.Текст = Запрос.Текст + ТекстЗапросаПериодов;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		НачалоПериода = ?(ЗначениеЗаполнено(Выборка.НачалоПериода), Выборка.НачалоПериода, Дата("00010101"));
		ОкончаниеПериода = ?(ЗначениеЗаполнено(Выборка.ОкончаниеПериода), Выборка.ОкончаниеПериода, Дата("00010101"));
	Иначе
		НачалоПериода = Дата("00010101");
		ОкончаниеПериода = Дата("00010101");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("НормируемыеРасходы", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	//-- НЕ УТ
	Запрос.Текст =
	// 0 ДвиженияПрочиеРасходыИзменение
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.СтатьяРасходов,
	|	ТаблицаИзменений.АналитикаРасходов,
	|	ТаблицаИзменений.НаправлениеДеятельности,
	|	СУММА(ТаблицаИзменений.Сумма) КАК СуммаИзменение,
	|	СУММА(ТаблицаИзменений.СуммаБезНДС) КАК СуммаБезНДСИзменение,
	|	СУММА(ТаблицаИзменений.СуммаРегл) КАК СуммаРеглИзменение,
	|	СУММА(ТаблицаИзменений.ПостояннаяРазница) КАК ПостояннаяРазницаИзменение,
	|	СУММА(ТаблицаИзменений.ВременнаяРазница) КАК ВременнаяРазницаИзменение
	|ПОМЕСТИТЬ ДвиженияПрочиеРасходыИзменение
	|ИЗ
	|	ТаблицаИзмененийПрочиеРасходы КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.СтатьяРасходов,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.АналитикаРасходов,
	|	ТаблицаИзменений.НаправлениеДеятельности
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Сумма) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаБезНДС) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаРегл) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ВременнаяРазница) <> 0
	|
	|;
	|
	//++ НЕ УТ
	|////////////////////////////////////////////////////////////////////////////////
	// 1 Служебная
	|ВЫБРАТЬ
	|	Резервы.Организация КАК Организация,
	|	Резервы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Резервы.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов
	|ПОМЕСТИТЬ ВтДвижениеРезервов
	|ИЗ
	|	РегистрНакопления.РезервыПредстоящихРасходов.Обороты(
	|		&НачалоПериода,
	|		&ОкончаниеПериода,
	|		,
	|		(Организация,НаправлениеДеятельности) В (ВЫБРАТЬ Т.Организация, Т.НаправлениеДеятельности ИЗ ДвиженияПрочиеРасходыИзменение КАК Т)
	|		) КАК Резервы
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2 Служебная
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.АналитикаРасходов КАК АналитикаРасходов,
	|	СтатьиРасходов.ЛюбаяАналитикаРасходов КАК ЛюбаяАналитикаРасходов,
	|	ВтДвижениеРезервов.Организация КАК Организация,
	|	ВтДвижениеРезервов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ СтатьиРезервов
	|ИЗ
	|	ВтДвижениеРезервов КАК ВтДвижениеРезервов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыУчетаРезервовПредстоящихРасходов.ИспользованиеРезерва КАК СтатьиРасходов
	|		ПО ВтДвижениеРезервов.ОбъектУчетаРезервов = СтатьиРасходов.Ссылка
	|
	|;
	//-- НЕ УТ
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3 ПрочиеРасходыЗаданияКРасчетуСебестоимости
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Месяц,
	|	ТаблицаИзменений.Организация                  КАК Организация,
	|	ТаблицаИзменений.Регистратор                  КАК Документ,
	|	ЛОЖЬ                                          КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ПОМЕСТИТЬ ПрочиеРасходыЗаданияКРасчетуСебестоимости
	|ИЗ
	|	ТаблицаИзмененийПрочиеРасходы КАК ТаблицаИзменений
	//++ НЕ УТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиРезервов КАК СтатьиРезервов
	|		ПО ТаблицаИзменений.Организация = СтатьиРезервов.Организация
	|		И ТаблицаИзменений.НаправлениеДеятельности = СтатьиРезервов.НаправлениеДеятельности
	|		И ТаблицаИзменений.СтатьяРасходов = СтатьиРезервов.СтатьяРасходов
	|		И (ТаблицаИзменений.АналитикаРасходов = СтатьиРезервов.АналитикаРасходов ИЛИ СтатьиРезервов.ЛюбаяАналитикаРасходов)
	//-- НЕ УТ
	|ГДЕ
	|	(ТаблицаИзменений.СтатьяРасходов.ВариантРаспределенияРасходовУпр
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	ИЛИ ТаблицаИзменений.СтатьяРасходов.ВариантРаспределенияРасходовРегл
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	//++ НЕ УТ
	|	ИЛИ ТаблицаИзменений.СтатьяРасходов.ВидРасходов В (&НормируемыеРасходы)
	|	ИЛИ НЕ СтатьиРезервов.СтатьяРасходов ЕСТЬ NULL
	//-- НЕ УТ
	|	)
	|	И НЕ ТаблицаИзменений.Регистратор ССЫЛКА Документ.РаспределениеДоходовПоНаправлениямДеятельности
	|	И (НЕ ТаблицаИзменений.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
	|		ИЛИ (ТаблицаИзменений.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
	|			И ВЫРАЗИТЬ(ТаблицаИзменений.Регистратор КАК Документ.РаспределениеПрочихЗатрат).НазначениеНастройкиРаспределения = 
	|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)))
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ),
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.СтатьяРасходов,
	|	ТаблицаИзменений.АналитикаРасходов,
	|	ТаблицаИзменений.НаправлениеДеятельности,
	|	ТаблицаИзменений.ХозяйственнаяОперация,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Сумма) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаБезНДС) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаРегл) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ВременнаяРазница) <> 0
	//++ НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 4 ПрочиеРасходы_ЗаданияКРасчетуСтоимостиВНАИзменение
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период            КАК Период,
	|	Таблица.Организация       КАК Организация,
	|	Таблица.АналитикаРасходов КАК ОбъектУчета,
	|	Таблица.Регистратор       КАК Документ
	|ПОМЕСТИТЬ ПрочиеРасходы_ЗаданияКРасчетуСтоимостиВНАИзменение
	|ИЗ
	|	ТаблицаИзмененийПрочиеРасходы КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ИспользоватьВнеоборотныеАктивы2_4 КАК ВНА2_4
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВНА2_4.Значение = ИСТИНА
	|	И (Таблица.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|		ИЛИ Таблица.АналитикаРасходов ССЫЛКА Справочник.ОбъектыСтроительства
	|		ИЛИ Таблица.АналитикаРасходов ССЫЛКА Справочник.НематериальныеАктивы)
	|	И Таблица.СтатьяРасходов.ТипРасходов = ЗНАЧЕНИЕ(Перечисление.ТипыРасходов.ФормированиеСтоимостиВНА)
	|	И (Таблица.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			ИЛИ Таблица.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы))
	//-- НЕ УТ
	|";
	Результат = Запрос.ВыполнитьПакет();
	
	// Информация о разнице между остатками была помещена во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей.
	Если Не ОбменДанными.Загрузка И ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Выборка = Результат[0].Выбрать();
		Выборка.Следующий();
		СтруктураВременныеТаблицы.Вставить("ДвиженияПрочиеРасходыИзменение", Выборка.Количество > 0);
	КонецЕсли;
	
	//++ НЕ УТ
	ИндексРезультата = Результат.ВГраница();
	Выборка = Результат[ИндексРезультата].Выбрать();
	Выборка.Следующий();
	СтруктураВременныеТаблицы.Вставить("ПрочиеРасходы_ЗаданияКРасчетуСтоимостиВНАИзменение", Выборка.Количество > 0);
	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
