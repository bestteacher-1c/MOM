#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Регистратор = Отбор.Регистратор.Значение;
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ПланПроизводства") Тогда
		
		РассчитатьПоребности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Регистратор, "Сценарий.ИспользоватьДляПланированияМатериалов");
		
		Если РассчитатьПоребности Тогда
		
			МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
			
			ПрочитатьСтарыеИНовыеДвижения(МенеджерВременныхТаблиц, Регистратор);
			
			ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		ИЛИ НЕ ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли;
	
	
	ОтменаПроведения = ?(ДополнительныеСвойства.Свойство("РежимЗаписи"),
		ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения,
		Ложь);
	
	Если НЕ ОтменаПроведения Тогда
		
		СверитьДвижения();
		
	КонецЕсли;
	
КонецПроцедуры


Процедура СверитьДвижения()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОчередьРасчета.ДатаПроизводства КАК ДатаПроизводства,
		|	ОчередьРасчета.ПланПроизводства КАК ПланПроизводства,
		|	ОчередьРасчета.Подразделение КАК Подразделение,
		|	ОчередьРасчета.Спецификация КАК Спецификация,
		|	ОчередьРасчета.Назначение КАК Назначение,
		|	ОчередьРасчета.Сценарий КАК Сценарий,
		|	ЛОЖЬ КАК ВыпускПолуфабрикатов
		|ИЗ
		|	(ВЫБРАТЬ
		|		НовыеДвижения.ПланПроизводства КАК ПланПроизводства,
		|		НовыеДвижения.Номенклатура КАК Номенклатура,
		|		НовыеДвижения.Характеристика КАК Характеристика,
		|		НовыеДвижения.Спецификация КАК Спецификация,
		|		НовыеДвижения.Количество КАК КоличествоРазность,
		|		НовыеДвижения.ДатаПроизводства КАК ДатаПроизводства,
		|		НовыеДвижения.Сценарий КАК Сценарий,
		|		НовыеДвижения.Подразделение КАК Подразделение,
		|		НовыеДвижения.Назначение КАК Назначение
		|	ИЗ
		|		НовыеДвижения КАК НовыеДвижения
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СтарыеДвижения.ПланПроизводства,
		|		СтарыеДвижения.Номенклатура,
		|		СтарыеДвижения.Характеристика,
		|		СтарыеДвижения.Спецификация,
		|		-СтарыеДвижения.Количество,
		|		СтарыеДвижения.ДатаПроизводства,
		|		СтарыеДвижения.Сценарий,
		|		СтарыеДвижения.Подразделение,
		|		СтарыеДвижения.Назначение
		|	ИЗ
		|		СтарыеДвижения КАК СтарыеДвижения) КАК ОчередьРасчета
		|
		|СГРУППИРОВАТЬ ПО
		|	ОчередьРасчета.ДатаПроизводства,
		|	ОчередьРасчета.ПланПроизводства,
		|	ОчередьРасчета.Подразделение,
		|	ОчередьРасчета.Спецификация,
		|	ОчередьРасчета.Назначение,
		|	ОчередьРасчета.Сценарий,
		|	ОчередьРасчета.Номенклатура,
		|	ОчередьРасчета.Характеристика
		|
		|ИМЕЮЩИЕ
		|	СУММА(ОчередьРасчета.КоличествоРазность) <> 0");
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	ТаблицаОчередь = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаОчередь.Количество() > 0 Тогда
		
		РегистрыСведений.ОчередьРасчетаПланаПроизводства.ЗаписатьОчередь(
			ТаблицаОчередь,
			Отбор.Регистратор.Значение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПрочитатьСтарыеИНовыеДвижения(МенеджерВременныхТаблиц, ПланПроизводства)
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПланПроизводства", ПланПроизводства);
	Запрос.УстановитьПараметр("НовыеДвиженияПланыПроизводства", ЭтотОбъект);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтарыеДвижения.Период КАК ДатаПроизводства,
	|	СтарыеДвижения.Сценарий КАК Сценарий,
	|	СтарыеДвижения.Номенклатура КАК Номенклатура,
	|	СтарыеДвижения.Характеристика КАК Характеристика,
	|	СтарыеДвижения.Подразделение КАК Подразделение,
	|	СтарыеДвижения.ПланПроизводства КАК ПланПроизводства,
	|	СтарыеДвижения.Спецификация КАК Спецификация,
	|	СтарыеДвижения.Назначение КАК Назначение,
	|	СтарыеДвижения.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	СтарыеДвижения.ДатаЗапускаВыпуска КАК ДатаЗапускаВыпуска,
	|	СтарыеДвижения.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
	|	СтарыеДвижения.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	СтарыеДвижения.Количество КАК Количество,
	|	СтарыеДвижения.РучнаяКорректировка КАК РучнаяКорректировка,
	|	СтарыеДвижения.ДатаКорректировки КАК ДатаКорректировки,
	|	СтарыеДвижения.АвторКорректировки КАК АвторКорректировки,
	|	СтарыеДвижения.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ СтарыеДвижения
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК СтарыеДвижения
	|ГДЕ
	|	СтарыеДвижения.Регистратор = &ПланПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДвижения.Период КАК ДатаПроизводства,
	|	НовыеДвижения.Сценарий КАК Сценарий,
	|	НовыеДвижения.Номенклатура КАК Номенклатура,
	|	НовыеДвижения.Характеристика КАК Характеристика,
	|	НовыеДвижения.Подразделение КАК Подразделение,
	|	НовыеДвижения.ПланПроизводства КАК ПланПроизводства,
	|	НовыеДвижения.Спецификация КАК Спецификация,
	|	НовыеДвижения.Назначение КАК Назначение,
	|	НовыеДвижения.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	НовыеДвижения.ДатаЗапускаВыпуска КАК ДатаЗапускаВыпуска,
	|	НовыеДвижения.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
	|	НовыеДвижения.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	НовыеДвижения.Количество КАК Количество,
	|	НовыеДвижения.РучнаяКорректировка КАК РучнаяКорректировка,
	|	НовыеДвижения.ДатаКорректировки КАК ДатаКорректировки,
	|	НовыеДвижения.АвторКорректировки КАК АвторКорректировки,
	|	НовыеДвижения.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ НовыеДвижения
	|ИЗ
	|	&НовыеДвиженияПланыПроизводства КАК НовыеДвижения";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
