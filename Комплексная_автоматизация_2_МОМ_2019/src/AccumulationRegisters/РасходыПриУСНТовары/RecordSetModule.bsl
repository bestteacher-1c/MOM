#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка ИЛИ УчетУСНСервер.ДвиженияЗаписываютсяПризнаниемРасходов(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	БлокироватьДляИзменения = Истина;
		
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	ТекстыЗапросовДляПолученияТаблицыИзменений = 
		ЗакрытиеМесяцаСервер.ТекстыЗапросовДляПолученияТаблицыИзмененийРегистра(ЭтотОбъект.Метаданные(), ЭтотОбъект.Отбор);
	
	Запрос = Новый Запрос;
	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
	КонецЕсли;
	Если Не ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура);
	КонецЕсли;
	Если Не ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.Свойство("МенеджерВременныхТаблиц") Тогда
		ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	КонецЕсли;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиНачальныхДанных;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("ТекстВыборкиТаблицыИзменений", ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиТаблицыИзменений);
			
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка ИЛИ УчетУСНСервер.ДвиженияЗаписываютсяПризнаниемРасходов(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.Текст = ДополнительныеСвойства.ТекстВыборкиТаблицыИзменений;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда // В РИБ регистр Заданий пишется только в главном узле.
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Месяц,
		|	ТаблицаИзменений.Организация,
		|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.ПризнаниеРасходовПриУСН) КАК Операция,
		|	ТаблицаИзменений.Регистратор КАК Документ
		|ПОМЕСТИТЬ РасходыПриУСНТоварыЗаданияКЗакрытиюМесяца
		|ИЗ
		|	ТаблицаИзмененийРасходыПриУСНТовары КАК ТаблицаИзменений";
		Запрос.Выполнить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли