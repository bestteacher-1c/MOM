#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.НематериальныйАктив,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.Количество
	|ПОМЕСТИТЬ ВыработкаНМАПередЗаписью
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация          КАК Организация,
	|	ТаблицаИзменений.НематериальныйАктив  КАК НематериальныйАктив,
	|	&Регистратор                          КАК Документ,
	|	ИСТИНА                                КАК ОтражатьВРеглУчете,
	|	ИСТИНА                                КАК ОтражатьВУпрУчете,
	|	МИНИМУМ(ТаблицаИзменений.Период)      КАК Период
	|ПОМЕСТИТЬ ВыработкаНМАИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Период,
	|		Таблица.Организация КАК Организация,
	|		Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|		Таблица.Количество КАК КоличествоИзменение
	|	ИЗ
	|		ВыработкаНМАПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|		Таблица.Организация,
	|		Таблица.НематериальныйАктив,
	|		-Таблица.Количество
	|	ИЗ
	|		РегистрНакопления.ВыработкаНМА КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НематериальныйАктив
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыработкаНМАПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ВыработкаНМАИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
