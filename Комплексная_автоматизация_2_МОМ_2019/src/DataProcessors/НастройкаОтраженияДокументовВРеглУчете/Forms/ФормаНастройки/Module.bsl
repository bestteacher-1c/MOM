#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	#Область ЗаполнениеПоПараметрам
	
	Организация = Параметры.Организация;
	ДатаОкончанияПериода = Параметры.ДатаОкончанияПериода;
	
	ОрганизацииДляНастройки = Новый Массив; // Пусто - все организации
	Если Параметры.Свойство("ОрганизацииДляНастройки") Тогда
		ОрганизацииДляНастройки = Параметры.ОрганизацииДляНастройки;
	КонецЕсли;
	
	МассивВидовСчетовРеглУчета = Неопределено;
	Параметры.Свойство("МассивВидовСчетовРеглУчета", МассивВидовСчетовРеглУчета);
	
	МассивСкрываемыхЭлементов = Неопределено;
	Параметры.Свойство("МассивСкрываемыхЭлементов", МассивСкрываемыхЭлементов);
	
	Если Параметры.Свойство("ПоказыватьТолькоТребующиеНастройки") Тогда
		ПоказыватьТолькоТребующиеНастройки = Параметры.ПоказыватьТолькоТребующиеНастройки;
	КонецЕсли;
	
	Если Параметры.Свойство("ГруппаФинансовогоУчета") Тогда
		РедактируемаяГФУ = Параметры.ГруппаФинансовогоУчета;
		ТекущаяТаблицаНастройки = ОпределитьТаблицуНастройкиПоПереданнойГФУ(РедактируемаяГФУ);
	КонецЕсли;
	
	Если Параметры.Свойство("ТекущаяСтраница") Тогда
		ТекущаяТаблицаНастройки = Параметры.ТекущаяСтраница;
	КонецЕсли;
	
	#КонецОбласти
	
	ЗаполнитьСписокВыбораОрганизации(ОрганизацииДляНастройки);
				
	ПанельУправленияСвернута = Ложь;
	РедактированиеЗаполненныхСчетов = Ложь;
	ПоказыватьОбщуюНастройку = 1;
	
	СоздатьСтруктуруТаблицФормы();
	ЗаполнитьСвойстваСтруктурыТаблицФормы(МассивВидовСчетовРеглУчета);
	
	ЗаполнитьТаблицыНастройки();
	
	УстановитьСвойстваЭлементовФормы(МассивВидовСчетовРеглУчета, МассивСкрываемыхЭлементов);
	
	УстановитьДоступныеСчетаУчета();
	
	Если ТекущаяТаблицаНастройки = "" Или Элементы.ТекущаяТаблицаНастройки.СписокВыбора.НайтиПоЗначению(ТекущаяТаблицаНастройки) = Неопределено Тогда
		ТекущаяТаблицаНастройки = Элементы.ТекущаяТаблицаНастройки.СписокВыбора.Получить(0).Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РедактируемаяГФУ) Тогда
		ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущаяТаблицаНастройки);
		ИмяКолонки = ИмяАналитикиУчетаПоТаблице(ИмяТаблицыДанных);
		НайденныеСтрокиИсключений = ЭтотОбъект[ИмяТаблицыДанных].НайтиСтроки(Новый Структура(ИмяКолонки, РедактируемаяГФУ));
		Если НайденныеСтрокиИсключений.Количество() Тогда
			Элементы[ИмяТаблицыДанных].ТекущаяСтрока = НайденныеСтрокиИсключений.Получить(0).ПолучитьИдентификатор();
		КонецЕсли;
		НайденныеСтрокиОбщие = Новый Массив;
		Если ЕстьОбщиеНастройки(ИмяТаблицыДанных) Тогда
			НайденныеСтрокиОбщие = ЭтотОбъект["Общие"+ИмяТаблицыДанных].НайтиСтроки(Новый Структура("АналитикаУчета", РедактируемаяГФУ));
			Если НайденныеСтрокиОбщие.Количество() Тогда
				Элементы["Общие"+ИмяТаблицыДанных].ТекущаяСтрока = НайденныеСтрокиОбщие.Получить(0).ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
		ПоказыватьОбщуюНастройку = ?(НайденныеСтрокиИсключений.Количество() = 0 И НайденныеСтрокиОбщие.Количество() > 0, 1, 0);
	КонецЕсли;
	
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	УстановитьТекущуюСтраницу(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И ЗавершениеРаботы Тогда
		Отказ = Истина;
		ВыполняетсяЗакрытие = Истина;
		ТекстПредупреждения = 
		НСтр("ru = 'Настройки счетов были изменены, но не сохранены. Для того, чтобы изменения вступили в силу следует сохранить настройку.'");
		Возврат;
	КонецЕсли;
		
	Если НЕ ВыполняетсяЗакрытие И Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Настройки счетов были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СохранитьНастройкиОтраженияВУчете();
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ИзмененыНастройкиОтраженияВУчете И Не ЗавершениеРаботы Тогда
		Оповестить("ИзмененыНастройкиОтраженияВУчете");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПорядокОтраженияНоменклатуры" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаНоменклатуры");
	ИначеЕсли ИмяСобытия = "Запись_ПорядокОтраженияНоменклатурыПереданной" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаНоменклатурыПереданной");
	ИначеЕсли ИмяСобытия = "Запись_ПорядокОтраженияРасчетовСПартнерами" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаРасчетовСПартнерами");
	ИначеЕсли ИмяСобытия = "Запись_ПорядокОтраженияДоходов" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаДоходов");
	ИначеЕсли ИмяСобытия = "Запись_ПорядокОтраженияРасходов" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаРасходов");
	ИначеЕсли ИмяСобытия = "Запись_ПорядокОтраженияПроизводства" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаПроизводства");
	ИначеЕсли ИмяСобытия = "Запись_Резервы" Тогда
		ЗаполнитьТаблицыНастройки("СчетаУчетаРезервов");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОтбораНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоТребующиеНастройкиПриИзменении(Элемент)
	
	УстановитьТекущуюСтраницу(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьОбщуюНастройкуПриИзменении(Элемент)
	
	УстановитьТекущуюСтраницу(ЭтотОбъект);
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяТаблицаНастройкиПриИзменении(Элемент)
	
	УстановитьТекущуюСтраницу(ЭтотОбъект);
	РедактируемаяГФУ = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаНоменклатуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаНоменклатурыОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаНоменклатурыСклад Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Склад);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаНоменклатурыГруппаФинансовогоУчета Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.ГруппаФинансовогоУчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаНоменклатуры");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаНоменклатуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаНоменклатурыГруппаФинансовогоУчета Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаНоменклатурыПереданной

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПереданнойВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаНоменклатурыПереданнойОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаНоменклатурыПереданнойГруппаФинансовогоУчета Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.ГруппаФинансовогоУчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПереданнойПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПереданнойПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПереданнойПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПереданнойПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНоменклатурыПереданнойПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаНоменклатурыПереданной");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаНоменклатурыПереданной

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПереданнойВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаНоменклатурыПереданнойГруппаФинансовогоУчета Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПереданнойПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПереданнойПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаНоменклатурыПереданнойПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаРасчетов

&НаКлиенте
Процедура СчетаУчетаРасчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаРасчетовСПартнерамиОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаРасчетовСПартнерамиГруппаФинансовогоУчета Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.ГруппаФинансовогоУчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасчетовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ИмяТаблицы = ИмяТекущейТаблицы(Элементы.Страницы.ТекущаяСтраница.Имя, ТекущаяТаблицаНастройки);
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, ИмяТаблицы, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасчетовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущийВидРасчетов = СтрЗаменить(ТекущаяТаблицаНастройки, "СчетаУчетаРасчетов", "");
	ВидыРасчетовДляОбновленияТребующихсяНастроек = Новый Массив;
	ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить(ТекущийВидРасчетов);
	Если ТекущийВидРасчетов = "СКлиентами" ИЛИ ТекущийВидРасчетов = "СПоставщиками" Тогда
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СКомиссионерами");
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СКомитентами");
	КонецЕсли;
	Если ТекущийВидРасчетов = "СКомиссионерами" ИЛИ ТекущийВидРасчетов = "СКомитентами" Тогда
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить(?(ТекущийВидРасчетов = "СКомиссионерами", "СКомитентами", "СКомиссионерами"));
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СКлиентами");
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СПоставщиками");
	КонецЕсли;	
	
	Для каждого ВидРасчетов Из ВидыРасчетовДляОбновленияТребующихсяНастроек Цикл
		ТаблицаДляОбновления = "СчетаУчетаРасчетов"+ВидРасчетов;
		Если СтруктураТаблицФормы[ТаблицаДляОбновления].Используется Тогда
			ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(ТаблицаДляОбновления, Элемент.ТекущиеДанные);
			ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные["ТребуетсяНастройкаРасчеты" + ВидРасчетов], ТребуетсяНастройка);
			ИзменитьЧислоТребующихсяНастроек(ТаблицаДляОбновления, ЧислоИзменения);
			Элемент.ТекущиеДанные["ТребуетсяНастройкаРасчеты" + ВидРасчетов] = ТребуетсяНастройка;
		КонецЕсли;
	КонецЦикла;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасчетовСПартнерамиПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасчетовСПартнерамиПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасчетовСПартнерамиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаРасчетовСПартнерами");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаРасчетовСПартнерами

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасчетовСПартнерамиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаРасчетовСПартнерамиГруппаФинансовогоУчета Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасчетовСПартнерамиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ИмяТаблицы = ИмяТекущейТаблицы(Элементы.Страницы.ТекущаяСтраница.Имя, ТекущаяТаблицаНастройки);
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, ИмяТаблицы, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасчетовСПартнерамиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущийВидРасчетов = СтрЗаменить(ТекущаяТаблицаНастройки, "СчетаУчетаРасчетов", "");
	ВидыРасчетовДляОбновленияТребующихсяНастроек = Новый Массив;
	ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить(ТекущийВидРасчетов);
	Если ТекущийВидРасчетов = "СКлиентами" ИЛИ ТекущийВидРасчетов = "СПоставщиками" Тогда
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СКомиссионерами");
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СКомитентами");
	КонецЕсли;
	Если ТекущийВидРасчетов = "СКомиссионерами" ИЛИ ТекущийВидРасчетов = "СКомитентами" Тогда
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить(?(ТекущийВидРасчетов = "СКомиссионерами", "СКомитентами", "СКомиссионерами"));
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СКлиентами");
		ВидыРасчетовДляОбновленияТребующихсяНастроек.Добавить("СПоставщиками");
	КонецЕсли;	
	
	Для каждого ВидРасчетов Из ВидыРасчетовДляОбновленияТребующихсяНастроек Цикл
		ТаблицаДляОбновления = "ОбщиеСчетаУчетаРасчетов"+ВидРасчетов;
		Если СтруктураТаблицФормы[ТаблицаДляОбновления].Используется Тогда
			ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(ТаблицаДляОбновления, Элемент.ТекущиеДанные);
			ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные["ТребуетсяНастройкаРасчеты" + ВидРасчетов], ТребуетсяНастройка);
			ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(ТаблицаДляОбновления, Элемент.ТекущиеДанные);
			ИзменитьЧислоТребующихсяНастроек(ТаблицаДляОбновления, ЧислоИзменения);
			Элемент.ТекущиеДанные["ТребуетсяНастройкаРасчеты" + ВидРасчетов] = ТребуетсяНастройка;
		КонецЕсли;
	КонецЦикла;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасчетовСПартнерамиПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаДоходов

&НаКлиенте
Процедура СчетаУчетаДоходовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаДоходовОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаДоходовПодразделение Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Подразделение);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаДоходовСтатьяДоходов Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.СтатьяДоходов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДоходовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДоходовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДоходовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДоходовПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДоходовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаДоходов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаДоходов

&НаКлиенте
Процедура ОбщиеСчетаУчетаДоходовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаДоходовСтатьяДоходов Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаДоходовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаДоходовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаДоходовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаРасходов

&НаКлиенте
Процедура СчетаУчетаРасходовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаРасходовОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаРасходовПодразделение Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Подразделение);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаРасходовСтатьяРасходов Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.СтатьяРасходов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасходовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасходовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасходовПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущийЭлемент.Имя = "СчетаУчетаРасходовСчетУчета" Тогда
		Элементы.СчетаУчетаРасходовСчетУчета.ПараметрыВыбора = ПараметрыВыбора("СчетаУчетаРасходов", "СчетУчета", Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасходовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасходовПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРасходовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаРасходов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаРасходов

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасходовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаРасходовСтатьяРасходов Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасходовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасходовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасходовПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущийЭлемент.Имя = "ОбщиеСчетаУчетаРасходовСчетУчета" Тогда
		Элементы.ОбщиеСчетаУчетаРасходовСчетУчета.ПараметрыВыбора = ПараметрыВыбора("ОбщиеСчетаУчетаРасходов", "СчетУчета", Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРасходовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаТМЦВЭксплуатации

&НаКлиенте
Процедура СчетаУчетаТМЦВЭксплуатацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	
	Если Поле = Элементы.СчетаУчетаТМЦВЭксплуатацииОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаТМЦВЭксплуатацииКатегорияЭксплуатации Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.КатегорияЭксплуатации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаТМЦВЭксплуатацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаТМЦВЭксплуатацииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаТМЦВЭксплуатацииПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаТМЦВЭксплуатацииПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаТМЦВЭксплуатацииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаТМЦВЭксплуатации");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаТМЦВЭксплуатации

&НаКлиенте
Процедура ОбщиеСчетаУчетаТМЦВЭксплуатацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаТМЦВЭксплуатацииКатегорияЭксплуатации Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаТМЦВЭксплуатацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаТМЦВЭксплуатацииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаТМЦВЭксплуатацииПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаСертификатов

&НаКлиенте
Процедура СчетаУчетаПодарочныхСертификатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаПодарочныхСертификатовОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаПодарочныхСертификатовВидСертификата Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.ВидСертификата);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПодарочныхСертификатовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПодарочныхСертификатовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПодарочныхСертификатовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПодарочныхСертификатовПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПодарочныхСертификатовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаПодарочныхСертификатов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаПодарочныхСертификатов

&НаКлиенте
Процедура ОбщиеСчетаУчетаПодарочныхСертификатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаПодарочныхСертификатовВидСертификата Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаПодарочныхСертификатовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаПодарочныхСертификатовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаПодарочныхСертификатовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаНезавершенногоПроизводства

&НаКлиенте
Процедура СчетаУчетаНезавершенногоПроизводстваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.СчетаУчетаПроизводстваОрганизация Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Организация);		
	ИначеЕсли Поле = Элементы.СчетаУчетаПроизводстваПодразделение Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.МестоПроизводства);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНезавершенногоПроизводстваПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаНезавершенногоПроизводстваПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПроизводстваПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПроизводстваПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаПроизводстваПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаПроизводства");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаДенежныхСредств

&НаКлиенте
Процедура СчетаУчетаДенежныхСредствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.СчетаУчетаДенежныхСредствОрганизация Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Организация);		
	ИначеЕсли Поле = Элементы.СчетаУчетаДенежныхСредствАналитикаУчета Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	ИначеЕсли Поле = Элементы.СчетаУчетаДенежныхСредствТипДенежныхСредств Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.ТипДенежныхСредств);
	ИначеЕсли Поле = Элементы.СчетаУчетаДенежныхСредствВалюта Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Валюта);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДенежныхСредствПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДенежныхСредствПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущийЭлемент.Имя = "СчетаУчетаДенежныхСредствСчетУчета" Тогда
		Элементы.СчетаУчетаДенежныхСредствСчетУчета.ПараметрыВыбора = ПараметрыВыбора("СчетаУчетаДенежныхСредств", "СчетУчета", Элемент.ТекущиеДанные.ТипДенежныхСредств);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДенежныхСредствПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаДенежныхСредствПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаВнеоборотныхАктивов

&НаКлиенте
Процедура СчетаУчетаВнеоборотныхАктивовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.СчетаУчетаВнеоборотныхАктивовАналитикаУчета Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаВнеоборотныхАктивовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаВнеоборотныхАктивовПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено И Элемент.ТекущийЭлемент.Имя <> "СчетаУчетаВнеоборотныхАктивовАналитикаУчета"
		И Элемент.ТекущийЭлемент.Имя <> "СчетаУчетаВнеоборотныхАктивовСчетУчетаВыбытия" Тогда
		Элемент.ТекущийЭлемент.ПараметрыВыбора = ПараметрыВыбора("СчетаУчетаВнеоборотныхАктивов", Элемент.ТекущийЭлемент.Имя, Элемент.ТекущиеДанные.ВидАктива);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаВнеоборотныхАктивовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаВнеоборотныхАктивовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСчетаУчетаРезервов

&НаКлиенте
Процедура СчетаУчетаРезервовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если Поле = Элементы.СчетаУчетаРезервовОрганизация Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Организация);
		
	ИначеЕсли Поле = Элементы.СчетаУчетаРезервовВидРезервов Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.ВидРезервов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРезервовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРезервовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРезервовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРезервовПослеУдаления(Элемент)
	Модифицированность = Истина;
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчетаРезервовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаРезервов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеСчетаУчетаРезервов

&НаКлиенте
Процедура ОбщиеСчетаУчетаРезервовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбщиеСчетаУчетаРезервовВидРезервов Тогда
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.АналитикаУчета);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРезервовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОбновитьИзмененныеДанные(СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, ""), Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРезервовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	ТребуетсяНастройка = ТребуетсяНастройкаСчетовУчета(Элемент.Имя, Элемент.ТекущиеДанные);
	ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(Элемент.ТекущиеДанные.ТребуетсяНастройка, ТребуетсяНастройка);
	ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(Элемент.Имя, Элемент.ТекущиеДанные);
	ИзменитьЧислоТребующихсяНастроек(Элемент.Имя, ЧислоИзменения);
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	Элемент.ТекущиеДанные.ТребуетсяНастройка = ТребуетсяНастройка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбщиеСчетаУчетаРезервовПередНачаломИзменения(Элемент, Отказ)
	
	ПроверкаВозможностиРедактирования(Элемент, Отказ);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	ИмяТекущейТаблицы = ИмяТекущейТаблицы(Элементы.Страницы.ТекущаяСтраница.Имя, ТекущаяТаблицаНастройки);
	ИмяСчета = СтрЗаменить(Элемент.Имя, ИмяТекущейТаблицы, "");
	ТекущаяСтрока = Элементы[ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТекущейТаблицы)].ТекущиеДанные;
	
	// Сбросим флаг по умолчанию:
	Если ТекущаяСтрока.Свойство(ИмяСчета + "ПоУмолчанию") Тогда
		ТекущаяСтрока[ИмяСчета + "ПоУмолчанию"] = Ложь;
	КонецЕсли;
	
	ОбновитьИзмененныеДанные(ИмяСчета, ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройкуИЗакрыть(Команда)
	
	СохранитьНастройкиОтраженияВУчете();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройку(Команда)
	
	СохранитьНастройкиОтраженияВУчете();
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройку(Команда)
	
	ЗаполнитьТаблицуКПрименениюНастроки();
	
	КоличествоДокументов = ТаблицаДокументов.Количество();
	Если КоличествоДокументов > 0 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Новая настройка будет применена для %1. Продолжить?'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
		КодОтвета = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("ПрименитьНастройкуЗавершение", ЭтотОбъект, Новый Структура("КоличествоДокументов", КоличествоДокументов)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
	Иначе
		СохранитьНастройкиОтраженияВУчете();
	КонецЕсли;
	
	ПрименитьНастройкуФрагмент(КоличествоДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуФрагмент(Знач КоличествоДокументов)
	
	ОповеститьПользователяОПримененииНастройки(КоличествоДокументов);

КонецПроцедуры

&НаКлиенте
Процедура АнализСчетовТребующихНастройки(Команда)
	
	Результат = АнализСчетовТребующихНастройкиСервер();
	
	Если Результат.Статус = "Выполняется" Тогда
		ОповещениеФормаДлительнойОперацииЗакрыта = Новый ОписаниеОповещения("ВыполнениеФоновогоЗаданияЗавершено", ЭтотОбъект, Ложь);
		ПараметрыФормы = ИнициализироватьПараметрыФормыДлительнойОперации(Результат);
		ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеФормаДлительнойОперацииЗакрыта, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьПанельУправления(Команда)
	
	ПанельУправленияСвернута = НЕ ПанельУправленияСвернута;
	
	УстановитьВидимостьПанелейУправления(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьПанельУправления(Команда)
	
	ПанельУправленияСвернута = НЕ ПанельУправленияСвернута;
	
	УстановитьВидимостьПанелейУправления(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьСчетаУчета(Команда)
	
	ИмяТекущейТаблицы = ИмяТекущейТаблицы(Элементы.Страницы.ТекущаяСтраница.Имя, ТекущаяТаблицаНастройки);
	ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТекущейТаблицы);
	Если СтрЗаканчиваетсяНа(ИмяТаблицыДанных, "СПартнерами") Тогда
		ВидРасчетов = СтрЗаменить(СтрЗаменить(ИмяТекущейТаблицы, "Общие", ""), "СчетаУчетаРасчетов", "Расчеты");
		ИмяПоляТребуетсяНастройка = "ТребуетсяНастройка" + ВидРасчетов;
	Иначе
		ИмяПоляТребуетсяНастройка = "ТребуетсяНастройка";
	КонецЕсли;
	
	МассивСчетов = СчетаУчетаТаблицы(ИмяТекущейТаблицы);
	
	ТребуетсяНастройка = Ложь;
	РедактированиеРазрешено = РедактированиеЗаполненныхСчетов;
	ВыделенныеСтроки = Элементы[ИмяТаблицыДанных].ВыделенныеСтроки;
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		СтрокаТаблицы = ЭтотОбъект[ИмяТаблицыДанных].НайтиПоИдентификатору(ИдентификаторСтроки);
		
		ТребуетсяНастройка = СтрокаТаблицы[ИмяПоляТребуетсяНастройка];
		
		Если Не РедактированиеРазрешено Тогда
			Для каждого Счет Из МассивСчетов Цикл
				Если Не ЗначениеЗаполнено(СтрокаТаблицы[Счет]) Тогда
					РедактированиеРазрешено = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
						
		Если ТребуетсяНастройка И РедактированиеРазрешено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ТребуетсяНастройка Тогда 
		ПоказатьПредупреждение(,НСтр("ru= 'Для выделенных строк все необходимые счета указаны.'")); 
		Возврат; 
	КонецЕсли;
	
	Если Не РедактированиеРазрешено Тогда
		ПоказатьПредупреждение(,НСтр("ru= 'Для выделенных строк нельзя изменить счета учета (нельзя изменять заполненные счета учета).'")); 
		Возврат; 
	КонецЕсли;
	
	СтруктураСчетов = Новый Структура;
	Для каждого Счет Из МассивСчетов Цикл
		ПараметрыВыбора = ПараметрыВыбора(ИмяТекущейТаблицы, Счет);
		Если Счет = "СчетУчета" ИЛИ Счет = "СчетЗабалансовогоУчета" Тогда
			ОкончаниеСчета = СтрЗаменить(ИмяТекущейТаблицы, "Общие", "");
			ОкончаниеСчета = СтрЗаменить(ОкончаниеСчета, "СчетаУчета", "");
			Счет = Счет + ОкончаниеСчета;
		ИначеЕсли СтрЗаканчиваетсяНа(ИмяТекущейТаблицы, "СчетаУчетаРасчетовСКомиссионерами") Тогда
			Счет = СтрЗаменить(Счет, "СКлиентами", "СКомиссионерамиТовары");
			Счет = СтрЗаменить(Счет, "Полученных", "СКомиссионерамиТовары");
			Счет = СтрЗаменить(Счет, "СПоставщиками", "СКомиссионерамиВознаграждение");
			Счет = СтрЗаменить(Счет, "Выданных", "СКомиссионерамиВознаграждение");
		ИначеЕсли СтрЗаканчиваетсяНа(ИмяТекущейТаблицы, "СчетаУчетаРасчетовСКомитентами") Тогда
			Счет = СтрЗаменить(Счет, "СПоставщиками", "СКомитентамиТовары");
			Счет = СтрЗаменить(Счет, "Выданных", "СКомитентамиТовары");
			Счет = СтрЗаменить(Счет, "СКлиентами", "СКомитентамиВознаграждение");
			Счет = СтрЗаменить(Счет, "Полученных", "СКомитентамиВознаграждение");
		КонецЕсли;
		СтруктураСчетов.Вставить(Счет, ПараметрыВыбора);
	КонецЦикла;
	СтруктураПараметры = ПараметрыОткрытияГрупповойНастройкиСчетов(СтруктураСчетов);
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("УказатьСчетаУчетаЗавершение", ЭтотОбъект, ИмяТекущейТаблицы);
	ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.ФормаГрупповойНастройкиСчетов", 
		СтруктураПараметры,,,,, ОповещениеОВыборе, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	ОповещениеРазблокированиеРеквизитовЗакрытие = Новый ОписаниеОповещения("РазрешитьРедактированиеРеквизитовЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("РедактированиеРазрешено", РедактированиеЗаполненныхСчетов);
	ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РазблокированиеРеквизитов",
		ПараметрыФормы, ЭтотОбъект, , , , ОповещениеРазблокированиеРеквизитовЗакрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
КонецПроцедуры

#Область ПроцедурыОбработчикиКомандОбновленияТаблицНастроек

&НаКлиенте
Процедура ОповеститьПользователяОПримененииНастройки(КоличествоДокументов)
	
	ТекстСообщения = "";
	
	Если КоличествоДокументов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройка применена для %1'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отработано %1'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
	Иначе
		ТекстСообщения = НСтр("ru = 'Отсутствуют документы, требующие применения настройки'");
		ТекстЗаголовка = НСтр("ru = 'Отсутствуют документы для настройки'");
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицу(Команда)
	ИмяТекущейТаблицы = СтрЗаменить(Элементы.Страницы.ТекущаяСтраница.Имя, "Страница", "");
	ЗаполнитьТаблицыНастройки(ИмяТекущейТаблицы);
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаПериод(Команда)
	ИмяТекущейТаблицы = СтрЗаменить(Элементы.Страницы.ТекущаяСтраница.Имя, "Страница", "");
	ПараметрыПериода = Новый Структура("НачалоПериода, КонецПериода", ДобавитьМесяц(ДатаОкончанияПериода.Дата, -1), ДатаОкончанияПериода.Дата);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборПериодаДобавленияЗавершение", ЭтотОбъект, ИмяТекущейТаблицы);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыПериода, ЭтотОбъект, , , , ОповещениеОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоНезаполненнымСчетам(Команда)
	
	Результат = ДобавитьЗаписиВТаблицуСервер("ПоНеотраженным");
	
	Если Результат.Статус = "Выполняется" Тогда
		ОповещениеФормаДлительнойОперацииЗакрыта = Новый ОписаниеОповещения("ВыполнениеФоновогоЗаданияЗавершено", ЭтотОбъект, Истина);
		ПараметрыФормы = ИнициализироватьПараметрыФормыДлительнойОперации(Результат);
		ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеФормаДлительнойОперацииЗакрыта, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиКомандОткрытияФормРасшифровокДанных

&НаКлиенте
Функция СтруктураОтбораДанных()
	
	СтрокаТаблицы = Элементы.СчетаУчетаНоменклатуры.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Склад, ГруппаФинансовогоУчета",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.Склад,
			СтрокаТаблицы.ГруппаФинансовогоУчета);
	Иначе
		СтруктураОтбор = Неопределено;
	КонецЕсли;
	
	Возврат СтруктураОтбор;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьПредупреждениеЕслиНеВыбранаСтрока()
	
	ПоказатьПредупреждение(, НСтр("ru= 'Выберите строку для расшифровки'"), 10);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоНоменклатуре(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаНоменклатуры.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("ГруппаФинансовогоУчета", СтрокаТаблицы.ГруппаФинансовогоУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Справочник.Номенклатура.ФормаСписка", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамПродажи(Команда)
	
	СтруктураОтбор = СтруктураОтбораДанных();
	Если СтруктураОтбор <> Неопределено Тогда
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамПродаж", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамПоступления(Команда)
	
	СтруктураОтбор = СтруктураОтбораДанных();
	Если СтруктураОтбор <> Неопределено Тогда
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамПоступления", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоСоглашениямСКлиентами(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаРасчетовСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("ГруппаФинансовогоУчета", СтрокаТаблицы.ГруппаФинансовогоУчета);
		СтруктураМенеджер = Новый Структура("Менеджер");
		СтруктураПараметры = Новый Структура("Отбор, СтруктураБыстрогоОтбора",
			СтруктураОтбор,
			СтруктураМенеджер);
		ОткрытьФорму("Справочник.СоглашенияСКлиентами.ФормаСписка", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоСоглашениямСПоставщиками(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаРасчетовСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("ГруппаФинансовогоУчета", СтрокаТаблицы.ГруппаФинансовогоУчета);
		СтруктураМенеджер = Новый Структура("Менеджер");
		СтруктураПараметры = Новый Структура("Отбор, СтруктураБыстрогоОтбора",
			СтруктураОтбор,
			СтруктураМенеджер);
		ОткрытьФорму("Справочник.СоглашенияСПоставщиками.ФормаСписка", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасчетовСКлиентами(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаРасчетовСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, ГруппаФинансовогоУчета", СтрокаТаблицы.Организация, СтрокаТаблицы.ГруппаФинансовогоУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамРасчетовСКлиентами", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасчетовСПоставщиками(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаРасчетовСПартнерами.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, ГруппаФинансовогоУчета", СтрокаТаблицы.Организация, СтрокаТаблицы.ГруппаФинансовогоУчета);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамРасчетовСПоставщиками", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамДоходы(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаДоходов.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Подразделение, СтатьяДоходов",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.Подразделение,
			СтрокаТаблицы.СтатьяДоходов);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамОтраженияДоходов", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасходы(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаРасходов.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, Подразделение, СтатьяРасходов",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.Подразделение,
			СтрокаТаблицы.СтатьяРасходов);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамОтраженияРасходов", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоДокументамРасчетыПоСертификатам(Команда)
	
	СтрокаТаблицы = Элементы.СчетаУчетаПодарочныхСертификатов.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		СтруктураОтбор = Новый Структура("Организация, ВидСертификата",
			СтрокаТаблицы.Организация,
			СтрокаТаблицы.ВидСертификата);
		СтруктураПараметры = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.РасшифровкаПоДокументамОтраженияСертификатов", СтруктураПараметры, ЭтаФорма);
	Иначе
		ПоказатьПредупреждениеЕслиНеВыбранаСтрока();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ПроцедурыОбработчикиКомандСозданияНовыхНастроекСчетовУчета

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаНоменклатуры(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаНоменклатуры");
	
КонецПроцедуры // СоздатьНастройкуСчетовУчетаНоменклатуры()

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаРасчетов(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаРасчетовСПартнерами");
	
КонецПроцедуры // СоздатьНастройкуСчетовУчетаРасчетов()

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаДоходов(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаДоходов");
	
КонецПроцедуры // СоздатьНастройкуСчетовУчетаДоходов()

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаРасходов(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаРасходов");
	
КонецПроцедуры // СоздатьНастройкуСчетовУчетаРасходов()

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаТМЦ(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаТМЦВЭксплуатации");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаСертификатов(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаПодарочныхСертификатов");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаПроизводства(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаПроизводства");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаНоменклатурыПереданной(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаНоменклатурыПереданной");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуСчетовУчетаРезервов(Команда)
	
	ОткрытьФормуСозданияНовойСтрокиИсключения("СчетаУчетаРезервов");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПервоначальноеЗаполнениеФормы

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область ОформлениеСчетов
	
	ТаблицыФормы = СоответствиеТаблицИПолейВидамСчетовРеглУчета();
	
	Для каждого ТаблицаФормы Из ТаблицыФормы Цикл
		
		ИмяТаблицы = ТаблицаФормы.Ключ;
		ИмяТаблицыДляОтбора = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы);
		
		Для каждого СоответствиеСчетаУчетаВидуСчета Из ТаблицаФормы.Значение Цикл
			
			СчетУчета = СоответствиеСчетаУчетаВидуСчета.Ключ;
			
			Если ЕстьОбщиеНастройки(ИмяТаблицы) Тогда
				// Оформление счетов по умолчанию:
				Элемент = УсловноеОформление.Элементы.Добавить();
				
				КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяТаблицы + СчетУчета].Имя);
				
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, ИмяТаблицыДляОтбора + "." + СчетУчета + "ПоУмолчанию", Истина);
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
					ИмяТаблицыДляОтбора + "." + СчетУчета, Неопределено, ВидСравненияКомпоновкиДанных.Заполнено);
				
				Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстИнформационнойНадписи);
			КонецЕсли;
			
			// Оформление счетов требующих заполнения:
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяТаблицы + СчетУчета].Имя);
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
				ИмяТаблицыДляОтбора + "." + "ТребуетсяНастройка" + ?(ТаблицаФормы.Значение.Количество() = 1, "", СчетУчета), Истина);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
				ИмяТаблицыДляОтбора + "." + СчетУчета, Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
			
			Если ЕстьОбщиеНастройки(ИмяТаблицы)	И СчетаУчетаТаблицы("Общие"+ИмяТаблицы).Найти(СчетУчета) <> Неопределено Тогда
				// Оформление счетов требующих заполнения:
				Элемент = УсловноеОформление.Элементы.Добавить();
				
				КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["Общие" + ИмяТаблицы + СчетУчета].Имя);
				
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "Общие" + ИмяТаблицыДляОтбора + "." + "ТребуетсяНастройка"
					+ ?(ТаблицаФормы.Значение.Количество() = 1, "", СчетУчета), Истина);
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
					"Общие" + ИмяТаблицыДляОтбора + "." + СчетУчета, Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
				
				Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
			КонецЕсли;
			
			// Оформление счетов не требующих заполнения:
			Элемент = УсловноеОформление.Элементы.Добавить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы[ИмяТаблицы + СчетУчета].Имя);
			
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
				ИмяТаблицыДляОтбора + "." + "ТребуетсяНастройка" + ?(ТаблицаФормы.Значение.Количество() = 1, "", СчетУчета), Ложь);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
				ИмяТаблицыДляОтбора + "." + СчетУчета, Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
				ИмяТаблицыДляОтбора + ".ИзмененныеДанные", СчетУчета, ВидСравненияКомпоновкиДанных.НеСодержит);
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
			
			Если ЕстьОбщиеНастройки(ИмяТаблицы)	И СчетаУчетаТаблицы("Общие"+ИмяТаблицы).Найти(СчетУчета) <> Неопределено Тогда
				Элемент = УсловноеОформление.Элементы.Добавить();
				
				КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["Общие" + ИмяТаблицы + СчетУчета].Имя);
				
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "Общие" + ИмяТаблицыДляОтбора + "." + "ТребуетсяНастройка"
					+ ?(ТаблицаФормы.Значение.Количество() = 1, "", СчетУчета), Ложь);
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
					"Общие" + ИмяТаблицыДляОтбора + "." + СчетУчета, Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор,
					"Общие" + ИмяТаблицыДляОтбора + ".ИзмененныеДанные", СчетУчета, ВидСравненияКомпоновкиДанных.НеСодержит);
				
				Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
				Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
			КонецЕсли;
			
		КонецЦикла;
						
	КонецЦикла;
	
	#КонецОбласти
		
	#Область Номенклатура
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыГруппаФинансовогоУчета.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.ГруппаФинансовогоУчета", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаНоменклатурыГруппаФинансовогоУчета.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.АналитикаУчета", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСклад.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.Склад", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
							
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродаж", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажПоУмолчанию", Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродаж", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажПоУмолчанию", Ложь);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СтатьяДоходовОСНО", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродаж", ПланыСчетов.Хозрасчетный.ПрочиеДоходы, ВидСравненияКомпоновкиДанных.НеРавно);
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаНоменклатурыСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродаж", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СтатьяДоходовОСНО", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаНоменклатурыСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродаж", ПланыСчетов.Хозрасчетный.ПрочиеДоходы, ВидСравненияКомпоновкиДанных.НеРавно);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СтатьяДоходовОСНО", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВД", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВДПоУмолчанию", Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВД", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВДПоУмолчанию", Ложь);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СтатьяДоходовЕНВД", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВД", ПланыСчетов.Хозрасчетный.ПрочиеДоходы, ВидСравненияКомпоновкиДанных.НеРавно);
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВД", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СтатьяДоходовЕНВД", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СчетУчетаВыручкиОтПродажЕНВД", ПланыСчетов.Хозрасчетный.ПрочиеДоходы, ВидСравненияКомпоновкиДанных.НеРавно);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатуры.СтатьяДоходовЕНВД", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	#КонецОбласти
		
	#Область НоменклатураПереданнаяНаКомиссию
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыПереданнойГруппаФинансовогоУчета.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.ГруппаФинансовогоУчета",
		Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаНоменклатурыПереданнойГруппаФинансовогоУчета.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаНоменклатурыПереданной.АналитикаУчета",
		Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
		
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыПереданнойСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.СчетУчетаВыручкиОтПродаж",
		ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.СчетУчетаВыручкиОтПродажПоУмолчанию", Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыПереданнойСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.СчетУчетаВыручкиОтПродаж",
		ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.СчетУчетаВыручкиОтПродажПоУмолчанию", Ложь);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.СтатьяДоходовОСНО",
		Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаНоменклатурыПереданнойСтатьяДоходовОСНО.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаНоменклатурыПереданной.СчетУчетаВыручкиОтПродаж",
		ПланыСчетов.Хозрасчетный.ПрочиеДоходы, ВидСравненияКомпоновкиДанных.НеРавно);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	#КонецОбласти
	
	#Область РасчетыСПартнерами
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаРасчетовСПартнерамиГруппаФинансовогоУчета.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаРасчетовСПартнерами.ГруппаФинансовогоУчета", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаРасчетовСПартнерамиГруппаФинансовогоУчета.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаРасчетовСПартнерами.АналитикаУчета", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
		
	#КонецОбласти
	
	#Область ТМЦВЭксплуатации
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаТМЦВЭксплуатацииКатегорияЭксплуатации.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаТМЦВЭксплуатации.КатегорияЭксплуатации", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.ОбщиеСчетаУчетаТМЦВЭксплуатацииКатегорияЭксплуатации.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "ОбщиеСчетаУчетаТМЦВЭксплуатации.АналитикаУчета", Неопределено, ВидСравненияКомпоновкиДанных.НеЗаполнено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указана'"));
		
	#КонецОбласти
	
	#Область ВнеоборотныеАктивы
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы.СчетаУчетаВнеоборотныхАктивовСчетУчетаВыбытия.Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "СчетаУчетаВнеоборотныхАктивов.ВидАктива", Перечисления.ВидыВнеоборотныхАктивов.НМА);
		
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы(МассивВидовСчетовРеглУчета, МассивСкрываемыхЭлементов)
	
	#Область ЗаполнениеСпискаКомандИзменяющихДанные
	
	МассивКоманд = Новый Массив;
	МассивКоманд.Добавить("СоздатьНастройкуСчетовУчета");
	МассивКоманд.Добавить("Удалить");
	МассивКоманд.Добавить("ГруппаДобавить");
	МассивКоманд.Добавить("УказатьСчетаУчета");
	
	МассивОбщихКоманд = Новый Массив;
	МассивОбщихКоманд.Добавить("УказатьСчетаУчета");
	
	#КонецОбласти
	
	#Область ЗаполнениеСпискаВыбораСтраниц
	
	МассивТаблицДляУдаления = Новый Массив;
	Для каждого Таблица Из Элементы.ТекущаяТаблицаНастройки.СписокВыбора Цикл;
		
		Если Не (СтруктураТаблицФормы[Таблица.Значение].Используется
			ИЛИ СтруктураТаблицФормы.Свойство("Общие"+Таблица.Значение)
			И СтруктураТаблицФормы["Общие"+Таблица.Значение].Используется) Тогда
				МассивТаблицДляУдаления.Добавить(Таблица);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ТаблицаДляУдаления Из МассивТаблицДляУдаления Цикл
		Элементы.ТекущаяТаблицаНастройки.СписокВыбора.Удалить(ТаблицаДляУдаления);
	КонецЦикла;
	
	// Если показывается только одна страница - скрываем поле переключателя страниц:
	Элементы.ГруппаУправлениеОткрытиемСтраниц.Видимость = Элементы.ТекущаяТаблицаНастройки.СписокВыбора.Количество() <> 1;
	
	#КонецОбласти
	
	Если МассивВидовСчетовРеглУчета <> Неопределено Тогда
		ТаблицыФормы = СоответствиеТаблицИПолейВидамСчетовРеглУчета();
	КонецЕсли;
	
	// Обнулим видимость и просмотр страниц учета с партнерами, чтобы включить если одна из таблиц будет использоваться:
	Элементы.СтраницаОбщиеСчетаУчетаРасчетовСПартнерами.Видимость = Ложь;
	Элементы.СтраницаОбщиеСчетаУчетаРасчетовСПартнерами.ТолькоПросмотр = Истина;
	Элементы.СтраницаСчетаУчетаРасчетовСПартнерами.Видимость = Ложь;
	Элементы.СтраницаСчетаУчетаРасчетовСПартнерами.ТолькоПросмотр = Истина;
	
	Для каждого Таблица Из СтруктураТаблицФормы Цикл
		
		// Управление свойствами соответствующих страниц:
		Если СтрНайти(Таблица.Ключ, "СчетаУчетаРасчетов") Тогда
			ИмяСтраницы = "Страница"+ИмяТаблицыДанныхПоИмениТекущейТаблицы(Таблица.Ключ);
			Элементы[ИмяСтраницы].Видимость = Таблица.Значение.Используется ИЛИ Элементы[ИмяСтраницы].Видимость;
			Элементы[ИмяСтраницы].ТолькоПросмотр = Не Таблица.Значение.Изменяется И Элементы[ИмяСтраницы].ТолькоПросмотр;
		Иначе
			ИмяСтраницы = "Страница"+Таблица.Ключ;
			Элементы[ИмяСтраницы].Видимость = Таблица.Значение.Используется;
			Элементы[ИмяСтраницы].ТолькоПросмотр = Не Таблица.Значение.Изменяется;
		КонецЕсли;
		
		Если Элементы[ИмяСтраницы].ТолькоПросмотр Тогда
			ОбщиеКоманды = СтрНачинаетсяС(Таблица.Ключ, "Общие") ИЛИ Таблица.Ключ = "СчетаУчетаДенежныхСредств"
				ИЛИ Таблица.Ключ = "СчетаУчетаПроизводства" ИЛИ Таблица.Ключ = "СчетаУчетаВнеоборотныхАктивов";
			Для каждого Команда Из ?(ОбщиеКоманды, МассивОбщихКоманд, МассивКоманд) Цикл
				ИмяЭлемента = СтрЗаменить(ИмяСтраницы, "Страница", "") + Команда;
				Элементы[ИмяЭлемента].Доступность = Ложь;
			КонецЦикла;
		КонецЕсли;
		
		// Скрытие элементов страниц в зависимости от отбора видов счетов:
		Если Таблица.Значение.Используется И МассивВидовСчетовРеглУчета <> Неопределено Тогда
			СоответствиеТекущейТаблицы = ТаблицыФормы.Получить(СтрЗаменить(Таблица.Ключ, "Общие", ""));
			Для каждого ПолеТаблицы Из СоответствиеТекущейТаблицы Цикл
				Если СчетаУчетаТаблицы(Таблица.Ключ).Найти(ПолеТаблицы.Ключ) = Неопределено Тогда
					// Для таблица общих счетов учета номенклатуры на комиссии есть только счет на комиссии, все остальные счета
					// настраиваются в "ОбщиеСчетаУчетаНоменклатуры".
					Продолжить;
				КонецЕсли;
				Элементы[Таблица.Ключ+ПолеТаблицы.Ключ].Видимость = МассивВидовСчетовРеглУчета.Найти(ПолеТаблицы.Значение) <> Неопределено;
			КонецЦикла;
		КонецЕсли;
			
	КонецЦикла;
	
	Если МассивСкрываемыхЭлементов <> Неопределено Тогда
		Для каждого ИмяСкрываемогоЭлемента Из МассивСкрываемыхЭлементов Цикл
			СкрываемыйЭлемент = Элементы.Найти(ИмяСкрываемогоЭлемента);
			Если Не СкрываемыйЭлемент = Неопределено Тогда
				СкрываемыйЭлемент.Видимость = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьСвойстваЭлементовПоОрганизацииИФункциональнымОпциям();
	УстановитьВидимостьПанелейУправления(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваСтруктурыТаблицФормы(ВидыСчетовРеглУчета)
		
	#Область СоответствиеТаблицИИспользуемыхФункциональныхОпций
	
	СоответствиеТаблицИИспользуемыхФункциональныхОпций = Новый Соответствие;
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаПодарочныхСертификатов", ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаРезервов", ПолучитьФункциональнуюОпцию("ФормироватьРезервыПредстоящихРасходов"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаВнеоборотныхАктивов", ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаРасчетовСКомиссионерами",
		ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаРасчетовСКомитентами",
		ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаРасчетовСКредиторами", ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаРасчетовСДебиторами", ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов"));
	СоответствиеТаблицИИспользуемыхФункциональныхОпций.Вставить("СчетаУчетаРасчетовСЛизингодателями", ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг"));
	
	#КонецОбласти
	
	#Область СоответствиеТаблицИИспользуемыхМетаданных
	
	СоответствиеТаблицИИспользуемыхМетаданных = Обработки.НастройкаОтраженияДокументовВРеглУчете.СоответствиеТаблицИИспользуемыхМетаданных();
	
	#КонецОбласти
	
	Если ВидыСчетовРеглУчета <> Неопределено Тогда
		ТаблицыФормы = СоответствиеТаблицИПолейВидамСчетовРеглУчета();
	КонецЕсли;
	
	Для каждого Таблица Из СтруктураТаблицФормы Цикл
		
		ИмяТаблицы = СтрЗаменить(Таблица.Ключ, "Общие", "");
		
		// Проверка функциональных опций:
		ЗначениеИспользуемойФункциональнойОпции = СоответствиеТаблицИИспользуемыхФункциональныхОпций.Получить(ИмяТаблицы);
		
		Если ЗначениеИспользуемойФункциональнойОпции = Ложь Тогда
			Таблица.Значение.Используется = Ложь;
			Таблица.Значение.Изменяется = Ложь;
			Продолжить;
		КонецЕсли; 
		
		// Если заданы используемые виды счетов, проверяем использует ли таблица указанные в параметрах счета:
		Если ВидыСчетовРеглУчета <> Неопределено Тогда
			
			ТаблицаИспользуется = Ложь;
			СоответствиеСчетовТаблицыФормы = ТаблицыФормы.Получить(ИмяТаблицы);
			
			Для каждого СоответствиеСчетаВидуСчета Из СоответствиеСчетовТаблицыФормы Цикл
				Если ВидыСчетовРеглУчета.Найти(СоответствиеСчетаВидуСчета.Значение) <> Неопределено Тогда
					ТаблицаИспользуется = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Не ТаблицаИспользуется Тогда
				Таблица.Значение.Используется = Ложь;
				Таблица.Значение.Изменяется = Ложь;
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		// Проверка прав:
		ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(Таблица.Ключ);
		МассивИспользуемыхМетаданных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СоответствиеТаблицИИспользуемыхМетаданных.Получить(ИмяТаблицы), ",");
		
		ПравоЧтения = Истина;
		ПравоИзменения = Истина;
		
		Для каждого МетаданныеСтрокой Из МассивИспользуемыхМетаданных Цикл
			ПравоЧтения = ПравоЧтения И ПравоДоступа("Чтение", Метаданные.НайтиПоПолномуИмени(МетаданныеСтрокой));
			ПравоИзменения = ПравоИзменения И ПравоДоступа("Изменение", Метаданные.НайтиПоПолномуИмени(МетаданныеСтрокой));
		КонецЦикла;
		
		Таблица.Значение.Используется = ПравоЧтения;
		Таблица.Значение.Изменяется = ПравоИзменения;			
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоответствиеТаблицИПолейВидамСчетовРеглУчета()
	
	ТаблицыФормы = Новый Соответствие;
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаНаСкладе", Перечисления.ВидыСчетовРеглУчета.НаСкладе);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаВыручкиОтПродаж", Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродаж);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаСебестоимостиПродаж", Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродаж);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаВыручкиОтПродажЕНВД", Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаСебестоимостиПродажЕНВД", Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродажЕНВД);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаНДСПриПродаже", Перечисления.ВидыСчетовРеглУчета.НДСПриПродаже);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаВПути", Перечисления.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаНДСВПути", Перечисления.ВидыСчетовРеглУчета.НДСПриОтгрузкеБезПереходаПраваСобственности);
	ТаблицыФормы.Вставить("СчетаУчетаНоменклатуры", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаПередачиНаКомиссию", Перечисления.ВидыСчетовРеглУчета.ПередачаНаКомиссию);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаПередачиВПереработку", Перечисления.ВидыСчетовРеглУчета.ПередачаВПереработку);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаВыручкиОтПродаж", Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродажКомиссионера);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаСебестоимостиПродаж", Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродажКомиссионера);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаНДСПриПродаже", Перечисления.ВидыСчетовРеглУчета.НДСПриПродажеКомиссионера);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаЗатратыНаПриобретение", Перечисления.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ);
	ТаблицыФормы.Вставить("СчетаУчетаНоменклатурыПереданной", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.ТМЦВЭксплуатации);
	СоответствиеПолейВидамСчетов.Вставить("СчетЗабалансовогоУчета", Перечисления.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом);
	ТаблицыФормы.Вставить("СчетаУчетаТМЦВЭксплуатации", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.ДенежныеСредства);
	ТаблицыФормы.Вставить("СчетаУчетаДенежныхСредств", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.РасчетыПоПодарочнымСертификатам);
	ТаблицыФормы.Вставить("СчетаУчетаПодарочныхСертификатов", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКлиентами", Перечисления.ВидыСчетовРеглУчета.РасчетыСКлиентами);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАвансовПолученных", Перечисления.ВидыСчетовРеглУчета.АвансыПолученные);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКлиентамиТара", Перечисления.ВидыСчетовРеглУчета.РасчетыСКлиентамиТара);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСКлиентами", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСПоставщиками", Перечисления.ВидыСчетовРеглУчета.РасчетыСПоставщиками);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАвансовВыданных", Перечисления.ВидыСчетовРеглУчета.АвансыВыданные);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовПоПретензиям", Перечисления.ВидыСчетовРеглУчета.РасчетыПоПретензиям);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСПоставщикамиТара", Перечисления.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаНеотфактурованныеПоставки", Перечисления.ВидыСчетовРеглУчета.НеотфактурованныеПоставки);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСПоставщиками", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКлиентами", Перечисления.ВидыСчетовРеглУчета.РасчетыСКлиентами);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАвансовПолученных", Перечисления.ВидыСчетовРеглУчета.АвансыПолученные);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСПоставщиками", Перечисления.ВидыСчетовРеглУчета.РасчетыСПоставщиками);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАвансовВыданных", Перечисления.ВидыСчетовРеглУчета.АвансыВыданные);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСКомиссионерами", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСПоставщиками", Перечисления.ВидыСчетовРеглУчета.РасчетыСПоставщиками);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАвансовВыданных", Перечисления.ВидыСчетовРеглУчета.АвансыВыданные);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКлиентами", Перечисления.ВидыСчетовРеглУчета.РасчетыСКлиентами);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАвансовПолученных", Перечисления.ВидыСчетовРеглУчета.АвансыПолученные);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСКомитентами", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКредиторамиОсновнойДолг", Перечисления.ВидыСчетовРеглУчета.РасчетыСКредиторамиОсновнойДолг);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКредиторамиПроценты", Перечисления.ВидыСчетовРеглУчета.РасчетыСКредиторамиПроценты);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСКредиторамиКомиссия", Перечисления.ВидыСчетовРеглУчета.РасчетыСКредиторамиКомиссия);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСКредиторами", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСДебиторамиОсновнойДолг", Перечисления.ВидыСчетовРеглУчета.РасчетыСДебиторамиОсновнойДолг);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСДебиторамиПроценты", Перечисления.ВидыСчетовРеглУчета.РасчетыСДебиторамиПроценты);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСДебиторамиКомиссия", Перечисления.ВидыСчетовРеглУчета.РасчетыСДебиторамиКомиссия);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСДебиторами", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСЛизингодателемЛизинговыеУслуги", Перечисления.ВидыСчетовРеглУчета.ЛизинговыеУслуги);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСЛизингодателемОбеспечительныйПлатеж", Перечисления.ВидыСчетовРеглУчета.ОбеспечительныйПлатежПоЛизингу);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСЛизингодателемВыкупПредметаЛизинга", Перечисления.ВидыСчетовРеглУчета.ВыкупПредметаЛизинга);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРасчетовСЛизингодателемАрендныеОбязательства", Перечисления.ВидыСчетовРеглУчета.АрендныеОбязательстваПоЛизингу);
	ТаблицыФормы.Вставить("СчетаУчетаРасчетовСЛизингодателями", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.Производство);
	ТаблицыФормы.Вставить("СчетаУчетаПроизводства", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.СтоимостьВНА);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаЦФ", Перечисления.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАмортизации", Перечисления.ВидыСчетовРеглУчета.АмортизацияВНА);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаАмортизацииЦФ", Перечисления.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаВыбытия", Перечисления.ВидыСчетовРеглУчета.ВыбытиеВНА);
	СоответствиеПолейВидамСчетов.Вставить("СчетУчетаРезерваДооценки", Перечисления.ВидыСчетовРеглУчета.РезервДооценкиВНА);
	СоответствиеПолейВидамСчетов.Вставить("СчетЗабалансовогоУчета", Перечисления.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА);
	ТаблицыФормы.Вставить("СчетаУчетаВнеоборотныхАктивов", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.Расходы);
	СоответствиеПолейВидамСчетов.Вставить("СчетСписанияОСНО", Перечисления.ВидыСчетовРеглУчета.СписаниеРасходовОСНО);
	СоответствиеПолейВидамСчетов.Вставить("СчетСписанияЕНВД", Перечисления.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД);
	ТаблицыФормы.Вставить("СчетаУчетаРасходов", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.Доходы);
	ТаблицыФормы.Вставить("СчетаУчетаДоходов", СоответствиеПолейВидамСчетов);
	
	СоответствиеПолейВидамСчетов = Новый Соответствие;
	СоответствиеПолейВидамСчетов.Вставить("СчетУчета", Перечисления.ВидыСчетовРеглУчета.Резервы);
	ТаблицыФормы.Вставить("СчетаУчетаРезервов", СоответствиеПолейВидамСчетов);
	
	Возврат ТаблицыФормы;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораОрганизации(Ограничение)
	
	СписокВыбора = Элементы.Организация.СписокВыбора;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеСправочника.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(&ВсеОрганизации ИЛИ ДанныеСправочника.Ссылка В (&СписокОрганизаций))
	|	И ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|";
	
	Запрос.УстановитьПараметр("ВсеОрганизации",    НЕ ЗначениеЗаполнено(Ограничение));
	Запрос.УстановитьПараметр("СписокОрганизаций", Ограничение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураТаблицыПоЗаголовку(Заголовок)
	СтруктураТаблицыПоЗаголовку = Новый Структура;
	СтруктураТаблицыПоЗаголовку.Вставить("Представление", Заголовок);
	СтруктураТаблицыПоЗаголовку.Вставить("Используется", Ложь);
	СтруктураТаблицыПоЗаголовку.Вставить("Изменяется", Ложь);
	СтруктураТаблицыПоЗаголовку.Вставить("ТребуютНастройки", 0);
	Возврат СтруктураТаблицыПоЗаголовку;
КонецФункции

&НаСервере
Процедура СоздатьСтруктуруТаблицФормы()
	
	СтруктураТаблиц = Новый Структура;
	
	Для каждого Таблица Из Элементы.ТекущаяТаблицаНастройки.СписокВыбора Цикл
		СтруктураТаблиц.Вставить(Таблица.Значение, СтруктураТаблицыПоЗаголовку(Таблица.Представление));
		Если ЕстьОбщиеНастройки(Таблица.Значение) Тогда
			СтруктураТаблиц.Вставить("Общие" + Таблица.Значение, СтруктураТаблицыПоЗаголовку(Таблица.Представление));
		КонецЕсли;
	КонецЦикла;

	СтруктураТаблицФормы = Новый ФиксированнаяСтруктура(СтруктураТаблиц);

КонецПроцедуры

#КонецОбласти

#Область УправлениеФормой

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПанелейУправления(Форма)
	
	Форма.Элементы.ГруппаПанельРазвернута.Видимость = НЕ Форма.ПанельУправленияСвернута;
	Форма.Элементы.ГруппаПанельСвернута.Видимость = Форма.ПанельУправленияСвернута;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовПоОрганизацииИФункциональнымОпциям()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне") Тогда
		Элементы.СчетаУчетаПроизводстваПодразделение.Заголовок = НСтр("ru='Подразделение / Переработчик'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФО = Новый Структура;
		ПараметрыФО.Вставить("Организация", Организация);
		ПлательщикЕНВД = ПолучитьФункциональнуюОпцию("ПлательщикЕНВД", ПараметрыФО);
	Иначе
		ПлательщикЕНВД = ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД");
	КонецЕсли;
	
	Элементы.СчетаУчетаРасходовСчетСписанияЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.СчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродажЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.СчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродажЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.СчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаРасходовСчетСписанияЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродажЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродажЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаНоменклатурыСтатьяДоходовЕНВД.Видимость = ПлательщикЕНВД;
	Элементы.СчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродаж.ОтображатьВШапке = ПлательщикЕНВД;
	Элементы.СчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродаж.ОтображатьВШапке = ПлательщикЕНВД;
	Элементы.СчетаУчетаНоменклатурыСтатьяДоходовОСНО.ОтображатьВШапке = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродаж.ОтображатьВШапке = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродаж.ОтображатьВШапке = ПлательщикЕНВД;
	Элементы.ОбщиеСчетаУчетаНоменклатурыСтатьяДоходовОСНО.ОтображатьВШапке = ПлательщикЕНВД;
	
	Если ПлательщикЕНВД Тогда
		Элементы.СчетаУчетаРасходовСчетСписанияОСНО.Заголовок = НСтр("ru='Счет списания (ОСНО)'");
		Элементы.ОбщиеСчетаУчетаРасходовСчетСписанияОСНО.Заголовок = НСтр("ru='Счет списания (ОСНО)'");
	Иначе
		Элементы.СчетаУчетаРасходовСчетСписанияОСНО.Заголовок = НСтр("ru='Счет списания'");
		Элементы.ОбщиеСчетаУчетаРасходовСчетСписанияОСНО.Заголовок = НСтр("ru='Счет списания'");
	КонецЕсли;
	
	Элементы.СчетаУчетаДоходовДекорация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов");
	Элементы.СчетаУчетаРасходовДекорация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов");
	Элементы.СчетаУчетаТМЦВЭксплуатацииДекорация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьВнутреннееПотребление");
	Элементы.СчетаУчетаРасчетовСПартнерамиДекорация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьГруппыФинансовогоУчета");
	Элементы.СчетаУчетаНоменклатурыДекорация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьГруппыФинансовогоУчета");
	Элементы.СчетаУчетаНоменклатурыПереданнойДекорация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьГруппыФинансовогоУчета");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиСохраненияНастроекФормы(Форма)
	
	Форма.Элементы.СохранитьНастройкуИЗакрыть.Доступность = Форма.Модифицированность;
	Форма.Элементы.СохранитьНастройку.Доступность = Форма.Модифицированность;
	Форма.Элементы.ПрименитьНастройку.Доступность = Форма.Модифицированность;
	
КонецПроцедуры // УправлениеЭлементамиСохраненияНастроекФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Форма)
	
	// Определим таблицу на основании настроек и данных о доступности таблиц:
	ОбщаяТаблица = Неопределено;
	ТаблицаИсключений = Неопределено;
	ОбщаяТаблицаИспользуется = Форма.СтруктураТаблицФормы.Свойство("Общие"+Форма.ТекущаяТаблицаНастройки, ОбщаяТаблица) И ОбщаяТаблица.Используется;
	ТаблицаИсключенийИспользуется = Форма.СтруктураТаблицФормы.Свойство(Форма.ТекущаяТаблицаНастройки, ТаблицаИсключений) И ТаблицаИсключений.Используется;	
	
	ЭтоОбщаяТаблица = Форма.ПоказыватьОбщуюНастройку И ОбщаяТаблицаИспользуется ИЛИ Не ТаблицаИсключенийИспользуется;
	ИмяТекущейТаблицы = ?(ЭтоОбщаяТаблица, "Общие", "") + Форма.ТекущаяТаблицаНастройки;
	
	СтруктураОтбора = СтруктураОтбора(ИмяТекущейТаблицы, Форма.ПоказыватьТолькоТребующиеНастройки, Форма.Организация);
	
	Если СтрНайти(Форма.ТекущаяТаблицаНастройки, "СчетаУчетаРасчетов") <> 0 Тогда
		
		МассивСкрываемыхЭлементов = Новый Массив;
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСКлиентами");
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСПоставщиками");
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСКомиссионерами");
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСКомитентами");
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСКредиторами");
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСДебиторами");
		МассивСкрываемыхЭлементов.Добавить("СчетаУчетаРасчетовСЛизингодателями");
		
		Для каждого СкрываемыйЭлемент Из МассивСкрываемыхЭлементов Цикл
			Форма.Элементы[СкрываемыйЭлемент].Видимость = (СкрываемыйЭлемент = ИмяТекущейТаблицы);
			Форма.Элементы["Общие" + СкрываемыйЭлемент].Видимость = ("Общие" + СкрываемыйЭлемент = ИмяТекущейТаблицы);
		КонецЦикла;
		
		ПредставлениеТаблицы = Форма.СтруктураТаблицФормы[ИмяТекущейТаблицы].Представление;
		ИмяТекущейТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТекущейТаблицы);
		
		ИмяЭлементаЗаголовка = "ГруппаЗаголовок" + ИмяТекущейТаблицы;
		Форма.Элементы[ИмяЭлементаЗаголовка].Заголовок = НСтр("ru = 'Счета учета'") + " " + НРег(ПредставлениеТаблицы);
		
	КонецЕсли;
	
	ОтборСтрок = ?(СтруктураОтбора.Количество(), Новый ФиксированнаяСтруктура(СтруктураОтбора), Неопределено);
	
	Если Форма.Элементы.Страницы.ТекущаяСтраница.Имя <> "Страница"+ИмяТекущейТаблицы Тогда
		Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы["Страница"+ИмяТекущейТаблицы];
	КонецЕсли;
	Форма.Элементы[ИмяТекущейТаблицы].ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставлениеСпискаВыбораСтраниц(Форма)
	
	ЕстьНеНастроенные = Ложь;
	
	Для каждого Таблица Из Форма.Элементы.ТекущаяТаблицаНастройки.СписокВыбора Цикл
		
		Данные = Неопределено;
		ОбщаяТаблицаПоказывается = Форма.СтруктураТаблицФормы.Свойство("Общие"+Таблица.Значение, Данные) И Данные.Используется;
		ИмяТекущейТаблицы = ?(Форма.ПоказыватьОбщуюНастройку И ОбщаяТаблицаПоказывается, "Общие", "") + Таблица.Значение;
		
		СтруктураТаблицыФормы = Форма.СтруктураТаблицФормы[ИмяТекущейТаблицы];
		
		Таблица.Представление = ПредставлениеТаблицы(СтруктураТаблицыФормы);
		
		ЕстьНеНастроенные = ЕстьНеНастроенные ИЛИ СтруктураТаблицыФормы.ТребуютНастройки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЧислоТребующихсяНастроек(ИмяТаблицы, ЧислоИзменения)
	
	Если ЧислоИзменения <> 0 Тогда
		СтруктураТаблицы = Неопределено;
		СтруктураТаблицФормы.Свойство(ИмяТаблицы, СтруктураТаблицы);
		Если СтруктураТаблицы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		СтруктураТаблицы.ТребуютНастройки = СтруктураТаблицы.ТребуютНастройки + ЧислоИзменения;
		ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеТаблицы(СтруктураТаблицы)
	
	ТекстПредставления = СтруктураТаблицы.Представление;
	Если СтруктураТаблицы.ТребуютНастройки <> 0 Тогда
		ТекстПредставления = ТекстПредставления + " (" + СтруктураТаблицы.ТребуютНастройки + ")";
	КонецЕсли;
	
	Возврат ТекстПредставления;
		
КонецФункции

#КонецОбласти

#Область ЗаполнениеДоступныхСчетов

&НаСервере
Процедура УстановитьДоступныеСчетаУчета()
	
	#Область Номенклатура
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаНоменклатуры();
	
	// Счета учета товаров на складе.
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаНаСкладе)));
	Элементы.СчетаУчетаНоменклатурыСчетУчетаНаСкладе.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаНаСкладе.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета товаров в пути и передачи на комиссию.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаПередачиНаКомиссию)));
	Элементы.СчетаУчетаНоменклатурыСчетУчетаВПути.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаНоменклатурыПереданнойСчетУчетаПередачиНаКомиссию.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаВПути.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыПереданнойСчетУчетаПередачиНаКомиссию.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета передачи в переработку.
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаПередачиВПереработку)));
	Элементы.СчетаУчетаНоменклатурыПереданнойСчетУчетаПередачиВПереработку.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыПереданнойСчетУчетаПередачиВПереработку.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета выручки от продаж.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаВыручкиОтПродаж)));
	Элементы.СчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродаж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаНоменклатурыПереданнойСчетУчетаВыручкиОтПродаж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродаж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродажЕНВД.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаВыручкиОтПродажЕНВД.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета себестоимости от продаж.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаСебестоимостиПродаж)));
	Элементы.СчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродаж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаНоменклатурыПереданнойСчетУчетаСебестоимостиПродаж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродаж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродажЕНВД.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаСебестоимостиПродажЕНВД.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета НДС при продаже.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаНДСПриПродаже)));
	Элементы.СчетаУчетаНоменклатурыСчетУчетаНДСПриПродаже.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаНоменклатурыПереданнойСчетУчетаНДСПриПродаже.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаНДСПриПродаже.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета НДС при отгрузке без перехода права собственности
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаНДСВПути)));
	Элементы.СчетаУчетаНоменклатурыСчетУчетаНДСВПути.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыСчетУчетаНДСВПути.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета затрат на приобретение
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчетаЗатратыНаПриобретение)));
	Элементы.СчетаУчетаНоменклатурыПереданнойСчетУчетаЗатратыНаПриобретение.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаНоменклатурыПереданнойСчетУчетаЗатратыНаПриобретение.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	#КонецОбласти
	
	#Область РасчетыСПартнерами
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаРасчетов();
	
	// Счета учета расчетов с поставщиками.
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСПоставщиками)));
	Элементы.СчетаУчетаРасчетовСПоставщикамиСчетУчетаРасчетовСПоставщиками.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСПоставщикамиСчетУчетаРасчетовСПоставщикамиТара.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСПоставщикамиСчетУчетаРасчетовСПоставщиками.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСПоставщикамиСчетУчетаРасчетовСПоставщикамиТара.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомиссионерамиСчетУчетаРасчетовСПоставщиками.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомитентамиСчетУчетаРасчетовСПоставщиками.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомиссионерамиСчетУчетаРасчетовСПоставщиками.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомитентамиСчетУчетаРасчетовСПоставщиками.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСПоставщикамиСчетУчетаНеотфактурованныеПоставки.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСПоставщикамиСчетУчетаНеотфактурованныеПоставки.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов по авансам выданным.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовПоАвансаВыданным)));
	Элементы.СчетаУчетаРасчетовСПоставщикамиСчетУчетаАвансовВыданных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСПоставщикамиСчетУчетаРасчетовПоПретензиям.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСПоставщикамиСчетУчетаАвансовВыданных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСПоставщикамиСчетУчетаРасчетовПоПретензиям.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомиссионерамиСчетУчетаАвансовВыданных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомитентамиСчетУчетаАвансовВыданных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомиссионерамиСчетУчетаАвансовВыданных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомитентамиСчетУчетаАвансовВыданных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с клиентами.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСКлиентами)));
	Элементы.СчетаУчетаРасчетовСКлиентамиСчетУчетаРасчетовСКлиентами.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКлиентамиСчетУчетаРасчетовСКлиентамиТара.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКлиентамиСчетУчетаРасчетовСКлиентами.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКлиентамиСчетУчетаРасчетовСКлиентамиТара.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомиссионерамиСчетУчетаРасчетовСКлиентами.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомитентамиСчетУчетаРасчетовСКлиентами.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомиссионерамиСчетУчетаРасчетовСКлиентами.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомитентамиСчетУчетаРасчетовСКлиентами.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов по авансам полученным.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовПоАвансаПолученным)));
	Элементы.СчетаУчетаРасчетовСКлиентамиСчетУчетаАвансовПолученных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКлиентамиСчетУчетаАвансовПолученных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомиссионерамиСчетУчетаАвансовПолученных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСКомитентамиСчетУчетаАвансовПолученных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомиссионерамиСчетУчетаАвансовПолученных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКомитентамиСчетУчетаАвансовПолученных.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с кредиторами по основному долгу.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСКредиторамиОсновнойДолг)));
	Элементы.СчетаУчетаРасчетовСКредиторамиСчетУчетаРасчетовСКредиторамиОсновнойДолг.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКредиторамиСчетУчетаРасчетовСКредиторамиОсновнойДолг.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с кредиторами по процентам.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСКредиторамиПроценты)));
	Элементы.СчетаУчетаРасчетовСКредиторамиСчетУчетаРасчетовСКредиторамиПроценты.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКредиторамиСчетУчетаРасчетовСКредиторамиПроценты.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с кредиторами по комиссии.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСКредиторамиКомиссия)));
	Элементы.СчетаУчетаРасчетовСКредиторамиСчетУчетаРасчетовСКредиторамиКомиссия.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСКредиторамиСчетУчетаРасчетовСКредиторамиКомиссия.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с дебиторами по основному долгу.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСДебиторамиОсновнойДолг)));
	Элементы.СчетаУчетаРасчетовСДебиторамиСчетУчетаРасчетовСДебиторамиОсновнойДолг.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСДебиторамиСчетУчетаРасчетовСДебиторамиОсновнойДолг.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с дебиторами по процентам.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСДебиторамиПроценты)));
	Элементы.СчетаУчетаРасчетовСДебиторамиСчетУчетаРасчетовСДебиторамиПроценты.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСДебиторамиСчетУчетаРасчетовСДебиторамиПроценты.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов с дебиторами по комиссии.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСДебиторамиКомиссия)));
	Элементы.СчетаУчетаРасчетовСДебиторамиСчетУчетаРасчетовСДебиторамиКомиссия.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСДебиторамиСчетУчетаРасчетовСДебиторамиКомиссия.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Лизинг
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовСПоставщиками)));
	Элементы.СчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемВыкупПредметаЛизинга.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемВыкупПредметаЛизинга.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемАрендныеОбязательства.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемАрендныеОбязательства.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.СчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемЛизинговыеУслуги.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемЛизинговыеУслуги.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовПоАвансаВыданным)));
	Элементы.СчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемОбеспечительныйПлатеж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРасчетовСЛизингодателямиСчетУчетаРасчетовСЛизингодателемОбеспечительныйПлатеж.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	// Счета учета расчетов по подарочным сертификатам.
	МассивПараметров.Очистить();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасчетовПоАвансаПолученным)));
	Элементы.СчетаУчетаПодарочныхСертификатовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаПодарочныхСертификатовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	#КонецОбласти
	
	#Область Доходы
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаПрочихДоходов();
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаПрочихДоходов)));
	Элементы.СчетаУчетаДоходовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаДоходовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	#КонецОбласти
	
	#Область Расходы
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаРасходов();
	
	ДоступныеСчетаУчетаРасходов = Новый Структура;
	ДоступныеСчетаУчетаРасходов.Вставить("СчетаУчетаРасходов", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРасходов));
	ДоступныеСчетаУчетаРасходов.Вставить("СчетаУчетаПрочихРасходов", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаПрочихРасходов));
	ДоступныеСчетаУчетаРасходов.Вставить("СчетаУчетаОС", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаОС));
	ДоступныеСчетаУчетаРасходов.Вставить("СчетаУчетаОбъектыСтроительства", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаОбъектыСтроительства));
	ДоступныеСчетаУчетаРасходов.Вставить("СчетаУчетаНМА", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаНМА));
	ДоступныеСчетаУчетаРасходов.Вставить("СчетаСписанияРасходов", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаСписания));
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеСчетаУчетаРасходов.СчетаУчетаРасходов));
	Элементы.СчетаУчетаРасходовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Элементы.ОбщиеСчетаУчетаРасходовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеСчетаУчетаРасходов.СчетаСписанияРасходов));
	Элементы.СчетаУчетаРасходовСчетСписанияОСНО.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Элементы.ОбщиеСчетаУчетаРасходовСчетСписанияОСНО.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеСчетаУчетаРасходов.СчетаСписанияРасходов));
	Элементы.СчетаУчетаРасходовСчетСписанияЕНВД.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Элементы.ОбщиеСчетаУчетаРасходовСчетСписанияЕНВД.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ДоступныеСчетаУчетаРасходов = Новый ФиксированнаяСтруктура(ДоступныеСчетаУчетаРасходов);
	
	#КонецОбласти
	
	#Область ТМЦВЭксплуатации
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаТМЦВЭксплуатации();
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаУчета)));
	Элементы.СчетаУчетаТМЦВЭксплуатацииСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Элементы.ОбщиеСчетаУчетаТМЦВЭксплуатацииСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаЗабалансовогоУчета)));
	Элементы.СчетаУчетаТМЦВЭксплуатацииСчетЗабалансовогоУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	Элементы.ОбщиеСчетаУчетаТМЦВЭксплуатацииСчетЗабалансовогоУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	#КонецОбласти
	
	#Область Производство
	
	СчетаУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаНЗП();
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СчетаУчета)));
	Элементы.СчетаУчетаПроизводстваСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	#КонецОбласти
	
	#Область ДенежныеСредства
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаДенежныхСредств();
	
	ДоступныеСчетаУчетаДенежныхСредств = Новый Структура;
	ДоступныеСчетаУчетаДенежныхСредств.Вставить("СчетаБезналичныхДенежныхСредств", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаБезналичныхДенежныхСредств));
	ДоступныеСчетаУчетаДенежныхСредств.Вставить("СчетаНаличныхДенежныхСредств", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаНаличныхДенежныхСредств));
	
	ДоступныеСчетаУчетаДенежныхСредств = Новый ФиксированнаяСтруктура(ДоступныеСчетаУчетаДенежныхСредств);
	
	#КонецОбласти
	
	#Область ВнеоборотныеАктивы
	
	СтруктураСчетовУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаВнеоборотныхАктивов();
	
	СчетаОС = Новый Структура;
	СчетаОС.Вставить("Стоимость", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаСтоимость_ОС));
	СчетаОС.Вставить("СтоимостьЦФ", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаСтоимость_ОС_ЦФ));
	СчетаОС.Вставить("Амортизация", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаАмортизация_ОС));
	СчетаОС.Вставить("АмортизацияЦФ", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаАмортизация_ОС_ЦФ));
	СчетаОС.Вставить("Выбытие", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаВыбытие_ОС));
	СчетаОС.Вставить("РезервДооценки", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРезервДооценки_ОС));
	СчетаОС.Вставить("Забалансовый", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаЗабалансовогоУчета_ОС));
	
	СчетаНМА = Новый Структура;
	СчетаНМА.Вставить("Стоимость", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаСтоимость_НМА));
	СчетаНМА.Вставить("СтоимостьЦФ", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаСтоимость_НМА_ЦФ));
	СчетаНМА.Вставить("Амортизация", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаАмортизация_НМА));
	СчетаНМА.Вставить("АмортизацияЦФ", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаАмортизация_НМА_ЦФ));
	СчетаНМА.Вставить("Выбытие", Новый ФиксированныйМассив(Новый Массив));
	СчетаНМА.Вставить("РезервДооценки", Новый ФиксированныйМассив(СтруктураСчетовУчета.СчетаРезервДооценки_НМА));
	СчетаНМА.Вставить("Забалансовый", Новый ФиксированныйМассив(Новый Массив));
	
	ДоступныеСчетаУчетаВнеоборотныхАктивов = Новый Структура("СчетаОС, СчетаНМА", СчетаОС, СчетаНМА);
	ДоступныеСчетаУчетаВнеоборотныхАктивов = Новый ФиксированнаяСтруктура(ДоступныеСчетаУчетаВнеоборотныхАктивов);
	
	#КонецОбласти
	
	#Область Резервы
	
	СчетаУчета = Обработки.НастройкаОтраженияДокументовВРеглУчете.ДоступныеСчетаУчетаРезервов();
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СчетаУчета)));
	Элементы.СчетаУчетаРезервовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	Элементы.ОбщиеСчетаУчетаРезервовСчетУчета.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
	#КонецОбласти
	
КонецПроцедуры

&НаКлиенте
Функция МассивДоступныхСчетовРасходов(СтрокаТаблицы)
	
	ОсновнаяДеятельность = ПредопределенноеЗначение("Перечисление.ВидыДеятельностиРасходов.ОсновнаяДеятельность");
	ПрочаяДеятельность = ПредопределенноеЗначение("Перечисление.ВидыДеятельностиРасходов.ПрочаяДеятельность");
	ОсновнаяИПрочаяДеятельность = ПредопределенноеЗначение("Перечисление.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность");
	
	Если СтрокаТаблицы = Неопределено Тогда
		МассивВозврата = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаОС);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаОбъектыСтроительства);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаНМА);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаРасходов);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаПрочихРасходов);
	ИначеЕсли СтрокаТаблицы.РасходыНаОбъектыЭксплуатации Тогда
		МассивВозврата = ДоступныеСчетаУчетаРасходов.СчетаУчетаОС;
	ИначеЕсли СтрокаТаблицы.РасходыНаОбъектыСтроительства Тогда
		МассивВозврата = ДоступныеСчетаУчетаРасходов.СчетаУчетаОбъектыСтроительства;
	ИначеЕсли СтрокаТаблицы.РасходыНаНМАиНИОКР Тогда
		МассивВозврата = ДоступныеСчетаУчетаРасходов.СчетаУчетаНМА;
	ИначеЕсли СтрокаТаблицы.ВидДеятельностиРасходов = ОсновнаяДеятельность Тогда
		МассивВозврата = ДоступныеСчетаУчетаРасходов.СчетаУчетаРасходов;
	ИначеЕсли СтрокаТаблицы.ВидДеятельностиРасходов = ПрочаяДеятельность Тогда
		МассивВозврата = ДоступныеСчетаУчетаРасходов.СчетаУчетаПрочихРасходов;
	Иначе
		МассивВозврата = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаРасходов);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаРасходов.СчетаУчетаПрочихРасходов);
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Функция МассивДоступныхСчетовДенежныхСредств(ТипДенежныхСредств)
	
	МассивВозврата = Новый Массив;
	Если ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные") Тогда
		МассивВозврата = ДоступныеСчетаУчетаДенежныхСредств.СчетаБезналичныхДенежныхСредств;
	ИначеЕсли ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные") Тогда
		МассивВозврата = ДоступныеСчетаУчетаДенежныхСредств.СчетаНаличныхДенежныхСредств;
	Иначе
		МассивВозврата = Новый Массив(ДоступныеСчетаУчетаДенежныхСредств.СчетаБезналичныхДенежныхСредств);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, ДоступныеСчетаУчетаДенежныхСредств.СчетаНаличныхДенежныхСредств, Истина);
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Функция МассивДоступныхСчетовВнеоборотныхАктивов(ВидАктива, Знач ИзменяемыйСчет)
	
	ИзменяемыйСчет = СтрЗаменить(ИзменяемыйСчет, "СчетаУчетаВнеоборотныхАктивов", "");
	
	СоответствиеИменСчетовСоответствующимМассивам = Новый Соответствие;
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетУчета", "Стоимость");
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетУчетаЦФ", "СтоимостьЦФ");
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетУчетаАмортизации", "Амортизация");
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетУчетаАмортизацииЦФ", "АмортизацияЦФ");
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетУчетаВыбытия", "Выбытие");
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетУчетаРезерваДооценки", "РезервДооценки");
	СоответствиеИменСчетовСоответствующимМассивам.Вставить("СчетЗабалансовогоУчета", "Забалансовый");
	
	Если ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.НМА") Тогда
		ДоступныеСчетаУчета = ДоступныеСчетаУчетаВнеоборотныхАктивов.СчетаНМА;
		МассивВозврата = ДоступныеСчетаУчета[СоответствиеИменСчетовСоответствующимМассивам.Получить(ИзменяемыйСчет)];
	ИначеЕсли ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество")
		ИЛИ ВидАктива = ПредопределенноеЗначение("Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство") Тогда
		ДоступныеСчетаУчета = ДоступныеСчетаУчетаВнеоборотныхАктивов.СчетаОС;
		МассивВозврата = ДоступныеСчетаУчета[СоответствиеИменСчетовСоответствующимМассивам.Получить(ИзменяемыйСчет)];
	Иначе
		МассивОС = ДоступныеСчетаУчетаВнеоборотныхАктивов.СчетаОС[СоответствиеИменСчетовСоответствующимМассивам.Получить(ИзменяемыйСчет)];
		МассивНМА = ДоступныеСчетаУчетаВнеоборотныхАктивов.СчетаНМА[СоответствиеИменСчетовСоответствующимМассивам.Получить(ИзменяемыйСчет)];
		МассивВозврата = Новый Массив;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, МассивОС, Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВозврата, МассивНМА, Истина);
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Функция НовыйСчетУдовлетворяетПараметрамВыбора(ИмяСчета, СтрокаТаблицы, Счет)
	
	Если ИмяСчета = "СчетУчетаРасходов" Тогда
		Возврат МассивДоступныхСчетовРасходов(СтрокаТаблицы).Найти(Счет) <> Неопределено;
	ИначеЕсли ИмяСчета = "СчетУчетаДенежныхСредств" Тогда
		Возврат МассивДоступныхСчетовДенежныхСредств(СтрокаТаблицы.ТипДенежныхСредств).Найти(Счет) <> Неопределено;
	ИначеЕсли ИмяСчета = "СчетУчетаВнеоборотныхАктивов" ИЛИ ИмяСчета = "СчетУчетаЦФ" ИЛИ ИмяСчета = "СчетУчетаАмортизации"
		ИЛИ ИмяСчета = "СчетУчетаАмортизацииЦФ" ИЛИ ИмяСчета = "СчетУчетаВыбытия" ИЛИ ИмяСчета = "СчетУчетаРезерваДооценки"
		ИЛИ ИмяСчета = "СчетЗабалансовогоУчета" Тогда
		Возврат МассивДоступныхСчетовВнеоборотныхАктивов(СтрокаТаблицы.ВидАктива, ИмяСчета).Найти(Счет) <> Неопределено;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПараметрыВыбора(ИмяТаблицы, Счет, ЗначениеУсловия = Неопределено)
	
	Если ИмяТаблицы = "СчетаУчетаРасходов" ИЛИ ИмяТаблицы = "ОбщиеСчетаУчетаРасходов" Тогда
		МассивПараметровВыбора = Новый Массив;
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", МассивДоступныхСчетовРасходов(ЗначениеУсловия)));
		ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	ИначеЕсли ИмяТаблицы = "СчетаУчетаДенежныхСредств" Тогда
		МассивПараметровВыбора = Новый Массив;
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", МассивДоступныхСчетовДенежныхСредств(ЗначениеУсловия)));
		ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	ИначеЕсли ИмяТаблицы = "СчетаУчетаВнеоборотныхАктивов" Тогда
		МассивПараметровВыбора = Новый Массив;
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", МассивДоступныхСчетовВнеоборотныхАктивов(ЗначениеУсловия, Счет)));
		ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	Иначе
		ПараметрыВыбора = Элементы[ИмяТаблицы+Счет].ПараметрыВыбора;
	КонецЕсли;
	
	Возврат ПараметрыВыбора;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаКлиенте
Функция ПараметрыОткрытияГрупповойНастройкиСчетов(СтруктураСчетов)
	
	СтруктураПараметры = Новый Структура();
	СтруктураПараметры.Вставить("Организация", Организация);
	СтруктураПараметры.Вставить("СтруктураСчетов", СтруктураСчетов);
	
	Возврат СтруктураПараметры;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииОтбораНастроек()
	
	УстановитьСвойстваЭлементовПоОрганизацииИФункциональнымОпциям();
	ОбновляемыеТаблицы = "";
	Для каждого Таблица Из СтруктураТаблицФормы Цикл
		Если Таблица.Значение.Используется И СтрНачинаетсяС(Таблица.Ключ, "Общие") Тогда
			ОбновляемыеТаблицы = ОбновляемыеТаблицы + ?(ОбновляемыеТаблицы = "", "", ",") + Таблица.Ключ;
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьТаблицыНастройки(ОбновляемыеТаблицы, Истина);
	ОбновитьИтоговыеДанныеПоКоличествуСтрокТребующихНастройки(Истина);
	УстановитьТекущуюСтраницу(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ОрганизацииДляНастройки()
	
	МассивОрганизаций = Новый Массив;
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивОрганизаций.Добавить(Организация);
	Иначе
		МассивОрганизаций = Элементы.Организация.СписокВыбора.ВыгрузитьЗначения();
	КонецЕсли;
	
	Возврат МассивОрганизаций;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЧислоДокументовПрописью(КоличествоДокументов)
	
	КоличествоПрописью = ЧислоПрописью(
		КоличествоДокументов,
		"Л = ru_RU; НП = Истина; НД = Ложь; ДП = Ложь;",
		НСтр("ru = 'документ,документа,документов,м,,,,,0'"));
	Поз = СтрНайти(КоличествоПрописью, "документ");
	Если Поз <> 0 Тогда
		КоличествоПрописью = Сред(КоличествоПрописью, Поз);
	КонецЕсли;
	КоличествоПрописью = Строка(КоличествоДокументов) + " " + НРег(КоличествоПрописью);
	
	Возврат КоличествоПрописью;
	
КонецФункции // ЧислоДокументовПрописью()

&НаСервере
Процедура ЗаполнитьТаблицыНастройки(ИменаТаблицДляЗаполнения = Неопределено, ОбновлятьТолькоТребуемыеСчета = Ложь)
	
	МассивОрганизаций = Элементы.Организация.СписокВыбора.ВыгрузитьЗначения();
	
	ТаблицыДляЗаполнения = Новый Структура;
		
	Если ИменаТаблицДляЗаполнения = Неопределено Тогда
		Для каждого Таблица Из СтруктураТаблицФормы Цикл
			Если Таблица.Значение.Используется Тогда
				ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(Таблица.Ключ);
				Если Не ТаблицыДляЗаполнения.Свойство(ИмяТаблицы) Тогда
					Значение = ?(ОбновлятьТолькоТребуемыеСчета, ЭтотОбъект[ИмяТаблицы].Выгрузить(), Неопределено);
					ТаблицыДляЗаполнения.Вставить(ИмяТаблицы, Значение);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		МассивТаблицДляЗаполнения = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблицДляЗаполнения);
		Для каждого ТаблицаДляЗаполнения Из МассивТаблицДляЗаполнения Цикл
			ИмяТаблицыДляЗаполнения = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТаблицаДляЗаполнения);
			Если Не ТаблицыДляЗаполнения.Свойство(ИмяТаблицыДляЗаполнения) Тогда
				Значение = ?(ОбновлятьТолькоТребуемыеСчета, ЭтотОбъект[ИмяТаблицыДляЗаполнения].Выгрузить(), Неопределено);
				ТаблицыДляЗаполнения.Вставить(ИмяТаблицыДляЗаполнения, Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполненныеТаблицы = Обработки.НастройкаОтраженияДокументовВРеглУчете.ТаблицыНастроекСчетов(ТаблицыДляЗаполнения, МассивОрганизаций, Организация);
	
	ОбновлениеТаблиц(ЗаполненныеТаблицы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СчетаУчетаТаблицы(ИмяТаблицы)
	
	МассивВозврата = Новый Массив;
	
	Если ИмяТаблицы = "СчетаУчетаНоменклатуры" Или ИмяТаблицы = "ОбщиеСчетаУчетаНоменклатуры" Тогда
		МассивВозврата.Добавить("СчетУчетаНаСкладе");
		МассивВозврата.Добавить("СчетУчетаВПути");
		МассивВозврата.Добавить("СчетУчетаНДСВПути");
		МассивВозврата.Добавить("СчетУчетаВыручкиОтПродажЕНВД");
		МассивВозврата.Добавить("СчетУчетаСебестоимостиПродажЕНВД");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаНоменклатурыПереданной" Или ИмяТаблицы = "ОбщиеСчетаУчетаНоменклатурыПереданной" Тогда
		МассивВозврата.Добавить("СчетУчетаПередачиНаКомиссию");
		МассивВозврата.Добавить("СчетУчетаЗатратыНаПриобретение");
		МассивВозврата.Добавить("СчетУчетаПередачиВПереработку");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаНоменклатуры" Или ИмяТаблицы = "ОбщиеСчетаУчетаНоменклатуры" Или ИмяТаблицы = "СчетаУчетаНоменклатурыПереданной" Тогда
		МассивВозврата.Добавить("СчетУчетаВыручкиОтПродаж");
		МассивВозврата.Добавить("СчетУчетаСебестоимостиПродаж");
		МассивВозврата.Добавить("СчетУчетаНДСПриПродаже");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСКлиентами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКлиентами"
		Или ИмяТаблицы = "СчетаУчетаРасчетовСКомиссионерами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКомиссионерами"
		Или ИмяТаблицы = "СчетаУчетаРасчетовСКомитентами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКомитентами" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовСКлиентами");
		МассивВозврата.Добавить("СчетУчетаАвансовПолученных");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСПоставщиками" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСПоставщиками"
		Или ИмяТаблицы = "СчетаУчетаРасчетовСКомиссионерами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКомиссионерами"
		Или ИмяТаблицы = "СчетаУчетаРасчетовСКомитентами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКомитентами" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовСПоставщиками");
		МассивВозврата.Добавить("СчетУчетаАвансовВыданных");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСКлиентами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКлиентами" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовСКлиентамиТара");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСПоставщиками" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСПоставщиками" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовПоПретензиям");
		МассивВозврата.Добавить("СчетУчетаРасчетовСПоставщикамиТара");
		МассивВозврата.Добавить("СчетУчетаНеотфактурованныеПоставки");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСКредиторами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСКредиторами" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовСКредиторамиОсновнойДолг");
		МассивВозврата.Добавить("СчетУчетаРасчетовСКредиторамиПроценты");
		МассивВозврата.Добавить("СчетУчетаРасчетовСКредиторамиКомиссия");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСДебиторами" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСДебиторами" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовСДебиторамиОсновнойДолг");
		МассивВозврата.Добавить("СчетУчетаРасчетовСДебиторамиПроценты");
		МассивВозврата.Добавить("СчетУчетаРасчетовСДебиторамиКомиссия");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасчетовСЛизингодателями" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасчетовСЛизингодателями" Тогда
		МассивВозврата.Добавить("СчетУчетаРасчетовСЛизингодателемЛизинговыеУслуги");
		МассивВозврата.Добавить("СчетУчетаРасчетовСЛизингодателемОбеспечительныйПлатеж");
		МассивВозврата.Добавить("СчетУчетаРасчетовСЛизингодателемВыкупПредметаЛизинга");
		МассивВозврата.Добавить("СчетУчетаРасчетовСЛизингодателемАрендныеОбязательства");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаТМЦВЭксплуатации" Или ИмяТаблицы = "ОбщиеСчетаУчетаТМЦВЭксплуатации"
		Или ИмяТаблицы = "СчетаУчетаПодарочныхСертификатов" Или ИмяТаблицы = "ОбщиеСчетаУчетаПодарочныхСертификатов"
		Или ИмяТаблицы = "СчетаУчетаДенежныхСредств" Или ИмяТаблицы = "СчетаУчетаПроизводства"
		Или ИмяТаблицы = "СчетаУчетаРасходов" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасходов"
		Или ИмяТаблицы = "СчетаУчетаДоходов" Или ИмяТаблицы = "ОбщиеСчетаУчетаДоходов" Тогда
			МассивВозврата.Добавить("СчетУчета");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаТМЦВЭксплуатации" Или ИмяТаблицы = "ОбщиеСчетаУчетаТМЦВЭксплуатации" Тогда
		МассивВозврата.Добавить("СчетЗабалансовогоУчета");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРасходов" Или ИмяТаблицы = "ОбщиеСчетаУчетаРасходов" Тогда
		МассивВозврата.Добавить("СчетСписанияОСНО");
		МассивВозврата.Добавить("СчетСписанияЕНВД");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаВнеоборотныхАктивов" Тогда
		МассивВозврата.Добавить("СчетУчета");
		МассивВозврата.Добавить("СчетУчетаЦФ");
		МассивВозврата.Добавить("СчетУчетаАмортизации");
		МассивВозврата.Добавить("СчетУчетаАмортизацииЦФ");
		МассивВозврата.Добавить("СчетУчетаВыбытия");
		МассивВозврата.Добавить("СчетУчетаРезерваДооценки");
		МассивВозврата.Добавить("СчетЗабалансовогоУчета");
	КонецЕсли;
	Если ИмяТаблицы = "СчетаУчетаРезервов" Или ИмяТаблицы = "ОбщиеСчетаУчетаРезервов" Тогда
		МассивВозврата.Добавить("СчетУчета");
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаСервере
Функция АнализСчетовТребующихНастройкиСервер()
	
	СтруктураЗаполняемыхТаблиц = Новый Структура;
	Для каждого Таблица Из СтруктураТаблицФормы Цикл
		Если Таблица.Значение.Используется Тогда
			ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(Таблица.Ключ);
			Если Не СтруктураЗаполняемыхТаблиц.Свойство(ИмяТаблицы) Тогда
				СтруктураЗаполняемыхТаблиц.Вставить(ИмяТаблицы, ЭтотОбъект[ИмяТаблицы].Выгрузить());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НаименованиеЗадания = НСтр("ru = 'Анализ счетов требующих настройки и обновление таблиц соответствующих счетов'");
	ВыполняемыйМетод = "Обработки.НастройкаОтраженияДокументовВРеглУчете.АнализСчетовТребующихНастройкиИОбновлениеТаблиц";
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("Таблицы", СтруктураЗаполняемыхТаблиц);
	ПараметрыОбработки.Вставить("МассивОрганизаций", ОрганизацииДляНастройки());
	ПараметрыОбработки.Вставить("ДатаОкончанияПериода", ДатаОкончанияПериода);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = ЭтотОбъект.УникальныйИдентификатор;
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	
	Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатФоновогоЗадания.АдресРезультата);
	ИначеЕсли РезультатФоновогоЗадания.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'При анализе требующихся счетов возникли ошибки. Подробнее см. в журнале регистрации.'"));
	КонецЕсли;
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаСервере
Функция ДобавитьЗаписиВТаблицуСервер(РежимАнализа, ДатаНачала = Неопределено, ДатаОкончания = Неопределено)
	
	НаименованиеЗадания = НСтр("ru = 'Обновление таблицы настройки счетов'");
	ИмяТаблицы = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ТекущаяТаблицаНастройки);
	ИмяРегистра = СтрЗаменить(ИмяТаблицы, "СчетаУчета", "ПорядокОтражения");
	ВыполняемыйМетод = "РегистрыСведений."+ИмяРегистра+".ДополнитьТаблицуНаОснованииАнализаОпераций";
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("РежимАнализа", РежимАнализа);
	ПараметрыОбработки.Вставить("МассивОрганизаций", ОрганизацииДляНастройки());
	ПараметрыОбработки.Вставить("ЗаполняемаяТаблица", ЭтотОбъект[ИмяТаблицы].Выгрузить());
	Если РежимАнализа = "ЗаПериод" Тогда
		ПараметрыОбработки.Вставить("НачалоПериода", ДатаНачала);
		ПараметрыОбработки.Вставить("КонецПериода", ДатаОкончания);
	Иначе
		ПараметрыОбработки.Вставить("ДатаОкончанияПериода", ДатаОкончанияПериода);
	КонецЕсли;
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = ЭтотОбъект.УникальныйИдентификатор;
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;
	
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	
	Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатФоновогоЗадания.АдресРезультата);
		УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	ИначеЕсли РезультатФоновогоЗадания.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'При обновлении таблицы счетов учета возникли ошибки. Подробнее см. в журнале регистрации.'"));
	КонецЕсли;
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаСервере
Процедура ОбновлениеТаблиц(Знач Результат)
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		Результат = ПолучитьИзВременногоХранилища(Результат);
	КонецЕсли;
	
	Для каждого Таблица Из Результат Цикл
		ЭтотОбъект[Таблица.Ключ].Загрузить(Таблица.Значение);
	КонецЦикла;
	
	ОбновитьИтоговыеДанныеПоКоличествуСтрокТребующихНастройки();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтоговыеДанныеПоКоличествуСтрокТребующихНастройки(ТолькоДетальные = Ложь)
	
	Для каждого Таблица Из СтруктураТаблицФормы Цикл
		
		Если Таблица.Значение.Используется Тогда
			КоличествоТребующихНастройки = 0;
			МассивСчетов = СчетаУчетаТаблицы(Таблица.Ключ);
			Для каждого Строка Из ЭтотОбъект[ИмяТаблицыДанныхПоИмениТекущейТаблицы(Таблица.Ключ)] Цикл
				Если ЗначениеЗаполнено(ЭтотОбъект.Организация)
					И Не СтрНачинаетсяС(Таблица.Ключ, "Общие")
					И Таблица.Ключ <> "СчетаУчетаВнеоборотныхАктивов"
					И Строка.Организация <> ЭтотОбъект.Организация Тогда
					Продолжить;
				КонецЕсли;
				Если СтрНайти(Таблица.Ключ, "СчетаУчетаРасчетов") <> 0 Тогда
					ИмяВидаРасчетов = СтрЗаменить(Таблица.Ключ, "СчетаУчетаРасчетов", "");
					ИмяВидаРасчетов = СтрЗаменить(ИмяВидаРасчетов, "Общие", "");
					Если Не Строка["Расчеты"+ИмяВидаРасчетов] Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				Если МассивСчетов.Количество() = 1 Тогда
					КоличествоТребующихНастройки = КоличествоТребующихНастройки + ?(Строка.ТребуетсяНастройка И Не ЗначениеЗаполнено(Строка[МассивСчетов.Получить(0)]), 1, 0);
				Иначе
					Для каждого Счет Из МассивСчетов Цикл
						ТребуетсяНастройка = Строка["ТребуетсяНастройка"+Счет] И Не ЗначениеЗаполнено(Строка[Счет]);
						КоличествоТребующихНастройки = КоличествоТребующихНастройки + ?(ТребуетсяНастройка, 1, 0);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			Таблица.Значение.ТребуютНастройки = КоличествоТребующихНастройки;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьПредставлениеСпискаВыбораСтраниц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ИнициализироватьПараметрыФормыДлительнойОперации(РезультатЗапускаФоновогоЗадания)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторЗадания", РезультатЗапускаФоновогоЗадания.ИдентификаторЗадания);
	ПараметрыФормы.Вставить("ВыводитьОкноОжидания", Истина);
	ПараметрыФормы.Вставить("АдресРезультата", РезультатЗапускаФоновогоЗадания.АдресРезультата);
		
	Возврат ПараметрыФормы;

КонецФункции

&НаСервереБезКонтекста
Функция СтруктураОтбора(ИмяТаблицы, ТребуетсяНастройка, Организация)
	
	СтруктураОтбора = Новый Структура;
	
	Если ТребуетсяНастройка Тогда
		СтруктураОтбора.Вставить("ТребуетсяНастройка", Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) И Не СтрНачинаетсяС(ИмяТаблицы, "Общие") И ИмяТаблицы <> "СчетаУчетаВнеоборотныхАктивов" Тогда
		// Отбор по организации добавляем, если организация заполнена и в выводимой таблице есть колонка "Организация" (это
		// все настройки исключений).
		СтруктураОтбора.Вставить("Организация", Организация);
	КонецЕсли;
	
	Если СтрНайти(ИмяТаблицы, "СчетаУчетаРасчетов") <> 0 Тогда
		
		// Отбор по виду расчетов добавляем только для таблиц расчетов с партнерами (клиенты, поставщики, комиссионеры,
		// комитенты, кредиторы, дебиторы).
		ВидРасчетов = СтрЗаменить(ИмяТаблицы, "СчетаУчетаРасчетов", "");
		ВидРасчетов = СтрЗаменить(ВидРасчетов, "Общие", "");
		СтруктураОтбора.Вставить("Расчеты" + ВидРасчетов, Истина);
		
		Если ТребуетсяНастройка Тогда
			СтруктураОтбора.Вставить("ТребуетсяНастройкаРасчеты" + ВидРасчетов, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураОтбора;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяНастройкаСчетовУчета(ИмяТаблицы, Строка)
	
	ТребуетсяНастройка = Ложь;
	
	МассивСчетов = СчетаУчетаТаблицы(ИмяТаблицы);
	
	Если МассивСчетов.Количество() = 1 Тогда
		ТребуетсяНастройка = Строка.ТребуетсяНастройка И Не ЗначениеЗаполнено(Строка[МассивСчетов.Получить(0)]);
	Иначе
		Для каждого Счет Из МассивСчетов Цикл
			Строка["ТребуетсяНастройка"+Счет] = Строка["ТребуетсяНастройка"+Счет] И Не ЗначениеЗаполнено(Строка[Счет]);
			ТребуетсяНастройка = ТребуетсяНастройка ИЛИ Строка["ТребуетсяНастройка"+Счет];
		КонецЦикла;
	КонецЕсли;	
	
	Возврат ТребуетсяНастройка;
	
КонецФункции

&НаКлиенте
Функция ИзменяемоеЧислоТребующихсяНастроек(СтароеЗначение, НовоеЗначение)
	
	Если СтароеЗначение = НовоеЗначение Тогда
		// Количество строк, требующих настройки не изменилось:
		Возврат 0;
	ИначеЕсли СтароеЗначение = Ложь Тогда
		// Строк, требующих настройки стало больше на 1:
		Возврат 1;
	Иначе
		// Строк, требующих настройки стало меньше на 1:
		Возврат -1;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПроверкаВозможностиРедактирования(Элемент, Отказ)
	
	Если Не РедактированиеЗаполненныхСчетов Тогда
		
		ТекущееЗначениеСчета = Неопределено;
		ЭтоСчетПоУмолчанию = Неопределено;
		ИмяТекущейТаблицы = ИмяТекущейТаблицы(Элементы.Страницы.ТекущаяСтраница.Имя, ТекущаяТаблицаНастройки);
		ИмяРеквизита = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, ИмяТекущейТаблицы, "");
		Элемент.ТекущиеДанные.Свойство(ИмяРеквизита, ТекущееЗначениеСчета);
		Элемент.ТекущиеДанные.Свойство(ИмяРеквизита+"ПоУмолчанию", ЭтоСчетПоУмолчанию);
		
		Если ЗначениеЗаполнено(ТекущееЗначениеСчета) И Не СтрНайти(Элемент.ТекущиеДанные.ИзмененныеДанные, ИмяРеквизита) Тогда
			ТекстПредупреждения = НСтр("ru = 'Редактирование указанных счетов не рекомендуется.'");
			Отказ = Истина;
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзмененныеДанные(ИмяПоля, СтрокаТаблицы);
	
	ЕстьИзмененныеДанные = ЗначениеЗаполнено(СтрокаТаблицы.ИзмененныеДанные);
	Если ЕстьИзмененныеДанные И Не СтрНайти(СтрокаТаблицы.ИзмененныеДанные, ИмяПоля) Тогда
		СтрокаТаблицы.ИзмененныеДанные = СтрокаТаблицы.ИзмененныеДанные + ", " + ИмяПоля;
	ИначеЕсли Не ЕстьИзмененныеДанные Тогда
		СтрокаТаблицы.ИзмененныеДанные = ИмяПоля;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСчетаИсключенийПриИзмененииСчетовПоУмолчанию(ИмяТаблицы, СтрокаОбщихНастроек)
	
	ИмяТаблицыИсключений = СтрЗаменить(ИмяТаблицы, "Общие", "");
	МассивСчетов = СчетаУчетаТаблицы(ИмяТаблицы);	
	
	ТаблицаИсключений = ЭтотОбъект[ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицыИсключений)];
	
	ИмяКолонкиАналитики = ИмяАналитикиУчетаПоТаблице(ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицыИсключений));
	ПараметрыОтбора = Новый Структура(ИмяКолонкиАналитики, СтрокаОбщихНастроек.АналитикаУчета);
	СтрокиДляИзменения = ТаблицаИсключений.НайтиСтроки(ПараметрыОтбора);
	
	ИзменениеСтрокТребующихНастройки = 0;
	Для каждого ИзменяемаяСтрока Из СтрокиДляИзменения Цикл
		Для каждого Счет Из МассивСчетов Цикл
			Если ИзменяемаяСтрока[Счет+"ПоУмолчанию"] Тогда
				ИзменяемаяСтрока[Счет] = СтрокаОбщихНастроек[Счет];
			КонецЕсли;
		КонецЦикла;
		ВидРасчетов = "Расчеты" + СтрЗаменить(ИмяТаблицыИсключений, "СчетаУчетаРасчетов", "");
		ИмяКолонкиТребуетсяНастройка = "ТребуетсяНастройка" + ?(СтрНачинаетсяС(ИмяТаблицыИсключений, "СчетаУчетаРасчетов"), ВидРасчетов, "");
		ТребуетсяНастройкаСчетовУчета = ТребуетсяНастройкаСчетовУчета(ИмяТаблицыИсключений, ИзменяемаяСтрока);
		ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(ИзменяемаяСтрока[ИмяКолонкиТребуетсяНастройка], ТребуетсяНастройкаСчетовУчета);
		ИзменениеСтрокТребующихНастройки = ИзменениеСтрокТребующихНастройки + ЧислоИзменения;
		ИзменяемаяСтрока[ИмяКолонкиТребуетсяНастройка] = ТребуетсяНастройкаСчетовУчета;
	КонецЦикла;
	ИзменитьЧислоТребующихсяНастроек(ИмяТаблицыИсключений, ИзменениеСтрокТребующихНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСозданияНовойСтрокиИсключения(ИмяТаблицыДанных)
	
	ЗначенияЗаполнения = Новый Структура("Организация", Организация);
	Если ЗначениеЗаполнено(РедактируемаяГФУ) Тогда
		ЗначенияЗаполнения.Вставить(ИмяАналитикиУчетаПоТаблице(ИмяТаблицыДанных), РедактируемаяГФУ);
	КонецЕсли;
	СтруктураПараметры = Новый Структура("ЗначенияЗаполнения, ВозвратЗначенияБезЗаписи", ЗначенияЗаполнения, Истина);
	Если ИмяТаблицыДанных = "СчетаУчетаРасчетовСПартнерами" Тогда
		ИмяТаблицы = ИмяТекущейТаблицы(ИмяТаблицыДанных, ТекущаяТаблицаНастройки);
		ТипыРасчетов = Новый Структура(СтрЗаменить(ИмяТаблицы, "СчетаУчетаРасчетов", "Расчеты"), Истина);
		СтруктураПараметры.Вставить("ТипыРасчетов", ТипыРасчетов);
	Иначе
		ИмяТаблицы = ИмяТаблицыДанных;
	КонецЕсли;
	ОповещениеОДобавленииЗаписиИсключений = Новый ОписаниеОповещения("ЗаполнениеНовойЗаписиИсключения", ЭтотОбъект, ИмяТаблицы);
	ИмяОткрываемойФормы = "РегистрСведений." + СтрЗаменить(ИмяТаблицыДанных, "СчетаУчета", "ПорядокОтражения") + ".ФормаЗаписи";
	ОткрытьФорму(ИмяОткрываемойФормы, СтруктураПараметры, ЭтотОбъект,,,, ОповещениеОДобавленииЗаписиИсключений, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ОпределитьТаблицуНастройкиПоПереданнойГФУ(РедактируемаяГФУ)
	
	НастраиваемаяТаблица = "";
	
	Если Не ЗначениеЗаполнено(РедактируемаяГФУ) Тогда
		Возврат НастраиваемаяТаблица;
	КонецЕсли;
			
	СоответствиеГФУНастраиваемымТаблицам = Новый Соответствие;
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов"), "СчетаУчетаРасходов");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов"), "СчетаУчетаДоходов");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("СправочникСсылка.КатегорииЭксплуатации"), "СчетаУчетаТМЦВЭксплуатации");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаВнеоборотныхАктивов"), "СчетаУчетаВнеоборотныхАктивов");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаРасчетов"), "СчетаУчетаРасчетовСПартнерами");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("СправочникСсылка.ГруппыФинансовогоУчетаНоменклатуры"), "СчетаУчетаНоменклатуры");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("СправочникСсылка.ВидыПодарочныхСертификатов"), "СчетаУчетаРасходов");
	СоответствиеГФУНастраиваемымТаблицам.Вставить(Тип("СправочникСсылка.Резервы"), "СчетаУчетаРезервов");
	
	НастраиваемаяТаблица = СоответствиеГФУНастраиваемымТаблицам.Получить(ТипЗнч(РедактируемаяГФУ));
	
	Если НастраиваемаяТаблица = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если НастраиваемаяТаблица = "СчетаУчетаРасчетовСПартнерами" Тогда
		МассивРеквизитовРасчетов = Новый Массив;
		Для каждого РеквизитРасчетов Из ВидыРасчетовСПартнерами() Цикл
			МассивРеквизитовРасчетов.Добавить("Расчеты" + РеквизитРасчетов);
		КонецЦикла;
		СтрокаРеквизитов = СтрСоединить(МассивРеквизитовРасчетов, ",");
		РасчетыГФУ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РедактируемаяГФУ, СтрокаРеквизитов);
		Для каждого ВидРасчетов Из ВидыРасчетовСПартнерами() Цикл
			Если РасчетыГФУ["Расчеты" + ВидРасчетов] Тогда
				НастраиваемаяТаблица = "СчетаУчетаРасчетов" + ВидРасчетов;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат НастраиваемаяТаблица;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура УказатьСчетаУчетаЗавершение(Результат, ИмяТаблицы) Экспорт
	
	СтруктураСчетов = Результат;
	
	Если СтруктураСчетов <> Неопределено Тогда
		
		ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы);
		ВидРасчетов = СтрЗаменить(СтрЗаменить(ИмяТаблицы, "Общие", ""), "СчетаУчетаРасчетов", "");
		ИзменениеСтрокТребующихНастройки = 0;
		ВыделенныеСтроки = Элементы[ИмяТаблицыДанных].ВыделенныеСтроки;
		Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
			
			СтрокаТаблицы = ЭтотОбъект[ИмяТаблицыДанных].НайтиПоИдентификатору(ИдентификаторСтроки);
			
			Для каждого ВыбранныйСчет Из СтруктураСчетов Цикл
				
				Если ВидРасчетов = "СКомиссионерами" Тогда
					ИмяСчета = СтрЗаменить(ВыбранныйСчет.Ключ, "РасчетовСКомиссионерамиТовары", "РасчетовСКлиентами");
					ИмяСчета = СтрЗаменить(ИмяСчета, "АвансовСКомиссионерамиТовары", "АвансовПолученных");
					ИмяСчета = СтрЗаменить(ИмяСчета, "РасчетовСКомиссионерамиВознаграждение", "РасчетовСПоставщиками");
					ИмяСчета = СтрЗаменить(ИмяСчета, "АвансовСКомиссионерамиВознаграждение", "АвансовВыданных");
				ИначеЕсли ВидРасчетов = "СКомитентами" Тогда
					ИмяСчета = СтрЗаменить(ВыбранныйСчет.Ключ, "РасчетовСКомитентамиТовары", "РасчетовСПоставщиками");
					ИмяСчета = СтрЗаменить(ИмяСчета, "АвансовСКомитентамиТовары", "АвансовВыданных");
					ИмяСчета = СтрЗаменить(ИмяСчета, "РасчетовСКомитентамиВознаграждение", "РасчетовСКлиентами");
					ИмяСчета = СтрЗаменить(ИмяСчета, "АвансовСКомитентамиВознаграждение", "АвансовПолученных");
				ИначеЕсли СтрНачинаетсяС(ВыбранныйСчет.Ключ, "СчетЗабалансовогоУчета") Тогда
					ИмяСчета = "СчетЗабалансовогоУчета";
				Иначе
					ИмяСчета = ?(СтрокаТаблицы.Свойство(ВыбранныйСчет.Ключ), ВыбранныйСчет.Ключ, "СчетУчета");
				КонецЕсли;
				
				ТребуетсяНастройкаСчета = ?(СтрокаТаблицы.Свойство("ТребуетсяНастройка"+ИмяСчета),
					СтрокаТаблицы["ТребуетсяНастройка"+ИмяСчета], СтрокаТаблицы.ТребуетсяНастройка);
				
				Если Не ЗначениеЗаполнено(СтрокаТаблицы[ИмяСчета]) И ЗначениеЗаполнено(ВыбранныйСчет.Значение) И ТребуетсяНастройкаСчета
					И НовыйСчетУдовлетворяетПараметрамВыбора(ВыбранныйСчет.Ключ, СтрокаТаблицы, ВыбранныйСчет.Значение) Тогда
						
						СтрокаТаблицы[ИмяСчета] = ВыбранныйСчет.Значение;
						Если СтрокаТаблицы.Свойство(ИмяСчета+"ПоУмолчанию") Тогда
							СтрокаТаблицы[ИмяСчета+"ПоУмолчанию"] = Ложь;
						КонецЕсли;
						ОбновитьИзмененныеДанные(ИмяСчета, СтрокаТаблицы);
						
				КонецЕсли;
				
			КонецЦикла;
			
			ИмяКолонкиТребуетсяНастройка = "ТребуетсяНастройка" + 
				?(ВидыРасчетовСПартнерами().Найти(ВидРасчетов) = Неопределено, "", "Расчеты" + ВидРасчетов);
			ТребуетсяНастройкаСчетовУчета = ТребуетсяНастройкаСчетовУчета(ИмяТаблицы, СтрокаТаблицы);
			ЧислоИзменения = ИзменяемоеЧислоТребующихсяНастроек(СтрокаТаблицы[ИмяКолонкиТребуетсяНастройка], ТребуетсяНастройкаСчетовУчета);
			ИзменениеСтрокТребующихНастройки = ИзменениеСтрокТребующихНастройки + ЧислоИзменения;
			СтрокаТаблицы[ИмяКолонкиТребуетсяНастройка] = ТребуетсяНастройкаСчетовУчета;
			
		КонецЦикла;
		
		ИзменитьЧислоТребующихсяНастроек(ИмяТаблицы, ИзменениеСтрокТребующихНастройки);
		
		Модифицированность = Истина;
			
		УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаДобавленияЗавершение(Результат, ИмяТаблицы = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Результат = ДобавитьЗаписиВТаблицуСервер("ЗаПериод", Результат.НачалоПериода, Результат.КонецПериода);
	
		Если Результат.Статус = "Выполняется" Тогда
			ОповещениеФормаДлительнойОперацииЗакрыта = Новый ОписаниеОповещения("ВыполнениеФоновогоЗаданияЗавершено", ЭтотОбъект, Истина);
			ПараметрыФормы = ИнициализироватьПараметрыФормыДлительнойОперации(Результат);
			ОткрытьФорму("ОбщаяФорма.ДлительнаяОперация", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеФормаДлительнойОперацииЗакрыта, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	РедактированиеЗаполненныхСчетов = Результат = Истина;
	Элементы.ФормаРазрешитьРедактированиеРеквизитовОбъекта.Пометка = Результат = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеФоновогоЗаданияЗавершено(РезультатЗакрытияФормыДлительнойОперации, ВыполняетсяДобавлениеЗаписейВТаблицу = Ложь) Экспорт
	Если РезультатЗакрытияФормыДлительнойОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если РезультатЗакрытияФормыДлительнойОперации.Статус = "Выполнено" Тогда
		ОбновлениеТаблиц(РезультатЗакрытияФормыДлительнойОперации.АдресРезультата);
		Если ВыполняетсяДобавлениеЗаписейВТаблицу Тогда
			УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
		КонецЕсли;
	ИначеЕсли РезультатЗакрытияФормыДлительнойОперации.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'При обновлении таблицы счетов учета возникли ошибки. Подробнее см. в журнале регистрации.'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеНовойЗаписиИсключения(Результат, ИмяТаблицы) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Для номенклатуры возвращается если и СчетУчетаНДСПриЗакупке, который не анализируется в этой форме, удалим:
	Если Результат.Свойство("СчетУчетаНДСПриЗакупке") Тогда
		Результат.Удалить("СчетУчетаНДСПриЗакупке");
	КонецЕсли;
	ИмяТаблицыДанных = ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы);
	
	#Область ПроверкиВозможностиДобавления
	
	СчетаУчета = СчетаУчетаТаблицы(ИмяТаблицы);
	СтруктураОтбора = Новый Структура;
	Для каждого ЭлементРезультата Из Результат Цикл
		Если СчетаУчета.Найти(ЭлементРезультата.Ключ) = Неопределено И ЭлементРезультата.Ключ <> "СтатьяДоходовОСНО" И ЭлементРезультата.Ключ <> "СтатьяДоходовЕНВД" Тогда
			// Добавляем в структуру отбора только данные по подразделениям:
			СтруктураОтбора.Вставить(ЭлементРезультата.Ключ, ЭлементРезультата.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СтрокиССовпадающимиИзмерениями = ЭтотОбъект[ИмяТаблицыДанных].НайтиСтроки(СтруктураОтбора);
	
	Если СтрокиССовпадающимиИзмерениями.Количество() > 0 Тогда
		Элементы[ИмяТаблицыДанных].ТекущаяСтрока = СтрокиССовпадающимиИзмерениями.Получить(0).ПолучитьИдентификатор();
		ТекстПредупреждения = НСтр("ru = 'Строка с соответствующими измерениями уже есть в таблице исключений.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПолучениеЗначенийПоУмолчанию
	
	Если ИмяТаблицы <> "СчетаУчетаПроизводства" Тогда
		
		СоответствиеТаблицИсключенийИИменАналитикУчета = Новый Соответствие;
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаНоменклатуры", "ГруппаФинансовогоУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаНоменклатурыПереданной", "ГруппаФинансовогоУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаТМЦВЭксплуатации", "КатегорияЭксплуатации");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаПодарочныхСертификатов", "ВидСертификата");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаРасчетовСПартнерами", "ГруппаФинансовогоУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаРасходов", "СтатьяРасходов");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаДоходов", "СтатьяДоходов");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаРезервов", "ВидРезервов");
		
		СтруктураОтбора = Новый Структура("АналитикаУчета", Результат[СоответствиеТаблицИсключенийИИменАналитикУчета.Получить(ИмяТаблицыДанных)]);
		СтрокиСДаннойАналитикой = ЭтотОбъект["Общие"+ИмяТаблицыДанных].НайтиСтроки(СтруктураОтбора);
		
		ЗначенияПоУмолчанию = ?(СтрокиСДаннойАналитикой.Количество() > 0, СтрокиСДаннойАналитикой.Получить(0), Неопределено);
		
	КонецЕсли;
	
	#КонецОбласти
	
	НоваяСтрокаТаблицы = ЭтотОбъект[ИмяТаблицыДанных].Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, Результат);
	НоваяСтрокаТаблицы.ИзмененныеДанные = СтрСоединить(СчетаУчета, ", ");
	Если ЗначенияПоУмолчанию <> Неопределено Тогда
		Для каждого СчетУчета Из СчетаУчетаТаблицы("Общие" + ИмяТаблицы) Цикл
			Если Не ЗначениеЗаполнено(НоваяСтрокаТаблицы[СчетУчета]) И ЗначениеЗаполнено(ЗначенияПоУмолчанию[СчетУчета]) Тогда
				НоваяСтрокаТаблицы[СчетУчета] = ЗначенияПоУмолчанию[СчетУчета];
				НоваяСтрокаТаблицы[СчетУчета+"ПоУмолчанию"] = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Элементы[ИмяТаблицыДанных].ТекущаяСтрока = НоваяСтрокаТаблицы.ПолучитьИдентификатор();
	УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	КоличествоДокументов = ДополнительныеПараметры.КоличествоДокументов;
	
	
	КодОтвета = РезультатВопроса;
	Если КодОтвета = КодВозвратаДиалога.Да Тогда
		ПрименитьНастройкуОтраженияВУчете();
		УправлениеЭлементамиСохраненияНастроекФормы(ЭтотОбъект);
	КонецЕсли;
	
	ПрименитьНастройкуФрагмент(КоличествоДокументов);

КонецПроцедуры

#КонецОбласти

#Область СохранениеДанных

&НаСервере
Процедура ЗаполнитьТаблицуКПрименениюНастроки()
	
	ТаблицаДокументов.Очистить();
	
	РегистрыСведений.ПорядокОтраженияНоменклатуры.НайтиДокументыСоответствующиеНастройкам(
		СчетаУчетаНоменклатуры,
		ДатаОкончанияПериода.Дата,
		ТаблицаДокументов);
	РегистрыСведений.ПорядокОтраженияНоменклатурыПереданной.НайтиДокументыСоответствующиеНастройкам(
		СчетаУчетаНоменклатурыПереданной,
		ДатаОкончанияПериода.Дата,
		ТаблицаДокументов);
	РегистрыСведений.ПорядокОтраженияРасчетовСПартнерами.НайтиДокументыСоответствующиеНастройкам(
		СчетаУчетаРасчетовСПартнерами,
		ДатаОкончанияПериода.Дата,
		ТаблицаДокументов);
	РегистрыСведений.ПорядокОтраженияДоходов.НайтиДокументыСоответствующиеНастройкам(
		СчетаУчетаДоходов,
		ДатаОкончанияПериода.Дата,
		ТаблицаДокументов);
	РегистрыСведений.ПорядокОтраженияРасходов.НайтиДокументыСоответствующиеНастройкам(
		СчетаУчетаРасходов,
		ДатаОкончанияПериода.Дата,
		ТаблицаДокументов);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьОбщиеСчетаУчетаНоменклатуры()
	
	СоответствиеГФУиСчетаПередачиНаКомиссию = Новый Соответствие;
	Для каждого Стр Из ОбщиеСчетаУчетаНоменклатурыПереданной Цикл
		Если ЗначениеЗаполнено(Стр.ИзмененныеДанные) И ЗначениеЗаполнено(Стр.АналитикаУчета) Тогда
			
			СтруктураСчетов = Новый Структура(Стр.ИзмененныеДанные);
			ЗаполнитьЗначенияСвойств(СтруктураСчетов, Стр);
			СоответствиеГФУиСчетаПередачиНаКомиссию.Вставить(Стр.АналитикаУчета, СтруктураСчетов);
			Стр.ИзмененныеДанные = "";
			
		КонецЕсли;
	КонецЦикла;			
	
	Для каждого СтрокаТаблицы Из ОбщиеСчетаУчетаНоменклатуры Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ИзмененныеДанные) ИЛИ Не ЗначениеЗаполнено(СтрокаТаблицы.АналитикаУчета) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы.ИзмененныеДанные = "";
		
		ИзменяемыйОбъект = СтрокаТаблицы.АналитикаУчета.ПолучитьОбъект();
		
		ЗаполнитьЗначенияСвойств(ИзменяемыйОбъект, СтрокаТаблицы);
		
		ИзмененныеСчетаПередачиНаКомиссию = СоответствиеГФУиСчетаПередачиНаКомиссию.Получить(СтрокаТаблицы.АналитикаУчета);
		Если ИзмененныеСчетаПередачиНаКомиссию <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ИзменяемыйОбъект, ИзмененныеСчетаПередачиНаКомиссию);
			СоответствиеГФУиСчетаПередачиНаКомиссию.Удалить(СтрокаТаблицы.АналитикаУчета);
		КонецЕсли;
		
		ИзменяемыйОбъект.Записать();
		
	КонецЦикла;
	
	// Записываем данные, которые не изменялись для номенклатуры, но были изменены для номенклатуры на комиссии:
	Для каждого КлючЗначениеСоответствия Из СоответствиеГФУиСчетаПередачиНаКомиссию Цикл
		
		ИзменяемыйОбъект = КлючЗначениеСоответствия.Ключ.ПолучитьОбъект();
		
		ЗаполнитьЗначенияСвойств(ИзменяемыйОбъект, КлючЗначениеСоответствия.Значение);
		
		ИзменяемыйОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьОбщиеСчетаУчета(Таблица)
	
	Для каждого СтрокаТаблицы Из Таблица Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ИзмененныеДанные) ИЛИ Не ЗначениеЗаполнено(СтрокаТаблицы.АналитикаУчета) Тогда
			Продолжить;
		КонецЕсли;
		
		ИзменяемыйОбъект = СтрокаТаблицы.АналитикаУчета.ПолучитьОбъект();
		
		ЗаполнитьЗначенияСвойств(ИзменяемыйОбъект, СтрокаТаблицы, СтрокаТаблицы.ИзмененныеДанные);
		
		ИзменяемыйОбъект.Записать();
		
		СтрокаТаблицы.ИзмененныеДанные = "";
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПорядокОтраженияСчетовУчета(ИмяТаблицы, МассивСчетовУчета)
	
	// Так как сохраняем общие счета учета в регистре сведений по пустой организации, а пользователи с ограниченными
	// правами тоже
	//	могут редактировать общие счета учета, производим запись в привелигированном режиме (счета учета по остальным организациям отсутствуют в списке выбора):
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяМетаданных = СтрЗаменить(ИмяТаблицы, "СчетаУчета", "ПорядокОтражения");
	НаборЗаписей = РегистрыСведений[ИмяМетаданных].СоздатьНаборЗаписей();
	
	ЗамещатьЗаписи = Истина;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		// Очистим записи регистра по организациям для настройки, для того чтобы реализовать замещение записей по записываемым организациям:
		ЗамещатьЗаписи = Ложь;
		ОграничениеНаУровнеЗаписей = УправлениеДоступом.РазрешенныеЗначенияДляДинамическогоСписка("РегистрСведений."+ИмяМетаданных, Тип("СправочникСсылка.Организации"));
		РедактируемыеОрганизации = Элементы.Организация.СписокВыбора.ВыгрузитьЗначения();
		Если ОграничениеНаУровнеЗаписей = Неопределено Или РедактируемыеОрганизации.Количество() <= ОграничениеНаУровнеЗаписей.Количество() Тогда
			ОрганизацииДляНастройки = РедактируемыеОрганизации;
		Иначе
			ОрганизацииДляНастройки = ОграничениеНаУровнеЗаписей;
		КонецЕсли;
		ОрганизацииДляНастройки.Добавить(Справочники.Организации.ПустаяСсылка());
		
		Для каждого ОрганизацияДляОчисткиЗаписей Из ОрганизацииДляНастройки Цикл
			НаборЗаписей.Отбор.Организация.Установить(ОрганизацияДляОчисткиЗаписей);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;
	
	НаборЗаписей.Отбор.Сбросить();
	
	Для каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицы] Цикл
		СтрокаЗаписейРегистра = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаписейРегистра, СтрокаТаблицы);
		Для каждого СчетУчета Из МассивСчетовУчета Цикл
			ЭтоСчетПоУмолчанию = СтрокаТаблицы.Свойство(СчетУчета+"ПоУмолчанию") И СтрокаТаблицы[СчетУчета+"ПоУмолчанию"];
			Если ЭтоСчетПоУмолчанию Тогда
				СтрокаЗаписейРегистра[СчетУчета] = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла;
		
		СтрокаТаблицы.ИзмененныеДанные = "";
		
	КонецЦикла;
	
	// Запишем общую настройку:
	Если ЕстьОбщиеНастройки(ИмяТаблицы) Тогда
		
		СтрокаПустойАналитики = Неопределено;
		Для каждого Стр Из ЭтотОбъект["Общие"+ИмяТаблицы] Цикл
			Если Не ЗначениеЗаполнено(Стр.АналитикаУчета) Тогда
				СтрокаПустойАналитики = Стр;
				Стр.ИзмененныеДанные = "";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если СтрокаПустойАналитики <> Неопределено Тогда
			СтрокаПустойЗаписейРегистра = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПустойЗаписейРегистра, СтрокаПустойАналитики);
		КонецЕсли;
		
		Если ИмяТаблицы = "СчетаУчетаНоменклатурыПереданной" Тогда
			СтрокаПустойАналитики = Неопределено;
			Для каждого Стр Из ЭтотОбъект["ОбщиеСчетаУчетаНоменклатуры"] Цикл
				Если Не ЗначениеЗаполнено(Стр.АналитикаУчета) Тогда
					СтрокаПустойАналитики = Стр;
					Стр.ИзмененныеДанные = "";
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если СтрокаПустойАналитики <> Неопределено Тогда
				СтрокаПустойЗаписейРегистра = ?(СтрокаПустойЗаписейРегистра = Неопределено, НаборЗаписей.Добавить(), СтрокаПустойЗаписейРегистра);
				ЗаполнитьЗначенияСвойств(СтрокаПустойЗаписейРегистра, СтрокаПустойАналитики);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	НаборЗаписей.Записать(ЗамещатьЗаписи);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтраженияВУчете()
	
	НачатьТранзакцию();
	
	Попытка
		
		ВидыРасчетовСПартнерами = ВидыРасчетовСПартнерами();
		СчетаУчетаСПартнерами = Новый Массив;
	
		Для каждого Таблица Из СтруктураТаблицФормы Цикл
			
			Если Не Таблица.Значение.Изменяется Тогда
				Продолжить;
			КонецЕсли;
			
			Если Таблица.Ключ = "СчетаУчетаДенежныхСредств" ИЛИ Таблица.Ключ = "СчетаУчетаВнеоборотныхАктивов" Тогда
				СохранитьОбщиеСчетаУчета(ЭтотОбъект[Таблица.Ключ]);
			ИначеЕсли Таблица.Ключ = "ОбщиеСчетаУчетаНоменклатуры" ИЛИ Таблица.Ключ = "ОбщиеСчетаУчетаНоменклатурыПереданной" Тогда
				СохранитьОбщиеСчетаУчетаНоменклатуры();
			ИначеЕсли ВидыРасчетовСПартнерами.Найти(СтрЗаменить(СтрЗаменить(Таблица.Ключ, "СчетаУчетаРасчетов", ""), "Общие", "")) <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчетаСПартнерами, СчетаУчетаТаблицы(Таблица.Ключ), Истина);
			Иначе
				Если СтрНачинаетсяС(Таблица.Ключ, "Общие") Тогда
					СохранитьОбщиеСчетаУчета(ЭтотОбъект[Таблица.Ключ]);
				Иначе
					СохранитьПорядокОтраженияСчетовУчета(Таблица.Ключ, СчетаУчетаТаблицы(Таблица.Ключ));
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если СчетаУчетаСПартнерами.Количество() Тогда
			СохранитьОбщиеСчетаУчета(ОбщиеСчетаУчетаРасчетовСПартнерами);
			СохранитьПорядокОтраженияСчетовУчета("СчетаУчетаРасчетовСПартнерами", СчетаУчетаСПартнерами);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось сохранить настройки отражения в регл. учете по причине: %1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Настройка отражения в регл. учете'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);		
			
		ВызватьИсключение НСтр("ru = 'Не удалось сохранить настройки отражения. Подробнее см. в Журнале регистрации'");
	КонецПопытки;
	
	Модифицированность = Ложь;
	ИзмененыНастройкиОтраженияВУчете = Истина;
	
	ЗаполнитьТаблицыНастройки();
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкуОтраженияВУчете()
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеглУчетПроведениеСервер.ВернутьДокументыКОтражению(ТаблицаДокументов.Выгрузить());
	СохранитьНастройкиОтраженияВУчете();
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительнаяОбработкаИменТаблиц

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьОбщиеНастройки(ИмяТаблицы)
	
	МассивИменТаблицНеИмеющихОбщихНастроек = Новый Массив;
	МассивИменТаблицНеИмеющихОбщихНастроек.Добавить("СчетаУчетаДенежныхСредств");
	МассивИменТаблицНеИмеющихОбщихНастроек.Добавить("СчетаУчетаПроизводства");
	МассивИменТаблицНеИмеющихОбщихНастроек.Добавить("СчетаУчетаВнеоборотныхАктивов");
	
	Возврат МассивИменТаблицНеИмеющихОбщихНастроек.Найти(ИмяТаблицы) = Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяТаблицыДанныхПоИмениТекущейТаблицы(ИмяТаблицы)
	
	СтрокаВозврата = ИмяТаблицы;
	Для каждого ВидРасчетов Из ВидыРасчетовСПартнерами() Цикл
		СтрокаВозврата = СтрЗаменить(СтрокаВозврата, ВидРасчетов, "СПартнерами");
	КонецЦикла;
	
	Возврат СтрокаВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВидыРасчетовСПартнерами()
	
	МассивВозврата = Новый Массив;
	
	МассивВозврата.Добавить("СКлиентами");
	МассивВозврата.Добавить("СПоставщиками");
	МассивВозврата.Добавить("СКомиссионерами");
	МассивВозврата.Добавить("СКомитентами");
	МассивВозврата.Добавить("СКредиторами");
	МассивВозврата.Добавить("СДебиторами");
	МассивВозврата.Добавить("СЛизингодателями");
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяТекущейТаблицы(ИмяТекущейСтраницы, ИмяТекущейТаблицы)
	
	СтрокаВозврата = СтрЗаменить(ИмяТекущейСтраницы, "Страница", "");
	
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, "СчетаУчетаРасчетовСПартнерами", ИмяТекущейТаблицы);
	
	Возврат СтрокаВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяАналитикиУчетаПоТаблице(ИмяТаблицы)
	
	Если СтрНачинаетсяС(ИмяТаблицы, "Общие") Тогда
		Возврат "АналитикаУчета";
	Иначе
		СоответствиеТаблицИсключенийИИменАналитикУчета = Новый Соответствие;
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаНоменклатуры", "ГруппаФинансовогоУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаНоменклатурыПереданной", "ГруппаФинансовогоУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаТМЦВЭксплуатации", "КатегорияЭксплуатации");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаПодарочныхСертификатов", "ВидСертификата");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаРасчетовСПартнерами", "ГруппаФинансовогоУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаРасходов", "СтатьяРасходов");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаДоходов", "СтатьяДоходов");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаРезервов", "ВидРезервов");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаВнеоборотныхАктивов", "АналитикаУчета");
		СоответствиеТаблицИсключенийИИменАналитикУчета.Вставить("СчетаУчетаДенежныхСредств", "АналитикаУчета");
	
		Возврат СоответствиеТаблицИсключенийИИменАналитикУчета.Получить(ИмяТаблицы);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти