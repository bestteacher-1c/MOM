
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
	Текст = ПрограммныйИнтерфейсСервиса.СтраницаВыбораТарифаОбслуживающейОрганизации(
		Параметры.КодОбслуживающейОрганизации);
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы
    
&НаКлиенте
Процедура ТекстПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
    
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
	// Ссылка "#" отличается в тонком и веб-клиенте, но левая часть должна быть равна.
	Если Лев(ДанныеСобытия.Href, СтрДлина(НавигационнаяСсылка)) <> НавигационнаяСсылка Тогда
		Возврат;
	КонецЕсли;

	ДанныеВыбора = Новый Структура;
	ДанныеВыбора.Вставить("Запрос", "");
	ДанныеВыбора.Вставить("КодТарифа", "");
	ДанныеВыбора.Вставить("КодПериода", "");

	НаборДанных = ДанныеСобытия.Anchor.dataset;
	Если НаборДанных = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не заданы параметры ''data-''.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	// При обращении к несуществующему параметру, параметр создается.
	ДанныеВыбора.Запрос = НаборДанных.query;
	Если Не ЗначениеЗаполнено(ДанныеВыбора.Запрос) Тогда
		ДанныеВыбора.КодТарифа = НаборДанных.tariff;
		Если Не ЗначениеЗаполнено(ДанныеВыбора.КодТарифа) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не определен параметр ''data-tariff''.'");
			Сообщение.Сообщить();
		КонецЕсли; 
		ДанныеВыбора.КодПериода = НаборДанных.period;
		Если Не ЗначениеЗаполнено(ДанныеВыбора.КодПериода) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не определен параметр ''data-period''.'");
			Сообщение.Сообщить();
		КонецЕсли; 
	КонецЕсли; 
	СтандартнаяОбработка = Ложь;
	Закрыть(ДанныеВыбора);	
    
КонецПроцедуры

#КонецОбласти 
