#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Команды

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.СправкаРасчетАмортизацииНМА2_4.ДобавитьКомандуОтчетаПоДокументуАмортизацииНМА(КомандыОтчетов);
	
	АмортизацияНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	АмортизацияНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
	АмортизацияНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область Прочее

// Формирует тексты запросов, которые создают таблицы объектов учета, по которым требуется расчет амортизации.
//
// Параметры:
//  СписокЗапросов	 - Массив	 - Список запросов, в который нужно добавить текст запроса.
//  ПараметрыРасчета - Структура - Содержит параметры расчета амортизации.
//  ПрефиксТаблицы	 - Строка	 - Используется для добавления префикса к созданным таблицам.
//
Процедура ТекстЗапросаСписокВсехНМА(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы = "") Экспорт
		
	#Область ВТ_СписокОбъектовУчета_Доначисление_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьНМА.Организация         КАК Организация,
		|	СтоимостьНМА.НематериальныйАктив КАК НематериальныйАктив
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_Доначисление_УУ
		|ИЗ
		|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
		|ГДЕ
		|	СтоимостьНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьНМА.Активность
		|	И СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И СтоимостьНМА.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА))
		|	И (СтоимостьНМА.Организация, СтоимостьНМА.НематериальныйАктив) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьНМА.Организация,
		|	СтоимостьНМА.НематериальныйАктив
		|
		|ИМЕЮЩИЕ
		|	СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0
		|		ИЛИ НЕ &ВедетсяРегламентированныйУчетВНА 
		|			И СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив,
		|	Организация";
		
		Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_Доначисление_УУ", "ВТ_СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
		КонецЕсли; 
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;	
	
	#КонецОбласти
	
	#Область ВТ_СписокОбъектовУчета_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&КонецМесяца                        КАК ПериодНачисления,
		|	&НачалоМесяца                       КАК НачалоМесяца,
		|	&КонецМесяца                        КАК КонецМесяца,
		|	ЛОЖЬ                                КАК ЭтоДоначисление,
		|	ПорядокУчетаНМА.Организация         КАК Организация,
		|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_УУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, НематериальныйАктив) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаНМА
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                        КАК ПериодНачисления,
		|	&НачалоПредыдущегоМесяца            КАК НачалоМесяца,
		|	&КонецПредыдущегоМесяца             КАК КонецМесяца,
		|	ИСТИНА                              КАК ЭтоДоначисление,
		|	ПорядокУчетаНМА.Организация         КАК Организация,
		|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
		|			&КонецПозапрошлогоМесяца,
		|			(Организация, НематериальныйАктив) В
		|				(ВЫБРАТЬ
		|					СписокНМА.Организация,
		|					СписокНМА.НематериальныйАктив
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)) КАК ПорядокУчетаНМА
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                        КАК ПериодНачисления,
		|	&НачалоМесяца                       КАК НачалоМесяца,
		|	&КонецМесяца                        КАК КонецМесяца,
		|	ЛОЖЬ                                КАК ЭтоДоначисление,
		|	ПорядокУчетаНМА.Организация         КАК Организация,
		|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМА
		|ГДЕ
		|	ПорядокУчетаНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И (ПорядокУчетаНМА.Организация, ПорядокУчетаНМА.НематериальныйАктив) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                        КАК ПериодНачисления,
		|	&НачалоПредыдущегоМесяца            КАК НачалоМесяца,
		|	&КонецПредыдущегоМесяца             КАК КонецМесяца,
		|	ИСТИНА                              КАК ЭтоДоначисление,
		|	ПорядокУчетаНМА.Организация         КАК Организация,
		|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМА
		|ГДЕ
		|	ПорядокУчетаНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И (Организация, НематериальныйАктив) В
		|				(ВЫБРАТЬ
		|					СписокНМА.Организация,
		|					СписокНМА.НематериальныйАктив
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&КонецМесяца                        КАК ПериодНачисления,
		|	&НачалоМесяца                       КАК НачалоМесяца,
		|	&КонецМесяца                        КАК КонецМесяца,
		|	ЛОЖЬ                                КАК ЭтоДоначисление,
		|	ПорядокУчетаНМА.Организация         КАК Организация,
		|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|
		|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|	
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_УУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, НематериальныйАктив) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаНМА";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив, 
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_УУ", "ВТ_СписокОбъектовУчета_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_Доначисление_УУ", "ВТ_СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаСписокВсехНМА(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'Амортизация НМА %1 за %2'"), Данные.Номер, Формат(Данные.Дата, "ДФ='ММММ гггг'"));
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет расчет амортизации в заданном месяце и записывает его с помощью проведения документа "Амортизация НМА".
//
// Параметры:
//  ПараметрыРасчета - Структура - Содержит параметры расчета (см. НовыеПараметрыРасчетаАмортизации()).
//
// Возвращаемое значение:
//  ДокументСсылка.АмортизацияНМА2_4, Неопределено - Ссылка на созданный документ, если расчет успешно выполнен.
//
Процедура РассчитатьАмортизацию(ИсходныеПараметрыРасчета, АдресХранилища) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ЗакрытиеМесяца.НачислениеАмортизацииНМА");
	
	ПараметрыРасчета = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ИсходныеПараметрыРасчета);
	ВнеоборотныеАктивыСлужебный.ДополнитьПараметрамиУчетаВнеоборотныхАктивов(ПараметрыРасчета);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИсходныеПараметрыРасчета.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Основной расчет амортизации выполняется с помощью запроса
	ТекстЗапроса = ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);
	
	КоличествоОбработанныхДанных = 0;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		// Расчет доначисления амортизации за прошлый месяц
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(РезультатЗапроса, ПараметрыРасчета);
		
		Запрос.Текст = ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.Выполнить();
		
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(РезультатЗапроса, ПараметрыРасчета);
		
		Запрос.Текст = ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		КоличествоОбработанныхДанных = Выборка.КоличествоДанных;
		
		ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		
		АмортизацияНМАЛокализация.ПослеРасчетаАмортизации(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц);
		
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц,, "ВТ_НачисленнаяАмортизация,НомераПакетовСФилиалами");
		
		Если ПараметрыРасчета.ЗаписатьДанные Тогда
			ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		КонецЕсли;
		
	Исключение
		
		ПараметрыРасчета.ЕстьОшибки = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета.ЭтапРасчета, 
			ПараметрыРасчета.Период, 
			ПараметрыРасчета.ПакетыАмортизации.ВыгрузитьКолонку("Организация"),
			ТекстОшибки);
	
	КонецПопытки; 
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОбработанныхДанных / 100);
	
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		ПоместитьВоВременноеХранилище(ПараметрыРасчета.ЕстьОшибки, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

Функция НомерПакетаПоУмолчанию(Организация) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПакетыАмортизацииНМА.НомерПакета) КАК НомерПакета,
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПакетыАмортизацииНМА.НомерПакета), 0) КАК КоличествоНомеров
	|ИЗ
	|	РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизацииНМА
	|ГДЕ
	|	ПакетыАмортизацииНМА.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыАмортизацииНМА.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НомерПакета = 0;
	Если Выборка.Следующий() Тогда
		Если Выборка.КоличествоНомеров = 1 Тогда
			НомерПакета = Выборка.НомерПакета;
		КонецЕсли; 
	КонецЕсли;

	Возврат НомерПакета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.АмортизацияНМА2_4";
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаАмортизацияНМА(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСтоимостьНМА(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаПартииПрочихРасходов_Амортизация(Запрос, ТекстыЗапроса, Регистры);
	
	АмортизацияНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Дата             КАК Период,
	|	ДанныеДокумента.Номер            КАК Номер,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления  КАК ПометкаУдаления,
	|	ДанныеДокумента.Комментарий      КАК Комментарий,
	|	ДанныеДокумента.Ответственный    КАК Ответственный,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.НомерПакета      КАК НомерПакета
	|ИЗ
	|	Документ.АмортизацияНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.АмортизацияНМА2_4"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация",  Перечисления.ХозяйственныеОперации.АмортизацияНМА);
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Амортизация НМА'"));
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаАмортизацияНМА(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияНМА";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК НематериальныйАктив,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Амортизация,
	|	НачисленнаяАмортизация.СуммаБУ    КАК АмортизацияРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизацияНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК АмортизацияПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК АмортизацияВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК АмортизацияЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК АмортизацияНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК АмортизацияВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление
	|	
	|ИЗ
	|	ВТ_НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьНМА(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьНМА";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК НематериальныйАктив,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Стоимость,
	|	НачисленнаяАмортизация.СуммаБУ    КАК СтоимостьРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК СтоимостьНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК СтоимостьПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК СтоимостьВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК СтоимостьЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК СтоимостьНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК СтоимостьПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК СтоимостьВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление
	|
	|ИЗ
	|	ВТ_НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период  КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	СписокЗапросовОбъединение = Новый Массив;
	
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПрочихРасходах());
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область РасчетАмортизации

Процедура ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц)

	НачатьТранзакцию();
	
	Попытка
	
		// Нужно заблокировать документы, чтобы не создать дублей
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("Документ.АмортизацияНМА2_4");
		ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(НачалоМесяца(ПараметрыРасчета.Период),КонецМесяца(ПараметрыРасчета.Период)));
		ЭлементБлокировки.ИсточникДанных = ПараметрыРасчета.ПакетыАмортизации;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НомерПакета", "НомерПакета");
		
		Блокировка.Заблокировать(); 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияНМА2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияНМА2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаНеПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.ПометкаУдаления, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияНМА2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаУдален
		|ИЗ
		|	НомераПакетовСФилиалами КАК НомераПакетовСФилиалами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АмортизацияНМА2_4 КАК РегламентныйДокумент
		|		ПО (РегламентныйДокумент.Организация = НомераПакетовСФилиалами.Организация)
		|			И (РегламентныйДокумент.НомерПакета = НомераПакетовСФилиалами.НомерПакета)
		|			И (РегламентныйДокумент.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца)
		|ГДЕ
		|	НомераПакетовСФилиалами.Организация В(&СписокОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("КонецМесяца",  КонецМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НомерПакета = ПараметрыРасчета.НомерПакета Тогда
				
				// Расчет выполняется из формы документа.
				Если ЗначениеЗаполнено(ПараметрыРасчета.Ссылка) Тогда
					ДокументОбъект = ПараметрыРасчета.Ссылка.ПолучитьОбъект();
				Иначе 
					ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
						Выборка.СсылкаПроведен, Неопределено, Неопределено, Документы.АмортизацияНМА2_4);
				КонецЕсли;
				
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Комментарий   = ПараметрыРасчета.Комментарий;
				ДокументОбъект.Ответственный = ПараметрыРасчета.Ответственный;
				
			Иначе	
				
				ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
					Выборка.СсылкаПроведен, Выборка.СсылкаНеПроведен, Выборка.СсылкаУдален, Документы.АмортизацияНМА2_4);
					
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
			КонецЕсли; 
				
			ДокументОбъект.Дата = КонецМесяца(ПараметрыРасчета.Период);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			ВнеоборотныеАктивыСлужебный.УничтожитьВременныеТаблицыПослеЗаписиРасчетаАмортизации(МенеджерВременныхТаблиц);
			
		КонецЦикла; 
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
КонецПроцедуры

Функция РассчитатьАмортизациюИзменений(РезультатЗапроса, ПараметрыРасчета)

	АмортизацияИзменений = РезультатЗапроса.Выгрузить();
	АмортизацияИзменений.Сортировать("НематериальныйАктив,Период");
	
	ТекущееНМА = Неопределено;
	НачисленнаяАмортизация = 0;
	НачисленнаяАмортизацияРегл = 0;
	РесурсВыработанныйЗаПрошлыйПериод = 0;
	РесурсНазначенный = 0;
	
	Для каждого ДанныеСтроки Из АмортизацияИзменений Цикл
		
		ЕстьОшибки = Ложь;
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определена остаточная стоимость. Необходимо проверить первичные документы.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.ОстаточнаяСтоимость, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определен срок использования. Необходимо проверить первичные документы.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.РесурсНазначенный, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определено количество прошедших месяцев.
                               |Необходимо проверить первичные документы.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определена ликвидационная стоимость. Необходимо проверить первичные документы.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.ЛиквидационнаяСтоимость, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определен метод начисления амортизации. Необходимо проверить первичные документы.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.МетодНачисленияАмортизации, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		Если ЕстьОшибки Тогда
			Продолжить;
		КонецЕсли;
		
		Если РесурсНазначенный <> ДанныеСтроки.РесурсНазначенный Тогда
			РесурсВыработанныйЗаПрошлыйПериод = 0;
			РесурсНазначенный = ДанныеСтроки.РесурсНазначенный;
		КонецЕсли;
		
		Если ДанныеСтроки.НематериальныйАктив <> ТекущееНМА Тогда
			НачисленнаяАмортизация = 0;
			НачисленнаяАмортизацияРегл = 0;
			РесурсВыработанныйЗаПрошлыйПериод = 0;
			ТекущееНМА = ДанныеСтроки.НематериальныйАктив;
		КонецЕсли; 
		
		Если ДанныеСтроки.ЭтоИзменения Тогда

			ДанныеСтроки.ОстаточнаяСтоимость = Макс(ДанныеСтроки.ОстаточнаяСтоимость - НачисленнаяАмортизация, 0);
			ДанныеСтроки.ОстаточнаяСтоимостьРегл = Макс(ДанныеСтроки.ОстаточнаяСтоимостьРегл - НачисленнаяАмортизацияРегл, 0);
			
			ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод = ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + РесурсВыработанныйЗаПрошлыйПериод;
			
			ДанныеСтроки.СуммаУУ = РассчитатьСуммуАмортизацииУУ(ДанныеСтроки, ДанныеСтроки.ОстаточнаяСтоимость, ДанныеСтроки.ЛиквидационнаяСтоимость);
			ДанныеСтроки.СуммаБУ = РассчитатьСуммуАмортизацииУУ(ДанныеСтроки, ДанныеСтроки.ОстаточнаяСтоимостьРегл, ДанныеСтроки.ЛиквидационнаяСтоимостьРегл);
			 
		КонецЕсли;
		
		НачисленнаяАмортизация = НачисленнаяАмортизация + ДанныеСтроки.СуммаУУ;
		НачисленнаяАмортизацияРегл = НачисленнаяАмортизацияРегл + ДанныеСтроки.СуммаБУ;
		
		РесурсВыработанныйЗаПрошлыйПериод = РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод;
		
	КонецЦикла; 
	
	Возврат АмортизацияИзменений;

КонецФункции

Функция РассчитатьСуммуАмортизацииУУ(ДанныеСтроки, ОстаточнаяСтоимость, ЛиквидационнаяСтоимость)

	СуммаУУ = 0;
	
	ОстаточнаяСтоимостьПослеНачисленияАмортизации = ОстаточнаяСтоимость - ЛиквидационнаяСтоимость;
		
	Если НЕ ДанныеСтроки.НачислятьАмортизацию 
		ИЛИ ОстаточнаяСтоимостьПослеНачисленияАмортизации <= 0 Тогда
		
		СуммаУУ = 0;
		
	ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
		
		СуммаУУ = ОстаточнаяСтоимость;
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка
			И (ДанныеСтроки.РесурсНазначенный
				- ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод
				- ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) <= 12 Тогда
		
		СуммаУУ = ОстаточнаяСтоимость
					* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда
		
		СуммаУУ = ОстаточнаяСтоимость
					* ДанныеСтроки.КоэффициентУскорения
					* ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.КоэффициентУскорения 
							*(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод - ДанныеСтроки.ОтработанныхМесяцевПолныхЛет));
	КонецЕсли; 
	
	Если СуммаУУ > ОстаточнаяСтоимостьПослеНачисленияАмортизации
		ИЛИ (ОстаточнаяСтоимостьПослеНачисленияАмортизации - СуммаУУ) < ВнеоборотныеАктивыСлужебный.МинимальнаяОстаточнаяСтоимость() Тогда
		СуммаУУ = Макс(ОстаточнаяСтоимостьПослеНачисленияАмортизации, 0);
	КонецЕсли; 
	
	Возврат Окр(СуммаУУ, 2);
	
КонецФункции
 
Процедура ПроверитьПараметрРасчета(ПараметрРасчета, ШаблонСообщения, Выборка, ПараметрыРасчета, ЕстьОшибки)

	Если ПараметрРасчета <> NULL Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибки = Истина;
	
	ТекстОшибки = СтрШаблон(ШаблонСообщения, Выборка.НематериальныйАктив, Выборка.НематериальныйАктивКод);
	
	ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
		ПараметрыРасчета.ЭтапРасчета, 
		ПараметрыРасчета.Период,
		Выборка.Организация,
		ТекстОшибки,,
		Выборка.НематериальныйАктив,
		ПараметрыРасчета.ЕстьОшибки);
	
КонецПроцедуры

Процедура ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц) Экспорт

	ТекстЗапроса = АмортизацияНМАЛокализация.ТекстЗапросаЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПроверкаПараметровРасчета.Организация КАК Организация,
		|	ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
		|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.НематериальныеАктивы).Наименование КАК ОбъектУчетаПредставление,
		|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.НематериальныеАктивы).Код КАК Код,
		|	ЛОЖЬ НеУказанаАмортизационнаяГруппа,
		|	ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки
		|ИЗ
		|	ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
		|ГДЕ
		|	ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
		|УПОРЯДОЧИТЬ ПО
		|	ПроверкаПараметровРасчета.ОбъектУчета";
		
	КонецЕсли; 
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонАмортизационнаяГруппа  = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не указана амортизационная группа.'");
	ШаблонОбъемНаработки         = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не указан предполагаемый объем наработки (продукции, работ).'");
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеУказанОбъемНаработки Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработки, Выборка.ОбъектУчетаПредставление, Выборка.Код);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		АмортизацияНМАЛокализация.ЗарегистрироватьОшибкиРасчетаАмортизации(
			Выборка, ПараметрыРасчета, ШаблонАмортизационнаяГруппа);
	
	КонецЦикла;
	
КонецПроцедуры

#Область ТекстыЗапросовДляРасчетаАмортизации

Функция ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запросов, в которых подготавливаются параметры для расчета
	ТекстЗапросаПакетыАмортизации(СписокЗапросов);
	ТекстЗапросаСписокВсехНМА(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета);
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета);
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов);
	
	// Выборка должна быть последней
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПакетыАмортизации(СписокЗапросов)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетов
	|ИЗ
	|	&ПакетыАмортизации КАК ПакетыАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераПакетов.Организация КАК Организация,
	|	НомераПакетов.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетовСФилиалами
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Филиалы.Ссылка,
	|	НомераПакетов.НомерПакета
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Филиалы
	|		ПО (Филиалы.ГоловнаяОрганизация = НомераПакетов.Организация)
	|			И (НЕ Филиалы.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НомерПакета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.НематериальныйАктив КАК ОбъектУчета
	|ПОМЕСТИТЬ ПакетыАмортизации
	|ИЗ
	|	НомераПакетовСФилиалами КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизации
	|		ПО (ПакетыАмортизации.Организация = НомераПакетов.Организация)
	|			И (ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияНМАЛокализация.ТекстЗапросаСписокОбъектовУчета(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокНМА.НачалоМесяца                        КАК НачалоМесяца,
		|	СписокНМА.КонецМесяца                         КАК КонецМесяца,
		|	СписокНМА.Организация                         КАК Организация,
		|	СписокНМА.НематериальныйАктив                 КАК НематериальныйАктив,
		|	СписокНМА.СпособОтраженияРасходовУУ           КАК СпособОтраженияРасходов,
		|	ЕСТЬNULL(ПакетыАмортизацииНМА.НомерПакета, 0) КАК НомерПакета
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета
		|ИЗ
		|	ВТ_СписокОбъектовУчета_УУ КАК СписокНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизацииНМА
		|		ПО ПакетыАмортизацииНМА.Организация = СписокНМА.Организация
		|			И ПакетыАмортизацииНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив,
		|	Организация";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета)

	ОбъектУчета = "НематериальныйАктив";
	ДобавитьТекстДляДоначисления = ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"МестоУчетаНМА", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПервоначальныеСведенияНМА", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПорядокУчетаНМА", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПараметрыАмортизацииНМАУУ", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	#Область СтоимостьНМАНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                             КАК НачалоМесяца,
	|	СтоимостьНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	СтоимостьНМА.СтоимостьОстаток             КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРеглОстаток         КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУОстаток           КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПРОстаток           КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВРОстаток           КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФОстаток           КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФОстаток         КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФОстаток         КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФОстаток         КАК СтоимостьВРЦФ
	|ПОМЕСТИТЬ ВТ_СтоимостьНМА_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					ВТ_СписокОбъектовУчета КАК СписокНМА)) КАК СтоимостьНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьНМА.НематериальныйАктив";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
		
	#Область ПредварительнаяСтоимостьНМА
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                                     КАК НачалоМесяца,
	|	СтоимостьНМА.НематериальныйАктив                  КАК НематериальныйАктив,
	|	СУММА(СтоимостьНМА.ПредварительнаяСтоимость)      КАК Стоимость,
	|	СУММА(СтоимостьНМА.ПредварительнаяСтоимостьРегл)  КАК СтоимостьРегл
	|ПОМЕСТИТЬ ВТ_ПредварительнаяСтоимостьНМА
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|ГДЕ
	|	СтоимостьНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И (СтоимостьНМА.ПредварительнаяСтоимость <> 0
	|		ИЛИ СтоимостьНМА.ПредварительнаяСтоимостьРегл <> 0)
	|	И СтоимостьНМА.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации))
	|	И СтоимостьНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				ВТ_СписокОбъектовУчета КАК СписокНМА)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьНМА.НематериальныйАктив";
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НачалоПредыдущегоМесяца              КАК НачалоМесяца,
		|	СтоимостьНМА.НематериальныйАктив      КАК НематериальныйАктив,
		|	ВЫБОР
		|		КОГДА СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0
		|			ТОГДА СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл) > 0
		|			ТОГДА СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьРегл
		|ИЗ
		|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
		|ГДЕ
		|	СтоимостьНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьНМА.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации))
		|	И СтоимостьНМА.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.НематериальныйАктив
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьНМА.НематериальныйАктив
		|
		|ИМЕЮЩИЕ 
		|	СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0
		|		ИЛИ СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьНМА.НематериальныйАктив";
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизацияНМАНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                           КАК НачалоМесяца,
	|	АмортизацияНМА.Организация              КАК Организация,
	|	АмортизацияНМА.НематериальныйАктив      КАК НематериальныйАктив,
	|	-АмортизацияНМА.АмортизацияОстаток      КАК Амортизация,
	|	-АмортизацияНМА.АмортизацияРеглОстаток  КАК АмортизацияРегл,
	|	-АмортизацияНМА.АмортизацияНУОстаток    КАК АмортизацияНУ,
	|	-АмортизацияНМА.АмортизацияПРОстаток    КАК АмортизацияПР,
	|	-АмортизацияНМА.АмортизацияВРОстаток    КАК АмортизацияВР,
	|	-АмортизацияНМА.АмортизацияЦФОстаток    КАК АмортизацияЦФ,
	|	-АмортизацияНМА.АмортизацияНУЦФОстаток  КАК АмортизацияНУЦФ,
	|	-АмортизацияНМА.АмортизацияПРЦФОстаток  КАК АмортизацияПРЦФ,
	|	-АмортизацияНМА.АмортизацияВРЦФОстаток  КАК АмортизацияВРЦФ
	|ПОМЕСТИТЬ ВТ_АмортизацияНМА_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.АмортизацияНМА.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					ВТ_СписокОбъектовУчета КАК СписокНМА)) КАК АмортизацияНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АмортизацияНМА.НематериальныйАктив,
	|	АмортизацияНМА.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
				
	#Область ВыработкаНМА
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ) = МЕСЯЦ(&НачалоМесяца) - 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ ВТ_СписокНМА_Выработка
	|ИЗ
	|	ВТ_СписокОбъектовУчета КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.Организация = СписокНМА.Организация)
	|			И (ПервоначальныеСведенияНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПервоначальныеСведенияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО (ПорядокУчетаНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПорядокУчетаНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО (ПараметрыАмортизацииНМАУУ.Организация = СписокНМА.Организация)
	|			И (ПараметрыАмортизацииНМАУУ.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУ.НачалоМесяца = СписокНМА.НачалоМесяца)
	|ГДЕ
	|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|		ИЛИ ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|	ВыработкаНМА.КоличествоОборот     КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВТ_ВыработкаНМА
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА.Обороты(
	|			&НачалоМесяца,
	|			&КонецМесяца,,
	|			НематериальныйАктив В
	|				(ВЫБРАТЬ
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					ВТ_СписокНМА_Выработка КАК СписокНМА)) КАК ВыработкаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
	
	Если НЕ ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СписокНМА_Выработка";
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область Изменения_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПараметрыАмортизацииНМАУУ.Период, ДЕНЬ) КАК Период,
		|	ПараметрыАмортизацииНМАУУ.Регистратор,
		|	ПараметрыАмортизацииНМАУУ.НематериальныйАктив,
		|	ПараметрыАмортизацииНМАУУ.Организация,
		|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
		|	ПараметрыАмортизацииНМАУУ.КоэффициентУскорения КАК КоэффициентУскорения,
		|	ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл
		|ПОМЕСТИТЬ ПараметрыАмортизацииНМАУУИзменения
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
		|ГДЕ
		|	ПараметрыАмортизацииНМАУУ.Активность
		|	И ПараметрыАмортизацииНМАУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПараметрыАмортизацииНМАУУ.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокНМА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПараметрыАмортизацииНМАУУ.Период, ДЕНЬ) КАК Период,
		|	ПараметрыАмортизацииНМАУУ.Регистратор,
		|	ПараметрыАмортизацииНМАУУ.НематериальныйАктив,
		|	ПараметрыАмортизацииНМАУУ.Организация,
		|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
		|	ПараметрыАмортизацииНМАУУ.КоэффициентУскорения КАК КоэффициентУскорения,
		|	ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
		|ГДЕ
		|	ПараметрыАмортизацииНМАУУ.Активность
		|	И ПараметрыАмортизацииНМАУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПараметрыАмортизацииНМАУУ.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.НематериальныйАктив
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМА.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаНМА.Регистратор,
		|	ПорядокУчетаНМА.НематериальныйАктив,
		|	ПорядокУчетаНМА.Организация,
		|	ПорядокУчетаНМА.ГруппаФинансовогоУчета,
		|	ПорядокУчетаНМА.НаправлениеДеятельности,
		|	ПорядокУчетаНМА.ОбъемНаработки
		|ПОМЕСТИТЬ ПорядокУчетаНМАИзменения
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМА КАК ПорядокУчетаНМА
		|ГДЕ
		|	ПорядокУчетаНМА.Активность
		|	И ПорядокУчетаНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПорядокУчетаНМА.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокНМА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМА.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаНМА.Регистратор,
		|	ПорядокУчетаНМА.НематериальныйАктив,
		|	ПорядокУчетаНМА.Организация,
		|	ПорядокУчетаНМА.ГруппаФинансовогоУчета,
		|	ПорядокУчетаНМА.НаправлениеДеятельности,
		|	ПорядокУчетаНМА.ОбъемНаработки
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМА КАК ПорядокУчетаНМА
		|ГДЕ
		|	ПорядокУчетаНМА.Активность
		|	И ПорядокУчетаНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПорядокУчетаНМА.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.НематериальныйАктив
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМАУУ.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаНМАУУ.Регистратор,
		|	ПорядокУчетаНМАУУ.НематериальныйАктив,
		|	ПорядокУчетаНМАУУ.Организация,
		|	ПорядокУчетаНМАУУ.Состояние,
		|	ПорядокУчетаНМАУУ.НачислятьАмортизациюУУ,
		|	ПорядокУчетаНМАУУ.СпособОтраженияРасходов,
		|	ПорядокУчетаНМАУУ.СтатьяРасходов,
		|	ПорядокУчетаНМАУУ.АналитикаРасходов,
		|	ПорядокУчетаНМАУУ.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаНМАУУ.ОрганизацияПолучательРасходов
		|ПОМЕСТИТЬ ПорядокУчетаНМАУУИзменения
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМАУУ
		|ГДЕ
		|	ПорядокУчетаНМАУУ.Активность
		|	И ПорядокУчетаНМАУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
		|	И (ПорядокУчетаНМАУУ.Организация, ПорядокУчетаНМАУУ.НематериальныйАктив) В
		|			(ВЫБРАТЬ
		|				СписокНМА.Организация,
		|				СписокНМА.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокНМА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМАУУ.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаНМАУУ.Регистратор,
		|	ПорядокУчетаНМАУУ.НематериальныйАктив,
		|	ПорядокУчетаНМАУУ.Организация,
		|	ПорядокУчетаНМАУУ.Состояние,
		|	ПорядокУчетаНМАУУ.НачислятьАмортизациюУУ,
		|	ПорядокУчетаНМАУУ.СпособОтраженияРасходов,
		|	ПорядокУчетаНМАУУ.СтатьяРасходов,
		|	ПорядокУчетаНМАУУ.АналитикаРасходов,
		|	ПорядокУчетаНМАУУ.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаНМАУУ.ОрганизацияПолучательРасходов
		|ИЗ
		|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМАУУ
		|ГДЕ
		|	ПорядокУчетаНМАУУ.Активность
		|	И ПорядокУчетаНМАУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
		|	И (ПорядокУчетаНМАУУ.Организация, ПорядокУчетаНМАУУ.НематериальныйАктив) В
		|			(ВЫБРАТЬ
		|				СписокНМА.Организация,
		|				СписокНМА.НематериальныйАктив
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(МестоУчетаНМА.Период, ДЕНЬ) КАК Период,
		|	МестоУчетаНМА.Регистратор,
		|	МестоУчетаНМА.НематериальныйАктив,
		|	МестоУчетаНМА.Организация,
		|	МестоУчетаНМА.Подразделение
		|ПОМЕСТИТЬ МестоУчетаНМАИзменения
		|ИЗ
		|	РегистрСведений.МестоУчетаНМА КАК МестоУчетаНМА
		|ГДЕ
		|	МестоУчетаНМА.Активность
		|	И МестоУчетаНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И МестоУчетаНМА.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокНМА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(МестоУчетаНМА.Период, ДЕНЬ) КАК Период,
		|	МестоУчетаНМА.Регистратор,
		|	МестоУчетаНМА.НематериальныйАктив,
		|	МестоУчетаНМА.Организация,
		|	МестоУчетаНМА.Подразделение
		|ИЗ
		|	РегистрСведений.МестоУчетаНМА КАК МестоУчетаНМА
		|ГДЕ
		|	МестоУчетаНМА.Активность
		|	И МестоУчетаНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
		|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И МестоУчетаНМА.НематериальныйАктив В
		|			(ВЫБРАТЬ
		|				СписокНМА.НематериальныйАктив
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	ТаблицаИзменений.Период КАК Период,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаИзменений.Период, МЕСЯЦ, 1), МЕСЯЦ) КАК НачалоСледующегоМесяца,
		|	ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
		|	ТаблицаИзменений.Организация КАК Организация,
		|	МАКСИМУМ(ПараметрыАмортизацииНМАУУПоследнее.Период) КАК ПараметрыАмортизацииНМАУУПоследнееПериод,
		|	МАКСИМУМ(ПорядокУчетаНМАИзмененияПоследнее.Период) КАК ПорядокУчетаНМАИзмененияПоследнееПериод,
		|	МАКСИМУМ(ПорядокУчетаНМАУУИзмененияПоследнее.Период) КАК ПорядокУчетаНМАУУИзмененияПоследнееПериод,
		|	МАКСИМУМ(МестоУчетаНМАИзмененияПоследнее.Период) КАК МестоУчетаНМАИзмененияПоследнееПериод
		|ПОМЕСТИТЬ ТаблицаИзменений
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период КАК Период,
		|		ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
		|		ТаблицаИзменений.Организация КАК Организация
		|	ИЗ
		|		ПараметрыАмортизацииНМАУУИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.НематериальныйАктив,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		ПорядокУчетаНМАИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.НематериальныйАктив,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		ПорядокУчетаНМАУУИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.НематериальныйАктив,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		МестоУчетаНМАИзменения КАК ТаблицаИзменений
		|
		|	) КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУИзменения КАК ПараметрыАмортизацииНМАУУПоследнее
		|		ПО (ПараметрыАмортизацииНМАУУПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПараметрыАмортизацииНМАУУПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ПараметрыАмортизацииНМАУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПараметрыАмортизацииНМАУУПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАИзменения КАК ПорядокУчетаНМАИзмененияПоследнее
		|		ПО (ПорядокУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ПорядокУчетаНМАИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаНМАИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАУУИзменения КАК ПорядокУчетаНМАУУИзмененияПоследнее
		|		ПО (ПорядокУчетаНМАУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаНМАУУИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМАИзменения КАК МестоУчетаНМАИзмененияПоследнее
		|		ПО (МестоУчетаНМАИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (МестоУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (МестоУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (МестоУчетаНМАИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.Период,
		|	ТаблицаИзменений.НематериальныйАктив,
		|	ТаблицаИзменений.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.НематериальныйАктив,
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	МИНИМУМ(ТаблицаИзмененийСледующее.Период) КАК ПериодСледующее
		|ПОМЕСТИТЬ ВТ_ТаблицаИзмененийСледующее
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзменений КАК ТаблицаИзмененийСледующее
		|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийСледующее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ТаблицаИзмененийСледующее.Период > ТаблицаИзменений.Период
		|				ИЛИ ТаблицаИзмененийСледующее.Период = ТаблицаИзменений.Период
		|						И ТаблицаИзмененийСледующее.Регистратор > ТаблицаИзменений.Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.НематериальныйАктив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.НематериальныйАктив,
		|	МИНИМУМ(ТаблицаИзменений.Период) КАК ПериодСледующее
		|ПОМЕСТИТЬ ВТ_ТаблицаИзмененийВЭтомМесяце
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	(ТаблицаИзменений.НачалоМесяца = &НачалоМесяца)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.НематериальныйАктив
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	КОНЕЦПЕРИОДА(ТаблицаИзменений.НачалоМесяца, МЕСЯЦ) КАК КонецМесяца,
		|	КОНЕЦПЕРИОДА(ТаблицаИзменений.Период, ДЕНЬ) КАК ДатаИзменений,
		|	ВЫБОР 
		|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
		|			ТОГДА МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца)) 
		|		ИНАЧЕ МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийСледующее.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца)) 
		|	КОНЕЦ КАК Период,
		|	РАЗНОСТЬДАТ(ТаблицаИзменений.Период, МИНИМУМ(ЕСТЬNULL(ТаблицаИзмененийСледующее.ПериодСледующее, ТаблицаИзменений.НачалоСледующегоМесяца)), ДЕНЬ) КАК СрокДействия,
		|	ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
		|	ТаблицаИзменений.Организация КАК Организация,
		|	ПараметрыАмортизацииНМАУУПоследнее.СрокИспользования,
		|	ПараметрыАмортизацииНМАУУПоследнее.КоэффициентУскорения,
		|	ПараметрыАмортизацииНМАУУПоследнее.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииНМАУУПоследнее.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииНМАУУПоследнее.ЛиквидационнаяСтоимостьРегл,
		|	ПорядокУчетаНМАИзмененияПоследнее.ГруппаФинансовогоУчета,
		|	ПорядокУчетаНМАИзмененияПоследнее.НаправлениеДеятельности,
		|	ПорядокУчетаНМАИзмененияПоследнее.ОбъемНаработки,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.Состояние,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.НачислятьАмортизациюУУ,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.СпособОтраженияРасходов,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.СтатьяРасходов,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.АналитикаРасходов,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.ОрганизацияПолучательРасходов,
		|	МестоУчетаНМАИзмененияПоследнее.Подразделение
		|ПОМЕСТИТЬ ВТ_Изменения_УУ
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзмененийСледующее КАК ТаблицаИзмененийСледующее
		|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийСледующее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ТаблицаИзмененийСледующее.Регистратор = ТаблицаИзменений.Регистратор)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзмененийВЭтомМесяце КАК ТаблицаИзмененийВЭтомМесяце
		|		ПО (ТаблицаИзмененийВЭтомМесяце.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийВЭтомМесяце.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУИзменения КАК ПараметрыАмортизацииНМАУУПоследнее
		|		ПО (ПараметрыАмортизацииНМАУУПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПараметрыАмортизацииНМАУУПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ПараметрыАмортизацииНМАУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПараметрыАмортизацииНМАУУПоследнее.Период = ТаблицаИзменений.ПараметрыАмортизацииНМАУУПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАИзменения КАК ПорядокУчетаНМАИзмененияПоследнее
		|		ПО (ПорядокУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ПорядокУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаНМАИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаНМАИзмененияПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАУУИзменения КАК ПорядокУчетаНМАУУИзмененияПоследнее
		|		ПО (ПорядокУчетаНМАУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаНМАУУИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаНМАУУИзмененияПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМАИзменения КАК МестоУчетаНМАИзмененияПоследнее
		|		ПО (МестоУчетаНМАИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (МестоУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
		|			И (МестоУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (МестоУчетаНМАИзмененияПоследнее.Период = ТаблицаИзменений.МестоУчетаНМАИзмененияПоследнееПериод)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Период,
		|	ТаблицаИзменений.НематериальныйАктив,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ПараметрыАмортизацииНМАУУПоследнееПериод,
		|	ТаблицаИзменений.ПорядокУчетаНМАУУИзмененияПоследнееПериод,
		|	ТаблицаИзменений.МестоУчетаНМАИзмененияПоследнееПериод,
		|	ПараметрыАмортизацииНМАУУПоследнее.СрокИспользования,
		|	ПараметрыАмортизацииНМАУУПоследнее.КоэффициентУскорения,
		|	ПараметрыАмортизацииНМАУУПоследнее.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииНМАУУПоследнее.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииНМАУУПоследнее.ЛиквидационнаяСтоимостьРегл,
		|	ПорядокУчетаНМАИзмененияПоследнее.ГруппаФинансовогоУчета,
		|	ПорядокУчетаНМАИзмененияПоследнее.НаправлениеДеятельности,
		|	ПорядокУчетаНМАИзмененияПоследнее.ОбъемНаработки,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.Состояние,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.НачислятьАмортизациюУУ,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.СпособОтраженияРасходов,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.СтатьяРасходов,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.АналитикаРасходов,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаНМАУУИзмененияПоследнее.ОрганизацияПолучательРасходов,
		|	МестоУчетаНМАИзмененияПоследнее.Подразделение
		|
		|;
		|УНИЧТОЖИТЬ ПараметрыАмортизацииНМАУУИзменения
		|;
		|УНИЧТОЖИТЬ ПорядокУчетаНМАИзменения
		|;
		|УНИЧТОЖИТЬ ПорядокУчетаНМАУУИзменения
		|;
		|УНИЧТОЖИТЬ МестоУчетаНМАИзменения
		|;
		|УНИЧТОЖИТЬ ТаблицаИзменений
		|";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК НачалоМесяца,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК КонецМесяца,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзменений,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	0    КАК СрокДействия,
		|	ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка) КАК НематериальныйАктив,
		|	NULL КАК Организация,
		|	0    КАК Стоимость,
		|	0    КАК СрокИспользования,
		|	0    КАК КоэффициентУскорения,
		|	0    КАК ОбъемНаработки,
		|	NULL КАК МетодНачисленияАмортизации,
		|	0    КАК ЛиквидационнаяСтоимость,
		|	0    КАК ЛиквидационнаяСтоимостьРегл,
		|	NULL КАК ГруппаФинансовогоУчета,
		|	NULL КАК НаправлениеДеятельности,
		|	NULL КАК Состояние,
		|	ЛОЖЬ КАК НачислятьАмортизациюУУ,
		|	ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка) КАК СпособОтраженияРасходов,
		|	NULL КАК СтатьяРасходов,
		|	NULL КАК АналитикаРасходов,
		|	ЛОЖЬ КАК ПередаватьРасходыВДругуюОрганизацию,
		|	NULL КАК ОрганизацияПолучательРасходов,
		|	NULL КАК Подразделение
		|ПОМЕСТИТЬ ВТ_Изменения_УУ
		|
		|ГДЕ
		|	ЛОЖЬ";
	
	КонецЕсли; 

	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПериодИзменения_УУ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения_УУ.НачалоМесяца           КАК НачалоМесяца,
	|	Изменения_УУ.НематериальныйАктив    КАК НематериальныйАктив,
	|	Изменения_УУ.Организация            КАК Организация,
	|	МИНИМУМ(Изменения_УУ.ДатаИзменений) КАК Период
	|ПОМЕСТИТЬ ВТ_ПериодИзменения_УУ
	|ИЗ
	|	ВТ_Изменения_УУ КАК Изменения_УУ
	|
	|СГРУППИРОВАТЬ ПО
	|	Изменения_УУ.НачалоМесяца,
	|	Изменения_УУ.НематериальныйАктив,
	|	Изменения_УУ.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
		
	#Область СписокНМАСПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА ПериодИзмененияВТекущемМесяце.Период ЕСТЬ NULL
	|			ТОГДА СписокНМА.ПериодНачисления
	|		КОГДА НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, МЕСЯЦ)
	|			ТОГДА ПериодИзмененияВТекущемМесяце.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ, -1)
	|	КОНЕЦ КАК Период,
	|	СписокНМА.ЭтоДоначисление,
	|	СписокНМА.НематериальныйАктив,
	|	СписокНМА.НематериальныйАктив.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.Организация,
	|
	|	ПорядокУчетаНМА.ГруппаФинансовогоУчета,
	|	ПорядокУчетаНМА.НаправлениеДеятельности,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаНМА.ОбъемНаработки, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.СрокИспользования, 0)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫБОР 
	|					КОГДА НЕ ПериодИзменения_УУ.Период ЕСТЬ NULL
	|						ТОГДА ВЫБОР 
	|								КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|									ТОГДА (РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|									ИНАЧЕ (РАЗНОСТЬДАТ(СписокНМА.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|							КОНЕЦ / ДЕНЬ(СписокНМА.КонецМесяца)
	|					ИНАЧЕ 1
	|				КОНЕЦ * ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	
	// Если расчет выполняется в месяце принятия к учету то ДеньПринятия/ВсегоДнейВМесяце.
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА (РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокНМА.КонецМесяца)
	
	// В остальных случаях КоличествоДнейДоПервогоИзмененияПараметров/ВсегоДнейВМесяце
	|		ИНАЧЕ (РАЗНОСТЬДАТ(СписокНМА.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокНМА.КонецМесяца)
	|	КОНЕЦ КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|				ИЛИ ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СписокНМА.НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ)
	|			ТОГДА (РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	
	// 		Расшифровка формулы: [кол-во отработаннных дней в мес принятия] / [всего дней в мес принятия] + [кол-во месяцев после принятия до начала месяца].
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ, 1), МЕСЯЦ), СписокНМА.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) <> ГОД(СписокНМА.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ)
	|				+ 12 * (ГОД(СписокНМА.НачалоМесяца) - Год(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	СписокНМА.НачислятьАмортизациюУУ,
	|	СписокНМА.СтатьяРасходовУУ,
	|	СписокНМА.АналитикаРасходовУУ,
	|	СписокНМА.СпособОтраженияРасходовУУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ,
	|
	// ОстаточнаяСтоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|
	// ОстаточнаяСтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимостьРегл,
	|
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьРегл,
	|	&ФактическийСрокИспользования КАК ФактическийСрокИспользования,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	0 КАК СтоимостьНаНачалоГода,
	|
	|	ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) КАК СтоимостьРегл,
	|	ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) КАК АмортизацияРегл,
	|
	|	ВЫБОР
	|		КОГДА СписокНМА.ЭтоДоначисление
	|			ТОГДА МестоУчетаНМАНаНачалоМесяца.Подразделение
	|		ИНАЧЕ МестоУчетаНМА.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|
	|	ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0) КАК ОбъемВыработки,
	|
	|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизацииНМАУУ.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации
	|ПОМЕСТИТЬ ВТ_СписокНМАСПараметрами_УУ
	|ИЗ
	|	ВТ_СписокОбъектовУчета_УУ КАК СписокНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодИзменения_УУ КАК ПериодИзменения_УУ
	|		ПО (ПериодИзменения_УУ.Организация = СписокНМА.Организация)
	|			И (ПериодИзменения_УУ.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПериодИзменения_УУ.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодИзменения_УУ КАК ПериодИзмененияВТекущемМесяце
	|		ПО (ПериодИзмененияВТекущемМесяце.Организация = СписокНМА.Организация)
	|			И (ПериодИзмененияВТекущемМесяце.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПериодИзмененияВТекущемМесяце.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО (ПорядокУчетаНМА.Организация = СписокНМА.Организация)
	|			И (ПорядокУчетаНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПорядокУчетаНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьНМА_НаНачалоМесяца КАК СтоимостьНМА
	|		ПО (СтоимостьНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (СтоимостьНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьНМА КАК ПредварительнаяСтоимостьНМА
	|		ПО (ПредварительнаяСтоимостьНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПредварительнаяСтоимостьНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияНМА_НаНачалоМесяца КАК АмортизацияНМА
	|		ПО (АмортизацияНМА.Организация = СписокНМА.Организация)
	|			И (АмортизацияНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (АмортизацияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестоУчетаНМА КАК МестоУчетаНМА
	|		ПО (МестоУчетаНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (МестоУчетаНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестоУчетаНМА КАК МестоУчетаНМАНаНачалоМесяца
	|		ПО (МестоУчетаНМАНаНачалоМесяца.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (МестоУчетаНМАНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.Организация = СписокНМА.Организация)
	|			И (ПервоначальныеСведенияНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПервоначальныеСведенияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО (ПараметрыАмортизацииНМАУУ.Организация = СписокНМА.Организация)
	|			И (ПараметрыАмортизацииНМАУУ.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУ.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаНМА КАК ВыработкаНМА
	|		ПО (ВыработкаНМА.НематериальныйАктив = ПорядокУчетаНМА.НематериальныйАктив)";	
	
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса, 
			"&ФактическийСрокИспользования", 
			ВнеоборотныеАктивыСлужебный.ТекстПоляФактическийСрокИспользования("ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокНМАСИзмененнымиПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Изменения_УУ.Период,
	|	Изменения_УУ.НематериальныйАктив КАК НематериальныйАктив,
	|	(Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца) КАК ЭтоДоначисление,
	|	ВЫРАЗИТЬ(Изменения_УУ.НематериальныйАктив КАК Справочник.НематериальныеАктивы).ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	Изменения_УУ.Организация,
	|
	|	ЕСТЬNULL(Изменения_УУ.ГруппаФинансовогоУчета,  ПорядокУчетаНМА.ГруппаФинансовогоУчета)  КАК ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(Изменения_УУ.НаправлениеДеятельности, ПорядокУчетаНМА.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(Изменения_УУ.ОбъемНаработки, ПорядокУчетаНМА.ОбъемНаработки)
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииНМАУУ.СрокИспользования)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	|		ИНАЧЕ 1
	|	КОНЕЦ * Изменения_УУ.СрокДействия / ДЕНЬ(Изменения_УУ.КонецМесяца) КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	|	ВЫБОР 
	|		КОГДА ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА 0
	// 		Расшифровка формулы: [Кол-во отработаннных дней в мес. принятия] / [всего дней в мес.принятия] + [кол-во мес. после принятия до начала месяца расчета].
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ, 1), МЕСЯЦ), Изменения_УУ.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) <> ГОД(Изменения_УУ.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ)
	|				+ 12 * (ГОД(Изменения_УУ.НачалоМесяца) - Год(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	ЕСТЬNULL(Изменения_УУ.НачислятьАмортизациюУУ, ЕСТЬNULL(СписокНМА.НачислятьАмортизациюУУ, ЛОЖЬ)) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.СтатьяРасходов, СписокНМА.СтатьяРасходовУУ) КАК СтатьяРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.АналитикаРасходов, СписокНМА.АналитикаРасходовУУ) КАК АналитикаРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.СпособОтраженияРасходов, СписокНМА.СпособОтраженияРасходовУУ) КАК СпособОтраженияРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.ПередаватьРасходыВДругуюОрганизацию, ЕСТЬNULL(СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ, ЛОЖЬ)) КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.ОрганизацияПолучательРасходов, СписокНМА.ОрганизацияПолучательРасходовУУ) КАК ОрганизацияПолучательРасходовУУ,
	|
	// ОстаточнаяСтоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|	КОНЕЦ - ВЫБОР 
	|				КОГДА НЕ Изменения_УУ.ЛиквидационнаяСтоимость ЕСТЬ NULL
	|					ТОГДА Изменения_УУ.ЛиквидационнаяСтоимость
	|				КОГДА НЕ ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость ЕСТЬ NULL
	|					ТОГДА ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ОстаточнаяСтоимость,	
	|
	// ОстаточнаяСтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|	КОНЕЦ - ВЫБОР 
	|				КОГДА НЕ Изменения_УУ.ЛиквидационнаяСтоимостьРегл ЕСТЬ NULL
	|					ТОГДА Изменения_УУ.ЛиквидационнаяСтоимостьРегл
	|				КОГДА НЕ ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл ЕСТЬ NULL
	|					ТОГДА ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ОстаточнаяСтоимостьРегл,	
	|
	|	0 КАК СтоимостьНаНачалоГода,
	|
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0)
	|	КОНЕЦ КАК Стоимость,
	|
	|	ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) КАК Амортизация,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА МестоУчетаНМАНаНачалоМесяца.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.Подразделение, МестоУчетаНМА.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|
	|	ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0) КАК ОбъемВыработки,
	|
	|	ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииНМАУУ.СрокИспользования) КАК СрокИспользования,
	|	ЕСТЬNULL(Изменения_УУ.КоэффициентУскорения, ПараметрыАмортизацииНМАУУ.КоэффициентУскорения) КАК КоэффициентУскорения,
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимость, ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимостьРегл, ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл) КАК ЛиквидационнаяСтоимостьРегл,
	|	ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации
	|ПОМЕСТИТЬ ВТ_СписокНМАСИзмененнымиПараметрами_УУ
	|ИЗ
	|	ВТ_Изменения_УУ КАК Изменения_УУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_УУ КАК СписокНМА
	|		ПО (СписокНМА.Организация = Изменения_УУ.Организация)
	|			И (СписокНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (СписокНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО (ПорядокУчетаНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПорядокУчетаНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьНМА_НаНачалоМесяца КАК СтоимостьНМА
	|		ПО (СтоимостьНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (СтоимостьНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьНМА КАК ПредварительнаяСтоимостьНМА
	|		ПО (ПредварительнаяСтоимостьНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПредварительнаяСтоимостьНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияНМА_НаНачалоМесяца КАК АмортизацияНМА
	|		ПО (АмортизацияНМА.Организация = Изменения_УУ.Организация)
	|			И (АмортизацияНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (АмортизацияНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестоУчетаНМА КАК МестоУчетаНМА
	|		ПО (МестоУчетаНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (МестоУчетаНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестоУчетаНМА КАК МестоУчетаНМАНаНачалоМесяца
	|		ПО (МестоУчетаНМАНаНачалоМесяца.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (МестоУчетаНМАНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.Организация = Изменения_УУ.Организация)
	|			И (ПервоначальныеСведенияНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПервоначальныеСведенияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО (ПараметрыАмортизацииНМАУУ.Организация = Изменения_УУ.Организация)
	|			И (ПараметрыАмортизацииНМАУУ.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУ.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаНМА КАК ВыработкаНМА
	|		ПО (ВыработкаНМА.НематериальныйАктив = ПорядокУчетаНМА.НематериальныйАктив)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоДоначисление,
	|	НематериальныйАктив";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СпособыОтраженияРасходов
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокВсехНМА.СпособОтраженияРасходов КАК Ссылка
	|ПОМЕСТИТЬ втСпособыОтраженияРасходов
	|ИЗ
	|	ВТ_СписокОбъектовУчета КАК СписокВсехНМА
	|ГДЕ
	|	СписокВсехНМА.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокВсехНМА.СпособОтраженияРасходов
	|ИЗ
	|	ВТ_Изменения_УУ КАК СписокВсехНМА
	|ГДЕ
	|	СписокВсехНМА.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
	|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
	|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
	|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизация.Коэффициент                          КАК База,
	|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
	|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизации КАК СуммаБазы
	|ПОМЕСТИТЬ ВТ_СпособыОтраженияРасходов
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4.АмортизационныеРасходы КАК ТаблицаАмортизация
	|ГДЕ
	|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходов КАК СпособыОтраженияРасходов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаПараметрыРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.НематериальныйАктив КАК ОбъектУчета,
	|
	// НеУказанОбъемНаработки
	|	 СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|		И СписокНМА.РесурсНазначенный = 0 КАК НеУказанОбъемНаработки
	|
	|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчета_УУ
	|ИЗ
	|	ВТ_СписокНМАСПараметрами_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.НачислятьАмортизациюУУ И СписокНМА.ОстаточнаяСтоимость > 0
	|	И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|		И СписокНМА.РесурсНазначенный = 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаПроверкаПараметровРасчета(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	ВЫБОР 
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - (&СуммаАмортизацииУУ)) > СписокНМА.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - СписокНМА.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимость - СписокНМА.ЛиквидационнаяСтоимость)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР 
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - (&СуммаАмортизацииБУ)) > СписокНМА.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - СписокНМА.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - СписокНМА.ЛиквидационнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокНМА.Стоимость,
	|	СписокНМА.Амортизация,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|	СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление
	|ИЗ
	|	ВТ_СписокНМАСПараметрами_УУ КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокНМА.НематериальныйАктив
	|ГДЕ
	|	СписокНМА.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(
		"СписокНМА.Стоимость", 
		"СписокНМА.Амортизация",
		"СписокНМА.ОстаточнаяСтоимость",
		"СписокНМА.ПервоначальнаяСтоимость",
		"СписокНМА.ЛиквидационнаяСтоимость",
		"0", 
		ПараметрыРасчета);
		
	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(
		"СписокНМА.СтоимостьРегл", 
		"СписокНМА.АмортизацияРегл",
		"СписокНМА.ОстаточнаяСтоимостьРегл",
		"СписокНМА.ПервоначальнаяСтоимостьРегл",
		"СписокНМА.ЛиквидационнаяСтоимостьРегл",
		"0", 
		ПараметрыРасчета);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(СписокНМА.СуммаУУ) КАК СуммаУУ,
	|	СУММА(СписокНМА.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Доначисление КАК СписокНМА
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНМА.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	ВЫБОР 
	|		КОГДА СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ) - СписокНМА.ЛиквидационнаяСтоимость < &МинимальнаяОстаточнаяСтоимость
	|				И &ВалютаРегламентированногоУчета = &ВалютаУправленческогоУчета
	|				И &ВедетсяРегламентированныйУчетВНА
	|				И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|
	|			ТОГДА СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокНМА.ЛиквидационнаяСтоимость
	|
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ)) > СписокНМА.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокНМА.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокНМА.ЛиквидационнаяСтоимость)
	|
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР 
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - (&СуммаАмортизацииБУ)) > СписокНМА.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокНМА.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокНМА.ЛиквидационнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокНМА.Стоимость,
	|	СписокНМА.Амортизация,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Предварительный
	|ИЗ
	|	ВТ_СписокНМАСПараметрами_УУ КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.НематериальныйАктив = СписокНМА.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокНМА.НематериальныйАктив
	|ГДЕ
	|	НЕ СписокНМА.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(
		"СписокНМА.Стоимость", 
		"СписокНМА.Амортизация",
		"СписокНМА.ОстаточнаяСтоимость",
		"СписокНМА.ПервоначальнаяСтоимость",
		"СписокНМА.ЛиквидационнаяСтоимость",
		"ЕСТЬNULL(Доначисление.СуммаУУ, 0)", 
		ПараметрыРасчета);
		
	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(
		"СписокНМА.СтоимостьРегл", 
		"СписокНМА.АмортизацияРегл",
		"СписокНМА.ОстаточнаяСтоимостьРегл",
		"СписокНМА.ПервоначальнаяСтоимостьРегл",
		"СписокНМА.ЛиквидационнаяСтоимостьРегл",
		"ЕСТЬNULL(Доначисление.СуммаБУ, 0)", 
		ПараметрыРасчета);
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код КАК НематериальныйАктивКод,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	1 КАК Коэффициент,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизацию,
	|	СписокНМА.Стоимость - СписокНМА.Амортизация КАК СтоимостьДляВычисленияАмортизации,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Предварительный КАК СписокНМА
	|ГДЕ
	|	СписокНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				ВТ_СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокНМА.Период,
	|	СписокНМА.Организация,
	|	СписокНМА.Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код,
	|	СписокНМА.ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ,
	|	СписокНМА.АналитикаРасходовУУ,
	|	СписокНМА.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	1,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокНМА.Период, МЕСЯЦ )= &НачалоМесяца
	|			ТОГДА СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокНМА.ОстаточнаяСтоимость 
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокНМА.Период, МЕСЯЦ )= &НачалоМесяца
	|			ТОГДА СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) 
	|		ИНАЧЕ СписокНМА.ОстаточнаяСтоимостьРегл 
	|	КОНЕЦ,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ,
	|	СписокНМА.Стоимость - СписокНМА.Амортизация,
	|	ИСТИНА
	|ИЗ
	|	ВТ_СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.НематериальныйАктив = СписокНМА.НематериальныйАктив
	|ГДЕ
	|	НЕ СписокНМА.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код КАК НематериальныйАктивКод,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	1 КАК Коэффициент,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизацию,
	|	0 КАК СтоимостьДляВычисленияАмортизации,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокНМА
	|ГДЕ
	|	СписокНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				ВТ_СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокНМА.Период,
	|	СписокНМА.Организация,
	|	СписокНМА.Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код,
	|	СписокНМА.ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ,
	|	СписокНМА.АналитикаРасходовУУ,
	|	СписокНМА.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	1,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ,
	|	0,
	|	ИСТИНА
	|ИЗ
	|	ВТ_СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_АмортизацияИзменений_УУ_Доначисление
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|	Амортизация.НематериальныйАктив,
	|	Амортизация.ВидОбъектаУчета,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Доначисление
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокНМА.Период КАК ПериодРасчета,
	|		СписокНМА.Организация КАК Организация,
	|		СписокНМА.Подразделение КАК Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ КАК СуммаУУ,
	|		СписокНМА.СуммаБУ КАК СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокНМА.Период,
	|		СписокНМА.Организация,
	|		СписокНМА.Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ,
	|		СписокНМА.СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации
	|	ИЗ
	|		ВТ_АмортизацияИзменений_УУ_Доначисление КАК СписокНМА) КАК Амортизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_АмортизацияИзменений_УУ
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.Подразделение КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|	Амортизация.НематериальныйАктив,
	|	Амортизация.ВидОбъектаУчета,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровНМА2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	Амортизация.ЭтоДоначисление КАК ЭтоДоначисление
	|
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокНМА.Период КАК ПериодРасчета,
	|		СписокНМА.Организация КАК Организация,
	|		СписокНМА.Подразделение КАК Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ КАК СуммаУУ,
	|		СписокНМА.СуммаБУ КАК СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.ОстаточнаяСтоимость КАК СтоимостьДляВычисленияАмортизации,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Предварительный КАК СписокНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокНМА.ПериодРасчета КАК ПериодРасчета,
	|		СписокНМА.Организация КАК Организация,
	|		СписокНМА.Подразделение КАК Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ КАК СуммаУУ,
	|		СписокНМА.СуммаБУ КАК СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		ИСТИНА КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Доначисление КАК СписокНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокНМА.Период,
	|		СписокНМА.Организация,
	|		СписокНМА.Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ,
	|		СписокНМА.СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_АмортизацияИзменений_УУ КАК СписокНМА) КАК Амортизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	ЕСТЬNULL(ПакетыАмортизацииНМА.НомерПакета, 0) КАК НомерПакета,
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив,
	|	НачисленнаяАмортизация.ВидОбъектаУчета,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаРасходыНИОКРВДругуюОрганизацию)
	|		КОГДА НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаРасходыНИОКР)
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМАвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) КАК СуммаУУ,
	|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ,
	|	СУММА(НачисленнаяАмортизация.СуммаПР) КАК СуммаПР,
	|	СУММА(НачисленнаяАмортизация.СуммаВР) КАК СуммаВР,
	|	СУММА(НачисленнаяАмортизация.СуммаЦФ) КАК СуммаЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУЦФ) КАК СуммаНУЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаПРЦФ) КАК СуммаПРЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаВРЦФ) КАК СуммаВРЦФ,
	|	НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов,
	|	ЛОЖЬ КАК НачислятьИзнос,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.ЭтоДоначисление КАК ЭтоДоначисление
	|ПОМЕСТИТЬ ВТ_НачисленнаяАмортизация
	|ИЗ
	|	(//ТекстПодзапроса//) КАК НачисленнаяАмортизация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизацииНМА
	|		ПО ПакетыАмортизацииНМА.Организация = НачисленнаяАмортизация.Организация
	|			И ПакетыАмортизацииНМА.НематериальныйАктив = НачисленнаяАмортизация.НематериальныйАктив
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	ЕСТЬNULL(ПакетыАмортизацииНМА.НомерПакета, 0),
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.НематериальныйАктив,
	|	НачисленнаяАмортизация.ВидОбъектаУчета,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.ЭтоДоначисление,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМАвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМА)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаБУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаНУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаПР) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаВР) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаНУЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаПРЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаВРЦФ) <> 0";
	
	СписокПодзапросов = Новый Массив;
	ТекстПодзапроса =  
	"	ВЫБРАТЬ
	|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
	|		РасчетАмортизации.Организация КАК Организация,
	|		РасчетАмортизации.Подразделение КАК Подразделение,
	|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
	|		РасчетАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|		РасчетАмортизации.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		РасчетАмортизации.ГруппаФинансовогоУчета,
	|		РасчетАмортизации.НаправлениеДеятельности,
	|		РасчетАмортизации.КорНаправлениеДеятельности,
	|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|		РасчетАмортизации.СуммаУУ КАК СуммаУУ,
	|		ВЫБОР 
	|			КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|				ТОГДА РасчетАмортизации.СуммаБУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаБУ,
	|		0 КАК СуммаНУ,
	|		0 КАК СуммаПР,
	|		0 КАК СуммаВР,
	|		0 КАК СуммаЦФ,
	|		0 КАК СуммаНУЦФ,
	|		0 КАК СуммаПРЦФ,
	|		0 КАК СуммаВРЦФ,
	|		РасчетАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		РасчетАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		РасчетАмортизации.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		РасчетАмортизации.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		РасчетАмортизации.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		РасчетАмортизации.ЭтоДоначисление КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ КАК РасчетАмортизации";
	СписокПодзапросов.Добавить(ТекстПодзапроса);
	
	АмортизацияНМАЛокализация.ДополнитьТекстПодзапросаНачисленнаяАмортизация(СписокПодзапросов, ПараметрыРасчета);
	
	ТекстПодзапроса = СтрСоединить(СписокПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстПодзапроса//", ТекстПодзапроса);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияНМАЛокализация.ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета); 
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(НачисленнаяАмортизация.НематериальныйАктив), 0) КАК КоличествоДанных
		|ИЗ
		|	ВТ_РасчетАмортизации_УУ КАК НачисленнаяАмортизация";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ТекстЗапросаСуммаАмортизацииУУ(ТекстСтоимость, ТекстАмортизация, ТекстОстаточнаяСтоимость, ТекстПервоначальнаяСтоимость, ТекстЛиквидационнаяСтоимость, ТекстСуммаДоначисления, ПараметрыРасчета)
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса =
		"ВЫБОР 
		|	КОГДА НЕ СписокНМА.НачислятьАмортизациюУУ ИЛИ (&ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 ТОГДА
		|		0
		|	КОГДА (СписокНМА.РесурсВыработанныйЗаПрошлыйПериод + СписокНМА.РесурсВыработанныйЗаТекущийПериод) >= СписокНМА.РесурсНазначенный ТОГДА
		|		&ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость
		|
			// Линейный или ПропорциональноОбъемуПродукции
		|	КОГДА СписокНМА.МетодНачисленияАмортизации В (
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.Линейный),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)) 
		|			ИЛИ (СписокНМА.РесурсНазначенный
		|					- СписокНМА.РесурсВыработанныйЗаПрошлыйПериод
		|					- СписокНМА.РесурсВыработанныйЗаТекущийПериод) <= 12 
		|				И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокНМА.ЭтоДоначисление
		|				ТОГДА ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления)
		|								* ((СписокНМА.РесурсВыработанныйЗаТекущийПериод)/(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод)) КАК ЧИСЛО(31,2)) 
		|			ИНАЧЕ ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость) 
		|								* ((СписокНМА.РесурсВыработанныйЗаТекущийПериод)/(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод)) КАК ЧИСЛО(31,2))
		|		КОНЕЦ
		|
			// УменьшаемогоОстатка
		|	КОГДА СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка) ТОГДА
		|   
		|		ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления)
		|			* СписокНМА.КоэффициентУскорения
		|			* СписокНМА.РесурсВыработанныйЗаТекущийПериод
		|			/ (СписокНМА.РесурсНазначенный 
		|					- СписокНМА.КоэффициентУскорения *(СписокНМА.РесурсВыработанныйЗаПрошлыйПериод - СписокНМА.ОтработанныхМесяцевПолныхЛет)) КАК ЧИСЛО(31,2))
		|КОНЕЦ";				
					
	Иначе

		ТекстЗапроса =
		"ВЫБОР 
		|	КОГДА НЕ СписокНМА.НачислятьАмортизациюУУ ИЛИ (&ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 ТОГДА
		|		0
			// Линейный
		|	КОГДА СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.Линейный) ТОГДА
		|		ВЫРАЗИТЬ(&ПервоначальнаяСтоимость / СписокНМА.РесурсНазначенный КАК ЧИСЛО(31,2))
		|   
			// ПропорциональноОбъемуПродукции
		|	КОГДА СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокНМА.РесурсНазначенный <> 0 ТОГДА
		|				ВЫРАЗИТЬ(&ПервоначальнаяСтоимость * СписокНМА.РесурсВыработанныйЗаТекущийПериод / СписокНМА.РесурсНазначенный КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|
			// УменьшаемогоОстатка
		|	КОГДА СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка) ТОГДА
		|		ВЫБОР 
		|			КОГДА (СписокНМА.РесурсНазначенный - СписокНМА.ФактическийСрокИспользования + 1) > 0 ТОГДА
		|				ВЫРАЗИТЬ((&Стоимость - &Амортизация)
		|							* СписокНМА.КоэффициентУскорения / (СписокНМА.РесурсНазначенный - СписокНМА.ФактическийСрокИспользования + 1) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|			
		|		КОНЕЦ
		|КОНЕЦ";				

	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Стоимость", ТекстСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Амортизация", ТекстАмортизация);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстаточнаяСтоимость", ТекстОстаточнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПервоначальнаяСтоимость", ТекстПервоначальнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЛиквидационнаяСтоимость", ТекстЛиквидационнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаДоначисления", ТекстСуммаДоначисления);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
