#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Заполнение

// Возвращает структуру необходимую для дальнейшего использования при заполнении документа.
//
// Возвращаемое значение:
//   Структура - структура параметров заполнения документа.
//
Функция ПараметрыЗаполненияДокумента() Экспорт
	
	ПараметрыЗаполнения = Новый Структура();
	
	ПараметрыЗаполнения.Вставить("МассивЗаказов",         Неопределено);
	ПараметрыЗаполнения.Вставить("ПоЗаказам",             Ложь);
	
	ПараметрыЗаполнения.Вставить("РеквизитыШапки",        Неопределено);
	
	ПараметрыЗаполнения.Вставить("ИмяДокумента",          "ОтчетПереработчика");
	ПараметрыЗаполнения.Вставить("ИмяРегистраЗаказ",      "ЗаказыКлиентов");
	ПараметрыЗаполнения.Вставить("ИмяПоляЗаказ",          "ЗаказПереработчику");
	
	ПараметрыЗаполнения.Вставить("КлючевыеПоля",          "Номенклатура, Характеристика");
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Производит инициализацию структуры параметров заполнения по реквизитам шапки и по заказам.
//
// Параметры:
//  ПараметрыЗаполнения	 - Структура - Параметры по умолчанию получаемые в методе ПараметрыЗаполненияДокумента()
//  РеквизитыШапки		 - Структура - Содержит ключи на основании которых будет происходить заполнение
//  МассивЗаказов		 - Массив - Ссылки на заказы по которым будет происходить заполнение.
//
Процедура ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения, РеквизитыШапки, МассивЗаказов) Экспорт
	
	ПараметрыЗаполнения.МассивЗаказов	= МассивЗаказов;
	
	ПараметрыЗаполнения.РеквизитыШапки	= РеквизитыШапки;
	ПараметрыЗаполнения.ПоЗаказам		= ТипЗнч(МассивЗаказов[0]) = Тип("ДокументСсылка.ЗаказПереработчику");
	
КонецПроцедуры

// Формирует структуру для создания документа по заказам
//  Если в переданных заказах отличаются реквизиты шапки, выдается сообщение об ошибке.
//
// Параметры:
//  МассивСсылок - Массив - заказы на внутреннее потребление, по которым необходимо ввести накладную.
//
// Возвращаемое значение:
//  Структура - структура, в которую будут помещены реквизиты шапки из массива заказов.
//
Функция ДанныеЗаполненияНакладной(МассивСсылок, СвойстваЗаказов = Неопределено) Экспорт
	
	Реквизиты = "Партнер,Контрагент,Договор,Организация,
				|Сделка,НаправлениеДеятельности,Подразделение,
				|Валюта,ЗакупкаПодДеятельность";
	
	Если ТипЗнч(МассивСсылок[0]) <> Тип("ДокументСсылка.ПоступлениеОтПереработчика") Тогда
		Реквизиты = Реквизиты + ",СпособРаспределенияЗатратНаВыходныеИзделия";
	ИначеЕсли ТипЗнч(МассивСсылок[0]) = Тип("ДокументСсылка.ПоступлениеОтПереработчика") Тогда
		Реквизиты = Реквизиты + ",УчетСырьяПоНазначениям";
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МассивСсылок[0], Реквизиты);
	
КонецФункции

// Возвращает список реквизитов, по которым можно сгруппировать распоряжения в пределах одного отчета.
//
// Возвращаемое значение:
//  Строка - имена реквизитов, разделенные запятыми.
//
Функция КлючевыеПоляШапкиРаспоряжения() Экспорт
	
	Возврат "Партнер,Контрагент,Договор,Организация,Сделка,Валюта,НаправлениеДеятельности,Подразделение";
	
КонецФункции

#КонецОбласти

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ОтчетПереработчикаЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
// 		Строка - Текст запроса временных таблиц, необходимых для отражения в регламентированном учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ОтчетПереработчикаЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Округление

// Возвращает параметры для округления
// 
// Возвращаемое значение:
// 	Структура - элементы содержат структуру параметров округления 
// 				см. ОбщегоНазначенияУТ.ПараметрыОкругленияКоличестваШтучныхТоваров
// 
Функция ПараметрыТЧДляОкругления() Экспорт
	
	ПараметрыТЧ = Новый Структура;
	
	ИмяТЧ = "Продукция";
	ПараметрыТЧ.Вставить(ИмяТЧ, ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества());
	ПараметрыТЧ[ИмяТЧ].ИмяТЧ = ИмяТЧ;
	ПараметрыТЧ[ИмяТЧ].ПроверитьВозможностьОкругления = Ложь;
	
	ИмяТЧ = "ВозвратныеОтходы";
	ПараметрыТЧ.Вставить(ИмяТЧ, ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества());
	ПараметрыТЧ[ИмяТЧ].ИмяТЧ = ИмяТЧ;
	ПараметрыТЧ[ИмяТЧ].ПроверитьВозможностьОкругления = Ложь;
	
	ИмяТЧ = "Материалы";
	ПараметрыТЧ.Вставить(ИмяТЧ, ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества());
	ПараметрыТЧ[ИмяТЧ].ИмяТЧ = ИмяТЧ;
	ПараметрыТЧ[ИмяТЧ].ПроверитьВозможностьОкругления = Ложь;
	
	Возврат ПараметрыТЧ;

КонецФункции

#КонецОбласти

#Область Прочее

// Осуществляет вычисление текущего состояния по поступлению без заказа
//
// Параметры:
//	ЗаказПоставщику         - ДокументСсылка.ПриобретениеТоваровУслуг - Документ, состояние которого необходимо вычислить
//	Договор                 - СправочникСсылка.ДоговорыКонтрагентов    - Договор с клиентом
//	СостояниеРасчетов       - ФормаКлиентскогоПриложения - Форма, в реквизиты которой будет помещено рассчитанное состояние.
//
Процедура РассчитатьСостояние(Знач ОтчетПереработчика, Знач Договор, СостояниеРасчетов) Экспорт
	
	ЗаполнитьЗначенияСвойств(СостояниеРасчетов, СтруктураСостоянияРасчетов());
	
	Если ЗначениеЗаполнено(ОтчетПереработчика) И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РасчетыСПоставщиками) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ 
		// СУММА ОПЛАТЫ /////////////////////////////////////////////////////////////
		|	ВЫБОР КОГДА Отчет.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|			И Отчет.Проведен
		|			И ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0 ТОГДА
		|		ВЫРАЗИТЬ (ЕСТЬNULL(ОстаткиРасчетов.КОплатеПриход, 0) КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаОплаты,
		// ПРОЦЕНТ ОПЛАТЫ ///////////////////////////////////////////////////////////
		|	ВЫБОР КОГДА Отчет.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|			И Отчет.Проведен
		|			И ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0 ТОГДА
		|		ВЫРАЗИТЬ (ЕСТЬNULL(ОстаткиРасчетов.КОплатеПриход, 0) * 100 / ОстаткиРасчетов.КОплатеРасход КАК ЧИСЛО(15, 0))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПроцентОплаты,
		// СУММА ПРОСРОЧЕННОЙ ОПЛАТЫ ////////////////////////////////////////////////
		|	ВЫБОР КОГДА Отчет.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|			И Отчет.Проведен
		|			И ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0
		|			И ЕСТЬNULL(АктуальныеРасчеты.КОплатеОстаток, 0) < 0 ТОГДА
		|		ВЫРАЗИТЬ (-ЕСТЬNULL(АктуальныеРасчеты.КОплатеОстаток, 0) КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаПросроченнойОплаты,
		// СУММА ПОСТУПЛЕНИЯ ////////////////////////////////////////////////////////
		|	ВЫБОР КОГДА Отчет.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|			И Отчет.Проведен
		|			И ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0 ТОГДА
		|		ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0)
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаПоступления,
		// ПРОЦЕНТ ПОСТУПЛЕНИЯ //////////////////////////////////////////////////////
		|	ВЫБОР КОГДА Отчет.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|			И Отчет.Проведен
		|			И ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0 ТОГДА
		|		ВЫРАЗИТЬ (ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) * 100 / ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) КАК ЧИСЛО(15, 0))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПроцентПоступления,
		// ДОЛГ (+ НАМ ДОЛЖНЫ, - МЫ ДОЛЖНЫ)//////////////////////////////////////////
		|	ВЫБОР КОГДА Отчет.Проведен
		|			И (ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0 ИЛИ
		|				Отчет.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)) ТОГДА
		|		ВЫРАЗИТЬ (ЕСТЬNULL(ОстаткиРасчетов.СуммаКонечныйОстаток, 0) КАК ЧИСЛО(31,2))
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК СуммаДолга,
		|	ВЫБОР КОГДА Отчет.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|			И Отчет.Проведен
		|			И ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0) > 0 ТОГДА
		|		ВЫБОР КОГДА ЕСТЬNULL(ОстаткиРасчетов.СуммаКонечныйОстаток, 0) > 0 ТОГДА
		|			ВЫРАЗИТЬ ((ЕСТЬNULL(ОстаткиРасчетов.СуммаКонечныйОстаток, 0) * 100 / ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0)) КАК ЧИСЛО(15, 0))
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ ((ЕСТЬNULL(-ОстаткиРасчетов.СуммаКонечныйОстаток, 0) * 100 / ЕСТЬNULL(ОстаткиРасчетов.КОплатеРасход, 0)) КАК ЧИСЛО(15, 0))
		|		КОНЕЦ
		|	ИНАЧЕ
		|		0
		|	КОНЕЦ КАК ПроцентДолга,
		|ВЫБОР
		|	КОГДА
		|		Отчет.Проведен И
		|		Отчет.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|	ТОГДА
		|		-ОстаткиРасчетов.КОплатеКонечныйОстаток
		|	ИНАЧЕ
		|		0
		|КОНЕЦ КАК СуммаКОплате
		|ИЗ
		|	Документ.ОтчетПереработчика КАК Отчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(, , , , ЗаказПоставщику = &РасчетныйДокумент) КАК ОстаткиРасчетов
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Остатки(КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ), ЗаказПоставщику = &РасчетныйДокумент) КАК АктуальныеРасчеты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	Отчет.Ссылка = &ОтчетПереработчика");
		
		УстановитьПривилегированныйРежим(Истина);
		
		ПорядокРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтчетПереработчика, "ПорядокРасчетов");
		
		РасчетныйДокумент = ?(ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов,
			Договор,
			ОтчетПереработчика);
		
		Запрос.УстановитьПараметр("ОтчетПереработчика", ОтчетПереработчика);
		Запрос.УстановитьПараметр("РасчетныйДокумент",  РасчетныйДокумент);
		Запрос.УстановитьПараметр("ТекущаяДата",        НачалоДня(ТекущаяДатаСеанса()));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(СостояниеРасчетов, Выборка);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ПоступлениеОтПереработчика.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ЗаявкаНаРасходованиеДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.РасходныйКассовыйОрдер.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.СписаниеБезналичныхДенежныхСредств.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ПриобретениеУслугПрочихАктивов.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ОтчетПереработчикаЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Отчет переработчика".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ОтчетПереработчика) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ОтчетПереработчика.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ОтчетПереработчика);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводствоНаСтороне";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	КомандаОтчет = Отчеты.КарточкаРасчетовСПоставщиками.ДобавитьКомандуКарточкаРасчетовСПоставщикомПоДокументам(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
	КонецЕсли;
	
	КомандаОтчет = Отчеты.СостояниеРасчетовСПоставщиками.ДобавитьКомандуОтчетаПоДокументам(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
	КонецЕсли;
	
	// Рабочее место
	КомандаОтчет = Отчеты.СостояниеРасчетовСПоставщиками.ДобавитьКомандуОтчетаПоДокументам(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.ВидимостьВФормах = "РабочееМесто";
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.Порядок = 1;
	КонецЕсли;
	
	КомандаОтчет = Отчеты.КарточкаРасчетовСПоставщиками.ДобавитьКомандуКарточкаРасчетовСПоставщикомПоДокументам(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.ВидимостьВФормах = "РабочееМесто";
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.Порядок = 2;
	КонецЕсли;
	
	Отчеты.ДеревоСебестоимостиПродукции.ДобавитьКомандуОтчета(КомандыОтчетов);
	
	ОтчетПереработчикаЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Функция определяет реквизиты выбранного документа
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ОтчетПереработчика - Ссылка на документ.
//
// Возвращаемое значение:
//	Структура - реквизиты выбранного документа.
//
Функция РеквизитыДокумента(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.Валюта КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ЗаказПереработчику КАК ЗаказПереработчику,
	|	ДанныеДокумента.ПоЗаказам КАК ПоЗаказам,
	|	ДанныеДокумента.СуммаСНДС КАК СуммаДокумента,
	|	ДанныеДокумента.СуммаСНДС КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Договор КАК Договор,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ПорядокРасчетов КАК ПорядокРасчетов,
	|	ДанныеДокумента.Курс КАК Курс,
	|	ДанныеДокумента.Кратность КАК Кратность
	|
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &ДокументСсылка
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Дата = Выборка.Дата;
		Организация = Выборка.Организация;
		Партнер = Выборка.Партнер;
		Контрагент = Выборка.Контрагент;
		Договор = Выборка.Договор;
		НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
		ПорядокРасчетов = Выборка.ПорядокРасчетов;
		ВалютаВзаиморасчетов = Выборка.ВалютаВзаиморасчетов;
		ПоЗаказу = Выборка.ПоЗаказам;
		СуммаДокумента = Выборка.СуммаДокумента;
		СуммаВзаиморасчетов = ?(Выборка.Проведен, Выборка.СуммаВзаиморасчетов, 0);
		Курс = Выборка.Курс;
		Кратность = Выборка.Кратность;
	Иначе
		Дата = Дата(1,1,1);
		Организация = Справочники.Организации.ПустаяСсылка();
		Партнер = Справочники.Партнеры.ПустаяСсылка();
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
		ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПустаяСсылка();
		ВалютаВзаиморасчетов = Справочники.Валюты.ПустаяСсылка();
		ПоЗаказу = Ложь;
		СуммаДокумента = 0;
		СуммаВзаиморасчетов = 0;
		Курс = 1;
		Кратность = 1;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Дата", Дата); 
	СтруктураРеквизитов.Вставить("Организация", Организация);
	СтруктураРеквизитов.Вставить("Партнер", Партнер); 
	СтруктураРеквизитов.Вставить("Контрагент", Контрагент); 
	СтруктураРеквизитов.Вставить("Договор", Договор); 
	СтруктураРеквизитов.Вставить("НаправлениеДеятельности", НаправлениеДеятельности);
	СтруктураРеквизитов.Вставить("ПорядокРасчетов", ПорядокРасчетов); 
	СтруктураРеквизитов.Вставить("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов); 
	СтруктураРеквизитов.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика); 
	СтруктураРеквизитов.Вставить("ПоЗаказу", ПоЗаказу); 
	СтруктураРеквизитов.Вставить("СуммаДокумента", СуммаДокумента); 
	СтруктураРеквизитов.Вставить("СуммаВзаиморасчетов", СуммаВзаиморасчетов);
	СтруктураРеквизитов.Вставить("Курс", Курс); 
	СтруктураРеквизитов.Вставить("Кратность", Кратность);

	Возврат СтруктураРеквизитов;

КонецФункции

// Определяет возможность указания цены и суммы возвратных отходов
//
// Параметры:
//  Объект	 - ДанныеФормыКоллекция - Данные документа "Отчет переработчика".
//
// Возвращаемое значение:
//  Булево - Истина, если для отходов указываются цена и сумма.
//
Функция ДоступноУказаниеЦенВозвратныхОтходов(Объект) Экспорт

	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	
	ПродукцияИОтходыПриПоступленииВОднойТЧ = РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ДатаДокумента);

	Возврат ПродукцияИОтходыПриПоступленииВОднойТЧ;

КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
//
//  Возвращаемое значение:
//  Структура - структура параметров, см. Справочники.Назначения.МакетФормыВыбораНазначений().
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы);
	ШаблонНазначения.НаправлениеДеятельности = "Объект.НаправлениеДеятельности";
	
	// Потребности в продукции на складе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.Продукция.Назначение");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.Продукция.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.Продукция.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.Партнер";
	
	// Потребности в возвратных отходах на складе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.ВозвратныеОтходы.Назначение");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.ВозвратныеОтходы.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.ВозвратныеОтходы.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.Партнер";
	
	// Потребности материалы
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "МатериалыИРаботыВПроизводстве", Истина, "Объект.Материалы.Назначение");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Количество").Пометка = Истина;
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.Материалы.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.Материалы.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Организация      = "Объект.Организация";
	ОписаниеКолонок.ПутиКДанным.Подразделение    = "Объект.Партнер";
	
	Возврат МакетФормы;
	
КонецФункции

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает параметры выбора спецификаций для изделий, указанных в документе.
//
// Параметры:
//   Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров выбора спецификаций.
//
// Возвращаемое значение:
//   Структура - Структура, переопределяющая умолчания, заданные в функции УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификаций().
//
Функция ПараметрыВыбораСпецификаций(Объект) Экспорт
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификаций();
	
	ПараметрыВыбораСпецификаций.ДоступныеТипы.Добавить(Перечисления.ТипыПроизводственныхПроцессов.Сборка);
	ПараметрыВыбораСпецификаций.ДоступныеСтатусы.Добавить(Перечисления.СтатусыСпецификаций.Действует);
	
	Возврат ПараметрыВыбораСпецификаций;
	
КонецФункции

// Имена реквизитов, от значений которых зависят параметры выбора спецификаций
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровВыбораСпецификаций() Экспорт
	
	ИменаРеквизитов = "";
	Возврат ИменаРеквизитов;
	
КонецФункции

// Возвращает параметры распределения затрат на выходные изделия.
//
// Параметры:
//   Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров распределения затрат.
//
// Возвращаемое значение:
//  Структура - параметры, уточняющие особенности распределения затрат на выходные изделия.
//
Функция ПараметрыРаспределенияЗатрат(Объект) Экспорт
	
	ПараметрыРаспределенияЗатрат = ПроизводствоКлиентСервер.ПараметрыРаспределенияЗатратНаВыходныеИзделия(
		"Продукция",
		Объект.СпособРаспределенияЗатратНаВыходныеИзделия);
	
	Если Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.БезГруппировки
		Или Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям
		Или Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоЗаказамНаПроизводство
		Или Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства Тогда
		
		ПараметрыРаспределенияЗатрат.РассчитыватьПолеДоляСтоимостиПроцент = Истина;
		ПараметрыРаспределенияЗатрат.РассчитыватьПризнакЕстьОшибкиЗаполнения = Истина;
		ПараметрыРаспределенияЗатрат.РассчитыватьПризнакДоляСтоимостиОбязательна = Истина;
		
		Если Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям
			Или Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоЗаказамНаПроизводство Тогда
			
			ПараметрыРаспределенияЗатрат.ПолеГруппировкиЗатрат = "НомерГруппыЗатрат";
			ПараметрыРаспределенияЗатрат.ПолеПредставленияГруппировкиЗатрат = "ГруппаЗатрат";
			
		ИначеЕсли Объект.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства Тогда
			
			ПараметрыРаспределенияЗатрат.ИмяПоляСпособРаспределенияЗатратНаВыходныеИзделия = "СпособРаспределенияЗатратНаВыходныеИзделия";
			
			ПараметрыРаспределенияЗатрат.ПолеГруппировкиЗатрат = "ЭтапПроизводства";
			ПараметрыРаспределенияЗатрат.ПолеПредставленияГруппировкиЗатрат = "ЭтапПроизводства";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПараметрыРаспределенияЗатрат;
	
КонецФункции


#КонецОбласти

#Область УчетНДС

// Инициализирует параметры заполнения вида деятельности НДС
//
// Параметры:
//  Объект		- ДокументОбъект.ОтчетПереработчика, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  Структура - структура параметров, см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС().
//
Функция ПараметрыЗаполненияВидаДеятельностиНДС(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС();
	ПараметрыЗаполнения.Организация							= Объект.Организация;
	ПараметрыЗаполнения.Дата								= Объект.Дата;
	ПараметрыЗаполнения.Договор								= Объект.Договор;
	ПараметрыЗаполнения.НаправлениеДеятельности				= Объект.НаправлениеДеятельности;
	ПараметрыЗаполнения.ПриобретениеРабот					= Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Инициализирует параметры регистрации счетов-фактур (полученных)
//
// Параметры:
//  Объект		- ДокументОбъект.ОтчетПереработчика, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  Структура - структура параметров, см. УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурПолученных().
//
Функция ПараметрыРегистрацииСчетовФактурПолученных(Объект) Экспорт
	
	ПараметрыРегистрации = УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурПолученных();
	
	ПараметрыРегистрации.Ссылка				= Объект.Ссылка;
	ПараметрыРегистрации.Дата				= Объект.Дата;
	ПараметрыРегистрации.Организация		= Объект.Организация;
	ПараметрыРегистрации.Контрагент			= Объект.Контрагент;
	ПараметрыРегистрации.НалогообложениеНДС	=
			ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС");
	
	ПараметрыРегистрации.ПриобретениеТоваровРаботУслуг = Истина;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Партнер)
	|	И ЗначениеРазрешено(Подразделение)
	|	И (ЗначениеРазрешено(Продукция.Получатель)
	|		ИЛИ ЗначениеРазрешено(ВозвратныеОтходы.Получатель)
	|		ИЛИ Продукция.Получатель ЕСТЬ NULL
	|		И ВозвратныеОтходы.Получатель ЕСТЬ NULL)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ГруппировкаЗатрат

// Возвращает перечень полей, идентифицирующих группу затрат.
//	Параметры:
//		ГруппировкаЗатрат - ПеречислениеСсылка.ГруппировкиЗатратВЗаказеПереработчику - тип группировки затрат.
//	Возвращаемое значение:
//		СписокЗначений - перечень полей, идентифицирующих группу затрат.
//
Функция ПереченьПолейГруппыЗатрат(ГруппировкаЗатрат) Экспорт
	
	СписокПолей = Новый СписокЗначений;
	
	Если ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции Тогда
		СписокПолей.Добавить("Номенклатура",	НСтр("ru = 'Номенклатура'"));
		СписокПолей.Добавить("Характеристика",	НСтр("ru = 'Характеристика'"));
		СписокПолей.Добавить("Назначение",		НСтр("ru = 'Назначение'"));
	ИначеЕсли ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям Тогда
		СписокПолей.Добавить("Спецификация",	НСтр("ru = 'Спецификация'"));
	ИначеЕсли ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства Тогда
		СписокПолей.Добавить("ЭтапПроизводства",	НСтр("ru = 'Этап производства'"));
	КонецЕсли;
	
	Возврат СписокПолей;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

#Область Инициализация

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ОтразитьВУчетеНДС(Запрос, ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаЗаказыПоставщикам(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаУслугиПереработчиковКОформлению(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыПереданныеПереработчику(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыПолученныеОтПереработчика(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходыНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПартииПроизводственныхЗатрат(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПартииНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаДвиженияНоменклатураДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаВыпускПродукции(Запрос, ТекстыЗапроса, Регистры);
	
	ОтчетПереработчикаЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ИнициализироватьВтСуммыДокументовВВалютахУчета(Запрос, ТекстыЗапроса) Экспорт
	
	ТекстДанныеДокумента = 
	"ВЫБРАТЬ
	|	Услуги.Ссылка							КАК Ссылка,
	|	Услуги.Ссылка.Дата						КАК Дата,
	|	Услуги.Ссылка.Валюта					КАК ВалютаДокумента,
	|	Услуги.Ссылка.ВалютаВзаиморасчетов		КАК ВалютаВзаиморасчетов,
	|	Услуги.ИдентификаторСтроки				КАК ИдентификаторСтроки,
	|	Услуги.СтавкаНДС						КАК СтавкаНДС,
	|	Услуги.СуммаСНДС						КАК СуммаСНДС,
	|	Услуги.СуммаНДС							КАК СуммаНДС,
	|	(Услуги.СуммаСНДС - Услуги.СуммаНДС)	КАК СуммаБезНДС,
	|	Услуги.СуммаВзаиморасчетов				КАК СуммаВзаиморасчетов,
	|	Услуги.СуммаНДСВзаиморасчетов			КАК СуммаНДСВзаиморасчетов
	|ИЗ
	|	Документ.ОтчетПереработчика.Услуги КАК Услуги
	|ГДЕ
	|	Услуги.Ссылка В (&Ссылка)
	|";
	ПроведениеСерверУТ.ИнициализироватьВтСуммыДокументовВВалютахУчета(Запрос, ТекстыЗапроса, ТекстДанныеДокумента);

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетПереработчика.Ссылка                  КАК Ссылка,
	|	ОтчетПереработчика.Дата                    КАК Период,
	|	ОтчетПереработчика.Номер                   КАК Номер,
	|	ОтчетПереработчика.Валюта                  КАК Валюта,
	|	ОтчетПереработчика.Партнер                 КАК Партнер,
	|	ОтчетПереработчика.Контрагент              КАК Контрагент,
	|	ОтчетПереработчика.Организация             КАК Организация,
	|	ОтчетПереработчика.Менеджер                КАК Менеджер,
	|	ОтчетПереработчика.Комментарий             КАК Комментарий,
	|	ОтчетПереработчика.Подразделение           КАК Подразделение,
	|	ОтчетПереработчика.Подразделение.ВариантОбособленногоУчетаТоваров 		  КАК ВариантОбособленногоУчетаТоваров,
	|	ОтчетПереработчика.Сделка 				   КАК Сделка,
	|	ЕСТЬNULL(ОтчетПереработчика.Сделка.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ) КАК ОбособленныйУчетТоваровПоСделке,
	|	ОтчетПереработчика.ХозяйственнаяОперация   КАК ХозяйственнаяОперация,
	|	ОтчетПереработчика.Договор                 КАК Договор,
	|	ПРЕДСТАВЛЕНИЕ(ОтчетПереработчика.Договор)  КАК ДоговорПредставление,
	|	ОтчетПереработчика.ДатаПлатежа             КАК ДатаПлатежа,
	|	ОтчетПереработчика.ФормаОплаты             КАК ФормаОплаты,
	|	ОтчетПереработчика.ВалютаВзаиморасчетов    КАК ВалютаВзаиморасчетов,
	|	ОтчетПереработчика.ПоЗаказам               КАК ПоЗаказам,
	|	ОтчетПереработчика.ЗаказПереработчику      КАК ЗаказПереработчику,
	|	ОтчетПереработчика.ДатаПоДаннымПартнера    КАК ДатаПоДаннымПартнера,
	|	ОтчетПереработчика.НомерПоДаннымПартнера   КАК НомерПоДаннымПартнера,
	|	ОтчетПереработчика.Проведен                КАК Проведен,
	|	ОтчетПереработчика.ПометкаУдаления         КАК ПометкаУдаления,
	|	ДокЗаказПереработчику.Валюта               КАК ВалютаЗаказа,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)  КАК ТипЗапасов,
	|
	|	ОтчетПереработчика.СтавкаНДС               КАК СтавкаНДС,
	|	ОтчетПереработчика.ЗакупкаПодДеятельность  КАК ЗакупкаПодДеятельность,
	|	НЕОПРЕДЕЛЕНО                               КАК Серия,
	|	ОтчетПереработчика.Подразделение           КАК Склад,
	|	ОтчетПереработчика.ГруппировкаЗатрат       КАК ГруппировкаЗатрат,
	|
	|	ОтчетПереработчика.СуммаСНДС               КАК СтоимостьУслуги,
	|	ОтчетПереработчика.СуммаДокумента          КАК СуммаДокумента,
	|
	|	ВЫБОР КОГДА ОтчетПереработчика.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ                                      КАК РасчетыПоДоговорам,
	|
	|	ВЫБОР КОГДА ОтчетПереработчика.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ                                      КАК РасчетыПоНакладным,
	|	ОтчетПереработчика.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ОтчетПереработчика.НаправлениеДеятельности.Назначение,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
	|	
	|	ЕСТЬNULL(ОтчетПереработчика.Договор.ЗаданГрафикИсполнения, ЛОЖЬ) КАК ГрафикИсполненияВДоговоре
	|
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ДокЗаказПереработчику
	|			ПО ДокЗаказПереработчику.Ссылка = ОтчетПереработчика.ЗаказПереработчику
	|ГДЕ
	|	ОтчетПереработчика.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПустаяСсылкаСтатья",                  Справочники.СтатьиКалькуляции.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСсылкаСерия",                   Справочники.СерииНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСсылкаНазначение",              Справочники.Назначения.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",      Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",          Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции", ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",             ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(ДокументСсылка)));
	
	ИнформацияПоДоговору = Неопределено;
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ИнформацияПоДоговору = НСтр("ru='По договору ""%Договор%""'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ИнформацияПоДоговору = СтрЗаменить(ИнформацияПоДоговору, "%Договор%", Реквизиты.ДоговорПредставление);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору", ИнформацияПоДоговору);
	
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета",
								ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Запрос)
	
	Если Запрос = Неопределено ИЛИ Запрос.Параметры.Свойство("КлючиАналитикиНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитики = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Склад             КАК Склад,
	|	ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|	ТаблицаТовары.Характеристика    КАК Характеристика,
	|	ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Назначение        КАК Назначение,
	|	ТаблицаТовары.Серия             КАК Серия
	|ИЗ
	// материалы без назначения
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		&ПустоеНазначение               КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Материалы КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// материалы c назначением
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|			ЕСТЬNULL(СпрПартииПроизводства.Назначение, &ПустоеНазначение)
	|		                                КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Материалы КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// услуги без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		&ПустоеНазначение               КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Услуги КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// услуги с назначением
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|			ЕСТЬNULL(СпрПартииПроизводства.Назначение, &ПустоеНазначение)
	|		                                КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Услуги КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// услуги для ПУ 2.1 на переработчике
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Партнер                        КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		&ПустаяСтатья                   КАК СтатьяКалькуляции,
	|		&ПустоеНазначение               КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Услуги КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// отходы без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|		&ПустоеНазначение               КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// отходы с назначением
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		ТаблицаТовары.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|			ЕСТЬNULL(СпрПартииПроизводства.Назначение, &ПустоеНазначение)
	|		                                КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// продукция без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		&ПустаяСтатья                   КАК СтатьяКалькуляции,
	|		&ПустоеНазначение               КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// продукция с назначением
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение                  КАК Склад,
	|		ТаблицаТовары.Номенклатура      КАК Номенклатура,
	|		ТаблицаТовары.Характеристика    КАК Характеристика,
	|		&ПустаяСтатья                   КАК СтатьяКалькуляции,
	|		ТаблицаТовары.Назначение        КАК Назначение,
	|		&ПустаяСерия                    КАК Серия
	|	ИЗ
	|		Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|
	|	) КАК ТаблицаТовары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|		И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|		И ТаблицаТовары.Серия = Аналитика.Серия
	|		И ТаблицаТовары.Склад = Аналитика.МестоХранения
	|		И ТаблицаТовары.Назначение = Аналитика.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.СтатьяКалькуляции,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Серия");
	
	ЗапросАналитики.УстановитьПараметр("Ссылка",                 Запрос.Параметры.Ссылка);
	ЗапросАналитики.УстановитьПараметр("Подразделение",          Запрос.Параметры.Подразделение);
	ЗапросАналитики.УстановитьПараметр("Партнер",                Запрос.Параметры.Партнер);
	ЗапросАналитики.УстановитьПараметр("ПустаяСерия",            Справочники.СерииНоменклатуры.ПустаяСсылка());
	ЗапросАналитики.УстановитьПараметр("ПустаяСтатья",           Справочники.СтатьиКалькуляции.ПустаяСсылка());
	ЗапросАналитики.УстановитьПараметр("ПустоеНазначение",       Справочники.Назначения.ПустаяСсылка());
	
	Выборка = ЗапросАналитики.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаАналитикаУчетаПоПартнерам(Запрос)
	
	Если Запрос.Параметры.Свойство("АналитикаУчетаПоПартнерам") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам",
		РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Запрос.Параметры));
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаАналитикаУчетаПартий(Запрос)
	
	Если Запрос.Параметры.Свойство("АналитикаУчетаПартий") Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПартии = Новый Структура;
	РеквизитыПартии.Вставить("Дата", 			   Запрос.Параметры.Период);
	РеквизитыПартии.Вставить("ВидЦенности", 	   Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги); 
	РеквизитыПартии.Вставить("Поставщик",   	   Запрос.Параметры.Партнер); 
	РеквизитыПартии.Вставить("Контрагент",  	   Запрос.Параметры.Контрагент); 
	РеквизитыПартии.Вставить("СтавкаНДС",   	   Запрос.Параметры.СтавкаНДС);
	РеквизитыПартии.Вставить("НалогообложениеНДС", Запрос.Параметры.ЗакупкаПодДеятельность);
	
	АналитикаУчетаПартий = Справочники.КлючиАналитикиУчетаПартий.ПолучитьКлючАналитики(РеквизитыПартии);
	
	Запрос.УстановитьПараметр("АналитикаУчетаПартий", АналитикаУчетаПартий);
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуЗаказа")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
																				Запрос.Параметры.ВалютаЗаказа,
																				Запрос.Параметры.Период);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуЗаказа", Коэффициенты.КоэффициентПересчетаВВалютуВзаиморасчетов);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",    Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",   Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросовПроведения

#Область ТекстыЗапросовВременныеТаблицы

Функция ТекстЗапросаВтПартииПроизводства(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПроизводства";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.ПартияВыпуска                      КАК ПартияВыпуска,
	|	ДД.ЭтапПроизводства                   КАК ЭтапПроизводства,
	|	ДД.ПартияПроизводства                 КАК ПартияПроизводства,
	|	ЕСТЬNULL(ДД.Назначение,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))                            КАК Назначение,
	|	ЕСТЬNULL(ДД.НаправлениеДеятельности,
	|		&НаправлениеДеятельности)         КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ДД.ГруппаПродукции,
	|		ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)) КАК ГруппаПродукции,
	|	ЕСТЬNULL(ДД.ВидДеятельностиНДС,
	|		&ЗакупкаПодДеятельность)          КАК ВидДеятельностиНДС,
	// для производства 2.1
	|	ДД.НомерГруппыЗатрат                  КАК НомерГруппыЗатрат,
	|	ДД.ДокументПоступления                КАК ДокументПоступления
	|ПОМЕСТИТЬ ВтПартииПроизводства
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Ссылка                                  КАК ПартияВыпуска,
	|		ТаблицаТовары.ЭтапПроизводства           КАК ЭтапПроизводства,
	|		СпрПартииПроизводства.Ссылка             КАК ПартияПроизводства,
	|		СпрПартииПроизводства.Назначение         КАК Назначение,
	|		СпрПартииПроизводства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СпрПартииПроизводства.ГруппаПродукции    КАК ГруппаПродукции,
	|		СпрПартииПроизводства.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	// для производства 2.1
	|		ТаблицаТовары.НомерГруппыЗатрат          КАК НомерГруппыЗатрат,
	|		ТаблицаТовары.ДокументПоступления        КАК ДокументПоступления
	|	ИЗ
	|		Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК Реквизиты
	|		ПО Реквизиты.Ссылка = ТаблицаТовары.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = &Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И НЕ Реквизиты.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	|	) КАК ДД
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса)
	
	ТекстВыборкаПоляАналитик =
	"ВЫБРАТЬ
	|	""Продукция""						КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки		КАК НомерСтроки,
	|	Реквизиты.Партнер					КАК Поставщик,
	|	Реквизиты.Контрагент				КАК Контрагент,
	|	Реквизиты.СтавкаНДС					КАК СтавкаНДС,
	|	Реквизиты.ЗакупкаПодДеятельность	КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)	КАК ВидЦенности,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА ТаблицаДокумента.КодСтрокиПродукция
	|		ИНАЧЕ 0
	|	КОНЕЦ								КАК КодСтроки
	|ПОМЕСТИТЬ ВТПоляАналитикУчетаПартий
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК Реквизиты
	|		ПО ТаблицаДокумента.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ВозвратныеОтходы""				КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки		КАК НомерСтроки,
	|	Реквизиты.Партнер					КАК Поставщик,
	|	Реквизиты.Контрагент				КАК Контрагент,
	|	Реквизиты.СтавкаНДС					КАК СтавкаНДС,
	|	Реквизиты.ЗакупкаПодДеятельность	КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|	0								КАК КодСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК Реквизиты
	|		ПО ТаблицаДокумента.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Услуги""								КАК ИмяТабличнойЧасти,
	|	ТаблицаУслуги.НомерСтроки				КАК НомерСтроки,
	|	Реквизиты.Партнер						КАК Поставщик,
	|	Реквизиты.Контрагент					КАК Контрагент,
	|	ТаблицаУслуги.СтавкаНДС					КАК СтавкаНДС,
	|	Реквизиты.ЗакупкаПодДеятельность		КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КАК ВидЦенности,
	|	0										КАК КодСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.Услуги КАК ТаблицаУслуги
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК Реквизиты
	|	ПО Реквизиты.Ссылка = ТаблицаУслуги.Ссылка
	|
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|";
	
	ТекстЗапроса = Справочники.КлючиАналитикиУчетаПартий.ТекстЗапросаВтТаблицаАналитикУчетаПартий(ТекстВыборкаПоляАналитик, Запрос, ТекстыЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтВидыЗапасов";
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПроизводства", ТекстыЗапроса) Тогда
		ТекстЗапросаВтПартииПроизводства(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	// материалы, строится по таблице ВидыЗапасов
	|ВЫБРАТЬ
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры                        КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики                            КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику) КАК РазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК ВидЗапасов,
	|	ПартииПроизводства.ПартияПроизводства                           КАК ПартияПроизводства,
	|	ПартииПроизводства.ПартияВыпуска                                КАК ПартияВыпуска,
	|	СУММА(ТаблицаТовары.Количество)                                 КАК Количество,
	|	0                                                               КАК Стоимость,
	|	0                                                               КАК СтоимостьБезНДС,
	|	0                                                               КАК СтоимостьРегл,
	|	0                                                               КАК СуммаНДС,
	|	0                                                               КАК НДСУпр,
	|	0                                                               КАК СтоимостьУпр,
	|	ПартииПроизводства.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК КорРазделУчета,
	|	АналитикаПроизводства.КлючАналитики                             КАК КорАналитикаУчетаНоменклатуры,
	|	АналитикаПроизводстваБезНазначения.КлючАналитики                КАК КорАналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.СтатьяКалькуляции                                 КАК СтатьяКалькуляции,
	|	ПартииПроизводства.ГруппаПродукции                              КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.ЭтапПроизводства                                  КАК ЭтапПроизводства,
	|	0                                                               КАК ДоляСтоимости,
	|	&Подразделение                                                  КАК Подразделение,
	|	ЛОЖЬ                                                            КАК СписатьНаРасходы,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ИдентификаторСтроки,
	// для производства 2.1
	|	ТаблицаТовары.НомерГруппыЗатрат                                 КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ДокументПоступления                               КАК ДокументПоступления,
	|	0                                                               КАК КодСтроки
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.ОтчетПереработчика.ВидыЗапасов КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПартииПроизводства КАК ПартииПроизводства
	|		ПО ТаблицаТовары.ЭтапПроизводства = ПартииПроизводства.ЭтапПроизводства
	|		И ТаблицаТовары.НомерГруппыЗатрат = ПартииПроизводства.НомерГруппыЗатрат
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И &ПустаяСсылкаНазначение = АналитикаБезНазначения.Назначение
	|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПроизводства
	|		ПО Аналитика.Номенклатура = АналитикаПроизводства.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПроизводства.Характеристика
	|		И Аналитика.Серия = АналитикаПроизводства.Серия
	|		И &Подразделение = АналитикаПроизводства.МестоХранения
	|		И ПартииПроизводства.Назначение = АналитикаПроизводства.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = АналитикаПроизводства.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПроизводстваБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаПроизводстваБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПроизводстваБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаПроизводстваБезНазначения.Серия
	|		И &Подразделение = АналитикаПроизводстваБезНазначения.МестоХранения
	|		И &ПустаяСсылкаНазначение = АналитикаПроизводстваБезНазначения.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = АналитикаПроизводстваБезНазначения.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики,
	|	ТаблицаТовары.ВидЗапасов,
	|	ПартииПроизводства.ПартияПроизводства,
	|	ПартииПроизводства.ПартияВыпуска,
	|	ПартииПроизводства.ГруппаПродукции,
	|	ПартииПроизводства.ВидДеятельностиНДС,
	|	АналитикаПроизводства.КлючАналитики,
	|	АналитикаПроизводстваБезНазначения.КлючАналитики,
	|	ТаблицаТовары.СтатьяКалькуляции,
	|	ТаблицаТовары.ЭтапПроизводства,
	|	ТаблицаТовары.НомерГруппыЗатрат,
	|	ТаблицаТовары.ДокументПоступления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// продукция, строится по таблице Продукция
	|ВЫБРАТЬ
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры                        КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики                            КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидЗапасов,
	|	ПартииПроизводства.ПартияПроизводства                           КАК ПартияПроизводства,
	|	ПартииПроизводства.ПартияВыпуска                                КАК ПартияВыпуска,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	0                                                               КАК Стоимость,
	|	0                                                               КАК СтоимостьБезНДС,
	|	0                                                               КАК СтоимостьРегл,
	|	0                                                               КАК СуммаНДС,
	|	0                                                               КАК НДСУпр,
	|	0                                                               КАК СтоимостьУпр,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|				<> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ЗакупкаПодДеятельность
	|	КОНЕЦ 								  							КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)    КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатурыБезНазначения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	ПартииПроизводства.ГруппаПродукции                              КАК ГруппаПродукции,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорГруппаПродукции,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий                 КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.ЭтапПроизводства                                  КАК ЭтапПроизводства,
	|	ВЫБОР КОГДА ТаблицаТовары.ДоляСтоимости = 0 ТОГДА ТаблицаТовары.Количество ИНАЧЕ ТаблицаТовары.ДоляСтоимости КОНЕЦ КАК ДоляСтоимости,
	|	ВЫБОР 
	|		КОГДА ТаблицаТовары.Получатель = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ                                                           КАК Подразделение,
	|	ТаблицаТовары.СписатьНаРасходы                                  КАК СписатьНаРасходы,
	|	ТаблицаТовары.СтатьяРасходов                                    КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов                                 КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов                          КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	// для производства 2.1
	|	ТаблицаТовары.НомерГруппыЗатрат                                 КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ДокументПоступления                               КАК ДокументПоступления,
	|	ТаблицаТовары.КодСтроки                                         КАК КодСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПартииПроизводства КАК ПартииПроизводства
	|		ПО ТаблицаТовары.ЭтапПроизводства = ПартииПроизводства.ЭтапПроизводства
	|		И ТаблицаТовары.НомерГруппыЗатрат = ПартииПроизводства.НомерГруппыЗатрат
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И &ПустаяСсылкаНазначение = АналитикаБезНазначения.Назначение
	|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|		ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ТаблицаТовары.НомерСтроки
	|		И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Продукция""
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// отходы, строится по таблице ВозвратныеОтходы
	|ВЫБРАТЬ
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры                        КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики                            КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК ВидЗапасов,
	|	ПартииПроизводства.ПартияПроизводства                           КАК ПартияПроизводства,
	|	ПартииПроизводства.ПартияВыпуска                                КАК ПартияВыпуска,
	|	ТаблицаТовары.Количество                                                          КАК Количество,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр  КАК ЧИСЛО(31,2)) КАК Стоимость,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр  КАК ЧИСЛО(31,2)) КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СтоимостьРегл,
	|	0                                                               КАК СуммаНДС,
	|	0                                                               КАК НДСУпр,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                           КАК СтоимостьУпр,
	|	ПартииПроизводства.ВидДеятельностиНДС	КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК КорРазделУчета,
	|	АналитикаПроизводства.КлючАналитики                             КАК КорАналитикаУчетаНоменклатуры,
	|	АналитикаПроизводстваБезНазначения.КлючАналитики                КАК КорАналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.СтатьяКалькуляции                                 КАК СтатьяКалькуляции,
	|	ПартииПроизводства.ГруппаПродукции                              КАК ГруппаПродукции,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорГруппаПродукции,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий                 КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.ЭтапПроизводства                                  КАК ЭтапПроизводства,
	|	0                                                               КАК ДоляСтоимости,
	|	ВЫБОР 
	|		КОГДА ТаблицаТовары.Получатель = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ                                                           КАК Подразделение,
	|	ТаблицаТовары.СписатьНаРасходы                                  КАК СписатьНаРасходы,
	|	ТаблицаТовары.СтатьяРасходов                                    КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов                                 КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов                          КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	// для производства 2.1
	|	ТаблицаТовары.НомерГруппыЗатрат                                 КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ДокументПоступления                               КАК ДокументПоступления,
	|	ТаблицаТовары.КодСтроки                                         КАК КодСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПартииПроизводства КАК ПартииПроизводства
	|		ПО ТаблицаТовары.ЭтапПроизводства = ПартииПроизводства.ЭтапПроизводства
	|		И ТаблицаТовары.НомерГруппыЗатрат = ПартииПроизводства.НомерГруппыЗатрат
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И &ПустаяСсылкаНазначение = АналитикаБезНазначения.Назначение
	|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПроизводства
	|		ПО Аналитика.Номенклатура = АналитикаПроизводства.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПроизводства.Характеристика
	|		И Аналитика.Серия = АналитикаПроизводства.Серия
	|		И &Подразделение = АналитикаПроизводства.МестоХранения
	|		И ПартииПроизводства.Назначение = АналитикаПроизводства.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = АналитикаПроизводства.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПроизводстваБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаПроизводстваБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПроизводстваБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаПроизводстваБезНазначения.Серия
	|		И &Подразделение = АналитикаПроизводстваБезНазначения.МестоХранения
	|		И &ПустаяСсылкаНазначение = АналитикаПроизводстваБезНазначения.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = АналитикаПроизводстваБезНазначения.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|		ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ТаблицаТовары.НомерСтроки
	|		И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""ВозвратныеОтходы""
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// услуги, строится по таблице Услуги
	|ВЫБРАТЬ
	|	АналитикаПроизводства.КлючАналитики                             КАК АналитикаУчетаНоменклатуры,
	|	АналитикаПроизводстваБезНазначения.КлючАналитики                КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидЗапасов,
	|	ПартииПроизводства.ПартияПроизводства                           КАК ПартияПроизводства,
	|	ПартииПроизводства.ПартияВыпуска                                КАК ПартияВыпуска,
	|	1                                                               КАК Количество,
	|	СУММА(ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр,
	|			ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|			КОНЕЦ))                                                 КАК Стоимость,
	|	СУММА(ЕСТЬNULL(Суммы.СуммаБезНДСУпр,
	|			ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов - ТаблицаТовары.СуммаНДСВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ(((ТаблицаТовары.СуммаСНДС-ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|			КОНЕЦ))                                                 КАК СтоимостьБезНДС,
	|	СУММА(ВЫБОР КОГДА &ЗакупкаПодДеятельность В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА
	|				ЕСТЬNULL(Суммы.СуммаБезНДСРегл,
	|					ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА ТаблицаТовары.СуммаВзаиморасчетов - ТаблицаТовары.СуммаНДСВзаиморасчетов
	|						ИНАЧЕ ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) 
	|					КОНЕЦ)
	|				+ ЕСТЬNULL(Суммы.СуммаНДСРегл, 
	|					ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА ТаблицаТовары.СуммаНДСВзаиморасчетов
	|						ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2))
	|					КОНЕЦ)
	|			ИНАЧЕ
	|				ЕСТЬNULL(Суммы.СуммаБезНДСРегл,
	|					ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА ТаблицаТовары.СуммаВзаиморасчетов - ТаблицаТовары.СуммаНДСВзаиморасчетов
	|						ИНАЧЕ ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2)) 
	|					КОНЕЦ)
	|			КОНЕЦ)                                                  КАК СтоимостьРегл,
	|	СУММА(ЕСТЬNULL(Суммы.СуммаНДСРегл,
	|			ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|				ТОГДА ТаблицаТовары.СуммаНДСВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2))
	|			КОНЕЦ))                                                 КАК СуммаНДС,
	|	СУММА(ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр,
	|			ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|				ИНАЧЕ ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2))
	|			КОНЕЦ)
	|			- ЕСТЬNULL(Суммы.СуммаБезНДСУпр,
	|				ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|					ТОГДА ТаблицаТовары.СуммаВзаиморасчетов - ТаблицаТовары.СуммаНДСВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ(((ТаблицаТовары.СуммаСНДС-ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2))
	|			КОНЕЦ))                                                 КАК НДСУпр,
	|	СУММА(ВЫБОР КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА
	|				ВЫБОР КОГДА &ЗакупкаПодДеятельность В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|				ТОГДА
	|					ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр,
	|						ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|							ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
	|							ИНАЧЕ ВЫРАЗИТЬ((ТаблицаТовары.СуммаСНДС * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2))
	|					КОНЕЦ)
	|				ИНАЧЕ
	|					ЕСТЬNULL(Суммы.СуммаБезНДСУпр,
	|						ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|							ТОГДА ТаблицаТовары.СуммаВзаиморасчетов - ТаблицаТовары.СуммаНДСВзаиморасчетов
	|							ИНАЧЕ ВЫРАЗИТЬ(((ТаблицаТовары.СуммаСНДС-ТаблицаТовары.СуммаНДС) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(15,2))
	|					КОНЕЦ)
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                          КАК СтоимостьУпр,
	|	ПартииПроизводства.ВидДеятельностиНДС  КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.СтатьяКалькуляции                                 КАК СтатьяКалькуляции,
	|	ПартииПроизводства.ГруппаПродукции                              КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорГруппаПродукции,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий                 КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.ЭтапПроизводства                                  КАК ЭтапПроизводства,
	|	0                                                               КАК ДоляСтоимости,
	|	&Подразделение                                                  КАК Подразделение,
	|	ЛОЖЬ                                                            КАК СписатьНаРасходы,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ИдентификаторСтроки,
	// для производства 2.1
	|	ТаблицаТовары.НомерГруппыЗатрат                                 КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ДокументПоступления                               КАК ДокументПоступления,
	|	0                                                               КАК КодСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.Услуги КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПартииПроизводства КАК ПартииПроизводства
	|		ПО ТаблицаТовары.ЭтапПроизводства = ПартииПроизводства.ЭтапПроизводства
	|		И ТаблицаТовары.НомерГруппыЗатрат = ПартииПроизводства.НомерГруппыЗатрат
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПроизводства
	|		ПО Аналитика.Номенклатура = АналитикаПроизводства.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПроизводства.Характеристика
	|		И Аналитика.Серия = АналитикаПроизводства.Серия
	|		И &Подразделение = АналитикаПроизводства.МестоХранения
	|		И ПартииПроизводства.Назначение = АналитикаПроизводства.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = АналитикаПроизводства.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПроизводстваБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаПроизводстваБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаПроизводстваБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаПроизводстваБезНазначения.Серия
	|		И &Подразделение = АналитикаПроизводстваБезНазначения.МестоХранения
	|		И &ПустаяСсылкаНазначение = АналитикаПроизводстваБезНазначения.Назначение
	|		И ТаблицаТовары.СтатьяКалькуляции = АналитикаПроизводстваБезНазначения.СтатьяКалькуляции
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|		ПО Суммы.Регистратор = ТаблицаТовары.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|		И Суммы.СуммаБезНДС = (ТаблицаТовары.СуммаСНДС - ТаблицаТовары.СуммаНДС)
	|		И Суммы.СуммаНДС = ТаблицаТовары.СуммаНДС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|		ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ТаблицаТовары.НомерСтроки
	|		И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Услуги""
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.СуммаСНДС > 0 // суммы не заполнены, услуги указываются отдельно
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	ПартииПроизводства.ПартияПроизводства,
	|	ПартииПроизводства.ПартияВыпуска,
	|	ПартииПроизводства.ГруппаПродукции,
	|	ПартииПроизводства.ВидДеятельностиНДС,
	|	АналитикаПроизводства.КлючАналитики,
	|	АналитикаПроизводстваБезНазначения.КлючАналитики,
	|	ТаблицаТовары.СтатьяКалькуляции,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий,
	|	ТаблицаТовары.ЭтапПроизводства,
	|	ТаблицаТовары.НомерГруппыЗатрат,
	|	ТаблицаТовары.ДокументПоступления
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция РаспределенныеВидыЗапасов(ОсновнойЗапрос)
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(ОсновнойЗапрос);
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Для Каждого Параметр Из ОсновнойЗапрос.Параметры Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	
	ТекстыЗапроса = Новый СписокЗначений;
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса));
	МассивТекстов.Добавить(ТекстЗапросаВтПартииПроизводства(Запрос, ТекстыЗапроса));
	МассивТекстов.Добавить(ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса));
	
	ТекстЗапроса =
	// шаблон распределенных видов запасов, ВГраница - 3
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|   ДД.ИдентификаторСтроки                           КАК ИдентификаторСтроки,
	|	ДД.АналитикаУчетаНоменклатуры                    КАК АналитикаУчетаНоменклатуры,
	|	ДД.АналитикаУчетаНоменклатурыБезНазначения       КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ДД.РазделУчета                                   КАК РазделУчета,
	|	ДД.ВидЗапасов                                    КАК ВидЗапасов,
	|	ДД.ПартияПроизводства                            КАК ПартияПроизводства,
	|	ДД.ПартияВыпуска                                 КАК ПартияВыпуска,
	|	ДД.ХозяйственнаяОперация                         КАК ХозяйственнаяОперация,
	|	ДД.ХозяйственнаяОперация                         КАК ХозяйственнаяОперацияИсходная,
	|	ДД.КорАналитикаУчетаНоменклатуры                 КАК КорАналитикаУчетаНоменклатуры,
	|	ДД.КорАналитикаУчетаНоменклатурыБезНазначения    КАК КорАналитикаУчетаНоменклатурыБезНазначения,
	|	ДД.КорРазделУчета                                КАК КорРазделУчета,
	|	ДД.ВидЗапасов                                    КАК КорВидЗапасов,
	|	ДД.ВидДеятельностиНДС                            КАК КорВидДеятельностиНДС,
	|	ДД.ГруппаПродукции                               КАК ГруппаПродукции,
	|	ДД.КорГруппаПродукции                            КАК КорГруппаПродукции,
	|	ДД.АналитикаУчетаПартий                          КАК АналитикаУчетаПартий,
	|	ДД.АналитикаУчетаПартий                          КАК КорАналитикаУчетаПартий,
	|	ДД.Количество                                    КАК Количество,
	|	ДД.Стоимость                                     КАК Стоимость,
	|	ДД.СтоимостьБезНДС                               КАК СтоимостьБезНДС,
	|	ДД.СтоимостьРегл                                 КАК СтоимостьРегл,
	|	ДД.СуммаНДС                                      КАК СуммаНДС,
	|	ДД.НДСУпр                                        КАК НДСУпр,
	|	ДД.СтоимостьУпр                                  КАК СтоимостьУпр,
	// для производства 2.1
	|	ДД.НомерГруппыЗатрат                             КАК НомерГруппыЗатрат,
	|	ДД.СтатьяКалькуляции                             КАК СтатьяКалькуляции,
	|	ДД.ДокументПоступления                           КАК ДокументПоступления,
	|	ДД.КодСтроки                                     КАК КодСтроки
	|ИЗ
	|	ВтВидыЗапасов КАК ДД
	|;
	|
	// партии, ВГраница - 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Партии.ЭтапПроизводства                        КАК ЭтапПроизводства,
	|	Партии.НомерГруппыЗатрат                       КАК НомерГруппыЗатрат,
	|	Партии.ДокументПоступления                     КАК ДокументПоступления
	|ИЗ
	|	ВтПартииПроизводства КАК Партии
	|;
	|
	// продукция, ВГраница - 1
	|ВЫБРАТЬ
	|	ТаблицаТовары.ИдентификаторСтроки              КАК ИдентификаторСтроки,
	|	ТаблицаТовары.ДоляСтоимости                    КАК ДоляСтоимости,
	|	ТаблицаТовары.НомерГруппыЗатрат                КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.АналитикаУчетаПартий             КАК КорАналитикаУчетаПартий,
	|	ТаблицаТовары.ЭтапПроизводства                 КАК ЭтапПроизводства,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры       КАК КорАналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения КАК КорАналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.КорГруппаПродукции               КАК КорГруппаПродукции,
	|	ТаблицаТовары.ВидЗапасов                       КАК КорВидЗапасов,
	|	ТаблицаТовары.РазделУчета                      КАК КорРазделУчета,
	|	ТаблицаТовары.ВидДеятельностиНДС               КАК КорВидДеятельностиНДС,
	|	ТаблицаТовары.ПартияВыпуска                    КАК ПартияВыпуска,
	|	ТаблицаТовары.ХозяйственнаяОперация            КАК ХозяйственнаяОперация,
	// для производства 2.1
	|	ТаблицаТовары.ДокументПоступления              КАК ДокументПоступления,
	|	ТаблицаТовары.КодСтроки                        КАК КодСтроки
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|;
	|
	// материалы, отходы и услуги к распределению, ВГраница
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерГруппыЗатрат                КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ЭтапПроизводства                 КАК ЭтапПроизводства,
	|	ТаблицаТовары.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.КорАналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.ГруппаПродукции                  КАК ГруппаПродукции,
	|	ТаблицаТовары.ВидЗапасов                       КАК ВидЗапасов,
	|	ТаблицаТовары.СтатьяКалькуляции                КАК СтатьяКалькуляции,
	|	ТаблицаТовары.ХозяйственнаяОперация            КАК ХозяйственнаяОперацияИсходная,
	|	ТаблицаТовары.КорРазделУчета                   КАК РазделУчета,
	|	ТаблицаТовары.ПартияПроизводства               КАК ПартияПроизводства,
	|	ТаблицаТовары.Количество                       КАК Количество,
	|	ТаблицаТовары.Стоимость                        КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                  КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                    КАК СтоимостьРегл,
	|	ТаблицаТовары.СуммаНДС                         КАК СуммаНДС,
	|	ТаблицаТовары.НДСУпр                           КАК НДСУпр,
	|	ТаблицаТовары.СтоимостьУпр                     КАК СтоимостьУпр,
	// для производства 2.1
	|	ТаблицаТовары.ДокументПоступления              КАК ДокументПоступления
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерГруппыЗатрат                КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ЭтапПроизводства                 КАК ЭтапПроизводства,
	|	ТаблицаТовары.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.ГруппаПродукции                  КАК ГруппаПродукции,
	|	ТаблицаТовары.ВидЗапасов                       КАК ВидЗапасов,
	|	ТаблицаТовары.СтатьяКалькуляции                КАК СтатьяКалькуляции,
	|	ТаблицаТовары.ХозяйственнаяОперация            КАК ХозяйственнаяОперацияИсходная,
	|	ТаблицаТовары.РазделУчета                      КАК РазделУчета,
	|	ТаблицаТовары.ПартияПроизводства               КАК ПартияПроизводства,
	|	ТаблицаТовары.Количество                       КАК Количество,
	|	ТаблицаТовары.Стоимость                        КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                  КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                    КАК СтоимостьРегл,
	|	ТаблицаТовары.СуммаНДС                         КАК СуммаНДС,
	|	ТаблицаТовары.НДСУпр                           КАК НДСУпр,
	|	ТаблицаТовары.СтоимостьУпр                     КАК СтоимостьУпр,
	// для производства 2.1
	|	ТаблицаТовары.ДокументПоступления              КАК ДокументПоступления
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерГруппыЗатрат                КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.ЭтапПроизводства                 КАК ЭтапПроизводства,
	|	ТаблицаТовары.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.КорАналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.ГруппаПродукции                  КАК ГруппаПродукции,
	|	ТаблицаТовары.ВидЗапасов                       КАК ВидЗапасов,
	|	ТаблицаТовары.СтатьяКалькуляции                КАК СтатьяКалькуляции,
	|	ТаблицаТовары.ХозяйственнаяОперация            КАК ХозяйственнаяОперацияИсходная,
	|	ТаблицаТовары.КорРазделУчета                   КАК РазделУчета,
	|	ТаблицаТовары.ПартияПроизводства               КАК ПартияПроизводства,
	|	- СУММА(ТаблицаТовары.Количество)              КАК Количество,
	|	- СУММА(ТаблицаТовары.Стоимость)               КАК Стоимость,
	|	- СУММА(ТаблицаТовары.СтоимостьБезНДС)         КАК СтоимостьБезНДС,
	|	- СУММА(ТаблицаТовары.СтоимостьРегл)           КАК СтоимостьРегл,
	|	- СУММА(ТаблицаТовары.СуммаНДС)                КАК СуммаНДС,
	|	- СУММА(ТаблицаТовары.НДСУпр)                  КАК НДСУпр,
	|	- СУММА(ТаблицаТовары.СтоимостьУпр)            КАК СтоимостьУпр,
	// для производства 2.1
	|	ТаблицаТовары.ДокументПоступления              КАК ДокументПоступления
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.НомерГруппыЗатрат,
	|	ТаблицаТовары.ЭтапПроизводства,
	|	ТаблицаТовары.АналитикаУчетаПартий,
	|	ТаблицаТовары.КорАналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаТовары.ГруппаПродукции,
	|	ТаблицаТовары.ВидЗапасов,
	|	ТаблицаТовары.СтатьяКалькуляции,
	|	ТаблицаТовары.ХозяйственнаяОперация,
	|	ТаблицаТовары.КорРазделУчета,
	|	ТаблицаТовары.ПартияПроизводства,
	// для производства 2.1
	|	ТаблицаТовары.ДокументПоступления
	|";
	
	МассивТекстов.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	Граница = ПакетРезультатов.ВГраница();
	
	РаспределенныеВидыЗапасов      = ПакетРезультатов[Граница - 3].Выгрузить();
	ТаблицаПартий                  = ПакетРезультатов[Граница - 2].Выгрузить();
	ТаблицаПродукции               = ПакетРезультатов[Граница - 1].Выгрузить();
	ТаблицаКРаспределению          = ПакетРезультатов[Граница].Выгрузить();
	
	ПоЗаказам = ОсновнойЗапрос.Параметры.ПоЗаказам;
	ГруппировкаЗатрат = ОсновнойЗапрос.Параметры.ГруппировкаЗатрат;
	
	Для каждого Партия Из ТаблицаПартий Цикл
		
		Если ПоЗаказам И ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.БезГруппировки Тогда
			СписокПродукции      = ТаблицаПродукции;
			СписокКРаспределению = ТаблицаКРаспределению;
		Иначе
			Если ПоЗаказам И ЗначениеЗаполнено(Партия.ЭтапПроизводства) Тогда
				СтруктураПоиска = Новый Структура("ЭтапПроизводства", Партия.ЭтапПроизводства);
			Иначе
				СтруктураПоиска = Новый Структура("НомерГруппыЗатрат, ДокументПоступления", Партия.НомерГруппыЗатрат, Партия.ДокументПоступления);
			КонецЕсли;
			СписокПродукции      = ТаблицаПродукции.НайтиСтроки(СтруктураПоиска);
			СписокКРаспределению = ТаблицаКРаспределению.НайтиСтроки(СтруктураПоиска);
		КонецЕсли;
		
		СтрокПродукции   = СписокПродукции.Количество();
		СтрокКРаспределению = СписокКРаспределению.Количество();
		
		МассивКоэффициентов = Новый Массив;
		Для каждого ДанныеСтроки Из СписокПродукции Цикл
			МассивКоэффициентов.Добавить(?(ДанныеСтроки.ДоляСтоимости <> 0, ДанныеСтроки.ДоляСтоимости, 1));
		КонецЦикла; 
		Если МассивКоэффициентов.Количество() = 1 Тогда
			МассивКоэффициентов[0] = 1;
		КонецЕсли;
		
		// распределим на выпущенную продукцию
		Для ИндексСтроки = 0 По СтрокКРаспределению - 1 Цикл
			
			РаспределяемаяСтрока = СписокКРаспределению[ИндексСтроки];
			
			Количество      = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.Количество, МассивКоэффициентов, 3);
			Стоимость       = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.Стоимость, МассивКоэффициентов, 3);
			СтоимостьБезНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.СтоимостьБезНДС, МассивКоэффициентов, 3);
			СтоимостьРегл   = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.СтоимостьРегл, МассивКоэффициентов, 3);
			СуммаНДС        = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.СуммаНДС, МассивКоэффициентов, 3);
			НДСУпр        	= ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.НДСУпр, МассивКоэффициентов, 3);
			СтоимостьУпр    = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(РаспределяемаяСтрока.СтоимостьУпр, МассивКоэффициентов, 3);
			
			Для ИндексПродукции = 0 По СтрокПродукции - 1 Цикл
				
				СтрокаПродукции = СписокПродукции[ИндексПродукции];
				
				НоваяСтрока = РаспределенныеВидыЗапасов.Добавить();
				
				// заполнение аналитики материала, отхода, услуги
				ЗаполнитьЗначенияСвойств(НоваяСтрока, РаспределяемаяСтрока);
				
				// заполнение аналитики продукции в кор части
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПродукции);
				
				НоваяСтрока.Количество      = ?(ЗначениеЗаполнено(Количество), Количество[ИндексПродукции], 0);
				НоваяСтрока.Стоимость       = ?(ЗначениеЗаполнено(Стоимость), Стоимость[ИндексПродукции], 0);
				НоваяСтрока.СтоимостьБезНДС = ?(ЗначениеЗаполнено(СтоимостьБезНДС), СтоимостьБезНДС[ИндексПродукции], 0);
				НоваяСтрока.СтоимостьРегл   = ?(ЗначениеЗаполнено(СтоимостьРегл), СтоимостьРегл[ИндексПродукции], 0);
				НоваяСтрока.СуммаНДС        = ?(ЗначениеЗаполнено(СуммаНДС), СуммаНДС[ИндексПродукции], 0);
				НоваяСтрока.НДСУпр        	= ?(ЗначениеЗаполнено(НДСУпр), НДСУпр[ИндексПродукции], 0);
				НоваяСтрока.СтоимостьУпр    = ?(ЗначениеЗаполнено(СтоимостьУпр), СтоимостьУпр[ИндексПродукции], 0);
				
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЦикла;
	
	КолонкиГруппировок = "";
	КолонкиСуммирования = "Количество,Стоимость,СтоимостьБезНДС,СтоимостьРегл,СуммаНДС,НДСУпр,СтоимостьУпр";
	
	МассивПоказателей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КолонкиСуммирования);
	Для Каждого Колонка Из РаспределенныеВидыЗапасов.Колонки Цикл
		ЭтоКолонкаГруппировки = Истина;
		Для Каждого Показатель Из МассивПоказателей Цикл
			Если СтрНайти(Колонка.Имя, Показатель) > 0 Тогда
				ЭтоКолонкаГруппировки = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЭтоКолонкаГруппировки Тогда
			Если ЗначениеЗаполнено(КолонкиГруппировок) Тогда
				КолонкиГруппировок = КолонкиГруппировок + "," + Колонка.Имя;
			Иначе
				КолонкиГруппировок = Колонка.Имя;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	РаспределенныеВидыЗапасов.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	Возврат РаспределенныеВидыЗапасов;
	
КонецФункции

Функция ТекстЗапросаВтРаспределенныеВидыЗапасов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтРаспределенныеВидыЗапасов";
	
	ТаблицаРаспределенныеВидыЗапасов = РаспределенныеВидыЗапасов(Запрос);
	Запрос.УстановитьПараметр("ТаблицаРаспределенныеВидыЗапасов", ТаблицаРаспределенныеВидыЗапасов);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВтРаспределенныеВидыЗапасов
	|
	|ИЗ
	|	&ТаблицаРаспределенныеВидыЗапасов КАК ДД
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаМатериалы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаМатериалы";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                  КАК Ссылка,
	|	&Партнер                                 КАК Склад,
	|	&Организация                             КАК Организация,
	|	ТаблицаТовары.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура       КАК Номенклатура,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры.Характеристика     КАК Характеристика,
	|	ТаблицаТовары.ВидЗапасов                 КАК ВидЗапасов,
	|	ТаблицаТовары.ВидЗапасов.ТипЗапасов      КАК ТипЗапасов,
	|	ТаблицаТовары.Количество                 КАК Количество
	|
	|ПОМЕСТИТЬ ВтТаблицаМатериалы
	|
	|ИЗ
	|	Документ.ОтчетПереработчика.ВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаТаблицаТоварыПереданныеПереработчику(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыПереданныеПереработчику";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаМатериалы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаМатериалы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	НачалоПериода(&Период, День)             КАК Период,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов                 КАК ВидЗапасов,
	|	ТаблицаТовары.Количество                 КАК Количество
	|ИЗ
	|	ВтТаблицаМатериалы КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "СебестоимостьТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПроизводства", ТекстыЗапроса) Тогда
		ТекстЗапросаВтПартииПроизводства(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРаспределенныеВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРаспределенныеВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	УстановитьПараметрыЗапросаАналитикаУчетаПартий(Запрос);
	
	ТекстЗапроса = "
	// расход материалов с товаров переданных
	|ВЫБРАТЬ
	|	1                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ТаблицаТовары.РазделУчета                                       КАК РазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.ПартияПроизводства
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаПартий,
	|	0                                                               КАК НДСРегл,
	|	0                                                               КАК НДСУпр,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			И НЕ ТаблицаТовары.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.ВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)            КАК ТипЗаписи,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	0                                                               КАК Стоимость,
	|	0                                                               КАК СтоимостьБезНДС,
	|	0                                                               КАК СтоимостьРегл,
	|	0                                                               КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.КорРазделУчета
	|		ИНАЧЕ // при ПУ 2.1 используется Производственные затраты
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ                                                           КАК КорРазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|			ВЫБОР
	|				КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|					ТОГДА ТаблицаТовары.КорАналитикаУчетаНоменклатуры
	|				ИНАЧЕ ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|			КОНЕЦ
	|		ИНАЧЕ // при ПУ 2.1 аналитика учета номенклатуры не меняется
	|			ВЫБОР
	|				КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|					ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|				ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|			КОНЕЦ 
	|	КОНЕЦ                                                           КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ТаблицаТовары.ГруппаПродукции                                   КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход материалов на незавершенное производство
	|ВЫБРАТЬ
	|	2                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|			ВЫБОР
	|				КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|					ТОГДА ТаблицаТовары.КорАналитикаУчетаНоменклатуры
	|				ИНАЧЕ ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|			КОНЕЦ
	|		ИНАЧЕ // при ПУ 2.1 аналитика учета номенклатуры не меняется
	|			ВЫБОР
	|				КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|					ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|				ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|			КОНЕЦ 
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.КорРазделУчета
	|		ИНАЧЕ // при ПУ 2.1 используется Производственные затраты
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ                                                           КАК РазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.ПартияПроизводства
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК Партия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			И НЕ ТаблицаТовары.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаФинансовогоУчета,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаТовары.ВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаПартий,
	|	0                                                               КАК НДСРегл,
	|	0                                                               КАК НДСУпр,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)            КАК ТипЗаписи,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	0                                                               КАК Стоимость,
	|	0                                                               КАК СтоимостьБезНДС,
	|	0                                                               КАК СтоимостьРегл,
	|	0                                                               КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ТаблицаТовары.ГруппаПродукции                                   КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход работ по переработке на незавершенное производство
	|ВЫБРАТЬ
	|	3                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|			ВЫБОР
	|				КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|					ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|				ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|			КОНЕЦ
	|		ИНАЧЕ // при ПУ 2.1 аналитика учета номенклатуры не меняется
	|			Аналитика21.КлючАналитики
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.РазделУчета
	|		ИНАЧЕ // при ПУ 2.1 используется Производственные затраты
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ                                                           КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаТовары.ПартияПроизводства
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК Партия,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			И НЕ ТаблицаТовары.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаФинансовогоУчета,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаТовары.ВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.СуммаНДС
	|	КОНЕЦ                                                           КАК НДСРегл,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.НДСУпр
	|	КОНЕЦ                                                           КАК НДСУпр,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ТипЗаписи,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	ТаблицаТовары.Стоимость                                         КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                                   КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                                     КАК СтоимостьРегл,
	|	ТаблицаТовары.СтоимостьУпр                                      КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ТаблицаТовары.ГруппаПродукции                                   КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика21
	|		ПО Аналитика.Номенклатура                             = Аналитика21.Номенклатура
	|		И Аналитика.Характеристика                            = Аналитика21.Характеристика
	|		И &Партнер                                            = Аналитика21.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика21.СтатьяКалькуляции
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)        = Аналитика21.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитика21.Серия
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	// номенклатура может быть не заполнена в очень старых документах
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// поступление продукции
	|ВЫБРАТЬ
	|	4                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ТаблицаТовары.РазделУчета                                       КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.ПартияВыпуска
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК Партия,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.КорГруппаПродукции                                КАК АналитикаФинансовогоУчета,
	|	ТаблицаТовары.ВидДеятельностиНДС                                КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорПартия,
	|	ВЫБОР КОГДА НЕ &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорАналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.СуммаНДС
	|	КОНЕЦ                                                           КАК НДСРегл,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.НДСУпр
	|	КОНЕЦ                                                           КАК НДСУпр,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)                 КАК ТипЗаписи,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	ТаблицаТовары.Стоимость                                         КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                                   КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                                     КАК СтоимостьРегл,
	|	ТаблицаТовары.СтоимостьУпр                                      КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ТаблицаТовары.ГруппаПродукции                                   КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|											ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|	И &ПартионныйУчетВерсии22
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// расход продукции на статью
	|ВЫБРАТЬ
	|	5                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	ТаблицаТовары.Подразделение                                     КАК Подразделение,
	|	ТаблицаТовары.РазделУчета                                       КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.ПартияВыпуска
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК Партия,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			И НЕ ТаблицаТовары.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		КОГДА НЕ ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			И НЕ ТаблицаТовары.КорГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.КорГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.ВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.СуммаНДС
	|	КОНЕЦ                                                           КАК НДСРегл,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.НДСУпр
	|	КОНЕЦ                                                           КАК НДСУпр,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)            КАК ТипЗаписи,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	ТаблицаТовары.Стоимость                                         КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                                   КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                                     КАК СтоимостьРегл,
	|	ТаблицаТовары.СтоимостьУпр                                      КАК СтоимостьУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	&НаправлениеДеятельности                                        КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|				ТОГДА ТаблицаТовары.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК СтатьяРасходовСписания,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|				ТОГДА ТаблицаТовары.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|				ТОГДА ТаблицаТовары.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК СтатьяАктивовПассивов,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаТовары.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|				ТОГДА ТаблицаТовары.АналитикаАктивовПассивов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ТаблицаТовары.ГруппаПродукции                                   КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|										   ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|	И ТаблицаТовары.СписатьНаРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// уменьшение незавершенного производства на фиксированную стоимость выпуска
	|ВЫБРАТЬ
	|	6                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ТаблицаТовары.КорРазделУчета                                    КАК РазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК ВидЗапасов,
	|	ТаблицаТовары.ПартияПроизводства                                КАК Партия,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаФинансовогоУчета,
	|	ТаблицаТовары.ВидДеятельностиНДС                                КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ -ТаблицаТовары.СуммаНДС
	|	КОНЕЦ                                                           КАК НДСРегл,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ -ТаблицаТовары.НДСУпр
	|	КОНЕЦ                                                           КАК НДСУпр,
	// чтобы заполнить проводку по кредиту счета производства
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.КорГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.КорГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)             КАК ТипЗаписи,
	|	-ТаблицаТовары.Количество                                       КАК Количество,
	|	-ТаблицаТовары.Стоимость                                        КАК Стоимость,
	|	-ТаблицаТовары.СтоимостьБезНДС                                  КАК СтоимостьБезНДС,
	|	-ТаблицаТовары.СтоимостьРегл                                    КАК СтоимостьРегл,
	|	-ТаблицаТовары.СтоимостьУпр                                     КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	// чтобы заполнить проводку по кредиту счета производства
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ТаблицаТовары.ГруппаПродукции                                   КАК ГруппаПродукции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументДвижения,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|	И &ПартионныйУчетВерсии22
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// распределение материалов и услуг переработчика на продукцию
	|ВЫБРАТЬ
	|	7                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ТаблицаТовары.РазделУчета                                       КАК РазделУчета,
	|	ТаблицаТовары.ВидЗапасов                                        КАК ВидЗапасов,
	|	ТаблицаТовары.ПартияПроизводства                                КАК Партия,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаФинансовогоУчета,
	|	ТаблицаТовары.ПартияПроизводства.ВидДеятельностиНДС             КАК ВидДеятельностиНДС,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.ПартияВыпуска
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорПартия,
	|	ВЫБОР КОГДА &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.КорАналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорАналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.СуммаНДС
	|	КОНЕЦ                                                           КАК НДСРегл,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.НДСУпр
	|	КОНЕЦ                                                           КАК НДСУпр,
	|	ТаблицаТовары.КорГруппаПродукции                                КАК КорАналитикаФинансовогоУчета,
	|	ТаблицаТовары.КорВидДеятельностиНДС                             КАК КорВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ХозяйственнаяОперацияИсходная В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
	|	КОНЕЦ                                                           КАК ТипЗаписи,
	|	ТаблицаТовары.Количество                                        КАК Количество,
	|	ТаблицаТовары.Стоимость                                         КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                                   КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                                     КАК СтоимостьРегл,
	|	ТаблицаТовары.СтоимостьУпр                                      КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	ТаблицаТовары.КорРазделУчета                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ГруппаПродукции,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ХозяйственнаяОперацияИсходная В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|			ТОГДА &Ссылка
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ДокументДвижения, // заполняется для исключения стоимости услуги из расчета суммы в приходе, т.к. приход с суммой уже есть
	|	НЕОПРЕДЕЛЕНО                                                    КАК ДокументИсточник
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	&ПартионныйУчетВерсии22
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// отнесение стоимости услуг на продукцию
	|ВЫБРАТЬ
	|	8                                                               КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                          КАК ВидДвижения,
	|	&Период                                                         КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.КорАналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ                                                           КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                                    КАК Организация,
	|	&Подразделение                                                  КАК Подразделение,
	|	ТаблицаТовары.КорРазделУчета                                    КАК РазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.ПартияВыпуска
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК Партия,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаТовары.КорАналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.КорГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК АналитикаФинансовогоУчета,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаТовары.КорВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорПартия,
	|	ТаблицаТовары.АналитикаУчетаПартий                              КАК КорАналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.СуммаНДС
	|	КОНЕЦ                                                           КАК НДСРегл,
	|	ВЫБОР КОГДА &ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.НДСУпр
	|	КОНЕЦ                                                           КАК НДСУпр,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.КорВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК КорВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)                 КАК ТипЗаписи,
	|	0                                                               КАК Количество,
	|	ТаблицаТовары.Стоимость                                         КАК Стоимость,
	|	ТаблицаТовары.СтоимостьБезНДС                                   КАК СтоимостьБезНДС,
	|	ТаблицаТовары.СтоимостьРегл                                     КАК СтоимостьРегл,
	|	ТаблицаТовары.СтоимостьУпр                                      КАК СтоимостьУпр,
	|	ТаблицаТовары.ХозяйственнаяОперация                             КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО                                                    КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.ИдентификаторСтроки                               КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО                                                    КАК КодСтроки,
	|	ВЫБОР
	|		КОГДА НЕ &ПартионныйУчетВерсии22
	|			ТОГДА ТаблицаТовары.ГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                           КАК ГруппаПродукции,
	|	&Ссылка                                                         КАК ДокументДвижения, // заполняется для исключения стоимости услуги из расчета суммы в приходе, т.к. приход с суммой уже есть
	|	&Ссылка                                                         КАК ДокументИсточник
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперацияИсходная В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|													ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|	И (&ПартионныйУчетВерсии22 ИЛИ ТаблицаТовары.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаУслугиПереработчиковКОформлению(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "УслугиПереработчиковКОформлению";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&ЗаказПереработчику                    КАК ЗаказПереработчику,
	|	&СтоимостьУслуги * &КоэффициентПересчетаВВалютуЗаказа КАК Сумма
	|ГДЕ
	|	&ПоЗаказам
	|	И &СтоимостьУслуги <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыПолученныеОтПереработчика(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыПолученныеОтПереработчика";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	НачалоПериода(&Период, День)             КАК Период,
	|	&Организация                             КАК Организация,
	|	ВЫБОР
	|		КОГДА &ПоЗаказам
	|			ТОГДА ТаблицаТовары.ЗаказПереработчику
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТовары.ДокументПоступления
	|	КОНЕЦ                                    КАК Распоряжение,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.КодСтроки                  КАК КодСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается) КАК ТипСтоимости,
	|	ТаблицаТовары.Количество                 КАК Количество
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО ТаблицаТовары.Номенклатура = ДанныеНоменклатуры.Ссылка
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|   И НЕ ДанныеНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	НачалоПериода(&Период, День)             КАК Период,
	|	&Организация                             КАК Организация,
	|	ВЫБОР
	|		КОГДА &ПоЗаказам
	|			ТОГДА ТаблицаТовары.ЗаказПереработчику
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТовары.ДокументПоступления
	|	КОНЕЦ                                    КАК Распоряжение,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.КодСтроки                  КАК КодСтроки,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Фиксированная)
	|	КОНЕЦ КАК ТипСтоимости,
	|	ТаблицаТовары.Количество                 КАК Количество
	|ИЗ
	|	Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО ТаблицаТовары.Номенклатура = ДанныеНоменклатуры.Ссылка
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|   И НЕ ДанныеНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	УстановитьПараметрыЗапросаАналитикаУчетаПоПартнерам(Запрос);
	
	//Порядок и состав полей запроса проведения
	ШаблонПолей = "
	|ВЫБРАТЬ
	|	Шаблон.Период                    КАК Период,
	|	Шаблон.ДатаРегистратора          КАК ДатаРегистратора,
	|	Шаблон.НомерРегистратора         КАК НомерРегистратора,
	|	Шаблон.ДатаПлатежа               КАК ДатаПлатежа,
	|	Шаблон.ВидДвижения               КАК ВидДвижения,
	|	Шаблон.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Шаблон.ЗаказПоставщику           КАК ЗаказПоставщику,
	|	Шаблон.ЗакупкаПоЗаказу           КАК ЗакупкаПоЗаказу,
	|	Шаблон.Валюта                    КАК Валюта,
	|	Шаблон.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	Шаблон.ФормаОплаты               КАК ФормаОплаты,
	|	Шаблон.Сумма                     КАК Сумма,
	|	Шаблон.СуммаРегл                 КАК СуммаРегл,
	|	Шаблон.СуммаУпр                  КАК СуммаУпр,
	|	Шаблон.КОплате                   КАК КОплате,
	|	Шаблон.КПоступлению              КАК КПоступлению,
	|	Шаблон.Организация               КАК Организация,
	|	Шаблон.ВалютаДокумента           КАК ВалютаДокумента,
	|	Шаблон.КорОбъектРасчетов         КАК КорОбъектРасчетов,
	|	Шаблон.КорАналитикаУчетаПоПартнерам КАК КорАналитикаУчетаПоПартнерам
	|ИЗ
	|	#Шаблон КАК Шаблон
	|ГДЕ
	|	&ОтборШаблон";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	&Период                                КАК ДатаРегистратора,
	|	&Номер                                 КАК НомерРегистратора,
	|	ВЫБОР 
	|		КОГДА &ДатаПлатежа = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|		ИНАЧЕ &ДатаПлатежа
	|	КОНЕЦ                                  КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам 
	|			ТОГДА &Договор
	|		КОГДА &ПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА РеквизитыОтчета.ЗаказПереработчику
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ                                  КАК ЗаказПоставщику,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам И &ПоЗаказам ТОГДА
	|		РеквизитыОтчета.ЗаказПереработчику
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                  КАК ЗакупкаПоЗаказу,
	|
	|	&ВалютаВзаиморасчетов                  КАК Валюта,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                           КАК ФормаОплаты,
	|
	|	РеквизитыОтчета.СуммаВзаиморасчетов    КАК Сумма,
	|	ВЫБОР
	|		КОГДА РеквизитыОтчета.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета 
	|			ТОГДА РеквизитыОтчета.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(РеквизитыОтчета.СуммаДокумента * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ                                  КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА РеквизитыОтчета.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета 
	|			ТОГДА РеквизитыОтчета.СуммаВзаиморасчетов
	|		ИНАЧЕ ВЫРАЗИТЬ(РеквизитыОтчета.СуммаДокумента * &КоэффициентПересчетаВВалютуУПР КАК ЧИСЛО(31,2))
	|	КОНЕЦ                                  КАК СуммаУпр,
	|	0                                      КАК КОплате,
	|	ВЫБОР 
	|		КОГДА &ПоЗаказам И НЕ &РасчетыПоНакладным ИЛИ &ГрафикИсполненияВДоговоре
	|			ТОГДА РеквизитыОтчета.СуммаВзаиморасчетов              
	|		ИНАЧЕ 0
	|	КОНЕЦ                                  КАК КПоступлению,
	|	&Организация КАК Организация,
	|	&Валюта КАК ВалютаДокумента,
	|	Неопределено  КАК КорОбъектРасчетов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК КорАналитикаУчетаПоПартнерам
	|ИЗ
	|	Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|ГДЕ
	|	РеквизитыОтчета.Ссылка = &Ссылка
	|	И РеквизитыОтчета.СуммаДокумента <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(&ДатаПлатежа, День)       КАК Период,
	|	&Период                                КАК ДатаРегистратора,
	|	&Номер                                 КАК НомерРегистратора,
	|	ВЫБОР 
	|		КОГДА &ДатаПлатежа = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &Период
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|	КОНЕЦ КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам 
	|			ТОГДА &Договор
	|		КОГДА &ПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА РеквизитыОтчета.ЗаказПереработчику
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ                                  КАК ЗаказПоставщику,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам И &ПоЗаказам ТОГДА
	|		РеквизитыОтчета.ЗаказПереработчику
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                  КАК ЗакупкаПоЗаказу,
	|
	|	&ВалютаВзаиморасчетов                  КАК Валюта,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	&ФормаОплаты                           КАК ФормаОплаты,
	|
	|	0                                      КАК Сумма,
	|	0                                      КАК СуммаРегл,
	|	0                                      КАК СуммаУпр,
	|	РеквизитыОтчета.СуммаВзаиморасчетов    КАК КОплате,
	|	0                                      КАК КПоступлению,
	|	&Организация КАК Организация,
	|	&Валюта КАК ВалютаДокумента,
	|	Неопределено  КАК КорОбъектРасчетов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК КорАналитикаУчетаПоПартнерам
	|ИЗ
	|	Документ.ОтчетПереработчика КАК РеквизитыОтчета
	|ГДЕ
	|	РеквизитыОтчета.Ссылка = &Ссылка
	|	И РеквизитыОтчета.СуммаДокумента <> 0
	|	И (НЕ &ПоЗаказам ИЛИ &РасчетыПоНакладным) И НЕ &ГрафикИсполненияВДоговоре
	|	И &ДатаПлатежа <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|
	|//ЗачетАвансовПоставщикуПоНакладной
	|
	|";
	
	ТекстЗапроса=СтрЗаменить(ТекстЗапроса,"//ЗачетАвансовПоставщикуПоНакладной",
		ВзаиморасчетыСервер.ТекстПроведенияЗачетАвансовПоставщикуПоНакладной("ОтчетПереработчика", ШаблонПолей));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходыНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходыНезавершенногоПроизводства";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРаспределенныеВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРаспределенныеВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДвиженияПоРегистру.ВидДвижения			КАК ВидДвижения,
	|	ДвиженияПоРегистру.Период				КАК Период,
	|	ДвиженияПоРегистру.Организация			КАК Организация,
	|	ДвиженияПоРегистру.Подразделение		КАК Подразделение,
	|	ДвиженияПоРегистру.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	ДвиженияПоРегистру.КодСтрокиПродукция	КАК КодСтрокиПродукция,
	|	ДвиженияПоРегистру.Этап					КАК Этап,
	|	ДвиженияПоРегистру.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ДвиженияПоРегистру.АналитикаУчетаПродукции КАК АналитикаУчетаПродукции,
	|	ДвиженияПоРегистру.Стоимость			КАК Стоимость,
	|	ДвиженияПоРегистру.СтоимостьБезНДС		КАК СтоимостьБезНДС,
	|	ДвиженияПоРегистру.СтоимостьРегл		КАК СтоимостьРегл
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|		&Период									КАК Период,
	|		&Организация							КАК Организация,
	|		&Подразделение							КАК Подразделение,
	|		&ЗаказПереработчику						КАК ЗаказНаПроизводство,
	|		ТаблицаТовары.КодСтроки					КАК КодСтрокиПродукция,
	|		НЕОПРЕДЕЛЕНО							КАК Этап,
	|		ТаблицаТовары.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|		НЕОПРЕДЕЛЕНО							КАК АналитикаУчетаПродукции,
	|		СУММА(ТаблицаТовары.Стоимость)			КАК Стоимость,
	|		СУММА(ТаблицаТовары.СтоимостьБезНДС)	КАК СтоимостьБезНДС,
	|		СУММА(ТаблицаТовары.СтоимостьРегл)		КАК СтоимостьРегл
	|
	|	ИЗ
	|		ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И НЕ &ПартионныйУчетВерсии22
	|
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаТовары.КодСтроки,
	|		ТаблицаТовары.СтатьяКалькуляции
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|		&Период									КАК Период,
	|		&Организация							КАК Организация,
	|		&Подразделение							КАК Подразделение,
	|		&ЗаказПереработчику						КАК ЗаказНаПроизводство,
	|		ТаблицаТовары.КодСтроки					КАК КодСтрокиПродукция,
	|		НЕОПРЕДЕЛЕНО							КАК Этап,
	|		ТаблицаТовары.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ТаблицаТовары.КорАналитикаУчетаНоменклатуры
	|			ИНАЧЕ ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|		КОНЕЦ									КАК АналитикаУчетаПродукции,
	|		СУММА(ТаблицаТовары.Стоимость)			КАК Стоимость,
	|		СУММА(ТаблицаТовары.СтоимостьБезНДС)	КАК СтоимостьБезНДС,
	|		СУММА(ТаблицаТовары.СтоимостьРегл)		КАК СтоимостьРегл
	|
	|	ИЗ
	|		ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И &ПартионныйУчетВерсии22
	|
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаТовары.КодСтроки,
	|		ТаблицаТовары.СтатьяКалькуляции,
	|		ТаблицаТовары.КорАналитикаУчетаНоменклатуры,
	|		ТаблицаТовары.КорАналитикаУчетаНоменклатурыБезНазначения
	|
	|	) КАК ДвиженияПоРегистру
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПроизводственныхЗатрат(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПроизводственныхЗатрат";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРаспределенныеВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРаспределенныеВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	УстановитьПараметрыЗапросаАналитикаУчетаПартий(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	Аналитика21.КлючАналитики			   КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                           КАК ВидЗапасов,
	|	&Ссылка                                КАК ДокументПоступления,
	|	ТаблицаТовары.АналитикаУчетаПартий     КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО                           КАК ЗаказНаПроизводство,
	|	0.                                     КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО                           КАК Этап,
	|	НЕОПРЕДЕЛЕНО                           КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО                           КАК НомерГруппыЗатрат,
	|	СУММА(ТаблицаТовары.Количество)        КАК Количество,
	|	СУММА(ТаблицаТовары.Стоимость)         КАК Стоимость,
	|	СУММА(ТаблицаТовары.СтоимостьБезНДС)   КАК СтоимостьБезНДС,
	|	СУММА(ТаблицаТовары.СтоимостьРегл)     КАК СтоимостьРегл,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА ТаблицаТовары.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|				ТОГДА 0 
	|			ИНАЧЕ ТаблицаТовары.СуммаНДС
	|		КОНЕЦ) 							   КАК НДСРегл
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика21
	|		ПО Аналитика.Номенклатура                             = Аналитика21.Номенклатура
	|		И Аналитика.Характеристика                            = Аналитика21.Характеристика
	|		И &Партнер                                            = Аналитика21.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика21.СтатьяКалькуляции
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)        = Аналитика21.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитика21.Серия
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперацияИсходная = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И НЕ &ПартионныйУчетВерсии22
	|
	|СГРУППИРОВАТЬ ПО
	|	Аналитика21.КлючАналитики,
	|	ТаблицаТовары.АналитикаУчетаПартий
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	&Организация                             КАК Организация,
	|	Аналитика21.КлючАналитики				 КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                             КАК ВидЗапасов,
	|	&Ссылка                                  КАК ДокументПоступления,
	|	ТаблицаТовары.АналитикаУчетаПартий       КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &ПоЗаказам
	|			ТОГДА &ЗаказПереработчику
	|		ИНАЧЕ ТаблицаТовары.ДокументПоступления
	|	КОНЕЦ                                    КАК ЗаказНаПроизводство,
	|	ТаблицаТовары.КодСтроки                  КАК КодСтрокиПродукция,
	|	НЕОПРЕДЕЛЕНО                             КАК Этап,
	|	ТаблицаТовары.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	ТаблицаТовары.НомерГруппыЗатрат          КАК НомерГруппыЗатрат,
	|	СУММА(ТаблицаТовары.Количество)          КАК Количество,
	|	СУММА(ТаблицаТовары.Стоимость)           КАК Стоимость,
	|	СУММА(ТаблицаТовары.СтоимостьБезНДС)     КАК СтоимостьБезНДС,
	|	СУММА(ТаблицаТовары.СтоимостьРегл)       КАК СтоимостьРегл,
	|	СУММА(ТаблицаТовары.СуммаНДС)            КАК НДСРегл
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика21
	|		ПО Аналитика.Номенклатура                             = Аналитика21.Номенклатура
	|		И Аналитика.Характеристика                            = Аналитика21.Характеристика
	|		И &Партнер                                            = Аналитика21.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика21.СтатьяКалькуляции
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)        = Аналитика21.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитика21.Серия
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперацияИсходная = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И НЕ &ПартионныйУчетВерсии22
	|
	|СГРУППИРОВАТЬ ПО
	|	Аналитика21.КлючАналитики,
	|	ТаблицаТовары.ДокументПоступления,
	|	ТаблицаТовары.КодСтроки,
	|	ТаблицаТовары.СтатьяКалькуляции,
	|	ТаблицаТовары.НомерГруппыЗатрат,
	|	ТаблицаТовары.АналитикаУчетаПартий
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииНезавершенногоПроизводства(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииНезавершенногоПроизводства";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРаспределенныеВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаАналитикаУчетаПартий(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	&Организация							 КАК Организация,
	|	Аналитика21.КлючАналитики 				 КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                             КАК ВидЗапасов,
	|	&ЗаказПереработчику                      КАК ЗаказНаПроизводство,
	|	ТаблицаТовары.КодСтроки                  КАК КодСтрокиПродукция,
	|	&Ссылка                                  КАК ДокументПоступления,
	|	НЕОПРЕДЕЛЕНО                             КАК Этап,
	|	ТаблицаТовары.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	ТаблицаТовары.АналитикаУчетаПартий       КАК АналитикаУчетаПартий,
	|	ТаблицаТовары.ДокументПоступления        КАК ДокументВыпуска,
	|	СУММА(ТаблицаТовары.Количество)          КАК Количество,
	|	СУММА(ТаблицаТовары.Стоимость)           КАК Стоимость,
	|	СУММА(ТаблицаТовары.СтоимостьБезНДС)     КАК СтоимостьБезНДС,
	|	СУММА(ТаблицаТовары.СтоимостьРегл)       КАК СтоимостьРегл,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА ТаблицаТовары.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|				ТОГДА 0 
	|			ИНАЧЕ ТаблицаТовары.СуммаНДС
	|		КОНЕЦ) 								 КАК НДСРегл
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика21
	|		ПО Аналитика.Номенклатура                             = Аналитика21.Номенклатура
	|		И Аналитика.Характеристика                            = Аналитика21.Характеристика
	|		И &Партнер                                            = Аналитика21.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика21.СтатьяКалькуляции
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)        = Аналитика21.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитика21.Серия
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И НЕ &ПартионныйУчетВерсии22
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.КодСтроки,
	|	ТаблицаТовары.СтатьяКалькуляции,
	|	ТаблицаТовары.ДокументПоступления,
	|	ТаблицаТовары.АналитикаУчетаПартий,
	|	Аналитика21.КлючАналитики
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СуммыДокументовВВалютеРегл";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период	КАК Период,
	|	&Валюта КАК Валюта,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Строки.СтавкаНДС КАК СтавкаНДС,
	|	Строки.СуммаСНДС - Строки.СуммаНДС КАК СуммаБезНДС,
	|	Строки.СуммаНДС КАК СуммаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)	КАК ТипРасчетов,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			И &ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета
	|			И &Валюта <> &ВалютаРегламентированногоУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл,
	|				ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА Строки.СуммаВзаиморасчетов - Строки.СуммаНДСВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ((Строки.СуммаСНДС - Строки.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) 
	|				КОНЕЦ)
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			И &ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета
	|			И &Валюта <> &ВалютаРегламентированногоУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаНДСРегл, 
	|				ВЫБОР КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|					ТОГДА Строки.СуммаНДСВзаиморасчетов
	|						ИНАЧЕ ВЫРАЗИТЬ(Строки.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|				КОНЕЦ)
	|	КОНЕЦ КАК СуммаНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			И &ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета
	|			И &Валюта <> &ВалютаУправленческогоУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр,
	|				ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА Строки.СуммаВзаиморасчетов - Строки.СуммаНДСВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ(((Строки.СуммаСНДС-Строки.СуммаНДС) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(31,2))
	|				КОНЕЦ)
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			И &ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета
	|			И &Валюта <> &ВалютаУправленческогоУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаНДСУпр,
	|				ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА Строки.СуммаНДСВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ((Строки.СуммаНДС * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(31,2))
	|				КОНЕЦ)
	|	КОНЕЦ КАК СуммаНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			И &ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета
	|			И &Валюта <> &ВалютаРегламентированногоУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.БазаНДСРегл, ЕСТЬNULL(Суммы.СуммаБезНДСРегл,
	|				ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА Строки.СуммаВзаиморасчетов - Строки.СуммаНДСВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ((Строки.СуммаСНДС - Строки.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|				КОНЕЦ))
	|	КОНЕЦ КАК БазаНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Регистратор ЕСТЬ NULL
	|			И &ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета
	|			И &Валюта <> &ВалютаУправленческогоУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.БазаНДСУпр, ЕСТЬNULL(Суммы.СуммаБезНДСУпр,
	|				ВЫБОР
	|					КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов
	|						ТОГДА Строки.СуммаВзаиморасчетов - Строки.СуммаНДСВзаиморасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ((Строки.СуммаСНДС - Строки.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) 
	|				КОНЕЦ))
	|	КОНЕЦ КАК БазаНДСУпр,
	|	
	|	Строки.СуммаВзаиморасчетов  КАК СуммаВзаиморасчетов,
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	|	
	|ИЗ
	|	Документ.ОтчетПереработчика.Услуги КАК Строки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = &Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|		И Суммы.СуммаБезНДСУпр <> 0
	|		И (Строки.СуммаСНДС - Строки.СуммаНДС) = Суммы.СуммаБезНДС
	|		И Строки.СуммаНДС = Суммы.СуммаНДС
	|
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И Строки.Сумма <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРаспределенныеВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРаспределенныеВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаПродукция", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаПродукция(Запрос, ТекстыЗапроса);	
	КонецЕсли;	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВозвратныеОтходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВозвратныеОтходы(Запрос, ТекстыЗапроса);	
	КонецЕсли;	
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)		КАК ВидДвижения,
	|	&Период										КАК Период,
	|	&Период										КАК ДатаРегистратора,
	|	МатериалыИУслуги.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	&Организация								КАК Организация,
	|	Аналитика.Номенклатура						КАК Номенклатура,
	|	Аналитика.Характеристика					КАК Характеристика,
	|	Аналитика.Серия								КАК Серия,
	|	&Партнер									КАК Подразделение,
	|	Аналитика21.КлючАналитики					КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА МатериалыИУслуги.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|			ТОГДА -1 * МатериалыИУслуги.Количество
	|		ИНАЧЕ 
	|			МатериалыИУслуги.Количество
	|	КОНЕЦ										КАК Количество,
	|	МатериалыИУслуги.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО								КАК Этап,
	|	ВЫБОР
	|		КОГДА &ПоЗаказам
	|			ТОГДА &ЗаказПереработчику
	|		ИНАЧЕ
	|			МатериалыИУслуги.ДокументПоступления
	|	КОНЕЦ										КАК ЗаказНаПроизводство,
	|	МатериалыИУслуги.КодСтроки					КАК КодСтрокиПродукция,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)КАК Назначение
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК МатериалыИУслуги
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО МатериалыИУслуги.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика21
	|		ПО Аналитика.Номенклатура                             = Аналитика21.Номенклатура
	|		И Аналитика.Характеристика                            = Аналитика21.Характеристика
	|		И &Партнер                                            = Аналитика21.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика21.СтатьяКалькуляции
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)        = Аналитика21.Назначение
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитика21.Серия
	|ГДЕ
	|	НЕ &ПартионныйУчетВерсии22
	|	И МатериалыИУслуги.ХозяйственнаяОперацияИсходная В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства),
	|														ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
    |
	|ОБЪЕДИНИТЬ ВСЕ
    |
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Период										        КАК Период,
	|	&Период										        КАК ДатаРегистратора,
	|	ТаблицаПродукции.ИдентификаторСтроки                КАК ИдентификаторСтроки,
	|	&Организация								        КАК Организация,
	|	ТаблицаПродукции.Номенклатура				        КАК Номенклатура,
	|	ТаблицаПродукции.Характеристика				        КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТаблицаПродукции.Получатель 						КАК Подразделение,
	|	ТаблицаПродукции.АналитикаУчетаНоменклатуры	        КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаПродукции.Количество					        КАК Количество,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции,
	|	ТаблицаПродукции.ЭтапПроизводства			        КАК Этап,
	|	ТаблицаПродукции.ЗаказПереработчику 				КАК ЗаказНаПроизводство,
	|	ТаблицаПродукции.КодСтроки					        КАК КодСтрокиПродукция,
	|	ТаблицаПродукции.Назначение                         КАК Назначение
	|ИЗ
	|	ВтТаблицаПродукция КАК ТаблицаПродукции
	|ГДЕ
	|	ТаблицаПродукции.Работа
	|	И НЕ ТаблицаПродукции.СписатьНаРасходы
    |
	|ОБЪЕДИНИТЬ ВСЕ
    |
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)              КАК ВидДвижения,
	|	&Период										        КАК Период,
	|	&Период										        КАК ДатаРегистратора,
	|	ТаблицаВозвратныхОтходов.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	&Организация								        КАК Организация,
	|	ТаблицаВозвратныхОтходов.Номенклатура				КАК Номенклатура,
	|	ТаблицаВозвратныхОтходов.Характеристика				КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ТаблицаВозвратныхОтходов.Получатель 				КАК Подразделение,
	|	ТаблицаВозвратныхОтходов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВозвратныхОтходов.Количество					КАК Количество,
	|	ТаблицаВозвратныхОтходов.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	ТаблицаВозвратныхОтходов.ЭтапПроизводства	        КАК Этап,
	|	ТаблицаВозвратныхОтходов.ЗаказПереработчику 		КАК ЗаказНаПроизводство,
	|	ТаблицаВозвратныхОтходов.КодСтроки					КАК КодСтрокиПродукция,
	|	ТаблицаВозвратныхОтходов.Назначение                 КАК Назначение
	|ИЗ
	|	ВтТаблицаВозвратныеОтходы КАК ТаблицаВозвратныхОтходов
	|ГДЕ
	|	ТаблицаВозвратныхОтходов.Работа
	|	И НЕ ТаблицаВозвратныхОтходов.СписатьНаРасходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРаспределенныеВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРаспределенныеВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	&НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	СУММА(ТаблицаТовары.Стоимость)			КАК Сумма,
	|	СУММА(ТаблицаТовары.СтоимостьБезНДС)	КАК СуммаБезНДС,
	|	СУММА(ТаблицаТовары.СтоимостьУпр)		КАК СуммаУпр,
	|	СУММА(ТаблицаТовары.СтоимостьРегл)		КАК СуммаРегл,
	|	0                                       КАК ПостояннаяРазница,
	|	0                                       КАК ВременнаяРазница,
	|	&ХозяйственнаяОперация					КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперацияИсходная = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТовары.Стоимость) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Подразделение							КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	&НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	СУММА(ТаблицаТовары.Стоимость)			КАК Сумма,
	|	СУММА(ТаблицаТовары.СтоимостьБезНДС)	КАК СуммаБезНДС,
	|	СУММА(ТаблицаТовары.СтоимостьУпр)		КАК СуммаУпр,
	|	СУММА(ТаблицаТовары.СтоимостьРегл)		КАК СуммаРегл,
	|	0                                       КАК ПостояннаяРазница,
	|	0                                       КАК ВременнаяРазница,
	|	&ХозяйственнаяОперация					КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтРаспределенныеВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.ХозяйственнаяОперацияИсходная = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТовары.Стоимость) <> 0
	|";
	
	ТекстЗапроса = ТекстЗапроса 
				   + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() 
				   + РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                    КАК Ссылка,
	|	&Период                    КАК ДатаДокументаИБ,
	|	&Номер                     КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных   КАК ТипСсылки,
	|	&Организация               КАК Организация,
	|	&ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	&Партнер                   КАК Партнер,
	|	&Контрагент                КАК Контрагент,
	|	&Договор                   КАК Договор,
	|	&НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО               КАК МестоХранения,
	|	&Подразделение             КАК Подразделение,
	|	&Менеджер                  КАК Ответственный,
	|	&Комментарий               КАК Комментарий,
	|	&Валюта                    КАК Валюта,
	|	&СуммаДокумента            КАК Сумма,
	|	НЕОПРЕДЕЛЕНО               КАК Статус,
	|	&Проведен                  КАК Проведен,
	|	&ПометкаУдаления           КАК ПометкаУдаления,
	|	ЛОЖЬ                       КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору      КАК Дополнительно,
	|	&ДатаПоДаннымПартнера      КАК ДатаПервичногоДокумента,
	|	&НомерПоДаннымПартнера     КАК НомерПервичногоДокумента,
	|	&Период                    КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВыпускПродукции(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ВыпускПродукции"; 
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период														КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ														КАК АналитикаУчетаНоменклатуры,
	|	&Организация												КАК Организация,
	|	&Подразделение												КАК Подразделение,
	|	ТаблицаТовары.Количество                                    КАК Количество,
	|	НЕОПРЕДЕЛЕНО                                                КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СписатьНаРасходы 
	|			ТОГДА ТаблицаТовары.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                       КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СписатьНаРасходы 
	|			ТОГДА ТаблицаТовары.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                       КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СписатьНаРасходы 
	|			ТОГДА ТаблицаТовары.АналитикаАктивовПассивов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                       КАК АналитикаАктивовПассивов
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &ПартионныйУчетВерсии22
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период														КАК Период,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ														КАК АналитикаУчетаНоменклатуры,
	|	&Организация												КАК Организация,
	|	&Подразделение												КАК Подразделение,
	|	ТаблицаТовары.Количество        							КАК Количество,
	|	ТаблицаТовары.ВидЗапасов									КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СписатьНаРасходы 
	|			ТОГДА ТаблицаТовары.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                       КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СписатьНаРасходы 
	|			ТОГДА ТаблицаТовары.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                       КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СписатьНаРасходы 
	|			ТОГДА ТаблицаТовары.АналитикаАктивовПассивов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                       КАК АналитикаАктивовПассивов
	|ИЗ
	|	Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &ПартионныйУчетВерсии22
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.ОтчетПереработчика";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору", """""");
	ПереопределениеРасчетаПараметров.Вставить("НомерПоДаннымПартнера", """""");
	ПереопределениеРасчетаПараметров.Вставить("УчитыватьСебестоимостьТоваровПоНазначениям",
		Формат(ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоНазначениям"), "БЛ=Ложь; БИ=Истина")
		+ " И НАЧАЛОПЕРИОДА(ТаблицаТовары.Ссылка.Дата, МЕСЯЦ) >= "
		+ Формат(РасчетСебестоимостиПовтИсп.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям(), "ДФ='""ДАТАВРЕМЯ""(гггг, ММ, дд)'; ДП="));
	ПереопределениеРасчетаПараметров.Вставить("ПартионныйУчетВерсии22",
		Формат(ПолучитьФункциональнуюОпцию("ПартионныйУчетВерсии22"), "БЛ=Ложь; БИ=Истина")
		+ " И НАЧАЛОПЕРИОДА(ТаблицаТовары.Ссылка.Дата, МЕСЯЦ) >= "
		+ Формат(РасчетСебестоимостиПовтИсп.ДатаПереходаНаПартионныйУчетВерсии22(), "ДФ='""ДАТАВРЕМЯ""(гггг, ММ, дд)'; ДП="));
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ВыпускПродукции" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаВыпускПродукции(Неопределено, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", 	ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", 		ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#Область ОтражениеНДС

Процедура ОтразитьВУчетеНДС(Запрос, ТекстыЗапроса, Регистры)
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПроизводства", ТекстыЗапроса) Тогда
		ТекстЗапросаВтПартииПроизводства(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЦенности =
	"ВЫБРАТЬ
	|	Услуги.Ссылка.Дата								КАК Период,
	|	Услуги.Ссылка									КАК Ссылка,
	|	Услуги.Ссылка.Организация						КАК Организация,
	|	Услуги.Ссылка.Подразделение						КАК Подразделение,
	|	Услуги.Ссылка.Подразделение						КАК ПодразделениеУчета,
	|	Услуги.Ссылка.Контрагент						КАК Контрагент,
	|	Услуги.Ссылка.Договор							КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)	КАК Грузоотправитель,
	|	Услуги.Ссылка									КАК ДокументПриобретения,
	|	Услуги.Ссылка									КАК ИсходныйТорговыйДокумент,
	|	ЛОЖЬ											КАК ИсправлениеОшибок,
	|	ЛОЖЬ											КАК КорректировкаПоСогласованиюСторон,
	|	НЕОПРЕДЕЛЕНО									КАК ДокументКорректировкиПриобретения,
	|	ВЫБОР
	|		КОГДА Услуги.Ссылка.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	КОНЕЦ											КАК НалогообложениеНДС,
	|	ВтПартииПроизводства.ВидДеятельностиНДС			КАК ВидДеятельностиНДС,
	|	Услуги.СтавкаНДС								КАК СтавкаНДС,
	|	Услуги.Номенклатура								КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)	КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)		КАК НомерГТД,
	|	ВтПартииПроизводства.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяПрочихАктивов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаПрочихАктивов,
	|	ВтПартииПроизводства.Назначение					КАК Назначение,
	|	Услуги.ИдентификаторСтроки						КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.Услуги КАК Услуги
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПартииПроизводства КАК ВтПартииПроизводства
	|		ПО Услуги.Ссылка = ВтПартииПроизводства.ПартияВыпуска
	|		И Услуги.ЭтапПроизводства = ВтПартииПроизводства.ЭтапПроизводства
	|		И Услуги.НомерГруппыЗатрат = ВтПартииПроизводства.НомерГруппыЗатрат
	|
	|ГДЕ
	|	Услуги.Ссылка В (&Ссылка)
	|";
	
	УчетНДСУП.ОтразитьПриобретениеУПоставщика(Запрос, ТекстыЗапроса, Регистры, ТекстЦенности);
	
КонецПроцедуры

#КонецОбласти

Функция ТекстЗапросаВтТаблицаПродукция(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаПродукция";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПродукция.Ссылка                     КАК Ссылка,
	|	ТаблицаПродукция.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаПродукция.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	ТаблицаПродукция.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,	
	|	ТаблицаПродукция.КодСтроки                  КАК КодСтроки,
	|	ТаблицаПродукция.Номенклатура               КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика             КАК Характеристика,
	|	ТаблицаПродукция.Назначение                 КАК Назначение,
	|	ТаблицаПродукция.ЭтапПроизводства           КАК ЭтапПроизводства,
	|	ТаблицаПродукция.ЗаказПереработчику         КАК ЗаказПереработчику,
	|	ТаблицаПродукция.Получатель                 КАК Получатель,
	|	ТаблицаПродукция.СписатьНаРасходы           КАК СписатьНаРасходы,
	|	ТаблицаПродукция.Количество				    КАК Количество,
	|	ВЫБОР
	|		КОГДА ДанныеНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                       КАК Работа
	|
	|ПОМЕСТИТЬ ВтТаблицаПродукция
	|
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО ТаблицаПродукция.Номенклатура = ДанныеНоменклатуры.Ссылка
	|ГДЕ
	|	ТаблицаПродукция.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Работа,
	|	СписатьНаРасходы";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаВтТаблицаВозвратныеОтходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВозвратныеОтходы";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаВозвратныеОтходы.Ссылка                     КАК Ссылка,
	|	ТаблицаВозвратныеОтходы.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаВозвратныеОтходы.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	ТаблицаВозвратныеОтходы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,	
	|	ТаблицаВозвратныеОтходы.КодСтроки                  КАК КодСтроки,
	|	ТаблицаВозвратныеОтходы.Номенклатура               КАК Номенклатура,
	|	ТаблицаВозвратныеОтходы.Характеристика             КАК Характеристика,
	|	ТаблицаВозвратныеОтходы.Назначение                 КАК Назначение,
	|	ТаблицаВозвратныеОтходы.ЭтапПроизводства           КАК ЭтапПроизводства,
	|	ТаблицаВозвратныеОтходы.ЗаказПереработчику         КАК ЗаказПереработчику,
	|	ТаблицаВозвратныеОтходы.Получатель                 КАК Получатель,
	|	ТаблицаВозвратныеОтходы.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	ТаблицаВозвратныеОтходы.СписатьНаРасходы           КАК СписатьНаРасходы,
	|	ТаблицаВозвратныеОтходы.СтатьяРасходов             КАК СтатьяРасходов,
	|	ТаблицаВозвратныеОтходы.АналитикаРасходов          КАК АналитикаРасходов,
	|	ТаблицаВозвратныеОтходы.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	ТаблицаВозвратныеОтходы.Количество				   КАК Количество,
	|	ТаблицаВозвратныеОтходы.Цена				       КАК Цена,
	|	ТаблицаВозвратныеОтходы.Сумма				       КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДанныеНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                              КАК Работа
	|
	|ПОМЕСТИТЬ ВтТаблицаВозвратныеОтходы
	|
	|ИЗ
	|	Документ.ОтчетПереработчика.ВозвратныеОтходы КАК ТаблицаВозвратныеОтходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО ТаблицаВозвратныеОтходы.Номенклатура = ДанныеНоменклатуры.Ссылка
	|ГДЕ
	|	ТаблицаВозвратныеОтходы.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СписатьНаРасходы,
	|	Работа";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВозвратныеОтходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВозвратныеОтходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки                                                         КАК НомерСтроки,
	|	&Период                                                                            КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                             КАК ВидДвижения,
	|	&Организация                                                                       КАК Организация,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Получатель = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ТабличнаяЧасть.Получатель
	|	КОНЕЦ                	                                                           КАК Подразделение,
	|	ТабличнаяЧасть.СтатьяРасходов                                                      КАК СтатьяРасходов,
	|	ТабличнаяЧасть.АналитикаРасходов                                                   КАК АналитикаРасходов,
	|	&НаправлениеДеятельности                                                           КАК НаправлениеДеятельности,
	|	&ЗакупкаПодДеятельность                                                            КАК ВидДеятельностиНДС,
	|
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))  КАК СуммаБезНДСУпр,
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаСНДСРегл,
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)) КАК СуммаБезНДСРегл,
	|	0                                                                                  КАК ПостояннаяРазница,
	|	0                                                                                  КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	ТабличнаяЧасть.АналитикаУчетаНоменклатуры                                          КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	ВтТаблицаВозвратныеОтходы КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.СписатьНаРасходы
	|	И ТИПЗНАЧЕНИЯ(ТабличнаяЧасть.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗаказыПоставщикам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЗаказыПоставщикам";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаПродукция", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаПродукция(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВозвратныеОтходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВозвратныеОтходы(Запрос, ТекстыЗапроса);
	КонецЕсли;	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки             КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТабличнаяЧасть.ЗаказПереработчику      КАК ЗаказПоставщику,
	|	ТабличнаяЧасть.Номенклатура            КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика          КАК Характеристика,
	|	ТабличнаяЧасть.КодСтроки               КАК КодСтроки,
	|	ТабличнаяЧасть.Количество              КАК Заказано,
	|	ТабличнаяЧасть.Количество              КАК КОформлению
	|
	|ИЗ
	|	ВтТаблицаПродукция КАК ТабличнаяЧасть
	|
	|ГДЕ
	|	ТабличнаяЧасть.КодСтроки <> 0
	|	И &ПоЗаказам
	|	И ТабличнаяЧасть.Работа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки             КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТабличнаяЧасть.ЗаказПереработчику      КАК ЗаказПоставщику,
	|	ТабличнаяЧасть.Номенклатура            КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика          КАК Характеристика,
	|	ТабличнаяЧасть.КодСтроки               КАК КодСтроки,
	|	ТабличнаяЧасть.Количество              КАК Заказано,
	|	ТабличнаяЧасть.Количество              КАК КОформлению
	|
	|ИЗ
	|	ВтТаблицаВозвратныеОтходы КАК ТабличнаяЧасть
	|
	|ГДЕ
	|	ТабличнаяЧасть.КодСтроки <> 0
	|	И &ПоЗаказам
	|	И ТабличнаяЧасть.Работа
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВозвратныеОтходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВозвратныеОтходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки                                                        КАК НомерСтроки,
	|	&Период                                                                           КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                            КАК ВидДвижения,
	|	&Организация                                                                      КАК Организация,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.Получатель = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ТабличнаяЧасть.Получатель
	|	КОНЕЦ                	                                                          КАК Подразделение,
	|	ТабличнаяЧасть.СтатьяРасходов                                                     КАК Статья,
	|	ТабличнаяЧасть.АналитикаАктивовПассивов                                           КАК Аналитика,
	|	ВЫРАЗИТЬ(ТабличнаяЧасть.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)) КАК Сумма
	|ИЗ
	|	ВтТаблицаВозвратныеОтходы КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.СписатьНаРасходы
	|	И ТИПЗНАЧЕНИЯ(ТабличнаяЧасть.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияНоменклатураДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияНоменклатураДоходыРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтВидыЗапасов", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период																				КАК Период,
		|	ВЫБОР
		|		КОГДА ВтВидыЗапасов.СтатьяРасходов.ВариантРаспределенияРасходовРегл =
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|			ТОГДА ВЫБОР
		|					КОГДА ВтВидыЗапасов.СтатьяРасходов.ВидЦенностиНДС В
		|							(ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС),
		|							ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОбъектыНезавершенногоСтроительства))
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставОС)
		|					КОГДА ВтВидыЗапасов.СтатьяРасходов.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставНМА)
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
		|	КОНЕЦ																				КАК ХозяйственнаяОперация,
		|	&Организация																		КАК Организация,
		|	ВтВидыЗапасов.Подразделение										                    КАК Подразделение,
		|	ВтВидыЗапасов.АналитикаУчетаНоменклатуры    								        КАК АналитикаУчетаНоменклатуры,
		|	&Подразделение																		КАК Склад,
		|	ВтВидыЗапасов.ВидЗапасов															КАК ВидЗапасов,
		|	ВтВидыЗапасов.ВидЗапасов.ТипЗапасов													КАК ТипЗапасов,
		|	ВтВидыЗапасов.СтатьяРасходов														КАК СтатьяДоходовРасходов,
		|	ВтВидыЗапасов.АналитикаРасходов														КАК АналитикаРасходов,
		|	ВтВидыЗапасов.АналитикаАктивовПассивов												КАК АналитикаАктивовПассивов,
		|	ВтВидыЗапасов.Количество															КАК Количество,
		|	ВтВидыЗапасов.Стоимость																КАК Стоимость,
		|	ВтВидыЗапасов.СтоимостьБезНДС														КАК СтоимостьБезНДС,
		|	ВтВидыЗапасов.СтоимостьРегл															КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
		|			ТОГДА ВтВидыЗапасов.ВидЗапасов
		|		ИНАЧЕ КлючиНоменклатурыПолучатель.Номенклатура
		|	КОНЕЦ																				КАК ИсточникГФУНоменклатуры
		|ИЗ
		|	ВтВидыЗапасов КАК ВтВидыЗапасов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиНоменклатурыПолучатель
		|		ПО ВтВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиНоменклатурыПолучатель.Ссылка
		
		|ГДЕ
		|	ВтВидыЗапасов.СписатьНаРасходы";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ОписаниеФормыДокументаДляЗаполненияРеквизитовСвязанныхСНаправлениемДеятельности() Экспорт
	
	ТабЧасти = Новый Структура();
	
	//
	
	ФильтрХозОперация = Новый Массив();
	ФильтрХозОперация.Добавить("ОТГРУЗКА");
	ОписаниеТабЧасти = Новый Структура("ФильтрХозОперация, ОформляетсяПоЗаказу", ФильтрХозОперация, Ложь);
	
	ТабЧасти.Вставить("Материалы", ОписаниеТабЧасти);
	
	//
	
	ФильтрХозОперация = Новый Массив();
	ФильтрХозОперация.Добавить("ПОСТУПЛЕНИЕ");
	ОписаниеТабЧасти = Новый Структура("ФильтрХозОперация, ОформляетсяПоЗаказу", ФильтрХозОперация, Истина);
	
	ТабЧасти.Вставить("Продукция", ОписаниеТабЧасти);
	
	//
	
	ФильтрХозОперация = Новый Массив();
	ФильтрХозОперация.Добавить("ПОСТУПЛЕНИЕ");
	ФильтрХозОперация.Добавить("ВОЗВРАТНЫЕОТХОДЫ");
	ОписаниеТабЧасти = Новый Структура("ФильтрХозОперация, ОформляетсяПоЗаказу", ФильтрХозОперация, Истина);
	
	ТабЧасти.Вставить("ВозвратныеОтходы", ОписаниеТабЧасти);
	
	//
	
	СтруктураОбъекта = Новый Структура();
	СтруктураОбъекта.Вставить("ТабЧасти", ТабЧасти);
	СтруктураОбъекта.Вставить("ОформляетсяПоЗаказу", Истина);
	
	СтруктураОбъекта.Вставить("ЭтоИсточникПотребности", Ложь);
	СтруктураОбъекта.Вставить("ЕстьНазначениеВТЧ", Истина);
	СтруктураОбъекта.Вставить("ВТЧНазначениеОтгрузки", Истина);
	
	Возврат СтруктураОбъекта;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	// Печатные формы не предусмотрены
	ОтчетПереработчикаЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

#Область ПартииПроизводства

// Формирует партии производства документа.
// Параметры:
//  Объект - ДокументОбъект.ОтчетПереработчика, ДокументСсылка.ОтчетПереработчика - объект, для которого необходимо сформировать партии производства.
//
Процедура СформироватьПартииПроизводства(Объект) Экспорт
	
	Реквизиты = Объект;
	
	Если ТипЗнч(Объект) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
		
		СписокРеквизитов =
			"Ссылка, Дата, Организация, ГруппировкаЗатрат, ЗакупкаПодДеятельность";
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, СписокРеквизитов);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка",					Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Дата",					Реквизиты.Дата);
	Запрос.УстановитьПараметр("Организация",			Реквизиты.Организация);
	Запрос.УстановитьПараметр("ГруппировкаЗатрат",		Реквизиты.ГруппировкаЗатрат);
	Запрос.УстановитьПараметр("ЗакупкаПодДеятельность",	Реквизиты.ЗакупкаПодДеятельность);
	
	Запрос.УстановитьПараметр("ПартионныйУчетВерсии22",
		РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(Реквизиты.Дата));
	
	СформироватьТаблицуПродукции(Объект, Запрос);
	
	ТекстВыборкаПолейПартий = ТекстВыборкаПолейПартий();
	Справочники.ПартииПроизводства.СформироватьПартииПроизводства(
		Запрос,
		ТекстВыборкаПолейПартий,
		Истина,
		Истина);
	
КонецПроцедуры

Процедура СформироватьТаблицуПродукции(Объект, Запрос)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка							КАК Ссылка,
	|	ТаблицаТовары.НомерСтроки		КАК НомерСтроки,
	|	ТаблицаТовары.НомерГруппыЗатрат	КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.Номенклатура		КАК Номенклатура,
	|	ТаблицаТовары.Характеристика	КАК Характеристика,
	|	ТаблицаТовары.Спецификация		КАК Спецификация,
	|	ТаблицаТовары.Назначение		КАК Назначение
	|ПОМЕСТИТЬ ТаблицаПродукция
	|ИЗ
	|	&ТаблицаПродукция КАК ТаблицаТовары
	|
	|ГДЕ
	|	&ПартионныйУчетВерсии22
	|	И &УсловиеСсылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаТовары.НомерГруппыЗатрат
	|";
	
	Если ТипЗнч(Объект) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаПродукция", "Документ.ОтчетПереработчика.Продукция");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСсылка", "ТаблицаТовары.Ссылка = &Ссылка");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСсылка", "ИСТИНА");
		Запрос.УстановитьПараметр("ТаблицаПродукция", Объект.Продукция.Выгрузить());
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстВыборкаПолейПартий() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	&Ссылка											КАК Документ,
	|	&ГруппировкаЗатрат								КАК ГруппировкаЗатрат,
	|	ТаблицаПродукция.НомерГруппыЗатрат				КАК НомерГруппыЗатрат,
	|	&Организация									КАК Организация,
	|	НЕОПРЕДЕЛЕНО									КАК ТипПроцесса,
	|	ВЫБОР
	|		КОГДА &ГруппировкаЗатрат В
	|				(ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.БезГруппировки),
	|				ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции))
	|			ТОГДА СпрНазначения.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК НаправлениеДеятельности,
	|	&ЗакупкаПодДеятельность							КАК ВидДеятельностиНДС,
	|	ОсновныеИзделия.Номенклатура					КАК ОсновноеИзделиеНоменклатура,
	|	ОсновныеИзделия.Характеристика					КАК ОсновноеИзделиеХарактеристика,
	|	ВЫБОР
	|		КОГДА &ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции)
	|			ТОГДА ТаблицаПродукция.Номенклатура
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции)
	|			ТОГДА ТаблицаПродукция.Характеристика
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК Характеристика,
	|	СпрОсновныхИзделий.ГруппаАналитическогоУчета	КАК ГруппаПродукции,
	|	ВЫБОР
	|		КОГДА &ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям)
	|			ТОГДА ТаблицаПродукция.Спецификация
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК Спецификация,
	|	ВЫБОР
	|		КОГДА &ГруппировкаЗатрат В
	|				(ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.БезГруппировки),
	|				ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции))
	|			ТОГДА ТаблицаПродукция.Назначение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК Назначение
	|ПОМЕСТИТЬ ВТПоляПартийПроизводства
	|ИЗ
	|	ТаблицаПродукция КАК ТаблицаПродукция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НомераСтрокОсновныхИзделий КАК НомераСтрокОсновныхИзделий
	|	ПО НомераСтрокОсновныхИзделий.НомерГруппыЗатрат = ТаблицаПродукция.НомерГруппыЗатрат
	|	И НомераСтрокОсновныхИзделий.Ссылка = &Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПродукция КАК ОсновныеИзделия
	|	ПО ОсновныеИзделия.НомерСтроки = НомераСтрокОсновныхИзделий.НомерСтроки
	|	И ОсновныеИзделия.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрОсновныхИзделий
	|	ПО СпрОсновныхИзделий.Ссылка = ОсновныеИзделия.Номенклатура
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|	ПО СпрНазначения.Ссылка = ТаблицаПродукция.Назначение
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.ОтчетПереработчика)
	|";
	
КонецФункции

#КонецОбласти

#Область Прочее

// Осуществляет инициализацию структуры состояния расчетов
Функция СтруктураСостоянияРасчетов()
	
	СтруктураСостоянияРасчетов = Новый Структура;
	СтруктураСостоянияРасчетов.Вставить("СуммаОплаты",             0);
	СтруктураСостоянияРасчетов.Вставить("СуммаПоступления",        0);
	СтруктураСостоянияРасчетов.Вставить("ПроцентОплаты",           0);
	СтруктураСостоянияРасчетов.Вставить("ПроцентПоступления",      0);
	СтруктураСостоянияРасчетов.Вставить("СуммаПросроченнойОплаты", 0);
	СтруктураСостоянияРасчетов.Вставить("СуммаДолга",              0);
	СтруктураСостоянияРасчетов.Вставить("ПроцентДолга",            0);
	СтруктураСостоянияРасчетов.Вставить("СуммаКОплате",            0);
	
	Возврат СтруктураСостоянияРасчетов
	
КонецФункции

#КонецОбласти

#Область ТекущиеДела

// Заполняет список текущих дел пользователя.
// Описание параметров процедуры см. в ТекущиеДелаСлужебный.НоваяТаблицаТекущихДел().
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	ИмяФормы = "Обработка.ЖурналДокументовПереработки.Форма.СписокДокументыПередачиКОформлению";
	
	ОбщиеПараметрыЗапросов = ТекущиеДелаСлужебный.ОбщиеПараметрыЗапросов();
	
	// Определим доступны ли текущему пользователю показатели группы
	Доступность =
		(ОбщиеПараметрыЗапросов.ЭтоПолноправныйПользователь
			Или ПравоДоступа("Просмотр", Метаданные.Документы.ОтчетПереработчика))
		И ПравоДоступа("Просмотр", Метаданные.Документы.ЗаказПереработчику)
		И (ПравоДоступа("Добавление", Метаданные.Документы.ОтчетПереработчика)
			ИЛИ ПравоДоступа("Изменение", Метаданные.Документы.ОтчетПереработчика))
		И ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне");
	
	Если НЕ Доступность Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(НакладныеСервер.ТекстЗапросаСостояний("СостоянияОтчетовПереработчика", НакладныеСервер.ПараметрыОтбораРаспоряжений(), Неопределено)
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() +
		"ВЫБРАТЬ
		|	ОстаткиСостояния.Распоряжение КАК Ссылка
		|ИЗ
		|	ВтСостоянияОтчетовПереработчика КАК ОстаткиСостояния");
	
	ОтчетыПереработчиковКОформлению = Запрос.Выполнить().Выбрать().Количество();
	
	// Заполнение дел.
	// ПроизводствоНаСтороне
	ДелоРодитель = ТекущиеДела.Найти("ПроизводствоНаСтороне", "Идентификатор");
	Если ДелоРодитель = Неопределено Тогда
		ДелоРодитель = ТекущиеДела.Добавить();
		ДелоРодитель.Идентификатор  = "ПроизводствоНаСтороне";
		ДелоРодитель.Представление  = НСтр("ru = 'Производство на стороне'");
		ДелоРодитель.Владелец       = Метаданные.Подсистемы.Производство;
	КонецЕсли;
	ДелоРодитель.ЕстьДела = ДелоРодитель.ЕстьДела Или ОтчетыПереработчиковКОформлению > 0;
	
	// ОтчетыПереработчиковКОформлению
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяТекущейСтраницы", "СтраницаОтчеты");
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", Новый Структура);
	ПараметрыФормы.Вставить("КлючНазначенияФормы", "ТекущиеДела");
	
	Дело = ТекущиеДела.Добавить();
	Дело.Идентификатор  = "ОтчетыПереработчиковКОформлению";
	Дело.ЕстьДела       = ОтчетыПереработчиковКОформлению > 0;
	Дело.Представление  = НСтр("ru = 'Отчеты переработчиков к оформлению'");
	Дело.Количество     = ОтчетыПереработчиковКОформлению;
	Дело.Важное         = Ложь;
	Дело.Форма          = ИмяФормы;
	Дело.ПараметрыФормы = ПараметрыФормы;
	Дело.Владелец       = "ПроизводствоНаСтороне";
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеДокументовПереработки

Функция ЕстьДокументыКОформлению(Параметры)
	
	ПараметрыОтбора = НакладныеСервер.ПараметрыОтбораРаспоряжений(Параметры.Организация,,,, Параметры.Менеджер);
	
	Запрос = Новый Запрос(НакладныеСервер.ТекстЗапросаСостояний("СостоянияОтчетовПереработчика", ПараметрыОтбора, Неопределено, Истина)
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() +
		"ВЫБРАТЬ
		|	КОформлению
		|ИЗ
		|	ВтСостоянияОтчетовПереработчика");
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		СписокОрганизаций = Справочники.Организации.ФилиалыСРасчетамиЧерезГоловнуюОрганизацию(Параметры.Организация);
		СписокОрганизаций.Добавить(Параметры.Организация);
	Иначе
		СписокОрганизаций = Новый Массив;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", СписокОрганизаций);
	Запрос.УстановитьПараметр("Менеджер", Параметры.Менеджер);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает текст гиперссылки перехода из журнала документов в рабочее место оформления.
//
// Параметры:
//	Параметры - Структура - параметры формирования текста гиперссылки.
//
// ВозвращаемоеЗначение:
//	ФорматированнаяСтрока - текст гиперссылки перехода в рабочее место оформления передач.
//
Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	ЕстьПраваНаЧтение = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыПолученныеОтПереработчика)
		И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам);
	
	Если Не ЕстьПраваНаЧтение Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстГиперссылки = НСтр("ru = 'Отчеты'");
	ТекстСсылки = "Обработка.ЖурналДокументовПереработки.Форма.СписокДокументыПередачиКОформлению/СтраницаОтчеты";
	
	Если ЕстьДокументыКОформлению(Параметры) Тогда
		Гиперссылка = Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ТекстСсылки);
	Иначе
		Гиперссылка = Новый ФорматированнаяСтрока(ТекстГиперссылки,, ЦветаСтиля.НезаполненноеПолеТаблицы,, ТекстСсылки);
	КонецЕсли;
	
	Возврат Гиперссылка;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


// Процедура идентична универсальной процедуре за исключением:
// для подбора доступны устервшие виды запасов. Это требуется для того, чтобы виды запасов в поступлениях и отчетах не разьехались.
Процедура ЗаполнитьВидыЗапасовПоУмолчаниюПриОбновленииИБ(
	МенеджерВременныхТаблиц, 
	ТабличнаяЧастьТовары, 
	ВидЗапасовДокумента = Неопределено, 
	ИмяПоля = "ВидЗапасов", 
	ЭтоПередачаТоваровМеждуОрганизациями = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ЭтоВозвратнаяТара
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ТаблицаТоваров.ТипЗапасов
	|	КОНЕЦ КАК ТипЗапасов,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ЭтоВозвратнаяТара
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		ИНАЧЕ ТаблицаТоваров.ХозяйственнаяОперация
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ТаблицаТоваров.Соглашение КАК Соглашение,
	|	ТаблицаТоваров.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ТаблицаТоваров.НалогообложениеНДС
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|	КОНЕЦ КАК НалогообложениеНДС,
	|	ТаблицаТоваров.НалогообложениеОрганизации КАК НалогообложениеОрганизации,
	|	ВЫБОР ТаблицаТоваров.ВладелецТовара
	|		КОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.ВладелецТовара
	|	КОНЕЦ КАК ВладелецТовара,
	|	ВЫБОР ТаблицаТоваров.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР ТаблицаТоваров.Договор
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Договор
	|	КОНЕЦ КАК Договор,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаТоваров.Номенклатура.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ТаблицаТоваров.ГруппаПродукции КАК ГруппаПродукции,
	|	ТаблицаТоваров.ВидЦены КАК ВидЦены
	|ПОМЕСТИТЬ ТаблицаТоваровСАналитикой
	|ИЗ
	|	ИсходнаяТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Организация КАК Организация
	|ПОМЕСТИТЬ ОтборВидовЗапасов
	|ИЗ
	|	ТаблицаТоваровСАналитикой КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасовРезерва.Ссылка КАК Ссылка,
	|	ВидыЗапасовРезерва.Организация КАК Организация,
	|	ВидыЗапасовРезерва.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасовРезерва.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВидыЗапасовРезерва.ВладелецТовара КАК ВладелецТовара,
	|	ВидыЗапасовРезерва.Соглашение КАК Соглашение,
	|	ВидыЗапасовРезерва.Валюта КАК Валюта,
	|	ВидыЗапасовРезерва.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ВидыЗапасовРезерва.Контрагент КАК Контрагент,
	|	ВидыЗапасовРезерва.Договор КАК Договор,
	|	ВидыЗапасовРезерва.ГруппаПродукции КАК ГруппаПродукции,
	|	ВидыЗапасовРезерва.ВидЦены КАК ВидЦены,
	|	1 КАК Приоритет
	|ПОМЕСТИТЬ ВидыЗапасовРезерва
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасовРезерва
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборВидовЗапасов КАК Отбор
	|		ПО ВидыЗапасовРезерва.Организация = Отбор.Организация
	|ГДЕ
	|	НЕ ВидыЗапасовРезерва.ЭтоДубль
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасовРезерва.Ссылка,
	|	ВидыЗапасовРезерва.Организация,
	|	ВидыЗапасовРезерва.ТипЗапасов,
	|	ВидыЗапасовРезерва.НалогообложениеНДС,
	|	ВидыЗапасовРезерва.ВладелецТовара,
	|	ВидыЗапасовРезерва.Соглашение,
	|	ВидыЗапасовРезерва.Валюта,
	|	ВидыЗапасовРезерва.ГруппаФинансовогоУчета,
	|	ВидыЗапасовРезерва.Контрагент,
	|	ВидыЗапасовРезерва.Договор,
	|	ВидыЗапасовРезерва.ГруппаПродукции,
	|	ВидыЗапасовРезерва.ВидЦены,
	|	0
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасовРезерва
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборВидовЗапасов КАК Отбор
	|		ПО ВидыЗапасовРезерва.Организация = Отбор.Организация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|		ПО ВидыЗапасовРезерва.Ссылка = РезервыТоваровОрганизаций.ВидЗапасов
	|ГДЕ
	|	&ЭтоПередачаТоваровМеждуОрганизациями
	|	И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ВидыЗапасовРезерва.Устаревший
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТипЗапасов,
	|	Соглашение,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаТоваров.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ТаблицаТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаТоваров.Соглашение КАК Соглашение,
	|	ТаблицаТоваров.Валюта КАК Валюта,
	|	ТаблицаТоваров.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаТоваров.НалогообложениеОрганизации КАК НалогообложениеОрганизации,
	|	ТаблицаТоваров.ВладелецТовара КАК ВладелецТовара,
	|	ТаблицаТоваров.Контрагент КАК Контрагент,
	|	ТаблицаТоваров.Договор КАК Договор,
	|	ТаблицаТоваров.ГруппаПродукции КАК ГруппаПродукции,
	|	ТаблицаТоваров.ВидЦены КАК ВидЦены,
	|	ВидыЗапасовРезерва.Ссылка КАК ВидЗапасов
	|ИЗ
	|	ТаблицаТоваровСАналитикой КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗапасовРезерва КАК ВидыЗапасовРезерва
	|		ПО ТаблицаТоваров.Организация = ВидыЗапасовРезерва.Организация
	|			И ТаблицаТоваров.ТипЗапасов = ВидыЗапасовРезерва.ТипЗапасов
	|			И ТаблицаТоваров.ВладелецТовара = ВидыЗапасовРезерва.ВладелецТовара
	|			И ТаблицаТоваров.Контрагент = ВидыЗапасовРезерва.Контрагент
	|			И ТаблицаТоваров.Соглашение = ВидыЗапасовРезерва.Соглашение
	|			И ТаблицаТоваров.Договор = ВидыЗапасовРезерва.Договор
	|			И ТаблицаТоваров.Валюта = ВидыЗапасовРезерва.Валюта
	|			И ТаблицаТоваров.ГруппаПродукции = ВидыЗапасовРезерва.ГруппаПродукции
	|			И ТаблицаТоваров.ВидЦены = ВидыЗапасовРезерва.ВидЦены
	|			И ТаблицаТоваров.НалогообложениеНДС = ВидыЗапасовРезерва.НалогообложениеНДС
	|			И ТаблицаТоваров.ГруппаФинансовогоУчета = ВидыЗапасовРезерва.ГруппаФинансовогоУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ВидыЗапасовРезерва.Приоритет
	|ИТОГИ ПО
	|	ГруппаФинансовогоУчета,
	|	ГруппаПродукции,
	|	ТипЗапасов,
	|	ТаблицаТоваров.ЭтоВозвратнаяТара,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОтборВидовЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВидыЗапасовРезерва
	|";

	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Запрос.УстановитьПараметр("ЭтоПередачаТоваровМеждуОрганизациями", ЭтоПередачаТоваровМеждуОрганизациями);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Результат = Запрос.Выполнить();
	
	ВыборкаПоГруппам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоГруппам.Следующий() Цикл
		
		ВыборкаПоГруппеПродукции = ВыборкаПоГруппам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоГруппеПродукции.Следующий() Цикл
		
			ВыборкаПоТипу = ВыборкаПоГруппеПродукции.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоТипу.Следующий() Цикл
				
				ВидЗапасов = Неопределено;
				
				ВыборкаПоТаре = ВыборкаПоТипу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоТаре.Следующий() Цикл
					
					ВидЗапасов = Неопределено;
					
					ВыборкаПоНомеруСтроки = ВыборкаПоТаре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаПоНомеруСтроки.Следующий() Цикл
						
						Выборка = ВыборкаПоНомеруСтроки.Выбрать();
						Выборка.Следующий();
						
						Если Не ЗначениеЗаполнено(ВидЗапасов) Тогда
							Если ЗначениеЗаполнено(Выборка.ВидЗапасов) Тогда
								ВидЗапасов = Выборка.ВидЗапасов;
							Иначе
								Если ПараметрыСеанса.ПараметрыОбработчикаОбновления.РежимВыполнения = "Отложенно" Тогда
									ВызватьИсключение НСтр("ru = 'Создание новых видов запасов в режиме отложенного обновления запрещено'");
								КонецЕсли;
								ВидЗапасов = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
									Выборка.Организация,
									Выборка.ХозяйственнаяОперация,
									Выборка);
							КонецЕсли;
						КонецЕсли;
						
						Если ТабличнаяЧастьТовары <> Неопределено Тогда
							СтрокаТаблицы = ТабличнаяЧастьТовары.Найти(Выборка.НомерСтроки, "НомерСтроки");
							СтрокаТаблицы[ИмяПоля]= ВидЗапасов;
						Иначе
							ВидЗапасовДокумента = ВидЗапасов;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла
				
			КонецЦикла
			
		КонецЦикла
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
