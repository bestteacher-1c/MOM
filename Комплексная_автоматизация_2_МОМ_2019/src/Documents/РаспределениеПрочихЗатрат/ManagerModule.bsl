#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Распределяет расходы на финансовый результат.
// Параметры:
//	Организации - Массив - список организаций, по которым необходимо выполнить распределение.
//
Процедура РаспределитьРасходыНаФинансовыйРезультат(Период, Организации, МенеджерВременныхТаблиц = Неопределено, ДокументРаспределения = Неопределено) Экспорт
	
	Если Не ДокументРаспределения = Неопределено Тогда
		Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
			"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.РаспределитьРасходыНаФинансовыйРезультат");
	КонецЕсли;
	
	ПараметрыРасчета = Неопределено;
	//++ НЕ УТ
	ПараметрыРасчета = РасчетСебестоимостиПостатейныеЗатраты.ПодготовитьБазуПоПрямымЗатрат(Период, Организации, ДокументРаспределения);
	//-- НЕ УТ
	
	Запрос = Новый Запрос;
	Если ПараметрыРасчета = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Иначе
		Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	КонецЕсли;
	
	ФормироватьФинансовыйРезультат = ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат");
	Запрос.УстановитьПараметр("НачалоПериода", 		НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода", 		КонецМесяца(Период));
	Запрос.УстановитьПараметр("ГраницаПериода", 	Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("МассивОрганизаций", 	Организации);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Организации.Количество() = 0);
	Запрос.УстановитьПараметр("ФормироватьФинансовыйРезультат", ФормироватьФинансовыйРезультат);
	Запрос.УстановитьПараметр("ДокументРаспределения", ДокументРаспределения);
	Запрос.УстановитьПараметр("ПредварительныйРасчет", Не ДокументРаспределения = Неопределено);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	//-- НЕ УТ
	
	ТипДенежногоПоля = Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип;
	ШаблонЧисловогоПоля = СтрШаблон("ЧИСЛО(%1, %2)",
		Формат(ТипДенежногоПоля.КвалификаторыЧисла.Разрядность, "ЧГ=0"),
		Формат(ТипДенежногоПоля.КвалификаторыЧисла.РазрядностьДробнойЧасти, "ЧГ=0"));
	
	ПодготовитьСлужебныеТаблицы(Период, Организации, Запрос);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Расходы.Организация КАК Организация,
		|	Расходы.Подразделение КАК Подразделение,
		|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|	Расходы.СуммаОстаток КАК Сумма,
		|	Расходы.СуммаРеглОстаток КАК СуммаРегл,
		|	Расходы.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	Расходы.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	Расходы.СуммаУпрОстаток КАК СуммаУпр
		|ПОМЕСТИТЬ ПредварительныеОстаткиРасходов
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы.Остатки(
		|			&ГраницаПериода,
		|			(Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов) В
		|				(ВЫБРАТЬ
		|					НастроенныеРасходы.Организация,
		|					НастроенныеРасходы.Подразделение,
		|					НастроенныеРасходы.НаправлениеДеятельности,
		|					НастроенныеРасходы.СтатьяРасходов,
		|					НастроенныеРасходы.АналитикаРасходов
		|				ИЗ
		|					Регистраторы КАК НастроенныеРасходы)) КАК Расходы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расходы.Организация КАК Организация,
		|	Расходы.Подразделение КАК Подразделение,
		|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(Расходы.Сумма) КАК Сумма,
		|	СУММА(Расходы.СуммаРегл) КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаУпр) КАК СуммаУпр
		|ПОМЕСТИТЬ ОстаткиРасходов
		|ИЗ
		|	(ВЫБРАТЬ
		|		Расходы.Организация КАК Организация,
		|		Расходы.Подразделение КАК Подразделение,
		|		Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|		Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|		Расходы.Сумма КАК Сумма,
		|		Расходы.СуммаРегл КАК СуммаРегл,
		|		Расходы.ПостояннаяРазница КАК ПостояннаяРазница,
		|		Расходы.ВременнаяРазница КАК ВременнаяРазница,
		|		Расходы.СуммаУпр КАК СуммаУпр
		|	ИЗ
		|		ПредварительныеОстаткиРасходов КАК Расходы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТекущиеДвижения.Организация,
		|		ТекущиеДвижения.Подразделение,
		|		ТекущиеДвижения.НаправлениеДеятельности,
		|		ТекущиеДвижения.СтатьяРасходов,
		|		ТекущиеДвижения.АналитикаРасходов,
		|		ВЫБОР
		|			КОГДА (ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ))
		|					ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|				ТОГДА ТекущиеДвижения.Сумма
		|			КОГДА Остатки.Сумма = 0
		|				И Остатки.СуммаУпр = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.Сумма
		|		КОНЕЦ КАК Сумма,
		|		ВЫБОР 
		|			КОГДА (ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ))
		|					ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|				ТОГДА ТекущиеДвижения.СуммаРегл
		|			КОГДА Остатки.СуммаРегл = 0
		|				И Остатки.ПостояннаяРазница = 0
		|				И Остатки.ВременнаяРазница = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.СуммаРегл
		|		КОНЕЦ КАК СуммаРегл,
		|		ВЫБОР 
		|			КОГДА (ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ))
		|					ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|				ТОГДА ТекущиеДвижения.ПостояннаяРазница
		|			КОГДА Остатки.СуммаРегл = 0
		|				И Остатки.ПостояннаяРазница = 0
		|				И Остатки.ВременнаяРазница = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.ПостояннаяРазница
		|		КОНЕЦ КАК ПостояннаяРазница,
		|		ВЫБОР 
		|			КОГДА (ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ))
		|					ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|				ТОГДА ТекущиеДвижения.ВременнаяРазница
		|			КОГДА Остатки.СуммаРегл = 0
		|				И Остатки.ПостояннаяРазница = 0
		|				И Остатки.ВременнаяРазница = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.ВременнаяРазница
		|		КОНЕЦ КАК ВременнаяРазница,
		|		ВЫБОР 
		|			КОГДА (ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ))
		|					ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|				ТОГДА ТекущиеДвижения.СуммаУпр
		|			КОГДА Остатки.Сумма = 0
		|				И Остатки.СуммаУпр = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.СуммаУпр
		|		КОНЕЦ КАК СуммаУпр
		|	ИЗ
		|		ПредварительныеОстаткиРасходов КАК Остатки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
		|			ПО (ТекущиеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|				И (ТекущиеДвижения.Организация = Остатки.Организация)
		|				И (ТекущиеДвижения.Подразделение = Остатки.Подразделение)
		|				И (ТекущиеДвижения.НаправлениеДеятельности = Остатки.НаправлениеДеятельности)
		|				И (ТекущиеДвижения.СтатьяРасходов = Остатки.СтатьяРасходов)
		|				И (ТекущиеДвижения.АналитикаРасходов = Остатки.АналитикаРасходов)
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РеквизитыДокументаРаспределения
		|			ПО РеквизитыДокументаРаспределения.Ссылка = ТекущиеДвижения.Регистратор
		|	ГДЕ
		|		ТекущиеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТекущиеДвижения.Активность
		|		И (ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) В (ТИП(Документ.РаспределениеПрочихЗатрат),
		|				ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
		|				И ТекущиеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		//++ НЕ УТ
		//++ Локализация		
		|			ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
		|				И НЕ ТекущиеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
		//-- Локализация		
		//-- НЕ УТ
		|			)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТекущиеДвижения.Организация,
		|		ТекущиеДвижения.Подразделение,
		|		ТекущиеДвижения.НаправлениеДеятельности,
		|		ТекущиеДвижения.СтатьяРасходов,
		|		ТекущиеДвижения.АналитикаРасходов,
		|		ТекущиеДвижения.Сумма,
		|		ТекущиеДвижения.СуммаРегл,
		|		ТекущиеДвижения.ПостояннаяРазница,
		|		ТекущиеДвижения.ВременнаяРазница,
		|		ТекущиеДвижения.СуммаУпр
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
		|	ГДЕ
		|		ТекущиеДвижения.Регистратор = &ДокументРаспределения) КАК Расходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.СтатьяРасходов,
		|	Расходы.АналитикаРасходов,
		|	Расходы.НаправлениеДеятельности,
		|	Расходы.Подразделение,
		|	Расходы.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиРасходов.Организация КАК Организация,
		|	ОстаткиРасходов.Подразделение КАК Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов КАК АналитикаРасходов,
		|	ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ) КАК Нормируемые,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.Сумма
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.СуммаУпр
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ ОстаткиРасходов.СуммаРегл
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.ПостояннаяРазница * ДолиСписания.ДоляСписания КАК ЧИСЛО(15, 2))
		|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
		|				И ОстаткиРасходов.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|			ТОГДА ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ВременнаяРазница 
		|					- ВЫРАЗИТЬ((ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ПостояннаяРазница - ОстаткиРасходов.ВременнаяРазница) * ДолиСписания.ДоляСписания КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ ОстаткиРасходов.ПостояннаяРазница
		|	КОНЕЦ КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ТОГДА 0
		//++ НЕ УТ
		|		КОГДА Регистраторы.КосвенныеПроизводственныеЗатраты
		|			ТОГДА -1 * (ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ВременнаяРазница)
		//-- НЕ УТ
		|		КОГДА НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.ВременнаяРазница * ДолиСписания.ДоляСписания КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ ОстаткиРасходов.ВременнаяРазница
		|	КОНЕЦ КАК ВременнаяРазница,
		|	Регистраторы.Регистратор КАК Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
		//++ НЕ УТ
		|	Регистраторы.КосвенныеПроизводственныеЗатраты,
		//-- НЕ УТ
		|	Регистраторы.ТребуетсяРаспределение
		|ПОМЕСТИТЬ РасходыКРаспределению
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|			И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО ОстаткиРасходов.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И ОстаткиРасходов.Организация = ДолиСписания.Организация
		|ГДЕ
		|	(НЕ(ОстаткиРасходов.Сумма = 0
		|		И ОстаткиРасходов.СуммаУпр = 0) И Регистраторы.УправленческийУчет)
		|	ИЛИ
		|		(НЕ(ОстаткиРасходов.СуммаРегл = 0
		|			И ОстаткиРасходов.ВременнаяРазница = 0 
		|			И ОстаткиРасходов.ПостояннаяРазница = 0) И Регистраторы.РегламентированныйУчет)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФинансовыеРезультаты.Организация,
		|	ФинансовыеРезультаты.НаправлениеДеятельности,
		|	СУММА(ФинансовыеРезультаты.Доходы) КАК Доходы,
		|	СУММА(ФинансовыеРезультаты.Расходы) КАК Расходы
		|ПОМЕСТИТЬ ФинансовыеРезультаты
		|ИЗ
		|	РегистрНакопления.ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|ГДЕ
		|	ФинансовыеРезультаты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ФинансовыеРезультаты.Организация В(&МассивОрганизаций)
		|	И ФинансовыеРезультаты.Активность
		|	И (ТИПЗНАЧЕНИЯ(ФинансовыеРезультаты.Регистратор) = ТИП(Документ.РасчетСебестоимостиТоваров)
		|			ИЛИ ФинансовыеРезультаты.РасчетСебестоимости)
		|
		|СГРУППИРОВАТЬ ПО
		|	ФинансовыеРезультаты.Организация,
		|	ФинансовыеРезультаты.НаправлениеДеятельности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор КАК Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
		|	ЕСТЬNULL(НаправленияДеятельности.НаправлениеДеятельности,
		|		ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))) КАК НаправлениеДеятельности,
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ПартияПроизводства,
		//-- НЕ УТ
		|	ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0) КАК База
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
		|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
		|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
		|			И НЕ ФинансовыеРезультаты.Доходы = 0
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		|	И Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям,
		|	ЕСТЬNULL(НаправленияДеятельности.НаправлениеДеятельности,
		|		ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))),
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|	ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
		|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
		|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
		|			И НЕ ФинансовыеРезультаты.Расходы = 0
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		|	И Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям,
		|	ЕСТЬNULL(НаправленияДеятельности.НаправлениеДеятельности,
		|		ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))),
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|	ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0) - ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
		|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
		|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
		|			И (НЕ ФинансовыеРезультаты.Доходы - ФинансовыеРезультаты.Расходы = 0)
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		|	И Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НаправленияДеятельности.Ссылка,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка),
		|	НаправленияДеятельности.НаправлениеДеятельности,
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|	НаправленияДеятельности.ДоляСтоимости
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.НаправленияДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		//++ НЕ УТ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	БазаПоЭтапам.Регистратор,
		|	БазаПоЭтапам.БазаРаспределенияПоПартиям,
		|	БазаПоЭтапам.НаправлениеДеятельности,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ЗаказНаПроизводство
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.КодСтрокиПродукция
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ПартияПроизводства
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	СУММА(БазаПоЭтапам.Стоимость)
		|ИЗ
		|	БазаПоЭтапам КАК БазаПоЭтапам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО БазаПоЭтапам.Регистратор = Регистраторы.Регистратор
		|			И Регистраторы.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаПоЭтапам.НаправлениеДеятельности,
		|	БазаПоЭтапам.Регистратор,
		|	БазаПоЭтапам.БазаРаспределенияПоПартиям,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ЗаказНаПроизводство
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.КодСтрокиПродукция
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ПартияПроизводства
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ
		//-- НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БазаРаспределения.Регистратор КАК Регистратор,
		|	БазаРаспределения.ТипБазыРаспределения КАК ТипБазыРаспределения,
		|	СУММА(БазаРаспределения.База) КАК База
		|ПОМЕСТИТЬ БазыРаспределенияСводно
		|ИЗ
		|	БазаРаспределения КАК БазаРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.Регистратор,
		|	БазаРаспределения.ТипБазыРаспределения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	ТипБазыРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.Сумма
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.Сумма * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.СуммаУпр
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаУпр * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.СуммаРегл
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаРегл * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|				ИЛИ (ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|						И РасходыКРаспределению.Нормируемые)
		|			ТОГДА РасходыКРаспределению.ПостояннаяРазница
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.ПостояннаяРазница * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.ВременнаяРазница
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.ВременнаяРазница * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК ВременнаяРазница,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ БазаРаспределения.База / БазыРаспределенияСводно.База
		|	КОНЕЦ КАК КоэффициентРаспределения,
		|	ЕСТЬNULL(БазаРаспределения.База, 0) КАК База,
		|	РасходыКРаспределению.Регистратор КАК Регистратор,
		|	РасходыКРаспределению.Организация КАК Организация,
		|	РасходыКРаспределению.Подразделение КАК Подразделение,
		|	РасходыКРаспределению.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РасходыКРаспределению.СтатьяРасходов КАК СтатьяРасходов,
		|	РасходыКРаспределению.АналитикаРасходов КАК АналитикаРасходов,
		//++ НЕ УТ
		|	БазаРаспределения.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	БазаРаспределения.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	БазаРаспределения.ПартияПроизводства КАК ПартияПроизводства,
		//-- НЕ УТ
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|			ТОГДА 
		|				ВЫБОР 
		|					КОГДА НЕ РасходыКРаспределению.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|						ТОГДА РасходыКРаспределению.НаправлениеДеятельности
		|					КОГДА ТИПЗНАЧЕНИЯ(РасходыКРаспределению.АналитикаРасходов) = ТИП(Справочник.НаправленияДеятельности)
		|						И НЕ РасходыКРаспределению.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|						ТОГДА РасходыКРаспределению.АналитикаРасходов
		|					ИНАЧЕ
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|				КОНЕЦ
		|		ИНАЧЕ
		|			БазаРаспределения.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельностиПриемник
		|ПОМЕСТИТЬ РаспределенныеРасходы
		|ИЗ
		|	РасходыКРаспределению КАК РасходыКРаспределению
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения
		|		ПО РасходыКРаспределению.Регистратор = БазаРаспределения.Регистратор
		|			И РасходыКРаспределению.ТипБазыРаспределения = БазаРаспределения.ТипБазыРаспределения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазыРаспределенияСводно КАК БазыРаспределенияСводно
		|		ПО РасходыКРаспределению.Регистратор = БазыРаспределенияСводно.Регистратор
		|			И РасходыКРаспределению.ТипБазыРаспределения = БазыРаспределенияСводно.ТипБазыРаспределения
		//++ НЕ УТ
		|ГДЕ
		|	НЕ РасходыКРаспределению.КосвенныеПроизводственныеЗатраты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	0,
		|	0,
		|	РасходыКРаспределению.ПостояннаяРазница,
		|	РасходыКРаспределению.ВременнаяРазница,
		|	0,
		|	0,
		|	РасходыКРаспределению.Регистратор КАК Регистратор,
		|	РасходыКРаспределению.Организация КАК Организация,
		|	РасходыКРаспределению.Подразделение КАК Подразделение,
		|	РасходыКРаспределению.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РасходыКРаспределению.СтатьяРасходов КАК СтатьяРасходов,
		|	РасходыКРаспределению.АналитикаРасходов КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ПартияПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиПриемник
		|ИЗ
		|	РасходыКРаспределению КАК РасходыКРаспределению
		|ГДЕ
		|	РасходыКРаспределению.КосвенныеПроизводственныеЗатраты
		//-- НЕ УТ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	КоэффициентРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспределенныеРасходы.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ДокументыРаспределения
		|ИЗ
		|	РаспределенныеРасходы КАК РаспределенныеРасходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазыРаспределенияСводно КАК БазаРаспределения
		|		ПО РаспределенныеРасходы.Регистратор = БазаРаспределения.Регистратор
		|ГДЕ
		|	НЕ (РаспределенныеРасходы.Сумма = 0
		|	И РаспределенныеРасходы.СуммаУпр = 0
		|	И РаспределенныеРасходы.СуммаРегл = 0
		|	И РаспределенныеРасходы.ПостояннаяРазница = 0
		|	И РаспределенныеРасходы.ВременнаяРазница = 0)
		|		ИЛИ НЕ БазаРаспределения.Регистратор ЕСТЬ NULL
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределенныеРасходы.Регистратор КАК Регистратор,
		|	МАКСИМУМ(РаспределенныеРасходы.КоэффициентРаспределения) КАК КоэффициентРаспределения
		|ПОМЕСТИТЬ МаксимальныеКоэффициентыПоРегистраторам
		|ИЗ
		|	РаспределенныеРасходы КАК РаспределенныеРасходы
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределенныеРасходы.Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимальныеКоэффициентыПоРегистраторам.Регистратор КАК Регистратор,
		|	МАКСИМУМ(РаспределенныеРасходы.НаправлениеДеятельностиПриемник) КАК НаправлениеДеятельностиПриемник
		|ПОМЕСТИТЬ НаправленияДеятельностиДляПогрешности
		|ИЗ
		|	МаксимальныеКоэффициентыПоРегистраторам КАК МаксимальныеКоэффициентыПоРегистраторам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределенныеРасходы КАК РаспределенныеРасходы
		|		ПО МаксимальныеКоэффициентыПоРегистраторам.Регистратор = РаспределенныеРасходы.Регистратор
		|			И МаксимальныеКоэффициентыПоРегистраторам.КоэффициентРаспределения = РаспределенныеРасходы.КоэффициентРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	МаксимальныеКоэффициентыПоРегистраторам.Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
		|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
		|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
		|	ВложенныйЗапрос.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ПогрешностиОкругления
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасходыКРаспределению.Сумма,
		|		РасходыКРаспределению.СуммаУпр,
		|		РасходыКРаспределению.СуммаРегл,
		|		РасходыКРаспределению.ПостояннаяРазница,
		|		РасходыКРаспределению.ВременнаяРазница,
		|		РасходыКРаспределению.Регистратор КАК Регистратор
		|	ИЗ
		|		РасходыКРаспределению КАК РасходыКРаспределению
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределения
		|			ПО РасходыКРаспределению.Регистратор = ДокументыРаспределения.Регистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		-РаспределенныеРасходы.Сумма,
		|		-РаспределенныеРасходы.СуммаУпр,
		|		-РаспределенныеРасходы.СуммаРегл,
		|		-РаспределенныеРасходы.ПостояннаяРазница,
		|		-РаспределенныеРасходы.ВременнаяРазница,
		|		РаспределенныеРасходы.Регистратор
		|	ИЗ
		|		РаспределенныеРасходы КАК РаспределенныеРасходы) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Регистратор
		|
		|ИМЕЮЩИЕ
		|	НЕ (СУММА(ВложенныйЗапрос.Сумма) = 0
		|		И СУММА(ВложенныйЗапрос.СуммаУпр) = 0
		|		И СУММА(ВложенныйЗапрос.СуммаРегл) = 0
		|		И СУММА(ВложенныйЗапрос.ПостояннаяРазница) = 0
		|		И СУММА(ВложенныйЗапрос.ВременнаяРазница) = 0)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор 				КАК Регистратор,
		|	ОстаткиРасходов.Организация 			КАК Организация,
		|	ОстаткиРасходов.Подразделение 			КАК Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов			КАК СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов		КАК АналитикаРасходов
		|ПОМЕСТИТЬ РегистраторыКОчисткеДвижений
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|			И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|ГДЕ
		|	Регистраторы.УправленческийУчет
		|		И НЕ Регистраторы.РегламентированныйУчет
		|		И ОстаткиРасходов.Сумма = 0
		|		И ОстаткиРасходов.СуммаУпр = 0
		|	ИЛИ
		|		Регистраторы.РегламентированныйУчет
		|			И НЕ Регистраторы.УправленческийУчет
		|			И ОстаткиРасходов.СуммаРегл = 0
		|			И ОстаткиРасходов.ВременнаяРазница = 0 
		|			И ОстаткиРасходов.ПостояннаяРазница = 0
		|	ИЛИ
		|		Регистраторы.РегламентированныйУчет
		|			И Регистраторы.УправленческийУчет
		|			И ОстаткиРасходов.Сумма = 0
		|			И ОстаткиРасходов.СуммаУпр = 0
		|			И ОстаткиРасходов.СуммаРегл = 0
		|			И ОстаткиРасходов.ВременнаяРазница = 0 
		|			И ОстаткиРасходов.ПостояннаяРазница = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АналитикиРасходов.Организация КАК Организация,
		|	АналитикиРасходов.Подразделение КАК Подразделение,
		|	АналитикиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	АналитикиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	АналитикиРасходов.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ РаспределенныеАналитикиРасходов
		|ИЗ
		|	(ВЫБРАТЬ
		|		РаспределенныеРасходы.Организация КАК Организация,
		|		РаспределенныеРасходы.Подразделение КАК Подразделение,
		|		РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|		РаспределенныеРасходы.АналитикаРасходов КАК АналитикаРасходов
		|	ИЗ
		|		РаспределенныеРасходы КАК РаспределенныеРасходы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК Документы
		|			ПО РаспределенныеРасходы.Регистратор = Документы.Регистратор
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		АналитикиРасходов.Организация КАК Организация,
		|		АналитикиРасходов.Подразделение КАК Подразделение,
		|		АналитикиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		АналитикиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|		АналитикиРасходов.АналитикаРасходов КАК АналитикаРасходов
		|	ИЗ
		|		РегистраторыКОчисткеДвижений КАК АналитикиРасходов) КАК АналитикиРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Подразделение,
		|	НаправлениеДеятельности,
		|	СтатьяРасходов,
		|	АналитикаРасходов
		|;
		//++ НЕ УТ
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеРасходы.*
		|ИЗ
		|	ДокументыРаспределения КАК ДокументыРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
		|		ПО ДокументыРаспределения.Регистратор = ПрочиеРасходы.Регистратор
		|ГДЕ
		|	НЕ ПрочиеРасходы.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеАктивыПассивы.*
		|ИЗ
		|	ДокументыРаспределения КАК ДокументыРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыПассивы КАК ПрочиеАктивыПассивы
		|		ПО ДокументыРаспределения.Регистратор = ПрочиеАктивыПассивы.Регистратор
		|ГДЕ
		|	ПрочиеАктивыПассивы.РасчетСебестоимости
		|	И НЕ ПрочиеАктивыПассивы.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ПустаяСсылка)
		|;
		//-- НЕ УТ
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
		|	ВложенныйЗапрос.Регистратор КАК Регистратор,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВложенныйЗапрос.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
		//++ НЕ УТ
		|	ВложенныйЗапрос.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ВложенныйЗапрос.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	ВложенныйЗапрос.ПартияПроизводства КАК ПартияПроизводства,
		//-- НЕ УТ
		|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
		|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
		|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
		|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ВложенныйЗапрос.Расходы) КАК Расходы
		|ПОМЕСТИТЬ РезультатРаспределения
		|ИЗ
		|	(ВЫБРАТЬ
		|		&КонецПериода КАК Период,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|		РаспределенныеРасходы.Регистратор КАК Регистратор,
		|		РаспределенныеРасходы.Организация КАК Организация,
		|		РаспределенныеРасходы.Подразделение КАК Подразделение,
		|		РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		РаспределенныеРасходы.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
		//++ НЕ УТ
		|		РаспределенныеРасходы.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|		РаспределенныеРасходы.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|		РаспределенныеРасходы.ПартияПроизводства КАК ПартияПроизводства,
		//-- НЕ УТ
		|		РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|		РаспределенныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
		|		РаспределенныеРасходы.Сумма КАК Сумма,
		|		РаспределенныеРасходы.СуммаУпр КАК СуммаУпр,
		|		РаспределенныеРасходы.СуммаРегл КАК СуммаРегл,
		|		РаспределенныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
		|		РаспределенныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
		|		РаспределенныеРасходы.Сумма КАК Расходы
		|	ИЗ
		|		РаспределенныеРасходы КАК РаспределенныеРасходы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&КонецПериода,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|		РасходыКРаспределению.Регистратор,
		|		РасходыКРаспределению.Организация,
		|		РасходыКРаспределению.Подразделение,
		|		РасходыКРаспределению.НаправлениеДеятельности,
		|		НаправленияДеятельностиДляПогрешности.НаправлениеДеятельностиПриемник,
		//++ НЕ УТ
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|		РасходыКРаспределению.СтатьяРасходов,
		|		РасходыКРаспределению.АналитикаРасходов,
		|		ПогрешностиОкругления.Сумма,
		|		ПогрешностиОкругления.СуммаУпр,
		|		ПогрешностиОкругления.СуммаРегл,
		|		ПогрешностиОкругления.ПостояннаяРазница,
		|		ПогрешностиОкругления.ВременнаяРазница,
		|		ПогрешностиОкругления.Сумма
		|	ИЗ
		|		РасходыКРаспределению КАК РасходыКРаспределению
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПогрешностиОкругления КАК ПогрешностиОкругления
		|			ПО РасходыКРаспределению.Регистратор = ПогрешностиОкругления.Регистратор
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаправленияДеятельностиДляПогрешности КАК НаправленияДеятельностиДляПогрешности
		|			ПО РасходыКРаспределению.Регистратор = НаправленияДеятельностиДляПогрешности.Регистратор
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&КонецПериода,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|		РегистраторыИАналитики.Регистратор,
		|		РегистраторыИАналитики.Организация,
		|		РегистраторыИАналитики.Подразделение,
		|		РегистраторыИАналитики.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
		//++ НЕ УТ
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|		РегистраторыИАналитики.СтатьяРасходов,
		|		РегистраторыИАналитики.АналитикаРасходов,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистраторыКОчисткеДвижений КАК РегистраторыИАналитики) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.ВидДвижения,
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности,
		|	ВложенныйЗапрос.НаправлениеДеятельностиПриемник,
		//++ НЕ УТ
		|	ВложенныйЗапрос.ЗаказНаПроизводство,
		|	ВложенныйЗапрос.КодСтрокиПродукция,
		|	ВложенныйЗапрос.ПартияПроизводства,
		//-- НЕ УТ
		|	ВложенныйЗапрос.СтатьяРасходов,
		|	ВложенныйЗапрос.АналитикаРасходов";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЧИСЛО(15, 2)", ШаблонЧисловогоПоля);
		
	Если ДокументРаспределения = Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|
		|ИТОГИ ПО
		|	Регистратор";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ РезультатРаспределения", "");
		
	Иначе
		
		Запрос.Выполнить();
		МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
		
		Возврат;
		
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаРегистраторов = Результат[Результат.Количество() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//++ НЕ УТ
	ВыборкаДвиженийПрочиеАктивыПассивы = Результат[Результат.Количество() - 2].Выбрать();
	ВыборкаДвиженийПрочиеРасходы = Результат[Результат.Количество() - 3].Выбрать();
	//-- НЕ УТ
	
	ФормироватьУправленческийБаланс = ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс");
	Пока ВыборкаРегистраторов.Следующий() Цикл 
		
		ФинансовыеРезультаты = РегистрыНакопления.ФинансовыеРезультаты.СоздатьНаборЗаписей();
		ФинансовыеРезультаты.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
		
		ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
		ПрочиеРасходы.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
		
		ПрочиеАктивыПассив = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей();
		ПрочиеАктивыПассив.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
		
		ДвиженияДоходыРасходыПрочиеАктивыПассивы = РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.СоздатьНаборЗаписей();
		ДвиженияДоходыРасходыПрочиеАктивыПассивы.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
		
		ВыборкаДетальная = ВыборкаРегистраторов.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Если Не (ВыборкаДетальная.Сумма = 0
				И ВыборкаДетальная.СуммаУпр = 0
				И ВыборкаДетальная.СуммаРегл = 0
				И ВыборкаДетальная.ПостояннаяРазница = 0
				И ВыборкаДетальная.ВременнаяРазница = 0) Тогда
				
				НоваяЗапись = ПрочиеРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
				НоваяЗапись.КорНаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
				НоваяЗапись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности;
				НоваяЗапись.РасчетСебестоимости = Истина;
				
			КонецЕсли;
			
			Если Не ВыборкаДетальная.Сумма = 0 Тогда
				
				НоваяЗапись = ПрочиеАктивыПассив.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная, "Период, Регистратор, Организация, НаправлениеДеятельности, Сумма");
				НоваяЗапись.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
				НоваяЗапись.Аналитика = Неопределено;
				НоваяЗапись.РасчетСебестоимости = Истина;
				
			КонецЕсли;
			
			Если Не (ВыборкаДетальная.Сумма = 0
				И ВыборкаДетальная.СуммаУпр = 0
				И ВыборкаДетальная.СуммаРегл = 0) Тогда
				
				НоваяЗапись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная, 
					"Период, Регистратор, Организация, Подразделение, НаправлениеДеятельности, 
					|АналитикаРасходов, Сумма, СуммаУпр, СуммаРегл");
				НоваяЗапись.Статья = ВыборкаДетальная.СтатьяРасходов;
				НоваяЗапись.КорСтатья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
				НоваяЗапись.КорНаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
				НоваяЗапись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности;
				
			КонецЕсли;
			
			Если Не ВыборкаДетальная.Расходы = 0 Тогда
				
				НоваяЗапись = ФинансовыеРезультаты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
				НоваяЗапись.НаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
				НоваяЗапись.РасчетСебестоимости = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
		//++ НЕ УТ
		ВыборкаДвиженийПрочиеРасходы.Сбросить();
		КлючПоискаДвижений = Новый Структура("Регистратор", ВыборкаРегистраторов.Регистратор);
		Пока ВыборкаДвиженийПрочиеРасходы.НайтиСледующий(КлючПоискаДвижений) Цикл
			ЗаполнитьЗначенияСвойств(ПрочиеРасходы.Добавить(), ВыборкаДвиженийПрочиеРасходы);
		КонецЦикла;

		ВыборкаДвиженийПрочиеАктивыПассивы.Сбросить();
		КлючПоискаДвижений = Новый Структура("Регистратор", ВыборкаРегистраторов.Регистратор);
		Пока ВыборкаДвиженийПрочиеАктивыПассивы.НайтиСледующий(КлючПоискаДвижений) Цикл
			ЗаполнитьЗначенияСвойств(ПрочиеАктивыПассив.Добавить(), ВыборкаДвиженийПрочиеАктивыПассивы);
		КонецЦикла;
		//-- НЕ УТ
		
		НачатьТранзакцию();
		Попытка
			
			ФинансовыеРезультаты.Записать();
			ПрочиеРасходы.Записать();
			ПрочиеАктивыПассив.Записать();
			ДвиженияДоходыРасходыПрочиеАктивыПассивы.Записать();
			//++ НЕ УТ
			РаспределениеПрочихЗатратЛокализация.ЗарегистрироватьОтражениеДокумента(
				ВыборкаРегистраторов.Регистратор);
			//-- НЕ УТ
		
			Если ФормироватьУправленческийБаланс Тогда
				ДвиженияУпрБаланса = Новый Структура;
				ДвиженияУпрБаланса.Вставить("ПрочиеРасходы", ПрочиеРасходы.Выгрузить());
				ДвиженияУпрБаланса.Вставить("ПрочиеАктивыПассивы", ПрочиеАктивыПассив.Выгрузить());
				
				УправленческийУчетПроведениеСервер.ОбновитьДвиженияАктивовПассивов(ВыборкаРегистраторов.Регистратор, ДвиженияУпрБаланса);
				
				ПрочиеАктивыПассив.Загрузить(ДвиженияУпрБаланса.ПрочиеАктивыПассивы);
				ПрочиеАктивыПассив.Записать();
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации("РаспределениеРасходовНаФинансовыйРезультат",
									УровеньЖурналаРегистрации.Ошибка,
									ВыборкаРегистраторов.Регистратор.Метаданные(),
									ВыборкаРегистраторов.Регистратор,
									ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
									
		КонецПопытки;
		
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаРегистраторов.Количество());
	
	Если Не ЕстьУстаревшиеДокументыСРасходами(Запрос) Тогда
		Возврат;
	КонецЕсли;
	
	// Удаление движений распределенных расходов в старом документе "Распределение доходов по направлениям деятельности".
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.Регистратор
		|ПОМЕСТИТЬ РегистраторыКПерезаписиДвижений
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Движения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределенныеАналитикиРасходов КАК Аналитики
		|		ПО Движения.Организация = Аналитики.Организация
		|			И Движения.Подразделение = Аналитики.Подразделение
		|			И Движения.НаправлениеДеятельности = Аналитики.НаправлениеДеятельности
		|			И Движения.СтатьяРасходов = Аналитики.СтатьяРасходов
		|			И Движения.АналитикаРасходов = Аналитики.АналитикаРасходов
		|ГДЕ
		|	Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Движения.Активность
		|	И ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Движения.*,
		|	(НЕ Аналитики.Организация ЕСТЬ NULL) КАК ПропуститьЗапись
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Движения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКПерезаписиДвижений КАК Регистраторы
		|		ПО Движения.Регистратор = Регистраторы.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеАналитикиРасходов КАК Аналитики
		|		ПО Движения.Организация = Аналитики.Организация
		|			И Движения.Подразделение = Аналитики.Подразделение
		|			И Движения.НаправлениеДеятельности = Аналитики.НаправлениеДеятельности
		|			И Движения.СтатьяРасходов = Аналитики.СтатьяРасходов
		|			И Движения.АналитикаРасходов = Аналитики.АналитикаРасходов
		|
		|ИТОГИ ПО
		|	Движения.Регистратор";
		
	Результат = Запрос.Выполнить();
	
	ОрганизацииКПересчету = Новый СписокЗначений;
	ВыборкаРегистраторовНовыхДвижений = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистраторовНовыхДвижений.Следующий() Цикл
		
		ОрганизацииКПересчету.Очистить();
		
		ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
		ПрочиеРасходы.Отбор.Регистратор.Установить(ВыборкаРегистраторовНовыхДвижений.Регистратор);
		
		ВыборкаДетальная = ВыборкаРегистраторовНовыхДвижений.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Если ОрганизацииКПересчету.НайтиПоЗначению(ВыборкаДетальная.Организация) = Неопределено Тогда
				ОрганизацииКПересчету.Добавить(ВыборкаДетальная.Организация);
			КонецЕсли;
			
			Если ВыборкаДетальная.ПропуститьЗапись Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ПрочиеРасходы.Добавить(), ВыборкаДетальная);
			
		КонецЦикла;
		
		ПрочиеРасходы.Записать();
		
		Для Каждого ПересчитываемаяОрганизация Из ОрганизацииКПересчету Цикл
			РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписьРегистра(Период, 
				ВыборкаРегистраторовНовыхДвижений.Регистратор,
				ПересчитываемаяОрганизация.Значение, 
				Перечисления.ОперацииЗакрытияМесяца.РаспределениеДоходовПоНаправлениямДеятельности);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет служебные таблицы в менеджер временных таблиц, необходимые для расчета
// базы прямых производственных затрат.
// Параметры:
//	ПараметрыРасчета - Структура - см. РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьОбщиеПараметрыРасчета()
//
Процедура ПодготовитьТаблицыКРасчету(ПараметрыРасчета, Запрос = Неопределено) Экспорт
	
	Если Запрос = Неопределено Тогда
		
		Запрос = Новый Запрос;
		РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(
			Запрос,
			ПараметрыРасчета);
			
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыРасчета) = Тип("Структура") И ПараметрыРасчета.Свойство("Регистратор") Тогда
		
		Запрос.УстановитьПараметр("Регистратор", ПараметрыРасчета.Регистратор);
		Запрос.УстановитьПараметр("ПоВсемДокументам", ПараметрыРасчета.Регистратор = Неопределено);
		
	Иначе
		
		Запрос.УстановитьПараметр("Регистратор", Неопределено);
		Запрос.УстановитьПараметр("ПоВсемДокументам", Истина);
		
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Дата,
		|	Реквизиты.НазначениеНастройкиРаспределения,
		|	ВЫБОР
		|		КОГДА Реквизиты.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.ПоКоэффициентам)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|		ИНАЧЕ	
		|			Реквизиты.БазаРаспределенияПоПартиям
		|	КОНЕЦ КАК БазаРаспределенияПоПартиям,
		//++ НЕ УТ
		|	Реквизиты.БазаРаспределенияПоПодразделениям,
		|	Реквизиты.Показатель,
		|	Реквизиты.СтатьяКалькуляции,
		|	Реквизиты.РаспределятьПоПартиям,
		|	Реквизиты.РаспределятьПоПодразделениям,
		|	Реквизиты.РаспределятьНаСтатьи,
		|	Реквизиты.ОставитьВНЗП,
		|	Реквизиты.НаВыпускТекущегоМесяца,
		|	Реквизиты.ПартииУказаныВручную,
		|	Реквизиты.ПодразделенияУказаныВручную,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПустаяСсылка) КАК ПравилоОтнесенияНаВыпуск,
		|	(Реквизиты.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|			И Реквизиты.РегламентированныйУчет
		|			И Реквизиты.СтатьяРасходов.КосвенныеЗатратыНУ) КАК КосвенныеПроизводственныеЗатраты,
		//-- НЕ УТ
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Подразделение КАК Подразделение,
		|	Реквизиты.СтатьяРасходов КАК СтатьяРасходов,
		|	Реквизиты.АналитикаРасходов КАК АналитикаРасходов,
		|	Реквизиты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	(НЕ Реквизиты.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.Текущее)) КАК ТребуетсяРаспределение,
		|	Реквизиты.РегламентированныйУчет КАК РегламентированныйУчет,
		|	Реквизиты.УправленческийУчет КАК УправленческийУчет
		|ПОМЕСТИТЬ Регистраторы
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.Проведен
		|	И (Реквизиты.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|		ИЛИ Реквизиты.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|			И Реквизиты.РегламентированныйУчет
		|			И Реквизиты.СтатьяРасходов.КосвенныеЗатратыНУ)
		|	И Реквизиты.Организация В (&МассивОрганизаций)
		|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		//++ НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка) КАК Показатель,
		|	ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка) КАК Регистратор
		|ПОМЕСТИТЬ РаспределитьПоПодразделениям
		//-- НЕ УТ
		|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает список статей, по которым необходимо выполнить распределение.
//
// Параметры;
//		Период - Дата - Период выборки.
//		Организации - Массив - Список организаций по которым анализируются статьи.
//		Подразделения - Массив - Список подразделений по которым анализируются статьи.
//		Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - Фильтр по состоянию.
//
//	Возвращаемое значение:
//		ТаблицаЗначений - Таблица статей к распределению.
//
Функция СтатьиКРаспределению(Период, Организации, Подразделения, Состояние, ВариантРаспределения = Неопределено) Экспорт
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.Период = Период;
	ПараметрыЗапроса.Организации = Организации;
	ПараметрыЗапроса.Подразделения = Подразделения;
	ПараметрыЗапроса.Состояние = Состояние;
	ПараметрыЗапроса.ВариантРаспределения = ВариантРаспределения;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Получает и помещает таблицу статей расходов для настройки распределения во временное хранилище.
// Параметры:
//	Параметры - Структура - параметры получения статей расходов.
//		* Период - Дата - месяц распределения.
//		* Организации - СправочникСсылка.Организация - организация расходов.
//		* Подразделения - Массив - подразделения расходов.
//		* Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода.
//	АдресХранилища - Строка - адрес во временном хранилище, куда следует поместить результат работы.
//
Процедура СтатьиКРаспределениюВФоне(Параметры, АдресХранилища) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.СтатьиКРаспределениюВФоне");
	
	Подразделения = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		Подразделения.Добавить(Параметры.Подразделение);
	КонецЕсли;
	
	Организации = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организации.Добавить(Параметры.Организация);
	КонецЕсли;
	
	Статьи = СтатьиКРаспределению(Параметры.Период, Организации, 
		Подразделения, Параметры.Состояние);
		
	ПоместитьВоВременноеХранилище(Статьи, АдресХранилища);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Статьи.Количество() / 100);
	
КонецПроцедуры

// Формирует документ "Распределение прочих затрат" по заданным условиям.
// Параметры:
//	 ПараметрыЗаполнения - Структура - Параметры заполнения документа.
// Возвращаемое значение:
//	Массив
//		*Структура - Содержит сведения о документе и состоянии распределения.
//
Функция СформироватьДокумент(ПараметрыЗаполнения) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Распределение прочих затрат'"));
	
	ЕдиныйДокумент = Не РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ПараметрыЗаполнения.Дата)
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты;
		
	Если Не ЕдиныйДокумент
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ПравилоРаспределенияРегл = ПараметрыЗаполнения.ПравилоРаспределенияУпр
		И ПараметрыЗаполнения.ДокументУпр = ПараметрыЗаполнения.ДокументРегл Тогда
		ЕдиныйДокумент = Истина;
	КонецЕсли;
	
	ПараметрыВидовУчета = Новый Структура;
	ПараметрыВидовУчета.Вставить("УправленческийУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеУпр", "ДокументУпр", "ОписаниеОшибкиУпр"));
	ПараметрыВидовУчета.Вставить("РегламентированныйУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеРегл", "ДокументРегл", "ОписаниеОшибкиРегл"));
		
	ДокументыКПроведению = Новый Массив;
	Док = Неопределено;
	ПараметрыПередачи = Новый Структура;
	
	Для Каждого ВидУчета Из ПараметрыВидовУчета Цикл
		
		РеквизитыВидаУчета = ВидУчета.Значение;
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ])
			Или ПараметрыЗаполнения[ПараметрыВидовУчета[ВидУчета.Ключ].Состояние] = Перечисления.СостоянияРаспределенияРасходов.НеТребуется Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЕдиныйДокумент И Не Док = Неопределено Тогда
			
			Если ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]) 
				И Не ПараметрыЗаполнения[РеквизитыВидаУчета.Документ] = Док.Ссылка Тогда
						
				Попытка
					
					ПредыдущийДокумент = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ].ПолучитьОбъект();
					ПредыдущийДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					
				Исключение
					
					ЗаписьЖурналаРегистрации(Док.Метаданные().Имя, 
						УровеньЖурналаРегистрации.Ошибка,,,
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ШаблонОшибки = НСтр("ru = 'Не удалось обновить настройки распределения по виду учета ""%1"" по причине: %2.'");
					
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], 
						Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Документ"], 
						ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]);
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["ОписаниеОшибки"], 
						СтрШаблон(ШаблонОшибки, ВидУчета.Ключ, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
					
					Продолжить;
					
				КонецПопытки;
				
			КонецЕсли;
			
			Док[ВидУчета.Ключ] = Истина;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ])
			Или (Не Док = Неопределено 
				И Док.Ссылка = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]) Тогда
			Док = Документы.РаспределениеПрочихЗатрат.СоздатьДокумент();
		Иначе
			
			Док = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ].ПолучитьОбъект();
			Док.УправленческийУчет = Ложь;
			Док.РегламентированныйУчет = Ложь;
			
		КонецЕсли;
			
		Док[ВидУчета.Ключ] = Истина;
		
		Док.Заполнить(ПараметрыЗаполнения);
		
		ДокументыКПроведению.Добавить(Док);
				
	КонецЦикла;
	
	Для Каждого ДокументКПроведению Из ДокументыКПроведению Цикл
		
		Если ДокументКПроведению.ПроверитьЗаполнение() Тогда
			Попытка
				ДокументКПроведению.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ЗаписьЖурналаРегистрации(ДокументКПроведению.Метаданные().Имя, 
					УровеньЖурналаРегистрации.Ошибка,,,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		
		ТекстОшибки = "";
		ТекстыОшибок = Новый Массив;
		Ошибки = ПолучитьСообщенияПользователю(Истина);
		Для Индекс = 0 По Ошибки.Количество() - 1 Цикл
			ТекстыОшибок.Добавить(Ошибки[Индекс].Текст);
		КонецЦикла;
		
		ТекстОшибки = Символы.Таб + СтрСоединить(ТекстыОшибок, Символы.ПС + Символы.Таб);
			
		Для Каждого ВидУчета Из ПараметрыВидовУчета Цикл
			
			Если Не ДокументКПроведению[ВидУчета.Ключ] Тогда
				Продолжить;
			КонецЕсли;
			
			РеквизитыВидаУчета = ВидУчета.Значение;
			
			Если Не ПустаяСтрока(ТекстОшибки) Тогда
				
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], 
					Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент);
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["ОписаниеОшибки"], ТекстОшибки);
				
			Иначе
				
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Документ"], ДокументКПроведению.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		
	КонецЦикла;
	
	Возврат ПараметрыПередачи;
	
КонецФункции

// Возвращает текст запроса для получения данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаПоступилоПрочихРасходов() Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределению
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|		ИЛИ Реквизиты.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|		ИЛИ Реквизиты.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			И Реквизиты.КосвенныеЗатратыНУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Реквизиты.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Расходы.Организация 			КАК Организация,
		|	Расходы.Подразделение 			КАК Подразделение,
		|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов 			КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов 		КАК АналитикаРасходов,
		
		|	СУММА(Расходы.Сумма) 					КАК Сумма,
		|	СУММА(Расходы.СуммаБезНДС) 				КАК СуммаБезНДС,
		|	СУММА(Расходы.СуммаРегл) 				КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) 		КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) 		КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаУпр) 				КАК СуммаУпр,
		|	СУММА(Расходы.СуммаРеглРасход) 			КАК СуммаРеглРасход,
		|	СУММА(Расходы.ПостояннаяРазницаРасход) 	КАК ПостояннаяРазницаРасход,
		|	СУММА(Расходы.ВременнаяРазницаРасход) 	КАК ВременнаяРазницаРасход		
		|ПОМЕСТИТЬ ПредварительныеРасходыКРаспределению
		|ИЗ
		|	(ВЫБРАТЬ
		|		Расходы.Организация КАК Организация,
		|		Расходы.Подразделение КАК Подразделение,
		|		Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|		Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|		Расходы.СуммаОстаток КАК Сумма,
		|		Расходы.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|		Расходы.СуммаРеглОстаток КАК СуммаРегл,
		|		Расходы.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|		Расходы.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|		Расходы.СуммаУпрОстаток КАК СуммаУпр,
		|		0 КАК СуммаРеглРасход,
		|		0 КАК ПостояннаяРазницаРасход,
		|		0 КАК ВременнаяРазницаРасход
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы.Остатки(
		|				&ГраницаКонецПериода,
		|				(&ПоВсемОрганизациям
		|					ИЛИ Организация В (&МассивОрганизаций))
		|					И (&ПоВсемПодразделениям
		|						ИЛИ Подразделение В (&СписокПодразделений))
		|					И СтатьяРасходов В
		|						(ВЫБРАТЬ
		|							Т.Ссылка
		|						ИЗ
		|							СтатьиРасходовКРаспределению КАК Т)) КАК Расходы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Движения.Организация,
		|		Движения.Подразделение,
		|		Движения.НаправлениеДеятельности,
		|		Движения.СтатьяРасходов,
		|		Движения.АналитикаРасходов,
		|		Движения.Сумма,
		|		Движения.СуммаБезНДС,
		|		Движения.СуммаРегл,
		|		Движения.ПостояннаяРазница,
		|		Движения.ВременнаяРазница,
		|		Движения.СуммаУпр,
		|		Движения.СуммаРегл,
		|		Движения.ПостояннаяРазница,
		|		Движения.ВременнаяРазница
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы КАК Движения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК СтатьиРасходовКРаспределению
		|			ПО Движения.СтатьяРасходов = СтатьиРасходовКРаспределению.Ссылка
		|	ГДЕ
		|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Движения.Активность
		|		И (ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
		|			ТИП(Документ.РаспределениеПрочихЗатрат), 
		|			ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
		//++ НЕ УТ
		//++ Локализация
		|		ИЛИ ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
		|			И НЕ Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
		//-- Локализация
		//-- НЕ УТ
		|		)
		|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И (&ПоВсемОрганизациям
		|			ИЛИ Организация В (&МассивОрганизаций))
		|		И (&ПоВсемПодразделениям
		|			ИЛИ Подразделение В (&СписокПодразделений))
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		СебестоимостьТоваров.Организация,
		|		СебестоимостьТоваров.Подразделение,
		|		СебестоимостьТоваров.КорНаправлениеДеятельности,
		|		СебестоимостьТоваров.СтатьяРасходовСписания,
		|		СебестоимостьТоваров.АналитикаРасходов,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
		|			ПО (Статьи.Ссылка = СебестоимостьТоваров.СтатьяРасходовСписания)
		|	ГДЕ
		|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И СебестоимостьТоваров.Активность
		|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
		|			ТИП(Документ.ВнутреннееПотреблениеТоваров),
		//++ НЕ УТ
		|			ТИП(Документ.ВыпускПродукции),
		|			ТИП(Документ.РаспределениеПроизводственныхЗатрат),
		|			ТИП(Документ.ПроизводствоБезЗаказа),
		|			ТИП(Документ.ОтчетПереработчика),
		//-- НЕ УТ
		|			ТИП(Документ.ПрочееОприходованиеТоваров),
		|			ТИП(Документ.СписаниеНедостачТоваров))
		|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|		И (&ПоВсемОрганизациям
		|				ИЛИ СебестоимостьТоваров.Организация В (&МассивОрганизаций))
		|		И (&ПоВсемПодразделениям
		|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		СебестоимостьТоваров.КорОрганизация,
		|		СебестоимостьТоваров.Подразделение,
		|		СебестоимостьТоваров.КорНаправлениеДеятельности,
		|		СебестоимостьТоваров.СтатьяРасходовСписания,
		|		СебестоимостьТоваров.АналитикаРасходов,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
		|			ПО (Статьи.Ссылка = СебестоимостьТоваров.СтатьяРасходовСписания)
		|	ГДЕ
		|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И СебестоимостьТоваров.Активность
		|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
		|			ТИП(Документ.ВнутреннееПотреблениеТоваров),
		//++ НЕ УТ
		|			ТИП(Документ.ВыпускПродукции),
		|			ТИП(Документ.РаспределениеПроизводственныхЗатрат),
		|			ТИП(Документ.ПроизводствоБезЗаказа),
		|			ТИП(Документ.ОтчетПереработчика),
		//-- НЕ УТ
		|			ТИП(Документ.ПрочееОприходованиеТоваров),
		|			ТИП(Документ.СписаниеНедостачТоваров))
		|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|		И НЕ СебестоимостьТоваров.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		И НЕ СебестоимостьТоваров.КорОрганизация = СебестоимостьТоваров.Организация
		|		И (&ПоВсемОрганизациям
		|				ИЛИ СебестоимостьТоваров.КорОрганизация В (&МассивОрганизаций))
		|		И (&ПоВсемПодразделениям
		|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
		//++ НЕ УТ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Затраты.Организация,
		|		Затраты.Подразделение,
		|		ЕСТЬNULL(Затраты.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|		ВЫРАЗИТЬ(Затраты.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов),
		|		Затраты.АналитикаРасходов,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
		|			ПО (Статьи.Ссылка = Затраты.СтатьяРасходов)
		|	ГДЕ
		|		Затраты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Затраты.Активность
		|		И Затраты.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|		И (&ПоВсемОрганизациям
		|				ИЛИ Затраты.Организация В (&МассивОрганизаций))
		|		И (&ПоВсемПодразделениям
		|				ИЛИ Затраты.Подразделение В (&СписокПодразделений))
		//-- НЕ УТ
		|) КАК Расходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.Организация,
		|	Расходы.Подразделение,
		|	Расходы.НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов,
		|	Расходы.АналитикаРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасходыКРаспределению.*
		|ПОМЕСТИТЬ РасходыКРаспределению
		|ИЗ
		|	ПредварительныеРасходыКРаспределению КАК РасходыКРаспределению
		|;
		|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения данных для рабочего места по распределению расходов.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныеДляРаспределения(ВыбиратьПервые = 0, ПоместитьСостояниеРасходовВВТ = Ложь) Экспорт
	
	// Распределяемые документы.
	ТекстЗапроса = ТекстЗапросаПоступилоПрочихРасходов() +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.НаправлениеДеятельности,
		|	ДД.СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0))) ЕстьДвиженияНаФинансовыйРезультатУпр,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0)) ЕстьДвиженияНаПроизводствоУпр,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.СуммаРегл <> 0 ИЛИ ДД.ПостояннаяРазница <> 0 ИЛИ ДД.ВременнаяРазница <> 0))) ЕстьДвиженияНаФинансовыйРезультатРегл,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.СуммаРегл <> 0 ИЛИ ДД.ПостояннаяРазница <> 0 ИЛИ ДД.ВременнаяРазница <> 0)) ЕстьДвиженияНаПроизводствоРегл
		|ПОМЕСТИТЬ ДвиженияСтатейРасходов
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РаспределениеРасходов
		|		ПО ДД.Регистратор = РаспределениеРасходов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК РаспределениеДоходов
		|		ПО ДД.Регистратор = РаспределениеДоходов.Ссылка
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (&ПоВсемОрганизациям ИЛИ ДД.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям ИЛИ ДД.Подразделение В (&СписокПодразделений))
		|	И ДД.Активность
		|	И НЕ (РаспределениеРасходов.Ссылка ЕСТЬ NULL
		|		И РаспределениеДоходов.Ссылка ЕСТЬ NULL)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.НаправлениеДеятельности,
		|	ДД.СтатьяРасходов,
		|	ДД.АналитикаРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.НаправлениеДеятельности,
		|	ДД.СтатьяРасходов,
		|	ДД.АналитикаРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Реквизиты.Ссылка КАК Документ,
		|	Реквизиты.РегламентированныйУчет,
		|	Реквизиты.УправленческийУчет,
		//++ НЕ УТ
		|	Реквизиты.ОставитьВНЗП,
		|	Реквизиты.ДоляСтоимостиНаНЗП,
		|	Реквизиты.ВсегоДолейСтоимости,
		//-- НЕ УТ
		|	Реквизиты.НазначениеНастройкиРаспределения КАК НазначениеНастройкиРаспределения,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Подразделение КАК Подразделение,
		|	Реквизиты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Реквизиты.СтатьяРасходов КАК СтатьяРасходов,
		|	Реквизиты.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ ДокументыРаспределения
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (&ПоВсемОрганизациям ИЛИ Реквизиты.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям ИЛИ Реквизиты.Подразделение В (&СписокПодразделений))
		|	И Реквизиты.Проведен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|
		|Организация,
		|Подразделение,
		|НаправлениеДеятельности,
		|СтатьяРасходов,
		|АналитикаРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НеРаспределенные.Организация КАК Организация,
		|	НеРаспределенные.Подразделение КАК Подразделение,
		|	НеРаспределенные.СтатьяРасходов КАК СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|	НеРаспределенные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.СтатьяРасходов.ВариантРаспределенияРасходовУпр В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаУУ)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаОстаток) = 0
		|					И СУММА(НеРаспределенные.СуммаБезНДСОстаток) = 0
		|					И СУММА(НеРаспределенные.СуммаУпрОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр
		|			И СУММА(НеРаспределенные.СуммаБезНДСОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр
		|			И СУММА(НеРаспределенные.СуммаУпрОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр)
		|	КОНЕЦ КАК РаспределеноВУУ,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаБУ)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаРеглОстаток) = 0
		|					И СУММА(НеРаспределенные.ВременнаяРазницаОстаток) = 0
		|					И СУММА(НеРаспределенные.ПостояннаяРазницаОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаРеглОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл
		|			И СУММА(НеРаспределенные.ВременнаяРазницаОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл
		|			И СУММА(НеРаспределенные.ПостояннаяРазницаОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл)
		|	КОНЕЦ КАК РаспределеноВБУ,
		|	(СУММА(НеРаспределенные.СуммаНУОстаток) = 0) КАК РаспределеноВНУ
		|ПОМЕСТИТЬ РасходыПослеРаспределения
		|ИЗ
		|	(ВЫБРАТЬ
		|		НеРаспределенные.Организация КАК Организация,
		|		НеРаспределенные.Подразделение КАК Подразделение,
		|		НеРаспределенные.СтатьяРасходов КАК СтатьяРасходов,
		|		НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|		НеРаспределенные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		НеРаспределенные.СуммаОстаток КАК СуммаОстаток,
		|		НеРаспределенные.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
		|		НеРаспределенные.СуммаУпрОстаток КАК СуммаУпрОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		//++ НЕ УТ
		|			КОГДА НеРаспределенные.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|				И НеРаспределенные.СтатьяРасходов.КосвенныеЗатратыНУ
		|				И НЕ ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
		|				ТОГДА НеРаспределенные.СуммаРеглОстаток - НеРаспределенные.ПостояннаяРазницаОстаток - НеРаспределенные.ВременнаяРазницаОстаток
		//-- НЕ УТ
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СуммаНУОстаток,
		|		НеРаспределенные.СуммаРеглОстаток КАК СуммаРеглОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.ВременнаяРазницаОстаток
		|		КОНЕЦ КАК ВременнаяРазницаОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.ПостояннаяРазницаОстаток
		|		КОНЕЦ КАК ПостояннаяРазницаОстаток,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаУУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаБУ
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы.Остатки(
		|				&ГраницаКонецПериода,
		|				(Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов) В 
		|					(ВЫБРАТЬ 
		|						Т.Организация,
		|						Т.Подразделение,
		|						Т.НаправлениеДеятельности,
		|						Т.СтатьяРасходов,
		|						Т.АналитикаРасходов 
		|					ИЗ РасходыКРаспределению КАК Т)) КАК НеРаспределенные
		//++ НЕ УТ
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|			ПО НеРаспределенные.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|				И НеРаспределенные.Организация = ДолиСписания.Организация
		//++ Локализация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РегламентныеДвижения.Организация КАК Организация,
		|		РегламентныеДвижения.Подразделение КАК Подразделение,
		|		РегламентныеДвижения.СтатьяРасходов КАК СтатьяРасходов,
		|		РегламентныеДвижения.АналитикаРасходов КАК АналитикаРасходов,
		|		РегламентныеДвижения.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		0 КАК СуммаОстаток,
		|		0 КАК СуммаБезНДСОстаток,
		|		0 КАК СуммаУпрОстаток,
		|		0 КАК СуммаНУОстаток,
		|		0 КАК СуммаРеглОстаток,
		|		0 КАК ВременнаяРазницаОстаток,
		|		РегламентныеДвижения.ПостояннаяРазница КАК ПостояннаяРазницаОстаток,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаУУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаБУ
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы КАК РегламентныеДвижения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасходыКРаспределению КАК Расходы
		|			ПО РегламентныеДвижения.Организация = Расходы.Организация
		|				И РегламентныеДвижения.Подразделение = Расходы.Подразделение
		|				И РегламентныеДвижения.НаправлениеДеятельности = Расходы.НаправлениеДеятельности
		|				И РегламентныеДвижения.СтатьяРасходов = Расходы.СтатьяРасходов
		|				И РегламентныеДвижения.АналитикаРасходов = Расходы.АналитикаРасходов
		|	ГДЕ
		|		РегламентныеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И РегламентныеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ТИПЗНАЧЕНИЯ(РегламентныеДвижения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
		|		И НЕ РегламентныеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
		//-- Локализация
		|		
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Расходы.Организация,
		|		Расходы.Подразделение,
		|		Расходы.СтатьяРасходов,
		|		Расходы.АналитикаРасходов,
		|		Расходы.НаправлениеДеятельности,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.Сумма * ДокументыРаспределенияУУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияУУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.СуммаБезНДС * ДокументыРаспределенияУУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияУУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.СуммаУпр * ДокументыРаспределенияУУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияУУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.СуммаРегл * ДокументыРаспределенияБУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияБУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL
		|				ТОГДА (Расходы.ВременнаяРазница
		|					+ ВЫБОР
		|						КОГДА ДокументыРаспределенияБУ.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|							И Расходы.СтатьяРасходов.КосвенныеЗатратыНУ
		|							ТОГДА Расходы.СуммаРегл - Расходы.ПостояннаяРазница - Расходы.ВременнаяРазница
		|						ИНАЧЕ
		|							0
		|					КОНЕЦ) * ДокументыРаспределенияБУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияБУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ * -1,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.ПостояннаяРазница * ДокументыРаспределенияБУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияБУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		(НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL),
		|		(НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL)
		|	ИЗ
		|		РасходыКРаспределению КАК Расходы
		|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределенияУУ
		|			ПО Расходы.Организация = ДокументыРаспределенияУУ.Организация
		|				И Расходы.Подразделение = ДокументыРаспределенияУУ.Подразделение
		|				И Расходы.НаправлениеДеятельности = ДокументыРаспределенияУУ.НаправлениеДеятельности
		|				И Расходы.СтатьяРасходов = ДокументыРаспределенияУУ.СтатьяРасходов
		|				И Расходы.АналитикаРасходов = ДокументыРаспределенияУУ.АналитикаРасходов
		|				И ДокументыРаспределенияУУ.УправленческийУчет
		|				И (ДокументыРаспределенияУУ.ОставитьВНЗП)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределенияБУ
		|			ПО Расходы.Организация = ДокументыРаспределенияБУ.Организация
		|				И Расходы.Подразделение = ДокументыРаспределенияБУ.Подразделение
		|				И Расходы.НаправлениеДеятельности = ДокументыРаспределенияБУ.НаправлениеДеятельности
		|				И Расходы.СтатьяРасходов = ДокументыРаспределенияБУ.СтатьяРасходов
		|				И Расходы.АналитикаРасходов = ДокументыРаспределенияБУ.АналитикаРасходов
		|				И ДокументыРаспределенияБУ.РегламентированныйУчет
		|				И (ДокументыРаспределенияБУ.ОставитьВНЗП)
		|	ГДЕ
		|		НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|		ИЛИ НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Расходы.Организация,
		|		Расходы.Подразделение,
		|		Расходы.СтатьяРасходов,
		|		Расходы.АналитикаРасходов,
		|		Расходы.НаправлениеДеятельности,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА НЕ ДолиСписания.Транспортные
		|				ТОГДА 0
		|			КОГДА ДолиСписания.ДоляСписания = 0
		|				ТОГДА Расходы.СуммаРегл
		|			КОГДА Расходы.СуммаРеглРасход = 0
		|				ТОГДА 0
		|			ИНАЧЕ
		|				Расходы.СуммаРеглРасход / ДолиСписания.ДоляСписания
		|					+ (Расходы.СуммаРегл * ДолиСписания.ДоляСписания - Расходы.СуммаРеглРасход) / ДолиСписания.ДоляСписания - Расходы.СуммаРеглРасход
		|		КОНЕЦ * (-1) КАК СуммаРегл,
		|		ВЫБОР
		|			КОГДА ДолиСписания.Нормируемые
		|				ТОГДА Расходы.ВременнаяРазница - Расходы.ВременнаяРазницаРасход
		|			КОГДА НЕ ДолиСписания.Транспортные
		|				ТОГДА 0
		|			КОГДА ДолиСписания.ДоляСписания = 0
		|				ТОГДА Расходы.ВременнаяРазница
		|			КОГДА Расходы.ВременнаяРазницаРасход = 0
		|				ТОГДА 0
		|			ИНАЧЕ Расходы.ВременнаяРазницаРасход / ДолиСписания.ДоляСписания
		|					+ (Расходы.ВременнаяРазница * ДолиСписания.ДоляСписания - Расходы.ВременнаяРазницаРасход) / ДолиСписания.ДоляСписания - Расходы.ВременнаяРазницаРасход
		|		КОНЕЦ * (-1) КАК ВременнаяРазница,
		|		ВЫБОР
		|			КОГДА НЕ (ДолиСписания.Транспортные
		|					ИЛИ ДолиСписания.Нормируемые)
		|				ИЛИ Расходы.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|				ТОГДА 0
		|			КОГДА ДолиСписания.Нормируемые
		|				ТОГДА Расходы.ПостояннаяРазница - (Расходы.СуммаРегл - Расходы.ВременнаяРазница 
		|					- ВЫРАЗИТЬ((Расходы.СуммаРегл - Расходы.ПостояннаяРазница - Расходы.ВременнаяРазница) * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)))
		|			КОГДА ДолиСписания.ДоляСписания = 0
		|				ТОГДА Расходы.ПостояннаяРазница
		|			КОГДА ДолиСписания.Транспортные
		|				И НЕ Расходы.ПостояннаяРазницаРасход = 0
		|				ТОГДА Расходы.ПостояннаяРазницаРасход / ДолиСписания.ДоляСписания
		|					+ (Расходы.ПостояннаяРазница * ДолиСписания.ДоляСписания - Расходы.ПостояннаяРазницаРасход) / ДолиСписания.ДоляСписания - Расходы.ПостояннаяРазницаРасход
		|			ИНАЧЕ 0
		|		КОНЕЦ * -1 КАК ПостояннаяРазница,
		|		ЛОЖЬ,
		|		ВЫБОР
		|			КОГДА ВЫРАЗИТЬ(Расходы.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)) = 0
		|				И ВЫРАЗИТЬ(Расходы.ВременнаяРазница * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)) = 0
		|				И ВЫРАЗИТЬ(Расходы.ПостояннаяРазница * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)) = 0
		|			ТОГДА ИСТИНА
		|			ИНАЧЕ 
		|				ЕСТЬNULL(ВозможныеДвижения.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|		КОНЕЦ
		|	ИЗ
		|		РасходыКРаспределению КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО Расходы.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И Расходы.Организация = ДолиСписания.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСтатейРасходов КАК ВозможныеДвижения
		|		ПО Расходы.Организация = ВозможныеДвижения.Организация
		|			И Расходы.Подразделение = ВозможныеДвижения.Подразделение
		|			И Расходы.НаправлениеДеятельности = ВозможныеДвижения.НаправлениеДеятельности
		|			И Расходы.СтатьяРасходов = ВозможныеДвижения.СтатьяРасходов
		|			И Расходы.АналитикаРасходов = ВозможныеДвижения.АналитикаРасходов
		|	ГДЕ
		|		НЕ &ИсключитьИзКонтроляРазницы
		//-- НЕ УТ
		|) КАК НеРаспределенные
		|
		|СГРУППИРОВАТЬ ПО
		|	НеРаспределенные.Организация,
		|	НеРаспределенные.Подразделение,
		|	НеРаспределенные.НаправлениеДеятельности,
		|	НеРаспределенные.СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Задания.Организация,
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(Задания.Месяц, МЕСЯЦ)) КАК Месяц
		|ПОМЕСТИТЬ Задания
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСебестоимости КАК Задания
		|ГДЕ
		|	&ПоВсемОрганизациям ИЛИ Задания.Организация В (&МассивОрганизаций)
		|СГРУППИРОВАТЬ ПО
		|	Задания.Организация
		|;
		|
		|ВЫБРАТЬ
		|	Источник.Организация КАК Организация,
		|	Источник.Подразделение КАК Подразделение,
		|	Источник.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Источник.СтатьяРасходов КАК СтатьяРасходов,
		|	Источник.АналитикаРасходов КАК АналитикаРасходов,
		|	ЕСТЬNULL(ДокументыУпр.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументУпр,
		|	ЕСТЬNULL(ДокументыРегл.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументРегл,
		|	Источник.Сумма КАК Сумма,
		|	Источник.СуммаБезНДС КАК СуммаБезНДС,
		|	Источник.СуммаРегл КАК СуммаРегл,
		|	Источник.СуммаУпр КАК СуммаУпр,
		|	Источник.ПостояннаяРазница КАК ПостояннаяРазница,
		|	Источник.ВременнаяРазница КАК ВременнаяРазница,
		|	ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА) КАК РаспределеноВУУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) КАК РаспределеноВБУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА) КАК РаспределеноВНУ,
		|	ВЫБОР
		// отличные варианты распределения от перечисленных, рабочее место не обслуживает.
		|		КОГДА Не Источник.СтатьяРасходов.ВариантРаспределенияРасходовУпр В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		// если месяц закрыт, расходов и движений нет
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) И НЕ ДокументыУпр.Документ ЕСТЬ NULL И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И НЕ (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		// если месяц закрыт и расходов нет, то состояние Распределено 
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) И ДокументыУпр.Документ ЕСТЬ NULL И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		// Если не настроено распределение долей, и есть остаток к распределению, то требуется настройка.
		|		КОГДА ДокументыУпр.Документ ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		// Если распределение долей есть, документ распределения сделал движения на производство, но есть остаток к распределеню, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		// Если распределение долей есть, документ распределения сделал движения на фин. рез., но есть остаток к распределеню, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		// Если распределение долей есть, документ распределения сделал движения и статья полностью распределена то расходы распределены.
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеУУ,
		|	ВЫБОР
		|		КОГДА Не Источник.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) И НЕ ДокументыРегл.Документ ЕСТЬ NULL
		|			И ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) И ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И НЕ (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ((ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) И ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА))
		//++ НЕ УТ
		|				ИЛИ Источник.СтатьяРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
		|						И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|							ИЛИ ДолиСписания.ДоляСписания = 0)
		//-- НЕ УТ
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		КОГДА ДокументыРегл.Документ ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И Источник.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			И Источник.СтатьяРасходов.КосвенныеЗатратыНУ
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) И ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеБУ
		|ПОМЕСТИТЬ ВТДанныеДляРаспределения
		|ИЗ
		|	РасходыКРаспределению КАК Источник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задания КАК Задания
		|		ПО Источник.Организация = Задания.Организация
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасходыПослеРаспределения КАК Остатки
		|		ПО	Источник.СтатьяРасходов 			= Остатки.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= Остатки.АналитикаРасходов
		|			И Источник.Подразделение			= Остатки.Подразделение
		|			И Источник.Организация				= Остатки.Организация
		|			И Источник.НаправлениеДеятельности	= Остатки.НаправлениеДеятельности
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыУпр
		|		ПО	Источник.СтатьяРасходов				= ДокументыУпр.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыУпр.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыУпр.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыУпр.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыУпр.Организация
		|			И ДокументыУпр.УправленческийУчет
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРегл
		|		ПО	Источник.СтатьяРасходов				= ДокументыРегл.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыРегл.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыРегл.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыРегл.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыРегл.Организация
		|			И ДокументыРегл.РегламентированныйУчет
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСтатейРасходов КАК ДвиженияСтатейРасходов
		|		ПО	Источник.СтатьяРасходов				= ДвиженияСтатейРасходов.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДвиженияСтатейРасходов.АналитикаРасходов
		|			И Источник.Подразделение			= ДвиженияСтатейРасходов.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДвиженияСтатейРасходов.НаправлениеДеятельности
		|			И Источник.Организация				= ДвиженияСтатейРасходов.Организация
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО Источник.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И Источник.Организация = ДолиСписания.Организация
		|			И ДолиСписания.Транспортные
		//-- НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ &ВыбиратьПервые,
		|	ВТДанныеДляЗагрузки.Организация КАК Организация,
		|	ВТДанныеДляЗагрузки.Подразделение КАК Подразделение,
		|	ВТДанныеДляЗагрузки.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВТДанныеДляЗагрузки.СтатьяРасходов КАК СтатьяРасходов,
		|	СтатьиРасходов.Родитель КАК ГруппаСтатей,
		|	ВТДанныеДляЗагрузки.АналитикаРасходов КАК АналитикаРасходов,
		|	ВТДанныеДляЗагрузки.ДокументУпр КАК ДокументУпр,
		|	ВТДанныеДляЗагрузки.ДокументРегл КАК ДокументРегл,
		|	ВТДанныеДляЗагрузки.СостояниеУУ КАК СостояниеУпр,
		|	ВТДанныеДляЗагрузки.СостояниеБУ КАК СостояниеРегл
		|ПОМЕСТИТЬ СостояниеРаспределенияРасходов
		|ИЗ
		|	ВТДанныеДляРаспределения КАК ВТДанныеДляЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО ВТДанныеДляЗагрузки.СтатьяРасходов = СтатьиРасходов.Ссылка
		|ГДЕ
		|	ВЫБОР
		|		КОГДА &ФильтрПоСостоянию = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			(СтатьиРасходов.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеУУ = &ФильтрПоСостоянию)
		|			ИЛИ
		|			(СтатьиРасходов.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеБУ = &ФильтрПоСостоянию)
		|	КОНЕЦ
		|;
		|";
	
	Если ВыбиратьПервые > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыбиратьПервые,", "ПЕРВЫЕ " + Строка(ВыбиратьПервые));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыбиратьПервые,", "");
	КонецЕсли;
	
	Если Не ПоместитьСостояниеРасходовВВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ СостояниеРаспределенияРасходов", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Устанавливает необходимые параметры запроса ТекстЗапросаДанныеДляРаспределения().
// Параметры:
//	Запрос - Запрос - запрос с текстом см. ТекстЗапросаДанныеДляРаспределения().
//	ПараметрыЗапроса - Структура - см. ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения().
//
Процедура ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса) Экспорт
	
	ПараметрыЗакрытия = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров();
	
	ПоддерживаемыеВариантыРаспределения = Новый Массив;
	Если ПараметрыЗапроса.ВариантРаспределения = Неопределено Тогда
		
		//++ НЕ УТ
		ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
		//-- НЕ УТ
		ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
		
	Иначе
		ПоддерживаемыеВариантыРаспределения.Добавить(ПараметрыЗапроса.ВариантРаспределения);
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("НачалоПериода")
		Или Не ЗначениеЗаполнено(Запрос.Параметры.НачалоПериода) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗапроса.НачалоПериода) Тогда
			Запрос.УстановитьПараметр("НачалоПериода", ПараметрыЗапроса.НачалоПериода);
		Иначе
			Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПараметрыЗапроса.Период));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("КонецПериода") 
		Или Не ЗначениеЗаполнено(Запрос.Параметры.КонецПериода) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
			Запрос.УстановитьПараметр("КонецПериода", ПараметрыЗапроса.КонецПериода);
		Иначе
			Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПараметрыЗапроса.Период));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("МассивОрганизаций") 
		Или Запрос.Параметры.МассивОрганизаций.Количество() = 0	Тогда
		
		Если ПараметрыЗапроса.Организации.Количество() = 0 Тогда
			ЗапросОрганизаций = Новый Запрос;
			ЗапросОрганизаций.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Организации.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Организации КАК Организации
				|ГДЕ
				|	Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)";
			
			Запрос.УстановитьПараметр("МассивОрганизаций", 
				ЗапросОрганизаций.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
				
		Иначе
			Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрыЗапроса.Организации);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ГраницаКонецПериода",  
		Новый Граница(Запрос.Параметры.КонецПериода, ВидГраницы.Включая));		
	Запрос.УстановитьПараметр("СписокПодразделений",  ПараметрыЗапроса.Подразделения);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Запрос.Параметры.СписокПодразделений.Количество() = 0);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям",   Запрос.Параметры.МассивОрганизаций.Количество() = 0);
	Запрос.УстановитьПараметр("ФильтрПоСостоянию",    ПараметрыЗапроса.Состояние);
	Запрос.УстановитьПараметр("ИсключитьИзКонтроляРазницы", ПараметрыЗапроса.ИсключитьИзКонтроляРазницы);
	Запрос.УстановитьПараметр("ЗначениеПогрешностиУпр",  ПараметрыЗакрытия.ЗначениеПогрешностиРасходыУпр);
	Запрос.УстановитьПараметр("ЗначениеПогрешностиРегл",  ПараметрыЗакрытия.ЗначениеПогрешностиРасходыРегл);
	Запрос.УстановитьПараметр("ВариантыРаспределенияРасходов", ПоддерживаемыеВариантыРаспределения);
	Запрос.УстановитьПараметр("УчетПоНаправлениямДеятельности", ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат"));
	//++ НЕ УТ
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	ПодготовитьСлужебныеТаблицы(Запрос.Параметры.КонецПериода, 
		Запрос.Параметры.МассивОрганизаций, Запрос, ПараметрыЗапроса.ИсключитьТранспортныеРасходы);
	//-- НЕ УТ	
	
КонецПроцедуры

// Возвращает структуру параметров запроса ТекстЗапросаДанныеДляРаспределения() необходимых для выполнения.
//
Функция ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения() Экспорт
	
	ОписаниеПараметров = Новый Структура;
	ОписаниеПараметров.Вставить("Период", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("НачалоПериода", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("КонецПериода", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("Организации", Новый Массив); // Тип элементов СправочникСсылка.Организации.
	ОписаниеПараметров.Вставить("Подразделения", Новый Массив); // Тип элементов СправочникСсылка.СтруктураПредприятия.
	ОписаниеПараметров.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ПустаяСсылка());
	ОписаниеПараметров.Вставить("ИсключитьИзКонтроляРазницы", Ложь);
	ОписаниеПараметров.Вставить("ИсключитьТранспортныеРасходы", Ложь);
	// Можно переопределить на Массив с элементами Перечисления.ВариантыРаспределенияРасходов.
	ОписаниеПараметров.Вставить("ВариантРаспределения", Неопределено);
	
	Возврат ОписаниеПараметров;
	
КонецФункции

// Возвращает настройки распределения для набора параметров.
// Параметры:
//	ПараметрыРасходов - Массив/Таблица значений - содержит структуру со свойствами или таблицу с колонками:
//		* Дата - Дата - дата документа
//		* ИДСтроки - Число - идентификатор строки в ТЧ
//		* Документ - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ
//		* СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья расходов
//		* АналитикаРасходов - Характеристика.СтатьиРасходов - аналитика расходов статьи
//		* Организация - СправочникСсылка.Организации - организация, в которой возник расход
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, в котором возник расход
//		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности.
//
// Возвращаемое значение:
//	Массив - Массив параметров распределения.
//
Функция ПолучитьНастройкиРаспределенияСтатейРасходов(ПараметрыРасходов) Экспорт

	Если ТипЗнч(ПараметрыРасходов) = Тип("Массив") Тогда
		ТаблицаДанных = КонвертироватьДанные(ПараметрыРасходов);
	Иначе
		ТаблицаДанных = ПараметрыРасходов;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл
		|ПОМЕСТИТЬ ДанныеПоРасходам
		|ИЗ
		|	&ДанныеПоРасходам КАК ДанныеПоРасходам
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл,
		|	РеквизитыСтатьи.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРегл,
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияУпр,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		//++ НЕ УТ
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделениюРегл.Настройка, 
		|		ЕСТЬNULL(НастройкиПоОрганизацииРегл.Настройка, 
		|			ЕСТЬNULL(НастройкиПоОрганизацииИПодразделениюОбщие.Настройка,
		|				ЕСТЬNULL(НастройкиПоОрганизацииОбщие.Настройка,
		//-- НЕ УТ
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл
		//++ НЕ УТ
		|	))))
		//-- НЕ УТ
		|		КАК ПравилоРаспределенияРегл,
		//++ НЕ УТ
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделениюУпр.Настройка, 
		|		ЕСТЬNULL(НастройкиПоОрганизацииУпр.Настройка,
		|			ЕСТЬNULL(НастройкиПоОрганизацииИПодразделениюОбщие.Настройка,
		|				ЕСТЬNULL(НастройкиПоОрганизацииОбщие.Настройка,
		//-- НЕ УТ
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр
		//++ НЕ УТ
		|	)))) 
		//-- НЕ УТ
		|		КАК ПравилоРаспределенияУпр		
		|ИЗ
		|	ДанныеПоРасходам КАК ДанныеПоРасходам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК РеквизитыСтатьи
		|		ПО ДанныеПоРасходам.СтатьяРасходов = РеквизитыСтатьи.Ссылка
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределения КАК НастройкиПоОрганизацииРегл
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииРегл.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииРегл.Организация
		| 			И (НастройкиПоОрганизацииРегл.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|			И НастройкиПоОрганизацииРегл.РазрезУчета = ЗНАЧЕНИЕ(Перечисление.РазрезыУчета.Регламентированный)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределения КАК НастройкиПоОрганизацииИПодразделениюРегл
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииИПодразделениюРегл.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииИПодразделениюРегл.Организация
		|			И ДанныеПоРасходам.Подразделение = НастройкиПоОрганизацииИПодразделениюРегл.Подразделение
		|			И НастройкиПоОрганизацииИПодразделениюРегл.РазрезУчета = ЗНАЧЕНИЕ(Перечисление.РазрезыУчета.Регламентированный)
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределения КАК НастройкиПоОрганизацииУпр
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииУпр.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииУпр.Организация
		| 			И (НастройкиПоОрганизацииУпр.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|			И НастройкиПоОрганизацииУпр.РазрезУчета = ЗНАЧЕНИЕ(Перечисление.РазрезыУчета.Управленческий)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределения КАК НастройкиПоОрганизацииИПодразделениюУпр
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииИПодразделениюУпр.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииИПодразделениюУпр.Организация
		|			И ДанныеПоРасходам.Подразделение = НастройкиПоОрганизацииИПодразделениюУпр.Подразделение
		|			И НастройкиПоОрганизацииИПодразделениюУпр.РазрезУчета = ЗНАЧЕНИЕ(Перечисление.РазрезыУчета.Управленческий)
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределения КАК НастройкиПоОрганизацииОбщие
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииОбщие.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииОбщие.Организация
		| 			И (НастройкиПоОрганизацииОбщие.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|			И НастройкиПоОрганизацииОбщие.РазрезУчета = ЗНАЧЕНИЕ(Перечисление.РазрезыУчета.ПустаяСсылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределения КАК НастройкиПоОрганизацииИПодразделениюОбщие
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииИПодразделениюОбщие.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииИПодразделениюОбщие.Организация
		|			И ДанныеПоРасходам.Подразделение = НастройкиПоОрганизацииИПодразделениюОбщие.Подразделение
		|			И НастройкиПоОрганизацииИПодразделениюОбщие.РазрезУчета = ЗНАЧЕНИЕ(Перечисление.РазрезыУчета.ПустаяСсылка)		
		//-- НЕ УТ
		|";
	
	Запрос.УстановитьПараметр("ДанныеПоРасходам", ТаблицаДанных);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МассивНастроек = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		ФорматДанныхРаспределенияСтатьиРасходов = ФорматДанныхРаспределенияСтатьиРасходов();
		ЗаполнитьЗначенияСвойств(ФорматДанныхРаспределенияСтатьиРасходов, Выборка);
		
		МассивНастроек.Добавить(ФорматДанныхРаспределенияСтатьиРасходов);
		
	КонецЦикла;
		
	Возврат МассивНастроек;
	
КонецФункции

// Формирует документы распределения расходов по переданным параметрам
// и помещает результат создания документов во временное хранилище.
// Параметры:
//	Параметры - Структура - см. Документ.РаспределениеПрочихЗатрат.ФормаРабочееМесто.ПолучитьПараметрыРасходов().
//	АдресХранилища - Строка - адрес во временном хранилище, куда следует поместить результат создания документов.
//
Функция СформироватьДокументы(Параметры, АдресХранилища) Экспорт
	
	Перем ПараметрыРасходов;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.СформироватьДокументы");
	
	Параметры.Свойство("ПараметрыРасходов", ПараметрыРасходов);
	
	РезультатыФормированияДокументов = Новый Соответствие();
	
	ШаблонСообщения = НСтр("ru = 'Не удалось создать документ распределения по %1 учету для статьи %2 с аналитикой %3 по причине: %4 %5.'");
	
	НастройкиРаспределенияСтатьиРасходов = 
		ПолучитьНастройкиРаспределенияСтатейРасходов(ПараметрыРасходов);

	Для Каждого НастройкаРаспределения Из НастройкиРаспределенияСтатьиРасходов Цикл
		
		ДетализацияРезультатовФормирования = РезультатыФормированияДокументов.Получить(НастройкаРаспределения.Организация);
		Если ДетализацияРезультатовФормирования = Неопределено Тогда
			
			ДетализацияРезультатов = Новый Структура;
			ДетализацияРезультатов.Вставить("СформированоДокументов", 0);
			ДетализацияРезультатов.Вставить("ОбновленныеДанные", Новый Массив);
			ДетализацияРезультатов.Вставить("ТекстыОшибок", Новый Массив);
			
			РезультатыФормированияДокументов.Вставить(НастройкаРаспределения.Организация,
				ДетализацияРезультатов);
				
		КонецЕсли;
		
		Результат = СформироватьДокумент(НастройкаРаспределения);
		Если Результат.Свойство("ОписаниеОшибкиРегл") Тогда
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				НСтр("ru = 'регламентированному'"),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиРегл);

			ДетализацияРезультатов.ТекстыОшибок.Добавить(ТекстСообщения);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		Если Результат.Свойство("ОписаниеОшибкиУпр") Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				НСтр("ru = 'управленческому'"),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиУпр);

			ДетализацияРезультатов.ТекстыОшибок.Добавить(ТекстСообщения);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		НовыеЗначения = Новый Структура("СтатьяРасходов, АналитикаРасходов, Организация, Подразделение, НаправлениеДеятельности, ИдСтроки");
		ЗаполнитьЗначенияСвойств(НовыеЗначения, НастройкаРаспределения);
		
		Для Каждого КлючИЗначение Из Результат Цикл
			НовыеЗначения.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		
		ДетализацияРезультатов.ОбновленныеДанные.Добавить(НовыеЗначения);
		
	КонецЦикла;
		
	Если Не АдресХранилища = Неопределено Тогда
		ПоместитьВоВременноеХранилище(РезультатыФормированияДокументов, АдресХранилища);
	КонецЕсли;
	
	СформированоДокументов = 0;
	Для Каждого РезультатПоОрганизации Из РезультатыФормированияДокументов Цикл
		СформированоДокументов = СформированоДокументов + РезультатПоОрганизации.Значение.СформированоДокументов;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, СформированоДокументов);
	
	Возврат РезультатыФормированияДокументов;
	
КонецФункции

//++ НЕ УТ

// Возвращает список подразделений по направлению распределения.
// Параметры:
//	Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение от которого требуется получить подразделения по направлению распределения.
//	НаправлениеРаспределения - ПеречислениеСсылка.НаправлениеРаспределенияПоПодразделениям - направление распределения.
// Возвращаемое значение:
//	Массив - подразделения по направлению.
//
Функция ПодразделенияПоНаправлению(Подразделение, НаправлениеРаспределения) Экспорт
	
	Если Не ЗначениеЗаполнено(НаправлениеРаспределения) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Если НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Текущее Тогда
		
		МассивПодразделений = Новый Массив;
		МассивПодразделений.Добавить(Подразделение);
		
		Возврат МассивПодразделений;
		
	КонецЕсли;
		
	Если НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Вышестоящее Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Родитель КАК Подразделение
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|ГДЕ
			|	СтруктураПредприятия.Ссылка = &Подразделение";
		
		
	ИначеЕсли НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Нижестоящие Тогда
	
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Ссылка КАК Подразделение
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|ГДЕ
			|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ(&Подразделение)
			|	И СтруктураПредприятия.Ссылка <> &Подразделение";
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	
КонецФункции
//-- НЕ УТ
#Область СтандартныеПодсистемы

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Распределение расходов на себестоимость продукции".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеПрочихЗатрат);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство,ИспользоватьУчетПрочихДоходовРасходов";
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	//++ НЕ УТ
	Отчеты.БазаРаспределенияПроизводственныхРасходов.ДобавитьКомандуОтчета(КомандыОтчетов);
	
	Если РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии21(ТекущаяДатаСеанса()) Тогда
		
		Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДанныеДляБазыРаспределенияРасходов) Тогда
			
			КомандаОтчет = КомандыОтчетов.Добавить();
			
			КомандаОтчет.Идентификатор		= Метаданные.Отчеты.ДанныеДляБазыРаспределенияРасходов.ПолноеИмя();
			КомандаОтчет.Менеджер			= "Отчет.ДанныеДляБазыРаспределенияРасходов";
			КомандаОтчет.Представление		= НСтр("ru = 'Данные базы распределения'");
			
			КомандаОтчет.МножественныйВыбор = Ложь;
			КомандаОтчет.Важность			= "Обычное";
			
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТ
	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область РегламентированныйУчет

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеПрочихЗатратЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - Текст запроса временной таблицы.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеПрочихЗатратЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти
//-- НЕ УТ
#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если Не ВидФормы = "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	НаФР = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат");
	//++ НЕ УТ
	НаПартии = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства");
	//-- НЕ УТ
	
	ФормыПоНазначению = Новый Соответствие;
	ФормыПоНазначению.Вставить(НаФР, "ФормаНастроекНаФР");
	//++ НЕ УТ
	ФормыПоНазначению.Вставить(НаПартии, "ФормаНастроекНаПартии");
	//-- НЕ УТ
	
	Если Параметры.Свойство("Ключ") Тогда
		
		ВыбраннаяФорма = ФормыПоНазначению.Получить(
			ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(
				Параметры.Ключ, 
				"НазначениеНастройкиРаспределения"));
				
	ИначеЕсли Параметры.Свойство("Основание") Тогда
		
		НазначенияПоВарианту = Новый Соответствие;
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности"),
			НаФР);
		//++ НЕ УТ
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты"),
			НаПартии);
		//-- НЕ УТ
			
		Если Параметры.Основание.УправленческийУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияУпр));
		КонецЕсли;
		
		Если Параметры.Основание.РегламентированныйУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияРегл));
		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения")
		И Параметры.ЗначенияЗаполнения.Свойство("НазначениеНастройкиРаспределения") Тогда
		ВыбраннаяФорма = ФормыПоНазначению.Получить(Параметры.ЗначенияЗаполнения.НазначениеНастройкиРаспределения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбраннаяФорма) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)

	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.РаспределениеПрочихЗатрат КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.РаспределениеПрочихЗатрат")));
								
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка						КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО				КАК РазделительЗаписи,
	|	&Дата						КАК ДатаДокументаИБ,
	|	&Номер						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
	|	&НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	&Подразделение				КАК Подразделение,
	|	&Ответственный				КАК Ответственный,
	|	&Комментарий				КАК Комментарий,
	|	0							КАК Сумма,
	|	&Проведен					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	""""						КАК Дополнительно,
	|	&Дата						КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать				КАК НомерПервичногоДокумента,
	|	&Подразделение				КАК МестоХранения,
	|	&Дата						КАК ДатаОтраженияВУчете
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.РаспределениеПрочихЗатрат";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеПроизводства
//++ НЕ УТ
Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.НачалоПериода = Дата(1, 1, 1);
	ПараметрыЗапроса.КонецПериода = КонецМесяца(ТекущаяДатаСеанса());
	ПараметрыЗапроса.Организации = ОбщегоНазначенияУТКлиентСервер.Массив(Параметры.Организация);
	ПараметрыЗапроса.Подразделения = ОбщегоНазначенияУТКлиентСервер.Массив(Параметры.Подразделение);
	ПараметрыЗапроса.Состояние = Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент;
	ПараметрыЗапроса.ВариантРаспределения = 
		ОбщегоНазначенияУТКлиентСервер.Массив(Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
	ПараметрыЗапроса.ИсключитьТранспортныеРасходы = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения(1);	
	ТекстГиперссылки = НСтр("ru = 'Распределение расходов'");
	
	УстановитьПривилегированныйРежим(Истина);
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,ЦветаСтиля.НезаполненноеПолеТаблицы,,
			"Документ.РаспределениеПрочихЗатрат.Форма.ФормаРабочееМесто");
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,,
			"Документ.РаспределениеПрочихЗатрат.Форма.ФормаРабочееМесто");
	КонецЕсли;
	
КонецФункции
//-- НЕ УТ

Процедура СформироватьГиперссылкуКОформлениюФоновоеЗадание(Параметры, АдресХранилища) Экспорт
	
	КОформлению = ОбщегоНазначенияУТ.СформироватьГиперссылкуКОформлению(Параметры[0], Параметры[1]);
	ПоместитьВоВременноеХранилище(КОформлению, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область Заполнение

Функция КонвертироватьДанные(ДанныеПоРасходам)
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ДокументУпр", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТаблицаДанных.Колонки.Добавить("ДокументРегл", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТаблицаДанных.Колонки.Добавить("СостояниеУпр", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	ТаблицаДанных.Колонки.Добавить("СостояниеРегл", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	ТаблицаДанных.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТаблицаДанных.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	ТаблицаДанных.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));

	Для Каждого ДанныеПоРасходу Из ДанныеПоРасходам Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанных.Добавить(), ДанныеПоРасходу);
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ФорматДанныхРаспределенияСтатьиРасходов()
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Дата");
	СтруктураЗаполнения.Вставить("ИДСтроки");
	СтруктураЗаполнения.Вставить("ДокументУпр");
	СтруктураЗаполнения.Вставить("ДокументРегл");
	СтруктураЗаполнения.Вставить("СостояниеУпр");
	СтруктураЗаполнения.Вставить("СостояниеРегл");
	СтруктураЗаполнения.Вставить("СтатьяРасходов");
	СтруктураЗаполнения.Вставить("АналитикаРасходов");
	СтруктураЗаполнения.Вставить("СтатьяКалькуляции");
	СтруктураЗаполнения.Вставить("ВариантРаспределенияРегл");
	СтруктураЗаполнения.Вставить("ВариантРаспределенияУпр");
	СтруктураЗаполнения.Вставить("Организация");
	СтруктураЗаполнения.Вставить("Подразделение");
	СтруктураЗаполнения.Вставить("НаправлениеДеятельности");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияУпр");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияРегл");
	
	Возврат СтруктураЗаполнения;
	
КонецФункции

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РаспределениеПрочихЗатрат);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	//++ НЕ УТ
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходыНезавершенногоПроизводства);
	//-- НЕ УТ
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.РаспределениеПрочихЗатрат.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.4.10.45";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1569eb5c-f5ee-4ea7-ae20-234836718229");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РаспределениеПрочихЗатрат.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет служебные реквизиты документа.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ПланыВидовХарактеристик.СтатьиРасходов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.ПустаяСсылка)";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.РаспределениеПрочихЗатрат;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
	Пока Выборка.Следующий() Цикл
		
		УстановленаБлокировка = Ложь;
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				
				ОтменитьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			УстановленаБлокировка = Истина;
			
			ДокументОбъект.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства;
			
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.СтатьяРасходов, "ВариантРаспределенияРасходовРегл, ВариантРаспределенияРасходовУпр");
			ДокументОбъект.РегламентированныйУчет = 
				(ЗначенияРеквизитов.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
			ДокументОбъект.УправленческийУчет = 
				(ЗначенияРеквизитов.ВариантРаспределенияРасходовУпр = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект,,, РежимЗаписиДокумента.Запись);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			Если УстановленаБлокировка Тогда
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
				ВызватьИсключение;
			Иначе
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойБлокировке(Выборка.Ссылка);
				Продолжить;
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти
//-- НЕ УТ
#Область РаспределениеНаФинансовыйРезультат

Процедура ПодготовитьСлужебныеТаблицы(Период, Знач Организации, Знач Запрос, ИсключитьТранспортныеРасходы = Ложь) Экспорт
	
	//++ НЕ УТ
	Если Организации.Количество() = 0 Тогда
		
		ЗапросОрганизаций = Новый Запрос;
		ЗапросОрганизаций.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Организации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)";
		
		Организации = ЗапросОрганизаций.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	
	Если ИсключитьТранспортныеРасходы Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
			|	0 КАК ДоляТранспортныхРасходов
			|ПОМЕСТИТЬ ДолиТранспортныхРасходов";
		Запрос.Выполнить();
		
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		НалоговыйУчет.ДоляТранспортныхРасходовТекущегоМесяца(Период, Организации, Запрос.МенеджерВременныхТаблиц);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка,
		|	Организации.ГоловнаяОрганизация
		|ПОМЕСТИТЬ втОтборОрганизации
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка В (&МассивОрганизаций)
		|	И НЕ &ПоВсемОрганизациям
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.Ссылка,
		|	Организации.ГоловнаяОрганизация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	&ПоВсемОрганизациям
		|	И Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтатьиРасходов.Ссылка КАК СтатьяРасходов,
		|	ЕСТЬNULL(Организации.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.ВидРасходов В (&ВидыРасходовНормируемые)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК Нормируемые,
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК Транспортные,
		//	Доля списания транспортных расходов
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
		|			ТОГДА ЕСТЬNULL(ДолиТранспортныхРасходов.ДоляТранспортныхРасходов, 0)
		//	Доли списания нормируемых расходов
		|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаРекламуНормируемые)
		|			ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаРекламу, 0)
		|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПредставительскиеРасходы)
		|			ТОГДА ЕСТЬNULL(ДолиСписания.ДоляПредставительскихРасходов, 0)
		|		КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование)
		|			ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеМедицинскоеСтрахование, 0)
		|		КОГДА СтатьиРасходов.ВидРасходов
		|			= ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников)
		|			ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеСтрахованиеЖизни, 0)
		|		КОГДА СтатьиРасходов.ВидРасходов
		|			= ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности)
		|			ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев, 0)
		|		КОГДА СтатьиРасходов.ВидРасходов
		|			= ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов)
		|			ТОГДА ЕСТЬNULL(ДолиСписания.ДоляРасходовНаВозмещениеПроцентовРаботникам, 0)
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ КАК ДоляСписания
		|ПОМЕСТИТЬ ВтДолиСписанияКосвенныхРасходов
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ втОтборОрганизации КАК Организации
		|		ПО ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО ДолиСписания.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|			И Организации.ГоловнаяОрганизация = ДолиСписания.Организация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДолиТранспортныхРасходов КАК ДолиТранспортныхРасходов
		|		ПО Организации.Ссылка = ДолиТранспортныхРасходов.Организация
		|ГДЕ
		|	(СтатьиРасходов.ВидРасходов В (&ВидыРасходовНормируемые)
		|		ИЛИ СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы))
		|	И СтатьиРасходов.ВариантРаспределенияРасходовРегл В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втОтборОрганизации
		|;
		|";

	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();	
	УстановитьПривилегированныйРежим(Ложь);
	//-- НЕ УТ
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "ВтДолиСписанияКосвенныхРасходов") Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
			|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
			|	ЛОЖЬ КАК Нормируемые,
			|	ЛОЖЬ КАК Транспортные,
			|	1 КАК ДоляСписания
			|ПОМЕСТИТЬ ВтДолиСписанияКосвенныхРасходов";
		Запрос.Выполнить();
	КонецЕсли;
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "Регистраторы") Тогда
		ПодготовитьТаблицыКРасчету(Неопределено, Запрос);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КорректировкаУстаревшихДокументов

Функция ЕстьУстаревшиеДокументыСРасходами(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РаспределениеДоходовПоНаправлениямДеятельностиРасходы.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеДоходовПоНаправлениямДеятельности.Расходы КАК РаспределениеДоходовПоНаправлениямДеятельностиРасходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК РаспределениеДоходовПоНаправлениямДеятельности
		|		ПО РаспределениеДоходовПоНаправлениямДеятельностиРасходы.Ссылка = РаспределениеДоходовПоНаправлениямДеятельности.Ссылка
		|ГДЕ
		|	РаспределениеДоходовПоНаправлениямДеятельности.Организация В(&МассивОрганизаций)
		|	И РаспределениеДоходовПоНаправлениямДеятельности.Проведен
		|	И РаспределениеДоходовПоНаправлениямДеятельности.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
