#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.НачислениеСписаниеРезервовПредстоящихРасходов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДоходыИРасходыСервер.ОтразитьПрочиеДоходы(ДополнительныеСвойства, Движения, Отказ);
	ДоходыИРасходыСервер.ОтразитьПрочиеРасходы(ДополнительныеСвойства, Движения, Отказ);	
	РегистрыНакопления.РезервыПредстоящихРасходов.ОтразитьРезервыПредстоящихРасходов(ДополнительныеСвойства, Движения, Отказ);

	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);

	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
		
	Если МассивНепроверяемыхРеквизитов.Найти("Резервы.АналитикаРасходовНачисления") = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, Новый Структура("Резервы", "СтатьяРасходовНачисления, АналитикаРасходовНачисления"), МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
	
	Если МассивНепроверяемыхРеквизитов.Найти("Резервы.АналитикаДоходовСписания") = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиДоходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, Новый Структура("Резервы", "СтатьяДоходовСписания, АналитикаДоходовСписания"), МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	// Запись наборов записей
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);

	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Дата = КонецМесяца(Дата);

	// Проверим, что в этом месяце больше нет аналогичных документов:
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	НачислениеСписаниеРезервовПредстоящихРасходов.Ссылка КАК Ссылка
		               |ИЗ
		               |	Документ.НачислениеСписаниеРезервовПредстоящихРасходов КАК НачислениеСписаниеРезервовПредстоящихРасходов
		               |ГДЕ
		               |	НачислениеСписаниеРезервовПредстоящихРасходов.Ссылка <> &Ссылка
		               |	И НАЧАЛОПЕРИОДА(НачислениеСписаниеРезервовПредстоящихРасходов.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
		               |	И НачислениеСписаниеРезервовПредстоящихРасходов.Организация = &Организация
		               |	И НачислениеСписаниеРезервовПредстоящихРасходов.ВидРезервов = &ВидРезервов
		               |	И НачислениеСписаниеРезервовПредстоящихРасходов.Проведен";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ВидРезервов", ВидРезервов);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			ТекстСообщения = НСтр("ru = 'По организации ""%1"" с видом резервов ""%2"" за ""%3"" месяц уже оформлен документ ""%4"".'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Организация, ВидРезервов, Формат(Дата, "ДФ='ММММ'"), Выборка.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеСерверУТ.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Дата") Тогда
			ЭтотОбъект.Дата = ДанныеЗаполнения.Дата;
		КонецЕсли;
		СтруктураПроверки = Новый Структура("Организация, ВидРезервов");
		ЗаполнитьЗначенияСвойств(СтруктураПроверки, ДанныеЗаполнения);
		Если ЗначениеЗаполнено(СтруктураПроверки.Организация) И ЗначениеЗаполнено(СтруктураПроверки.ВидРезервов) Тогда
			Резервы.Загрузить(Документы.НачислениеСписаниеРезервовПредстоящихРасходов.ДанныеДляЗаполненияРезервов(ЭтотОбъект).Выгрузить());
		КонецЕсли;
	КонецЕсли;
		
	НачислениеСписаниеРезервовПредстоящихРасходовЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
