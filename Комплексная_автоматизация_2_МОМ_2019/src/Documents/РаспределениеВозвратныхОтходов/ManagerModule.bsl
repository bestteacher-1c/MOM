#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область СтандартныеПодсистемыКоманды

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ПроизводствоСервер.ДобавитьКомандуСоздатьВыпускПродукцииБезЗаказаНаОсновании(КомандыСозданияНаОсновании);
	
	РаспределениеВозвратныхОтходовЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Распределение возвратных отходов".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.БазаРаспределенияМатериаловИРабот.ДобавитьКомандуРаспределениеВозвратногоОтхода(КомандыОтчетов);
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	
	РаспределениеВозвратныхОтходовЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
#Область ПечатьВыпускаПродукции

	Если ПравоДоступа("Чтение", Метаданные.Документы.РаспределениеВозвратныхОтходов) Тогда
		
		// Выпуск продукции
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Накладная";
		КомандаПечати.Представление = НСтр("ru = 'Выпуск продукции'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;
	
#КонецОбласти

	РаспределениеВозвратныхОтходовЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий.
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат "Получатель, Дата";
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
//	Параметры:
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//	Возвращаемое значение:
//			Структура - Структура параметров указания серий для объекта.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ИспользоватьОрдернуюСхемуПриПриемке = СкладыСервер.ИспользоватьОрдернуюСхемуПриПоступлении(Объект.Получатель, Объект.Дата);
	ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.Получатель, Ложь);
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.РаспределениеВозвратныхОтходов";
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = ПараметрыСерийСклада.ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПродукцииИзПроизводства);
	
	ПараметрыУказанияСерий.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии = Истина;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	ПараметрыУказанияСерий.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерий.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерий.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерий.РегистрироватьСерии = Истина;
	
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИмяПоляСклад = "Получатель";
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий.
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	СУММА(Товары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	ТаблицаСерий.Назначение,
	|	СУММА(ТаблицаСерий.Количество) КАК Количество
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК ТаблицаСерий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	ТаблицаСерий.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|							И ТоварыДляЗапроса.Количество > 0
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И &Дата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 8
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И &ФактОтбора
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузкеКомплектовДляРазборки
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 4
	|								ИНАЧЕ 3
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 2
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|			ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|			ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|				И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|				И ТоварыДляЗапроса.Назначение = СерииДляЗапроса.Назначение
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|				ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|			ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|				И ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Назначение = ТоварыДляЗапроса.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область Заполнение

// Формирует список документов, на основании переданных параметров.
//	Параметры:
//		Параметры - Структура - параметры для формирования документов.
//			Имеет структуру {ИзделияПоПравилу, ДанныеШапки, ГруппировкаЗатрат}, все ключи являются обязательными.
//			ИзделияПоПравилу - таблица значений, содержащая перечень выпущенной продукции, распределяемой по правилам.
//			ДанныеШапки - Структура - данные заполнения шапки документов.
//		СписокОбъектов - Структура - список сформированных документов.
//
Процедура ДокументыПоПараметрам(Параметры, СписокОбъектов) Экспорт
	
	ТаблицаПродукции = ПолучитьИзВременногоХранилища(Параметры.ИзделияПоПравилу);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.ПравилоРаспределения,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Количество
		|ПОМЕСТИТЬ ТаблицаПродукции
		|ИЗ
		|	&ТаблицаПродукции КАК ТаблицаПродукции
		|ГДЕ
		|	НЕ ТаблицаПродукции.Количество = 0
		|	И НЕ ТаблицаПродукции.ОшибкаВНастройкахМодели
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ВЫБОР
		|		КОГДА &ЗаполнятьАвтоматически
		|			ТОГДА ТаблицаПродукции.ПравилоРаспределения
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ПравилоРаспределения,
		|	ПравилаРаспределения.БазаРаспределения КАК БазаРаспределения,
		|	ПравилаРаспределения.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыйВидЦены.Значение КАК ВидЦены,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	СУММА(ТаблицаПродукции.Количество) КАК Количество,
		|	СУММА(ТаблицаПродукции.Количество) КАК КоличествоПоПравилу,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	СУММА(ТаблицаПродукции.Количество * ЦеныНоменклатуры.Цена) КАК Сумма
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределения
		|		ПО ТаблицаПродукции.ПравилоРаспределения = ПравилаРаспределения.Ссылка
		|			И (&ЗаполнятьАвтоматически)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВидЦеныПлановойСтоимостиМатериаловРабот КАК ПлановыйВидЦены
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ) КАК ЦеныНоменклатуры
		|		ПО (ПлановыйВидЦены.Значение = ЦеныНоменклатуры.ВидЦены)
		|			И ТаблицаПродукции.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ТаблицаПродукции.Характеристика = ЦеныНоменклатуры.Характеристика
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыйВидЦены.Значение,
		|	ТаблицаПродукции.Организация,
		|	ПравилаРаспределения.СтатьяКалькуляции,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.ПравилоРаспределения,
		|	ПравилаРаспределения.БазаРаспределения,
		|	ЦеныНоменклатуры.Цена";
	
	Запрос.УстановитьПараметр("ТаблицаПродукции",		ТаблицаПродукции);
	Запрос.УстановитьПараметр("ЗаполнятьАвтоматически",	Параметры.ЗаполнятьАвтоматически);
	Запрос.УстановитьПараметр("Дата",					Параметры.ДанныеШапки.Дата);
	
	ТаблицаПродукции = Запрос.Выполнить().Выгрузить();
	
	КоличествоДокументов		= СписокОбъектов.Количество();
	КоличествоНовыхДокументов	= 0;
	
	Для Каждого ТекПродукция Из ТаблицаПродукции Цикл
		
		ТекДокумент = Документы.РаспределениеВозвратныхОтходов.СоздатьДокумент();
		ТекДокумент.ПолучитьСсылкуНового();
		
		СписокОбъектов.Добавить(ТекДокумент);
		КоличествоНовыхДокументов = КоличествоНовыхДокументов + 1;
		
		ТекДокумент.Заполнить(Новый Структура("Организация", ТекПродукция.Организация));
		
		ЗаполнитьЗначенияСвойств(ТекДокумент, Параметры.ДанныеШапки);
		ЗаполнитьЗначенияСвойств(ТекДокумент, ТекПродукция);
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ТекДокумент, Документы.РаспределениеВозвратныхОтходов);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ТекДокумент, ПараметрыУказанияСерий);
		
		Если (ТекДокумент.СтатусУказанияСерий = 3
				ИЛИ ТекДокумент.СтатусУказанияСерий = 5
				ИЛИ ТекДокумент.СтатусУказанияСерий = 7
				ИЛИ ТекДокумент.СтатусУказанияСерий = 9)
			И ЗначениеЗаполнено(ТекДокумент.Серия) Тогда
			ЗаполнитьЗначенияСвойств(ТекДокумент.Серии.Добавить(), ТекПродукция);
			ТекДокумент.СтатусУказанияСерий = ТекДокумент.СтатусУказанияСерий + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Возвращает текст запроса для получениях доступных назначений.
//	Параметры:
//		ПараметрыФормированияЗапроса - Структура - параметры для формирования текстов запросов.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаДоступныхНазначений(ПараметрыФормированияЗапроса) Экспорт
	
	Возврат Справочники.Назначения.ТекстЗапросаНазначенийРасширенный();
	
КонецФункции

#Область УчетНДС

// Инициализирует параметры заполнения вида деятельности НДС
//
// Параметры:
//  Объект		- ДокументОбъект.РаспределениеВозвратныхОтходов, ДанныеФормыСтруктура	- документ, для которого необходимо получить параметры.
//
// Возвращаемое значение:
//  Структура - структура параметров, см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС().
//
Функция ПараметрыЗаполненияВидаДеятельностиНДС(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС();
	ПараметрыЗаполнения.Организация							= Объект.Организация;
	ПараметрыЗаполнения.Дата								= Объект.Дата;
	ПараметрыЗаполнения.НаправлениеДеятельности				= Объект.НаправлениеДеятельности;
	ПараметрыЗаполнения.ВыпускПродукцииИРабот				= Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

#Область Инициализация

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказовРаботами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКПоступлению(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры);
	
	// Исполнение запроса и выгрузка полученных таблиц для движений.
	РаспределениеВозвратныхОтходовЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст =
		"ВЫБРАТЬ
		|	Реквизиты.Дата,
		|	Реквизиты.Ссылка,
		|	Реквизиты.Номер,
		|	Реквизиты.Проведен,
		|	Реквизиты.ПометкаУдаления,
		|	Реквизиты.Организация,
		|	Реквизиты.ВыпускПодДеятельность,
		|	Реквизиты.НаправлениеДеятельности,
		|	Реквизиты.ПравилоРаспределения,
		|	Реквизиты.КоличествоПоПравилу,
		|	Реквизиты.СтатьяКалькуляции,
		|	Реквизиты.ВидЦены,
		|	Реквизиты.Подразделение,
		|	Реквизиты.Получатель,
		|	Реквизиты.Номенклатура,
		|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СпрНоменклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаАналитическогоУчета,
		|	Реквизиты.Характеристика,
		|	Реквизиты.Серия,
		|	Реквизиты.СтатусУказанияСерий,
		|	Реквизиты.Количество,
		|	Реквизиты.Назначение,
		|	СпрНазначения.ДвиженияПоСкладскимРегистрам КАК ДвиженияПоСкладскимРегистрам,
		|	Реквизиты.Цена,
		|	Реквизиты.Сумма,
		|	Реквизиты.АналитикаУчетаНоменклатуры,
		|	Реквизиты.Валюта,
		|	Реквизиты.Ответственный,
		|	Реквизиты.Комментарий
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Ссылка = Реквизиты.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
		|		ПО (СпрНазначения.Ссылка = Реквизиты.Назначение)
		|ГДЕ
		|	Реквизиты.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции",
								ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.РаспределениеВозвратныхОтходов")));
								
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Дата);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Запрос)

	Если Запрос = Неопределено ИЛИ Запрос.Параметры.Свойство("КлючиАналитикиНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитики = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.СтатьяКалькуляции
	|ИЗ
	|	(
	// аналитика получателя возвратных отходов с пустым назначением
	|	ВЫБРАТЬ
	|		&Получатель											КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ВЫБОР
	|			КОГДА &СтатусУказанияСерий = 14
	|				ТОГДА &Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ												КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика подразделения при выпуске на склад
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		&Назначение											КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика подразделения при выпуске на склад без назначения
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	| 		ЕСТЬNULL(СпрПартииПроизводства.Назначение, 
	| 			ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))	КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ТаблицаТовары.СтатьяКалькуляции						КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|			ПО СпрПартииПроизводства.Ссылка = ТаблицаТовары.ПартияПроизводства
	|
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства с пустым назначением
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ТаблицаТовары.СтатьяКалькуляции						КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаТовары
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика по правилам
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		&Назначение											КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика по правилам с пустым назначением
	|	ВЫБРАТЬ
	|		&Подразделение										КАК Склад,
	|		&Номенклатура										КАК Номенклатура,
	|		&Характеристика										КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)		КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям) КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|			И ТаблицаТовары.Серия = Аналитика.Серия
	|			И ТаблицаТовары.Склад = Аналитика.МестоХранения
	|			И ТаблицаТовары.Назначение = Аналитика.Назначение
	|			И ТаблицаТовары.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.СтатьяКалькуляции");
	
	ЗапросАналитики.УстановитьПараметр("Ссылка",					Запрос.Параметры.Ссылка);
	ЗапросАналитики.УстановитьПараметр("Получатель",				Запрос.Параметры.Получатель);
	ЗапросАналитики.УстановитьПараметр("Номенклатура",				Запрос.Параметры.Номенклатура);
	ЗапросАналитики.УстановитьПараметр("Характеристика",			Запрос.Параметры.Характеристика);
	ЗапросАналитики.УстановитьПараметр("Назначение",				Запрос.Параметры.Назначение);
	ЗапросАналитики.УстановитьПараметр("СтатусУказанияСерий",		Запрос.Параметры.СтатусУказанияСерий);
	ЗапросАналитики.УстановитьПараметр("Серия",						Запрос.Параметры.Серия);
	ЗапросАналитики.УстановитьПараметр("Подразделение",				Запрос.Параметры.Подразделение);
	ЗапросАналитики.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям", Запрос.Параметры.УчитыватьСебестоимостьТоваровПоНазначениям);
	
	Выборка = ЗапросАналитики.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

	Запрос.УстановитьПараметр("КлючиАналитикиНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеВозвратныхОтходовЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламетированном учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеВозвратныхОтходовЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область ТекстыЗапросовПроведения

#Область ТекстыЗапросовВременныеТаблицы

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Организация							КАК Организация,
	|	ВидыЗапасов.Подразделение				КАК Подразделение,
	|	&НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	СпрНазначения.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	ВидыЗапасов.СтатьяРасходов				КАК СтатьяРасходов,
	|	ВидыЗапасов.АналитикаРасходов			КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО							КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|
	|	-ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаСНДС,
	|	-ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДС,
	|	-ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДСУпр,
	|	-ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК СуммаСНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СуммаБезНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК ПостояннаяРазница,
	|	0										КАК ВременнаяРазница
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ВидыЗапасов.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|		ПО СпрНазначения.Ссылка = &Назначение
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И НЕ ВидыЗапасов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Организация							КАК Организация,
	|	ТаблицаРасходы.Подразделение			КАК Подразделение,
	|	&НаправлениеДеятельности				КАК НаправлениеДеятельности,
	|	СпрНазначения.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	ТаблицаРасходы.СтатьяРасходов			КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО							КАК ВидДеятельностиНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация,
	|	&АналитикаУчетаНоменклатуры				КАК АналитикаУчетаНоменклатуры,
	|
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаСНДС,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДС,
	|	-ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДСУпр,
	|	-ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК СуммаСНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СуммаБезНДСРегл,
	|	-ВЫБОР
	|		КОГДА СтатьиРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ									КАК ПостояннаяРазница,
	|	0										КАК ВременнаяРазница
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ТаблицаРасходы.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
	|		ПО СпрНазначения.Ссылка = &Назначение
	|ГДЕ
	|	ТаблицаРасходы.Ссылка = &Ссылка
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход возвратных отходов в кладовую
	"ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры											КАК АналитикаУчетаНоменклатуры,
	|	&Организация																		КАК Организация,
	|	ТаблицаТовары.ВидЗапасов															КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)											КАК НомерГТД,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	&ВыпускПодДеятельность																КАК НалогообложениеНДС,
	|	&Номенклатура																		КАК Номенклатура,
	|	&Характеристика																		КАК Характеристика,
	|	ИСТИНА																				КАК Первичное
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыНаСкладах";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Получатель								КАК Склад,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ДвиженияПоСкладскимРегистрам
	|			ТОГДА &Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	&Серия									КАК Серия,
	|	&Количество								КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА &СтатусУказанияСерий = 14
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ГДЕ
	|	НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ &СтатусУказанияСерий В (4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Получатель								КАК Склад,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ДвиженияПоСкладскимРегистрам
	|			ТОГДА &Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ТаблицаТовары.Серия						КАК Серия,
	|	ТаблицаТовары.Количество				КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА &СтатусУказанияСерий В (6, 8, 10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И &СтатусУказанияСерий В (4, 6, 8, 10)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "СебестоимостьТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	// приход возвратных отходов (товаров) по получателю
	"ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА СпрСклады.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
	|	КОНЕЦ									КАК ТипЗаписи,
	|	ВЫБОР
	|		КОГДА СпрСклады.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства)
	|	КОНЕЦ									КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		КОГДА НЕ СпрСклады.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ									КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	ТаблицаТовары.ВидЗапасов				КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ									КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|				И СпрСклады.ЦеховаяКладовая
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА СпрСклады.ЦеховаяКладовая
	|			И &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ВыпускПодДеятельность
	|	КОНЕЦ																				КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	ВЫБОР
	|		КОГДА СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК Стоимость,
	|	ВЫБОР
	|		КОГДА СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА СпрСклады.ЦеховаяКладовая
	|			И ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ																				КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			И СпрСклады.ЦеховаяКладовая
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаТовары.АналитикаУчетаНоменклатуры
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|		ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|			И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|			И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|			И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|			И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ТаблицаТовары.СтатьяРасходов
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов (товаров) по подразделению при выпуске на склад
	|ВЫБРАТЬ
	|	&Дата											КАК Период,
	|	&Ссылка											КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)	КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ											КАК РазделУчета,
	|	&Организация									КАК Организация,
	|	ТаблицаТовары.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА АналитикаПоПодразделению.КлючАналитики
	|		ИНАЧЕ АналитикаПоПодразделениюБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК Партия,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО									КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО									КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО									КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО									КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО									КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО									КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО									КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО									КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО									КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.Количество						КАК Количество,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))	КАК СтоимостьРегл,
	|	0												КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаТовары.АналитикаУчетаНоменклатуры
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделению
	|		ПО АналитикаПоПолучателю.Номенклатура = АналитикаПоПодразделению.Номенклатура
	|		И АналитикаПоПолучателю.Характеристика = АналитикаПоПодразделению.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаПоПодразделению.Серия
	|		И &Подразделение = АналитикаПоПодразделению.МестоХранения
	|		И АналитикаПоПолучателю.Назначение = АналитикаПоПодразделению.Назначение
	|		И АналитикаПоПолучателю.СтатьяКалькуляции = АналитикаПоПодразделению.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделениюБезНазначения
	|		ПО АналитикаПоПодразделению.Номенклатура = АналитикаПоПодразделениюБезНазначения.Номенклатура
	|		И АналитикаПоПодразделению.Характеристика = АналитикаПоПодразделениюБезНазначения.Характеристика
	|		И АналитикаПоПодразделению.Серия = АналитикаПоПодразделениюБезНазначения.Серия
	|		И АналитикаПоПодразделению.МестоХранения = АналитикаПоПодразделениюБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПоПодразделениюБезНазначения.Назначение
	|		И АналитикаПоПодразделению.СтатьяКалькуляции = АналитикаПоПодразделениюБезНазначения.СтатьяКалькуляции
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ СпрСклады.ЦеховаяКладовая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Расход возвратных отходов (товаров) по подразделению при выпуске на склад
	|ВЫБРАТЬ
	|	&Дата											КАК Период,
	|	&Ссылка											КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)			КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)							КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства)	КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ											КАК РазделУчета,
	|	&Организация									КАК Организация,
	|	ТаблицаТовары.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА АналитикаПоПодразделению.КлючАналитики
	|		ИНАЧЕ АналитикаПоПодразделениюБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО									КАК Партия,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ВыпускПодДеятельность
	|	КОНЕЦ											КАК ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ТаблицаТовары.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ											КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО									КАК КорОрганизация,
	|	ТаблицаТовары.ВидЗапасов						КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТаблицаТовары.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ											КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО									КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО									КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО									КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО									КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО									КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО									КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО									КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.Количество						КАК Количество,
	|	0												КАК Стоимость,
	|	0												КАК СтоимостьБезНДС,
	|	0												КАК СтоимостьРегл,
	|	0												КАК ПостояннаяРазница,
	|	0												КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ТаблицаТовары
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = ТаблицаТовары.АналитикаУчетаНоменклатуры
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = АналитикаПоПолучателю.МестоХранения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|		ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|		И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|		И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|		И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|		И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделению
	|		ПО АналитикаПоПолучателю.Номенклатура = АналитикаПоПодразделению.Номенклатура
	|		И АналитикаПоПолучателю.Характеристика = АналитикаПоПодразделению.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаПоПодразделению.Серия
	|		И &Подразделение = АналитикаПоПодразделению.МестоХранения
	|		И АналитикаПоПолучателю.Назначение = АналитикаПоПодразделению.Назначение
	|		И АналитикаПоПолучателю.СтатьяКалькуляции = АналитикаПоПодразделению.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПодразделениюБезНазначения
	|		ПО АналитикаПоПодразделению.Номенклатура = АналитикаПоПодразделениюБезНазначения.Номенклатура
	|		И АналитикаПоПодразделению.Характеристика = АналитикаПоПодразделениюБезНазначения.Характеристика
	|		И АналитикаПоПодразделению.Серия = АналитикаПоПодразделениюБезНазначения.Серия
	|		И АналитикаПоПодразделению.МестоХранения = АналитикаПоПодразделениюБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПоПодразделениюБезНазначения.Назначение
	|		И АналитикаПоПодразделению.СтатьяКалькуляции = АналитикаПоПодразделениюБезНазначения.СтатьяКалькуляции
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ СпрСклады.ЦеховаяКладовая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов (работ, по правилам) по получателю
	|ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)		КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА &АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ																				КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ВыпускПодДеятельность
	|	КОНЕЦ																				КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	&КоличествоПоПравилу																КАК Количество,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))	КАК СтоимостьРегл,
	|	0																					КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВЫРАЗИТЬ(&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК СтоимостьУпр
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|		ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|			И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|			И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|			И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|			И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|ГДЕ
	|	АналитикаПоПолучателю.Ссылка = &АналитикаУчетаНоменклатуры
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ &КоличествоПоПравилу = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов (работ, по партиям) по получателю
	|ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)		КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА &АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ																				КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ВыпускПодДеятельность
	|	КОНЕЦ																				КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ПартииПроизводства.Количество														КАК Количество,
	|	ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))	КАК СтоимостьРегл,
	|	0																					КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ПартииПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = &АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|		ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|			И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|			И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|			И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|			И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|
	|ГДЕ
	|	ПартииПроизводства.Ссылка = &Ссылка
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов (работ, по расходам) по получателю
	|ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)		КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА &АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ																				КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <>
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА АналитикаПоПолучателю.Назначение.ВидДеятельностиНДС
	|		ИНАЧЕ &ВыпускПодДеятельность
	|	КОНЕЦ																				КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ТаблицаПрочиеРасходы.Количество														КАК Количество,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаПрочиеРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаПрочиеРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА ВЫРАЗИТЬ(&Цена * ТаблицаПрочиеРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(&Цена * ТаблицаПрочиеРасходы.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ																				КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВЫРАЗИТЬ(&Цена * ТаблицаПрочиеРасходы.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаПрочиеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПоПолучателю
	|		ПО АналитикаПоПолучателю.Ссылка = &АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателюБезНазначения
	|		ПО АналитикаПоПолучателюБезНазначения.Номенклатура = АналитикаПоПолучателю.Номенклатура
	|			И АналитикаПоПолучателюБезНазначения.Характеристика = АналитикаПоПолучателю.Характеристика
	|			И АналитикаПоПолучателюБезНазначения.Серия = АналитикаПоПолучателю.Серия
	|			И АналитикаПоПолучателюБезНазначения.МестоХранения = АналитикаПоПолучателю.МестоХранения
	|			И АналитикаПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаПоПолучателю.СтатьяКалькуляции
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ТаблицаПрочиеРасходы.СтатьяРасходов
	|
	|ГДЕ
	|	ТаблицаПрочиеРасходы.Ссылка = &Ссылка
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов (товаров) по партии производства
	|ВЫБРАТЬ
	|	&Дата																							КАК Период,
	|	&Ссылка																					КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)				КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)	КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)		КАК РазделУчета,
	|	&Организация																			КАК Организация,
	|	ПартииПроизводства.ВидЗапасов										КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА АналитикаНоменклатурыПоПартии.КлючАналитики
	|		ИНАЧЕ АналитикаНоменклатурыПоПартииБезНазначения.КлючАналитики
	|	КОНЕЦ																						КАК АналитикаУчетаНоменклатуры,
	|	ПартииПроизводства.ПартияПроизводства						КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА НЕ СпрПартииПроизводства.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА СпрПартииПроизводства.ГруппаПродукции
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																						КАК АналитикаФинансовогоУчета,
	|	СпрПартииПроизводства.ВидДеятельностиНДС			КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ПартииПроизводства.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаНоменклатурыПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ																						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА &ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																						КАК КорАналитикаФинансовогоУчета,
	|	ПартииПроизводства.СтатьяКалькуляции						КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	-ПартииПроизводства.Количество										КАК Количество,
	|	-ВЫРАЗИТЬ(ПартииПроизводства.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Стоимость,
	|	-ВЫРАЗИТЬ(ПартииПроизводства.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СтоимостьБезНДС,
	|	-ВЫРАЗИТЬ(ПартииПроизводства.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))	КАК СтоимостьРегл,
	|	0																									КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА -ВЫРАЗИТЬ(ПартииПроизводства.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																						КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ПартииПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ПартииПроизводства.ПартияПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаНоменклатурыПоПолучателю
	|		ПО АналитикаНоменклатурыПоПолучателю.Ссылка = ПартииПроизводства.АналитикаУчетаНоменклатуры
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПолучателюБезНазначения
	|		ПО АналитикаНоменклатурыПоПолучателюБезНазначения.Номенклатура = АналитикаНоменклатурыПоПолучателю.Номенклатура
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.Характеристика = АналитикаНоменклатурыПоПолучателю.Характеристика
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.Серия = АналитикаНоменклатурыПоПолучателю.Серия
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.МестоХранения = АналитикаНоменклатурыПоПолучателю.МестоХранения
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаНоменклатурыПоПолучателю.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартии
	|		ПО АналитикаНоменклатурыПоПартии.Номенклатура = &Номенклатура
	|			И АналитикаНоменклатурыПоПартии.Характеристика = &Характеристика
	|			И АналитикаНоменклатурыПоПартии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И АналитикаНоменклатурыПоПартии.МестоХранения = &Подразделение
	|			И АналитикаНоменклатурыПоПартии.Назначение = СпрПартииПроизводства.Назначение
	|			И АналитикаНоменклатурыПоПартии.СтатьяКалькуляции = ПартииПроизводства.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартииБезНазначения
	|		ПО АналитикаНоменклатурыПоПартииБезНазначения.Номенклатура = &Номенклатура
	|			И АналитикаНоменклатурыПоПартииБезНазначения.Характеристика = &Характеристика
	|			И АналитикаНоменклатурыПоПартииБезНазначения.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И АналитикаНоменклатурыПоПартииБезНазначения.МестоХранения = &Подразделение
	|			И АналитикаНоменклатурыПоПартииБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаНоменклатурыПоПартииБезНазначения.СтатьяКалькуляции = ПартииПроизводства.СтатьяКалькуляции
	|
	|ГДЕ
	|	ПартииПроизводства.Ссылка = &Ссылка
	|	И НЕ ПартииПроизводства.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов (работ) по партии производства
	|ВЫБРАТЬ
	|	&Дата																							КАК Период,
	|	&Ссылка																					КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)				КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)	КАК РазделУчета,
	|	&Организация																			КАК Организация,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА АналитикаНоменклатурыПоПартии.КлючАналитики
	|		ИНАЧЕ АналитикаНоменклатурыПоПартииБезНазначения.КлючАналитики
	|	КОНЕЦ																						КАК АналитикаУчетаНоменклатуры,
	|	ПартииПроизводства.ПартияПроизводства						КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА НЕ СпрПартииПроизводства.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА СпрПартииПроизводства.ГруппаПродукции
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																						КАК АналитикаФинансовогоУчета,
	|	СпрПартииПроизводства.ВидДеятельностиНДС			КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА &АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаНоменклатурыПоПолучателюБезНазначения.КлючАналитики
	|	КОНЕЦ																						КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																						КАК КорАналитикаФинансовогоУчета,
	|	ПартииПроизводства.СтатьяКалькуляции						КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	-ПартииПроизводства.Количество										КАК Количество,
	|	-ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))		КАК Стоимость,
	|	-ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))		КАК СтоимостьБезНДС,
	|	-ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))	КАК СтоимостьРегл,
	|	0																									КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА -ВЫРАЗИТЬ(&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																						КАК СтоимостьУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ПартииПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = ПартииПроизводства.ПартияПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаНоменклатурыПоПолучателю
	|		ПО АналитикаНоменклатурыПоПолучателю.Ссылка = &АналитикаУчетаНоменклатуры
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПолучателюБезНазначения
	|		ПО АналитикаНоменклатурыПоПолучателюБезНазначения.Номенклатура = АналитикаНоменклатурыПоПолучателю.Номенклатура
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.Характеристика = АналитикаНоменклатурыПоПолучателю.Характеристика
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.Серия = АналитикаНоменклатурыПоПолучателю.Серия
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.МестоХранения = АналитикаНоменклатурыПоПолучателю.МестоХранения
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И АналитикаНоменклатурыПоПолучателюБезНазначения.СтатьяКалькуляции = АналитикаНоменклатурыПоПолучателю.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартии
	|		ПО &Номенклатура = АналитикаНоменклатурыПоПартии.Номенклатура
	|			И &Характеристика = АналитикаНоменклатурыПоПартии.Характеристика
	|			И АналитикаНоменклатурыПоПартии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И &Подразделение = АналитикаНоменклатурыПоПартии.МестоХранения
	|			И АналитикаНоменклатурыПоПартии.Назначение = СпрПартииПроизводства.Назначение
	|			И ПартииПроизводства.СтатьяКалькуляции = АналитикаНоменклатурыПоПартии.СтатьяКалькуляции
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартииБезНазначения
	|		ПО &Номенклатура = АналитикаНоменклатурыПоПартииБезНазначения.Номенклатура
	|			И &Характеристика = АналитикаНоменклатурыПоПартииБезНазначения.Характеристика
	|			И АналитикаНоменклатурыПоПартииБезНазначения.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И &Подразделение = АналитикаНоменклатурыПоПартииБезНазначения.МестоХранения
	|			И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаНоменклатурыПоПартииБезНазначения.Назначение
	|			И ПартииПроизводства.СтатьяКалькуляции = АналитикаНоменклатурыПоПартииБезНазначения.СтатьяКалькуляции
	|
	|ГДЕ
	|	ПартииПроизводства.Ссылка = &Ссылка
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// приход работ по фиксированной стоимости по получателю
	"ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Получатель								КАК Подразделение,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	&Назначение								КАК Назначение,
	|	&Количество								КАК Количество
	|ГДЕ
	|	&ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбеспечениеЗаказов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	Реквизиты.Дата							КАК Период,
	|	Реквизиты.Получатель					КАК Склад,
	|	Реквизиты.Номенклатура					КАК Номенклатура,
	|	Реквизиты.Характеристика				КАК Характеристика,
	|	Реквизиты.Назначение					КАК Назначение,
	|	0										КАК Потребность,
	|	-Реквизиты.Количество					КАК КЗаказу,
	|	Реквизиты.Количество					КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО Реквизиты.Номенклатура = СпрНоменклатура.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(Реквизиты.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ Реквизиты.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И СпрНоменклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказовРаботами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОбеспечениеЗаказовРаботами";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	&Количество								КАК КОбеспечению,
	|	&Получатель								КАК Подразделение,
	|	&Назначение								КАК Назначение
	|ГДЕ
	|	&Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СвободныеОстатки";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	// Поступление побочных изделий в кладовую.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Получатель								КАК Склад,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	0										КАК ВРезервеСоСклада,
	|	ВЫБОР
	|		КОГДА &Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА &Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК ВРезервеПодЗаказ,
	|	&Количество								КАК ВНаличии
	|ГДЕ
	|	НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Дата																	КАК Период,
		|	&Номенклатура															КАК Номенклатура,
		|	&Характеристика															КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ДвиженияПоСкладскимРегистрам
		|			ТОГДА &Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ																	КАК Назначение,
		|	&Серия																	КАК Серия,
		|	&Количество																КАК Количество,
		|	&Получатель																КАК Отправитель,
		|	&Получатель																КАК Получатель,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
		|	&Ссылка																	КАК Документ,
		|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
		|ИЗ
		|	Справочник.Склады КАК СпрСклады
		|ГДЕ
		|	НЕ &Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|	И СпрСклады.Ссылка = &Получатель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Дата																	КАК Период,
		|	&Номенклатура															КАК Номенклатура,
		|	&Характеристика															КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ДвиженияПоСкладскимРегистрам
		|			ТОГДА &Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ																	КАК Назначение,
		|	ТаблицаТовары.Серия														КАК Серия,
		|	ТаблицаТовары.Количество												КАК Количество,
		|	&Получатель																КАК Отправитель,
		|	&Получатель																КАК Получатель,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
		|	&Ссылка																	КАК Документ,
		|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Шапка
		|		ПО ТаблицаТовары.Ссылка = Шапка.Ссылка
		|			И ТаблицаТовары.Номенклатура = Шапка.Номенклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
		|		ПО (СпрСклады.Ссылка = &Получатель)
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Ссылка
		|	И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДатыПоступленияТоваровОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход возвратных отходов в кладовую
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Дата										КАК ДатаПоступления,
	|	&Номенклатура								КАК Номенклатура,
	|	&Характеристика								КАК Характеристика,
	|	&Серия										КАК Серия,
	|	&Назначение									КАК Назначение,
	|	ВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|		ПО ВидыЗапасов.ВидЗапасов = ПоступленияТоваров.ВидЗапасов
	|			И &Номенклатура = ПоступленияТоваров.Номенклатура
	|			И &Характеристика = ПоступленияТоваров.Характеристика
	|			И &Серия = ПоступленияТоваров.Серия
	|			И &Назначение = ПоступленияТоваров.Назначение
	|			И (ПоступленияТоваров.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И (ПоступленияТоваров.ВидЗапасов ЕСТЬ NULL 
	|			ИЛИ ПоступленияТоваров.ДатаПоступления < &Дата)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка						КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО				КАК РазделительЗаписи,
	|	&Дата						КАК ДатаДокументаИБ,
	|	&Номер						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
	|	&НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	&Подразделение				КАК Подразделение,
	|	&Ответственный				КАК Ответственный,
	|	&Комментарий				КАК Комментарий,
	|	0							КАК Сумма,
	|	&Проведен					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	""""						КАК Дополнительно,
	|	&Дата						КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать				КАК НомерПервичногоДокумента,
	|	&Подразделение				КАК МестоХранения,
	|	&Дата						КАК ДатаОтраженияВУчете
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СуммыДокументовВВалютеРегл";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата										КАК Период,
	|	&Валюта										КАК Валюта,
	|	ТаблицаПрочихРасходов.ИдентификаторСтроки	КАК ИдентификаторСтроки,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаПрочихРасходов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК СуммаБезНДС,
	|	НЕОПРЕДЕЛЕНО								КАК СтавкаНДС,
	|	0											КАК СуммаНДС,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаПрочихРасходов.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО (15,2))	КАК СуммаБезНДСРегл,
	|	0											КАК СуммаНДСРегл,
	|	ВЫРАЗИТЬ(&Цена * ТаблицаПрочихРасходов.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО (15,2))	КАК СуммаБезНДСУпр,
	|	0											КАК СуммаНДСУпр
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаПрочихРасходов
	|ГДЕ
	|	ТаблицаПрочихРасходов.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл()

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата																			КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)											КАК ВидДвижения,
	|	&Организация																	КАК Организация,
	|	ВидыЗапасов.Подразделение														КАК Подразделение,
	|	&НаправлениеДеятельности 														КАК НаправлениеДеятельности,
	|	ВидыЗапасов.СтатьяРасходов														КАК Статья,
	|	ВидыЗапасов.АналитикаАктивовПассивов											КАК Аналитика,
	|	ВЫРАЗИТЬ(ВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))	КАК Сумма
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ВидыЗапасов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И НЕ ВидыЗапасов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы(Ложь);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКПоступлению(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКПоступлению";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Ссылка									КАК ДокументПоступления,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &СтатусУказанияСерий = 14
	|			ТОГДА &Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ									КАК Серия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫРАЗИТЬ(&Назначение КАК Справочник.Назначения).ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА &Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	&Получатель								КАК Склад,
	|	&Подразделение							КАК Отправитель,
	|	0										КАК КОформлениюНакладныхПоРаспоряжению,
	|	&Количество								КАК КОформлениюПоступленийПоОрдерам,
	|	0										КАК КОформлениюОрдеров,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация
	|ИЗ
	|	Справочник.Склады КАК СпрСклады
	|ГДЕ
	|	СпрСклады.Ссылка = &Получатель
	|	И СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении <= &Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Дата												КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)				КАК ВидДвижения,
	|	&Ссылка												КАК ДокументПоступления,
	|	&Номенклатура										КАК Номенклатура,
	|	&Характеристика										КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫРАЗИТЬ(&Назначение КАК Справочник.Назначения).ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА &Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ												КАК Назначение,
	|	&Получатель											КАК Склад,
	|	&Подразделение										КАК Отправитель,
	|	0													КАК КОформлениюНакладныхПоРаспоряжению,
	|	0													КАК КОформлениюПоступленийПоОрдерам,
	|	&Количество											КАК КОформлениюОрдеров,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость) КАК ХозяйственнаяОперация
	|ИЗ
	|	Справочник.Склады КАК СпрСклады
	|ГДЕ
	|	СпрСклады.Ссылка = &Получатель
	|	И СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении <= &Дата";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.РаспределениеВозвратныхОтходов";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "ОбеспечениеЗаказов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "Реквизиты";
		
	ИначеЕсли ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Печать

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя области
//                                              в которой был выведен объект)
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Накладная",
			НСтр("ru = 'Выпуск продукции'"),
			СформироватьПечатнуюФормуНакладной(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуНакладной(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДвижениеПродукцииИМатериалов.ПФ_MXL_Накладная");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ДанныеДокумента.Номер						КАК Номер,
	|	ДанныеДокумента.Дата						КАК Дата,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	1											КАК НомерСтроки,
	|	ДанныеДокумента.Номенклатура				КАК Номенклатура,
	|	ДанныеДокумента.Характеристика				КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ДанныеДокумента.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ										КАК Назначение,
	|	ДанныеДокумента.Получатель					КАК Склад,
	|	ДанныеДокумента.Подразделение.Представление	КАК ПредставлениеПодразделения,
	|	ДанныеДокумента.Количество					КАК Количество,
	|	ДанныеДокумента.Серия						КАК Серия
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|	И НЕ ДанныеДокумента.СтатусУказанияСерий В (4, 6, 8, 10)
	|	И ДанныеДокумента.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТИПЗНАЧЕНИЯ(ДанныеДокумента.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ ДанныеДокумента.Получатель.ЦеховаяКладовая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ДанныеДокумента.Номер						КАК Номер,
	|	ДанныеДокумента.Дата						КАК Дата,
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	1											КАК НомерСтроки,
	|	ДанныеДокумента.Номенклатура				КАК Номенклатура,
	|	ДанныеДокумента.Характеристика				КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДокумента.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ДанныеДокумента.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ										КАК Назначение,
	|	ДанныеДокумента.Получатель					КАК Склад,
	|	ДанныеДокумента.Подразделение.Представление	КАК ПредставлениеПодразделения,
	|	ДанныеДокумента.Количество					КАК Количество,
	|	ТаблицаСерии.Серия							КАК Серия
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК ДанныеДокумента
	|		ПО ТаблицаСерии.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ТаблицаСерии.Ссылка В(&МассивДокументов)
	|	И ДанныеДокумента.СтатусУказанияСерий В (4, 6, 8, 10)
	|	И ТаблицаСерии.Количество <> 0
	|	И ДанныеДокумента.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТИПЗНАЧЕНИЯ(ДанныеДокумента.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ ДанныеДокумента.Получатель.ЦеховаяКладовая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка				КАК Ссылка,
	|	ТаблицаТовары.Номер					КАК Номер,
	|	ТаблицаТовары.Дата					КАК Дата,
	|	ТаблицаТовары.Организация			КАК Организация,
	|	ТаблицаТовары.Организация.Префикс	КАК Префикс,
	|	ТаблицаТовары.Номенклатура,
	//|	ТаблицаТовары.Номенклатура." + ?(КолонкаКодов = "", "Код", КолонкаКодов) + " КАК КодАртикул,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	ТаблицаТовары.Серия.Наименование КАК СерияНаименование,
	|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) КАК НаправлениеВыпуска,
	|	ТаблицаТовары.Склад.Представление КАК ПредставлениеПолучателя,
	|	ТаблицаТовары.ПредставлениеПодразделения КАК ПредставлениеПодразделения
	|ИЗ
	|	ВтТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Номер),
	|	МАКСИМУМ(Дата),
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(ПредставлениеПолучателя),
	|	МАКСИМУМ(ПредставлениеПодразделения),
	|	МАКСИМУМ(Префикс)
	|ПО
	|	Ссылка";

	Запрос.УстановитьПараметр("МассивДокументов",           МассивОбъектов);
	Запрос.УстановитьПараметр("ТипПолучателяСклад",         НСтр("ru = 'Склад'"));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыпускПродукции_Накладная";
	
	МассивРезультатов		= Запрос.ВыполнитьПакет();
	ВыборкаПоДокументам 	= МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтраницы = 1;
		
		ПервыйДокумент    = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим общие реквизиты шапки
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ВыборкаПоДокументам);
		ОбластьМакета.Параметры.ТипПолучателя = НСтр("ru = 'Склад'");
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ВыборкаПоДокументам, НСтр("ru='Выпуск продукции'"));
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаПоДокументам.Организация, ВыборкаПоДокументам.Дата);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Создаем массив для проверки вывода
		МассивВыводимыхОбластей = Новый Массив;
		
		// Выводим многострочную часть докмента
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть(?(КолонкаКодов = "", "ЗаголовокТаб", "ЗаголовокТабСКодом"));
		ОбластьМакета           = Макет.ПолучитьОбласть(?(КолонкаКодов = "", "Строка",       "СтрокаСКодом"));
		ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
		
		СтрокаТовары = ВыборкаПоДокументам.Выбрать();
		КоличествоСтрок = СтрокаТовары.Количество();
		НомерСтроки = 0;
		ПараметрыСтрокиТовары = Новый Структура("НомерСтроки,ТоварНаименование");
		Пока СтрокаТовары.Следующий() Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			ПараметрыСтрокиТовары.НомерСтроки = НомерСтроки;
			ПараметрыСтрокиТовары.ТоварНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
															СтрокаТовары.ТоварНаименование, 
															СтрокаТовары.ХарактеристикаНаименование,,
															СтрокаТовары.СерияНаименование);
															
			ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
			ОбластьМакета.Параметры.Заполнить(ПараметрыСтрокиТовары);
			
			Если НомерСтроки = 1 Тогда
				
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Страница %1'"), НомерСтраницы); 
				ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
				
			Иначе
				
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьМакета);
				
				Если НомерСтроки = КоличествоСтрок Тогда
					
					МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
					
				КонецЕсли;
				
				Если НомерСтроки <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Страница %1'"), НомерСтраницы);
					ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
					
				КонецЕсли;
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		// Выводим подвал документа
		ОбластьПодвала.Параметры.ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Всего наименований %1'"), СтрокаТовары.Количество());
		
		ТабличныйДокумент.Вывести(ОбластьПодвала);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
