
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура обновляет движения документа принятия к учету после отражения в регл. учете
// Вызывается при отражения в регл. учете из функции "РеглУчетПроведениеСервер.ДополнительнаяОбработкаПриОтраженииДокумента".
//
// Параметры:
// 		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджера временных таблиц проводок в регл. учете.
//
Процедура ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПринятиеКУчетуНМА.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ПринятиеКУчетуНМА.Ссылка = ДокументыКОтражению.Ссылка";
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	
	Если Не РеквизитыДокумента.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, РеквизитыДокумента.Ссылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Неопределено);
	ПервоначальныеСведенияНМАНалоговыйУчет(ТекстыЗапроса, Неопределено);
	
	ТаблицыДляДвижений = Новый Структура;
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		#Область БлокировкаДанных
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрочиеРасходы.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		Блокировка.Заблокировать();
		
		#КонецОбласти
		
		НаборЗаписей = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПрочиеРасходы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПервоначальныеСведенияНМАБухгалтерскийУчет);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ТаблицаПервоначальныеСведенияНМАНалоговыйУчет);
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Заполнение регистров НМА после отражения документа в регламентированном учете'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Принятие к учету НМА".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПринятиеКУчетуНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПринятиеКУчетуНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПринятиеКУчетуНМА);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПринятиеКУчетуНМА";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАНалоговыйУчет(ТекстыЗапроса, Регистры);
	СостоянияНМАОрганизаций(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	СчетаБухгалтерскогоУчетаНМА(ТекстыЗапроса, Регистры);
	ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	ПараметрыЦелевогоФинансированияНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.НематериальныйАктив,
	|	ДанныеДокумента.НематериальныйАктив.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	ДанныеДокумента.СпособПоступления,
	|	ДанныеДокумента.СчетУчета,
	|	ДанныеДокумента.СчетНачисленияАмортизации,
	|	ДанныеДокумента.НачислятьАмортизациюБУ,
	|	ДанныеДокумента.СрокИспользованияБУ,
	|	ДанныеДокумента.СпособНачисленияАмортизацииБУ,
	|	ДанныеДокумента.ОбъемНаработкиБУ,
	|	ДанныеДокумента.КоэффициентБУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУ,
	|	ДанныеДокумента.ПорядокСписанияНИОКРНаРасходыНУ,
	|	ДанныеДокумента.ПорядокУчетаНУ,
	|	ДанныеДокумента.СрокИспользованияНУ,
	|	ДанныеДокумента.СпециальныйКоэффициентНУ,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов,
	|	ДанныеДокумента.СчетПередачиРасходов,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.НаправлениеДеятельности,
	|	ДанныеДокумента.СуммаЦелевыхСредств,
	|	ДанныеДокумента.СчетУчетаЦФ,
	|	ДанныеДокумента.СчетАмортизацииЦФ,
	|	ДанныеДокумента.СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ГраницаКонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Дата), ВидГраницы.Включая));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПринятиеКУчетуНМА"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПринятиеКУчетуНМА);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ДанныеДокументаИУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втДанныеДокументаИУчета
	|"+
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаОстатокДт, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаНУОстатокДт, 0)) КАК ПервоначальнаяСтоимостьНУ,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаПРОстатокДт, 0)) КАК ПервоначальнаяСтоимостьПР,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаВРОстатокДт, 0)) КАК ПервоначальнаяСтоимостьВР,
	|	ПринятиеКУчетуНМА.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ГраницаКонецМесяца,
	|				Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеНематериальныхАктивов), 
	|						ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыполнениеНИОКР),
	|						ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеПоисковыеАктивы)),
	|				,
	|				Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И НаправлениеДеятельности = &НаправлениеДеятельности
	|					И Субконто1 = &НематериальныйАктив) КАК ДанныеУчета
	|		ПО ПринятиеКУчетуНМА.НематериальныйАктив = ДанныеУчета.Субконто1
	|ГДЕ
	|	ПринятиеКУчетуНМА.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуНМА.НематериальныйАктив,
	|	ПринятиеКУчетуНМА.СуммаЦелевыхСредств";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
	Возврат Текст
	
КонецФункции

Процедура ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втДанныеДокументаИУчета
	|"+
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.Сумма, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаНУКт, 0)) КАК ПервоначальнаяСтоимостьНУ,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаПРКт, 0)) КАК ПервоначальнаяСтоимостьПР,
	|	СУММА(ЕСТЬNULL(ДанныеУчета.СуммаВРКт, 0)) КАК ПервоначальнаяСтоимостьВР,
	|	ПринятиеКУчетуНМА.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				,
	|				,
	|				Регистратор = &Ссылка
	|					И СчетДт В (&СчетУчета, &СчетУчетаЦФ),
	|				,
	|				) КАК ДанныеУчета
	|		ПО ПринятиеКУчетуНМА.НематериальныйАктив = ДанныеУчета.СубконтоДт1
	|ГДЕ
	|	ПринятиеКУчетуНМА.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуНМА.НематериальныйАктив,
	|	ПринятиеКУчетуНМА.СуммаЦелевыхСредств";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Функция ТаблицаНМА(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеДокументаИУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаНМА
	|"+
	"ВЫБРАТЬ
	|	втДанныеДокументаИУчета.НематериальныйАктив КАК НематериальныйАктив,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Неиспользуется)
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьНУ
	|		КОГДА втДанныеДокументаИУчета.СуммаЦелевыхСредств > втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР - втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьНУ - втДанныеДокументаИУчета.СуммаЦелевыхСредств + втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР + втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|		ИНАЧЕ втДанныеДокументаИУчета.СуммаЦелевыхСредств
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ,
	|	ВЫБОР
	|		КОГДА &ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Неиспользуется)
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР
	|		КОГДА втДанныеДокументаИУчета.СуммаЦелевыхСредств > втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР - втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|			ТОГДА втДанныеДокументаИУчета.СуммаЦелевыхСредств - втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|		ИНАЧЕ втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьПР,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР КАК ПервоначальнаяСтоимостьВР,
	|	втДанныеДокументаИУчета.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	втДанныеДокументаИУчета КАК втДанныеДокументаИУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
	Возврат Текст;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТаблицаНМА(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	- ТаблицаНМА.ПервоначальнаяСтоимостьНУ КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	(&ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|		И &ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.ПриПринятииКУчету)
	|	ИЛИ &ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|		И &ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	ТаблицаНМА(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	0 КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	- ТаблицаНМА.ПервоначальнаяСтоимостьНУ КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	(&ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|		И &ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.ПриПринятииКУчету)
	|	ИЛИ &ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|		И &ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	&СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	&СпособПоступления КАК СпособПоступления,
	|	&НачислятьАмортизациюБУ КАК НачислятьАмортизацию,
	|	&СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	&СрокИспользованияБУ КАК СрокПолезногоИспользования,
	|	&ОбъемНаработкиБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	&КоэффициентБУ КАК Коэффициент
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ПервоначальныеСведенияНМАНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	0 КАК АмортизацияДо2002,
	|	0 КАК АмортизацияДо2009,
	|	НЕОПРЕДЕЛЕНО КАК ДатаПриобретения,
	|	НЕОПРЕДЕЛЕНО КАК МетодНачисленияАмортизации,
	|	&НачислятьАмортизациюНУ КАК НачислятьАмортизацию,
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	&СрокИспользованияНУ КАК СрокПолезногоИспользования,
	|	0 КАК СтоимостьДо2002,
	|	0 КАК ФактическийСрокИспользованияДо2009,
	|	&ПорядокУчетаНУ КАК ПорядокУчетаНУ
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
	Возврат Текст;
	
КонецФункции

Процедура СостоянияНМАОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СостоянияНМАОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СостоянияНМАОрганизаций
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету) КАК Состояние
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	&ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	&СчетПередачиРасходов КАК СчетПередачиРасходов
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&НачислятьАмортизациюБУ ИЛИ &НачислятьАмортизациюНУ";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура СчетаБухгалтерскогоУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СчетаБухгалтерскогоУчетаНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СчетаБухгалтерскогоУчетаНМА
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	&НематериальныйАктив КАК НематериальныйАктив,
	|	&СчетУчета КАК СчетУчета,
	|	&СчетНачисленияАмортизации КАК СчетНачисленияАмортизации";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ОтражениеДокументовВРеглУчете
	|" +
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения,
	|	
	|	&Организация КАК Организация,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура ПараметрыЦелевогоФинансированияНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыЦелевогоФинансированияНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНМА(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыЦелевогоФинансированияНМА
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ИСТИНА КАК ПрименениеЦелевогоФинансирования,
	|	&СчетУчетаЦФ КАК СчетУчета,
	|	&СчетАмортизацииЦФ КАК СчетАмортизации,
	|	&СтатьяДоходов КАК СтатьяДоходов,
	|	&АналитикаДоходов КАК АналитикаДоходов
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|	
	|ГДЕ
	|	&ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное)
	|	)";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0                                       КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&Проведен                               КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	&НематериальныйАктив                    КАК НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеРегламентированныйУчет

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ВременнаяТаблицаДанныеДокумента());
	ТекстыОтражения.Добавить(ВременнаяТаблицаОстаткиСчетаКапитализации());
	ТекстыОтражения.Добавить(ВременнаяТаблицаЦелевоеФинансирование());
	ТекстыОтражения.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
КонецФункции

Функция ВременнаяТаблицаДанныеДокумента()
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.СчетНачисленияАмортизации,
	|	ТаблицаДокумента.СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов,
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ТаблицаДокумента.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств,
	|	ТаблицаДокумента.СчетУчетаЦФ,
	|	ТаблицаДокумента.СчетАмортизацииЦФ,
	|	ТаблицаДокумента.СтатьяДоходов,
	|	ТаблицаДокумента.АналитикаДоходов,
	|	ТаблицаДокумента.ПорядокСписанияНИОКРНаРасходыНУ,
	|	ТаблицаДокумента.ПорядокУчетаНУ
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
	|				&Дата,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.Организация
	|					ИЗ
	|						Документ.ПринятиеКУчетуНМА КАК Т
	|					ГДЕ
	|						Т.Ссылка = &Ссылка)) КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаДокумента.Организация = УчетнаяПолитикаОрганизаций.Организация
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
КонецФункции

Функция ВременнаяТаблицаОстаткиСчетаКапитализации()
	
	Возврат
	"ВЫБРАТЬ
	|	ДанныеУчета.Счет КАК Счет,
	|	ДанныеУчета.Субконто1 КАК НематериальныйАктив,
	|	СУММА(ДанныеУчета.СуммаОборот) КАК СуммаБУ,
	|	СУММА(ДанныеУчета.СуммаНУОборот) КАК СуммаНУ,
	|	СУММА(ДанныеУчета.СуммаПРОборот) КАК СуммаПР,
	|	СУММА(ДанныеУчета.СуммаВРОборот) КАК СуммаВР
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&ГраницаМесяцОкончание,
	|			Регистратор,
	|			Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеНематериальныхАктивов), 
	|					ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыполнениеНИОКР),
	|					ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеПоисковыеАктивы)),
	|			,
	|			(Организация, Подразделение, НаправлениеДеятельности, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.НаправлениеДеятельности,
	|					Т.НематериальныйАктив
	|				ИЗ
	|					втДанныеДокумента КАК Т),
	|			,
	|			) КАК ДанныеУчета
	|ГДЕ
	|	ДанныеУчета.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.Субконто1,
	|	ДанныеУчета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.НематериальныйАктив,
	|	СУММА(втОстатки.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втОстатки.СуммаНУ) КАК СуммаНУ,
	|	СУММА(втОстатки.СуммаПР) КАК СуммаПР,
	|	СУММА(втОстатки.СуммаВР) КАК СуммаВР
	|ПОМЕСТИТЬ втОстаткиСгруппированные
	|ИЗ
	|	втОстатки КАК втОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.НематериальныйАктив,
	|	ВЫРАЗИТЬ(втДанныеДокумента.СуммаЦелевыхСредств * (втОстатки.СуммаБУ / втОстаткиСгруппированные.СуммаБУ) КАК ЧИСЛО(31,2)) КАК Сумма
	|ПОМЕСТИТЬ втСуммыЦелевыхСредствПоСчетам
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиСгруппированные КАК втОстаткиСгруппированные
	|		ПО втОстатки.НематериальныйАктив = втОстаткиСгруппированные.НематериальныйАктив
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.НематериальныйАктив = втДанныеДокумента.НематериальныйАктив
	|ГДЕ
	|	втДанныеДокумента.СуммаЦелевыхСредств <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втСуммыЦелевыхСредствПоСчетам.Счет) КАК Счет,
	|	втСуммыЦелевыхСредствПоСчетам.НематериальныйАктив,
	|	СУММА(втСуммыЦелевыхСредствПоСчетам.Сумма) КАК Сумма
	|ПОМЕСТИТЬ втСуммыЦелевыхСредствПоСчетамСгруппированные
	|ИЗ
	|	втСуммыЦелевыхСредствПоСчетам КАК втСуммыЦелевыхСредствПоСчетам
	|
	|СГРУППИРОВАТЬ ПО
	|	втСуммыЦелевыхСредствПоСчетам.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСуммыЦелевыхСредствПоСчетам.Счет КАК Счет,
	|	втСуммыЦелевыхСредствПоСчетам.НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА втДанныеДокумента.СуммаЦелевыхСредств = втСуммыЦелевыхСредствПоСчетамСгруппированные.Сумма
	|			ТОГДА втСуммыЦелевыхСредствПоСчетам.Сумма
	|		КОГДА втДанныеДокумента.СуммаЦелевыхСредств <> втСуммыЦелевыхСредствПоСчетамСгруппированные.Сумма
	|				И втСуммыЦелевыхСредствПоСчетам.Счет = втСуммыЦелевыхСредствПоСчетамСгруппированные.Счет
	|			ТОГДА втСуммыЦелевыхСредствПоСчетам.Сумма + (втДанныеДокумента.СуммаЦелевыхСредств - втСуммыЦелевыхСредствПоСчетамСгруппированные.Сумма)
	|		ИНАЧЕ втСуммыЦелевыхСредствПоСчетам.Сумма
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втСуммыЦелевыхСредств
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыЦелевыхСредствПоСчетам КАК втСуммыЦелевыхСредствПоСчетам
	|		ПО втДанныеДокумента.НематериальныйАктив = втСуммыЦелевыхСредствПоСчетам.НематериальныйАктив
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыЦелевыхСредствПоСчетамСгруппированные КАК втСуммыЦелевыхСредствПоСчетамСгруппированные
	|		ПО втДанныеДокумента.НематериальныйАктив = втСуммыЦелевыхСредствПоСчетамСгруппированные.НематериальныйАктив
	|ГДЕ
	|	втДанныеДокумента.СуммаЦелевыхСредств <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.НематериальныйАктив,
	|	втСуммыЦелевыхСредств.Сумма КАК СуммаБУЦФ,
	|	втСуммыЦелевыхСредств.Сумма
	|	- ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втСуммыЦелевыхСредств.Сумма
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втОстатки.СуммаПР
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|	- ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА 0
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаВР > втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втОстатки.СуммаВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУЦФ,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втСуммыЦелевыхСредств.Сумма
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втОстатки.СуммаПР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПРЦФ,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА 0
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаВР > втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втОстатки.СуммаВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВРЦФ
	|ПОМЕСТИТЬ втЦелевоеФинансированиеСтоимости
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыЦелевыхСредств КАК втСуммыЦелевыхСредств
	|		ПО втОстатки.НематериальныйАктив = втСуммыЦелевыхСредств.НематериальныйАктив
	|			И втОстатки.Счет = втСуммыЦелевыхСредств.Счет
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.НематериальныйАктив,
	|	втОстатки.СуммаБУ - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаБУЦФ, 0) КАК СуммаБУ,
	|	втОстатки.СуммаНУ - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаНУЦФ, 0) КАК СуммаНУ,
	|	втОстатки.СуммаПР - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаПРЦФ, 0) КАК СуммаПР,
	|	втОстатки.СуммаВР - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаВРЦФ, 0) КАК СуммаВР,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаБУЦФ, 0) КАК СуммаБУЦФ,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаНУЦФ, 0) КАК СуммаНУЦФ,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаПРЦФ, 0) КАК СуммаПРЦФ,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаВРЦФ, 0) КАК СуммаВРЦФ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализации
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.НематериальныйАктив = втДанныеДокумента.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦелевоеФинансированиеСтоимости КАК втЦелевоеФинансированиеСтоимости
	|		ПО втОстатки.НематериальныйАктив = втЦелевоеФинансированиеСтоимости.НематериальныйАктив
	|			И втОстатки.Счет = втЦелевоеФинансированиеСтоимости.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.НематериальныйАктив,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаНУ) КАК СуммаНУ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаПР) КАК СуммаПР,
	|	СУММА(втДанныеСчетаКапитализации.СуммаВР) КАК СуммаВР,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУЦФ) КАК СуммаБУЦФ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаНУЦФ) КАК СуммаНУЦФ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаПРЦФ) КАК СуммаПРЦФ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаВРЦФ) КАК СуммаВРЦФ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализацииСгруппированные
	|ИЗ
	|	втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.НематериальныйАктив";
	
КонецФункции

Функция ВременнаяТаблицаЦелевоеФинансирование()
	
	Возврат
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.Счет,
	|	ТабличнаяЧасть.Субконто1,
	|	ТабличнаяЧасть.Субконто2,
	|	ТабличнаяЧасть.Субконто3,
	|	ТабличнаяЧасть.Сумма,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Дата,
	|	ТабличнаяЧасть.НомерСтроки
	|ПОМЕСТИТЬ втТабличнаяЧастьЦелевоеФинансирование
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуНМА.ЦелевоеФинансирование КАК ТабличнаяЧасть
	|		ПО (ТабличнаяЧасть.Ссылка = ТаблицаДокумента.Ссылка)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТабличнаяЧастьЦелевоеФинансирование.Счет,
	|	втТабличнаяЧастьЦелевоеФинансирование.Субконто1,
	|	втТабличнаяЧастьЦелевоеФинансирование.Субконто2,
	|	втТабличнаяЧастьЦелевоеФинансирование.Субконто3,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстаток, втТабличнаяЧастьЦелевоеФинансирование.Сумма) КАК Сумма,
	|	втТабличнаяЧастьЦелевоеФинансирование.Организация,
	|	втТабличнаяЧастьЦелевоеФинансирование.Подразделение,
	|	втТабличнаяЧастьЦелевоеФинансирование.НаправлениеДеятельности,
	|	втТабличнаяЧастьЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования,
	|	втТабличнаяЧастьЦелевоеФинансирование.Ссылка,
	|	втТабличнаяЧастьЦелевоеФинансирование.Дата,
	|	втТабличнаяЧастьЦелевоеФинансирование.НомерСтроки
	|ПОМЕСТИТЬ втИсточникиЦелевогоФинансирования
	|ИЗ
	|	втТабличнаяЧастьЦелевоеФинансирование КАК втТабличнаяЧастьЦелевоеФинансирование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&Дата,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.Счет
	|					ИЗ
	|						втТабличнаяЧастьЦелевоеФинансирование КАК Т),
	|				,
	|				(Организация, Подразделение, НаправлениеДеятельности) В
	|					(ВЫБРАТЬ
	|						втТабличнаяЧастьЦелевоеФинансирование.Организация,
	|						втТабличнаяЧастьЦелевоеФинансирование.Подразделение,
	|						втТабличнаяЧастьЦелевоеФинансирование.НаправлениеДеятельности
	|					ИЗ
	|						втТабличнаяЧастьЦелевоеФинансирование КАК втТабличнаяЧастьЦелевоеФинансирование)) КАК ХозрасчетныйОстатки
	|		ПО (втТабличнаяЧастьЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное))
	|			И втТабличнаяЧастьЦелевоеФинансирование.Счет = ХозрасчетныйОстатки.Счет
	|			И втТабличнаяЧастьЦелевоеФинансирование.Субконто1 = ХозрасчетныйОстатки.Субконто1
	|			И втТабличнаяЧастьЦелевоеФинансирование.Субконто2 = ХозрасчетныйОстатки.Субконто2
	|			И втТабличнаяЧастьЦелевоеФинансирование.Субконто3 = ХозрасчетныйОстатки.Субконто3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втИсточникиЦелевогоФинансирования.Сумма) КАК Сумма,
	|	КОЛИЧЕСТВО(втИсточникиЦелевогоФинансирования.Счет) КАК Количество
	|ПОМЕСТИТЬ втИсточникиЦелевогоФинансированияСгруппированные
	|ИЗ
	|	втИсточникиЦелевогоФинансирования КАК втИсточникиЦелевогоФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.НематериальныйАктив,
	|	СУММА(ВЫБОР
	|			КОГДА втДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|				ТОГДА втДанныеСчетаКапитализации.СуммаБУ
	|			ИНАЧЕ втДанныеСчетаКапитализации.СуммаБУЦФ
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ втЦелевоеФинансированиеНМА
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|		ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансированиеНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	втИсточникиЦелевогоФинансирования.Счет КАК Счет,
	|	втИсточникиЦелевогоФинансирования.Субконто1 КАК Субконто1,
	|	втИсточникиЦелевогоФинансирования.Субконто2 КАК Субконто2,
	|	втИсточникиЦелевогоФинансирования.Субконто3 КАК Субконто3,
	|	ВЫБОР
	|		КОГДА втИсточникиЦелевогоФинансированияСгруппированные.Сумма = 0
	|			ТОГДА ВЫРАЗИТЬ(втЦелевоеФинансированиеНМА.Сумма / втИсточникиЦелевогоФинансированияСгруппированные.Количество КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ВЫРАЗИТЬ(втЦелевоеФинансированиеНМА.Сумма * (втИсточникиЦелевогоФинансирования.Сумма / втИсточникиЦелевогоФинансированияСгруппированные.Сумма) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	втИсточникиЦелевогоФинансирования.Организация,
	|	втИсточникиЦелевогоФинансирования.Подразделение,
	|	втИсточникиЦелевогоФинансирования.НаправлениеДеятельности,
	|	втИсточникиЦелевогоФинансирования.ВариантПримененияЦелевогоФинансирования,
	|	втИсточникиЦелевогоФинансирования.Ссылка,
	|	втИсточникиЦелевогоФинансирования.Дата,
	|	втИсточникиЦелевогоФинансирования.НомерСтроки,
	|	втЦелевоеФинансированиеНМА.Сумма КАК ПолнаяСуммаПоНМА
	|ПОМЕСТИТЬ втЦелевоеФинансирование
	|ИЗ
	|	втЦелевоеФинансированиеНМА КАК втЦелевоеФинансированиеНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсточникиЦелевогоФинансирования КАК втИсточникиЦелевогоФинансирования
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсточникиЦелевогоФинансированияСгруппированные КАК втИсточникиЦелевогоФинансированияСгруппированные
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансирование.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(втЦелевоеФинансирование.Сумма) КАК Сумма,
	|	МАКСИМУМ(втЦелевоеФинансирование.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ втЦелевоеФинансированиеСгруппированное
	|ИЗ
	|	втЦелевоеФинансирование КАК втЦелевоеФинансирование
	|
	|СГРУППИРОВАТЬ ПО
	|	втЦелевоеФинансирование.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансирование.НематериальныйАктив,
	|	втЦелевоеФинансирование.Счет,
	|	втЦелевоеФинансирование.Субконто1,
	|	втЦелевоеФинансирование.Субконто2,
	|	втЦелевоеФинансирование.Субконто3,
	|	ВЫБОР
	|		КОГДА НЕ втЦелевоеФинансированиеСгруппированное.НематериальныйАктив ЕСТЬ NULL 
	|				И втЦелевоеФинансированиеСгруппированное.Сумма <> втЦелевоеФинансирование.ПолнаяСуммаПоНМА
	|			ТОГДА втЦелевоеФинансирование.Сумма + втЦелевоеФинансирование.ПолнаяСуммаПоНМА - втЦелевоеФинансированиеСгруппированное.Сумма
	|		ИНАЧЕ втЦелевоеФинансирование.Сумма
	|	КОНЕЦ КАК Сумма,
	|	втЦелевоеФинансирование.Организация,
	|	втЦелевоеФинансирование.Подразделение,
	|	втЦелевоеФинансирование.НаправлениеДеятельности,
	|	втЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования,
	|	втЦелевоеФинансирование.Ссылка,
	|	втЦелевоеФинансирование.Дата
	|ПОМЕСТИТЬ втЦелевоеФинансированиеИтог
	|ИЗ
	|	втЦелевоеФинансирование КАК втЦелевоеФинансирование
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦелевоеФинансированиеСгруппированное КАК втЦелевоеФинансированиеСгруппированное
	|		ПО втЦелевоеФинансирование.НематериальныйАктив = втЦелевоеФинансированиеСгруппированное.НематериальныйАктив
	|			И втЦелевоеФинансирование.НомерСтроки = втЦелевоеФинансированиеСгруппированное.НомерСтроки";
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ПринятиеКУчету());
	ТекстыОтражения.Добавить(ПринятиеКУчетуЦелевыеСредства());
	ТекстыОтражения.Добавить(ПринятиеКУчетуЧастичныеЦелевыеСредства());
	ТекстыОтражения.Добавить(ВключениеВРасходыПриПринятииКУчету());
	ТекстыОтражения.Добавить(ПризнаниеДоходовБудущихПериодов());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция ПринятиеКУчету()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету  (Дт СчетУчета:: Кт СчетаКапитальныхЗатрат)
	|
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втДанныеДокумента.СчетУчета КАК СчетДт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУДт,
	|	втДанныеСчетаКапитализации.СуммаПР КАК СуммаПРДт,
	|	втДанныеСчетаКапитализации.СуммаВР КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУКт,
	|	втДанныеСчетаКапитализации.СуммаПР КАК СуммаПРКт,
	|	втДанныеСчетаКапитализации.СуммаВР КАК СуммаВРКт,
	|	""Принятие к учету НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|ГДЕ
	|	втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.НеИспользуется),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.ПустаяСсылка)
	|	)
	|";
	
КонецФункции

Функция ПринятиеКУчетуЦелевыеСредства()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету  (Дт СчетУчета:: Кт СчетаКапитальныхЗатрат)
	|
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втДанныеДокумента.СчетУчетаЦФ КАК СчетДт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	втДанныеСчетаКапитализации.СуммаНУ + втДанныеСчетаКапитализации.СуммаПР + втДанныеСчетаКапитализации.СуммаВР КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУКт,
	|	втДанныеСчетаКапитализации.СуммаПР КАК СуммаПРКт,
	|	втДанныеСчетаКапитализации.СуммаВР КАК СуммаВРКт,
	|	""Принятие к учету НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|ГДЕ
	|	втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|	)
	|";
	
КонецФункции

Функция ПринятиеКУчетуЧастичныеЦелевыеСредства()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету (Дт СчетУчета:: Кт СчетКапитальныхЗатрат)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втДанныеДокумента.СчетУчетаЦФ КАК СчетДт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	втДанныеСчетаКапитализации.СуммаНУЦФ + втДанныеСчетаКапитализации.СуммаПРЦФ + втДанныеСчетаКапитализации.СуммаВРЦФ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУЦФ КАК СуммаНУКт,
	|	втДанныеСчетаКапитализации.СуммаПРЦФ КАК СуммаПРКт,
	|	втДанныеСчетаКапитализации.СуммаВРЦФ КАК СуммаВРКт,
	|	""Принятие к учету НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|ГДЕ
	|	втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное)
	|	)
	|";
	
КонецФункции

Функция ВключениеВРасходыПриПринятииКУчету()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Включение в расходы при принятии к учету(Дт СчетЗатрат:: СчетУчетаНМА)
	|
	|ВЫБРАТЬ
	|
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	втДанныеДокумента.СтатьяРасходов КАК АналитикаУчетаДт,
	|	втДанныеДокумента.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	
	|	втДанныеДокумента.СтатьяРасходов КАК СубконтоДт1,
	|	втДанныеДокумента.Ссылка КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	- втДанныеСчетаКапитализации.СуммаНУ КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеДокумента.СчетУчета КАК СчетКт,
	|	
	|	втДанныеДокумента.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	- втДанныеСчетаКапитализации.СуммаНУ КАК СуммаВРКт,
	|	""Включение в расходы при принятии к учету"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.НематериальныйАктив = втДанныеСчетаКапитализации.НематериальныйАктив
	|ГДЕ
	|	втДанныеДокумента.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|		И втДанныеДокумента.ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.ПриПринятииКУчету)
	|	ИЛИ	втДанныеДокумента.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|		И втДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|";
	
КонецФункции

Функция ПризнаниеДоходовБудущихПериодов()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов будущих периодов (Дт 86:: Кт 98)
	|//
	|ВЫБРАТЬ
	|	
	|	втЦелевоеФинансирование.Ссылка КАК Ссылка,
	|	втЦелевоеФинансирование.Дата КАК Период,
	|	втЦелевоеФинансирование.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втЦелевоеФинансирование.Сумма КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втЦелевоеФинансирование.Подразделение КАК ПодразделениеДт,
	|	втЦелевоеФинансирование.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втЦелевоеФинансирование.Счет КАК СчетДт,
	|	
	|	втЦелевоеФинансирование.Субконто1 КАК СубконтоДт1,
	|	втЦелевоеФинансирование.Субконто2 КАК СубконтоДт2,
	|	втЦелевоеФинансирование.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втЦелевоеФинансирование.Сумма КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втЦелевоеФинансирование.Подразделение ПодразделениеКт,
	|	втЦелевоеФинансирование.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияНМА) КАК СчетКт,
	|	
	|	втЦелевоеФинансирование.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втЦелевоеФинансирование.Сумма КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Признание доходов будущих периодов"" КАК Содержание
	|ИЗ
	|	втЦелевоеФинансированиеИтог КАК втЦелевоеФинансирование
	|	
	|ГДЕ
	|	втЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное)
	|	)
	|";
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
