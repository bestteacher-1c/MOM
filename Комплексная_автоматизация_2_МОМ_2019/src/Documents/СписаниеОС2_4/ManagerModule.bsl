#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ПрочееОприходованиеТоваров.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Команда = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаСписка";
	КонецЕсли;
	
	СписаниеОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Списание ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании	 - ТаблицаЗначений	 - Таблица с командами создания на основании. Для изменения.
//  	См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//  Порядок						 - Число			 - Порядок команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеОС2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СписаниеОС2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СписаниеОС2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	СписаниеОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт


	СписаниеОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.ПеремещениеОС2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса);
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса);
	ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	СписаниеОСЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДляДвижений = Новый Структура;
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтНаработкиОбъектовЭксплуатации(ТекстыЗапроса) Экспорт

	ИмяТаблицы = "ВтНаработкиОбъектовЭксплуатации";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта(), "втПараметрыОС");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации КАК ОбъектЭксплуатации,
	|	НаработкиОбъектовЭксплуатации.Значение КАК Значение
	|ПОМЕСТИТЬ ВтНаработкиОбъектовЭксплуатации
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&Период, МЕСЯЦ),
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					втПараметрыОС КАК СписокОС)) КАК НаработкиОбъектовЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектЭксплуатации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ПредставлениеРеквизитов() Экспорт

	ПредставлениеРеквизитов = Новый Соответствие;

	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ВедетсяРегламентированныйУчетВНА = ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	Если ВедетсяРегламентированныйУчетВНА Тогда
		
		ПредставлениеРеквизитов.Вставить("ОС.СуммаСписанияБУ", СтрШаблон(НСтр("ru = 'Сумма регл. (%1)'"), Строка(ВалютаРегл)));
		ПредставлениеРеквизитов.Вставить("ОС.СуммаСписанияУУ", СтрШаблон(НСтр("ru = 'Сумма упр. (%1)'"), Строка(ВалютаУпр)));
		
	Иначе
		
		Если ВалютаРегл = ВалютаУпр Тогда
			ПредставлениеРеквизитов.Вставить("ОС.СуммаСписанияБУ", НСтр("ru = 'Сумма'"));
			ПредставлениеРеквизитов.Вставить("ОС.СуммаСписанияУУ", НСтр("ru = 'Сумма'"));
		Иначе
			ПредставлениеРеквизитов.Вставить("ОС.СуммаСписанияБУ", СтрШаблон(НСтр("ru = 'Сумма (%1)'"), Строка(ВалютаРегл)));
			ПредставлениеРеквизитов.Вставить("ОС.СуммаСписанияУУ", СтрШаблон(НСтр("ru = 'Сумма (%1)'"), Строка(ВалютаУпр)));
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат ПредставлениеРеквизитов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.СписаниеОС2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
	
	СписаниеОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Ссылка,
	|	ДанныеДокумента.Дата                       КАК Период,
	|	ДанныеДокумента.Номер                      КАК Номер,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС)   КАК ХозяйственнаяОперацияСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеАмортизацииОС) КАК ХозяйственнаяОперацияАмортизация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиСтоимостиОС)   КАК ХозяйственнаяОперацияРезервСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиАмортизацииОС) КАК ХозяйственнаяОперацияРезервАмортизация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства) КАК СтатьяАП_ОС,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки)   КАК СтатьяАП_ПрибыльУбытки,
	|	ДанныеДокумента.Ответственный              КАК Ответственный,
	|	ДанныеДокумента.Комментарий                КАК Комментарий,
	|	ДанныеДокумента.Проведен                   КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления            КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете         КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете          КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ДанныеДокумента.ЧастичнаяЛиквидация        КАК ЧастичнаяЛиквидация,
	|	ДанныеДокумента.СтатьяРасходов             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов          КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов
	|ИЗ
	|	Документ.СписаниеОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КонецДня", Новый Граница(КонецДня(Реквизиты.Период),ВидГраницы.Включая));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СписаниеОС2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Списание ОС'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.СписаниеОС);
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Списание ОС'"));
	
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихДоходовРасходов",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихАктивовПассивов",ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	ЗначенияПараметровПроведения.Вставить("ПорядокУчетаВНАВУпрУчете", Константы.ПорядокУчетаВНАВУпрУчете.Получить());
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство
	|ИЗ
	|	Документ.СписаниеОС2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                         КАК Ссылка,
	|	&Период                         КАК ДатаДокументаИБ,
	|	&Номер                          КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных        КАК ТипСсылки,
	|	&Организация                    КАК Организация,
	|	&ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                    КАК НаправлениеДеятельности,
	|	&Подразделение                  КАК Подразделение,
	|	&Ответственный                  КАК Ответственный,
	|	&Комментарий                    КАК Комментарий,
	|	&Проведен                       КАК Проведен,
	|	&ПометкаУдаления                КАК ПометкаУдаления,
	|	ЛОЖЬ                            КАК ДополнительнаяЗапись,
	|	&Период                         КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                  КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|	ПервоначальныеСведенияОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	&Ссылка КАК ДокументСнятияСУчета,
	|	ПервоначальныеСведенияОС.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|		&КонецДня,
	|		Регистратор <> &Ссылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)
	|	) КАК ПервоначальныеСведенияОС
	|	ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|		И ПервоначальныеСведенияОС.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И НЕ &ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ                       КАК НачислятьАмортизациюУУ
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|		&КонецДня,
	|		Регистратор <> &Ссылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)
	|	) КАК ПорядокУчета
	|	ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете 
	|	И НЕ &ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса)

	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта(), "втПараметрыОС");
	ТекстЗапросаТаблицаВтНаработкиОбъектовЭксплуатации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                      КАК Регистратор,
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	&Период                      КАК ДатаПоследнегоИзменения,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования           КАК СрокИспользования,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения        КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации  КАК МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость     КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.Стоимость, 0) = 0 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|				- ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|				- (ТаблицаОС.СуммаСписанияУУ 
	|					- (ВЫРАЗИТЬ(ЕСТЬNULL(АмортизацияОС.Амортизация, 0) 
	|						* ТаблицаОС.СуммаСписанияУУ 
	|						/ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) КАК ЧИСЛО(31,2))))
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) = 0
	|				ИЛИ &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|				- ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)
	|				- (ТаблицаОС.СуммаСписанияБУ 
	|					- (ВЫРАЗИТЬ(ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) 
	|						* ТаблицаОС.СуммаСписанияБУ 
	|						/ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) КАК ЧИСЛО(31,2))))
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И &Организация = Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)) КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = &Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО ТаблицаОС.ОсновноеСредство = НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И &ЧастичнаяЛиквидация
	|	И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Ссылка                                КАК Регистратор,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК Амортизация,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияПР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияВР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияПРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизацияВРЦФ,
	|
	|	0                                      КАК РезервПереоценкиАмортизации,
	|	0                                      КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация      КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                        КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                     КАК КорАналитикаРасходов,
	|	ТаблицаОС.НачислятьИзнос               КАК НачислятьИзнос
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Ссылка                                 КАК Регистратор,
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	0                                       КАК Амортизация,
	|	0                                       КАК АмортизацияРегл,
	|	0                                       КАК АмортизацияНУ,
	|	0                                       КАК АмортизацияПР,
	|	0                                       КАК АмортизацияВР,
	|	0                                       КАК АмортизацияЦФ,
	|	0                                       КАК АмортизацияНУЦФ,
	|	0                                       КАК АмортизацияПРЦФ,
	|	0                                       КАК АмортизацияВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.РезервПереоценкиАмортизации 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                   КАК РезервПереоценкиАмортизации,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.РезервПереоценкиАмортизацииРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                   КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                         КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.НачислятьИзнос                КАК НачислятьИзнос
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиАмортизации <> 0
	|		ИЛИ ТаблицаОС.РезервПереоценкиАмортизацииРегл <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК Стоимость,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                   КАК СтоимостьЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВРЦФ,
	|
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|	0                                      КАК АмортизационнаяПремия,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация      КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                        КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                     КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание остаточной стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК Стоимость,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВРЦФ,
	|
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизационнаяПремия 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК АмортизационнаяПремия,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияСтоимость        КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                        КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                     КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	0                                      КАК Стоимость,
	|	0                                      КАК СтоимостьРегл,
	|	0                                      КАК СтоимостьНУ,
	|	0                                      КАК СтоимостьПР,
	|	0                                      КАК СтоимостьВР,
	|	0                                      КАК СтоимостьЦФ,
	|	0                                      КАК СтоимостьНУЦФ,
	|	0                                      КАК СтоимостьПРЦФ,
	|	0                                      КАК СтоимостьВРЦФ,
	|
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|	0                                      КАК АмортизационнаяПремия,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.РезервПереоценкиСтоимости 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК РезервПереоценкиСтоимости,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.РезервПереоценкиСтоимостиРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияРезервСтоимость  КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                        КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                     КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	&ОтражатьВУпрУчете И ТаблицаОС.РезервПереоценкиСтоимости <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ТаблицаОС.РезервПереоценкиСтоимостиРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание предварительной стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	0                                      КАК Стоимость,
	|	0                                      КАК СтоимостьРегл,
	|	0                                      КАК СтоимостьНУ,
	|	0                                      КАК СтоимостьПР,
	|	0                                      КАК СтоимостьВР,
	|	0                                      КАК СтоимостьЦФ,
	|	0                                      КАК СтоимостьНУЦФ,
	|	0                                      КАК СтоимостьПРЦФ,
	|	0                                      КАК СтоимостьВРЦФ,
	|
	|	СтоимостьОС.ПредварительнаяСтоимость     КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимостьРегл КАК ПредварительнаяСтоимостьРегл,
	|	0                                      КАК АмортизационнаяПремия,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                           КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                           КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОбъектУчета
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|	И &ОтражатьВУпрУчете
	|	И (СтоимостьОС.ПредварительнаяСтоимость <> 0
	|		ИЛИ СтоимостьОС.ПредварительнаяСтоимостьРегл <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() +
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&СтатьяРасходов                                 КАК СтатьяРасходов,
	|	&АналитикаРасходов                              КАК АналитикаРасходов,
	|	ТаблицаОС.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК СуммаСНДС,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК СуммаБезНДС,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК СуммаБезНДСУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьРегл + ТаблицаОС.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК СуммаСНДСРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьРегл + ТаблицаОС.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК СуммаБезНДСРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьПР + ТаблицаОС.СтоимостьПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК ПостояннаяРазница,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьВР + ТаблицаОС.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                           КАК ВременнаяРазница,
	|
	|	&ХозяйственнаяОперацияСтоимость                 КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов();
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() +
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ТаблицаОС.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ТаблицаОС.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|
	|	0 КАК НДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СтоимостьРегл + ТаблицаОС.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СтоимостьПР + ТаблицаОС.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СтоимостьВР + ТаблицаОС.СтоимостьВРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	&ХозяйственнаяОперацияСтоимость КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&ХозяйственнаяОперацияСтоимость        КАК ХозяйственнаяОперация,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяРасходов                        КАК Статья,
	|	&АналитикаАктивовПассивов              КАК Аналитика,
	|
	|	ТаблицаОС.Стоимость                    КАК Сумма
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Стоимость <> 0
	|	И ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Доходы от целевого финансирования
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&ХозяйственнаяОперацияСтоимость        КАК ХозяйственнаяОперация,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_ОС                           КАК Статья,
	|	ТаблицаОС.ОсновноеСредство             КАК Аналитика,
	|
	|	ТаблицаОС.АмортизацияЦФ                КАК Сумма
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.АмортизацияЦФ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_ОС                            КАК Статья,
	|	ТаблицаОС.ОсновноеСредство              КАК Аналитика,
	|
	|	ТаблицаОС.РезервПереоценкиАмортизации   КАК Сумма
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиАмортизации <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&ХозяйственнаяОперацияРезервСтоимость  КАК ХозяйственнаяОперация,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_ОС                           КАК Статья,
	|	ТаблицаОС.ОсновноеСредство             КАК Аналитика,
	|
	|	ТаблицаОС.РезервПереоценкиСтоимости    КАК Сумма
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиСтоимости <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание амортизации
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияАмортизация КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	&АналитикаРасходов                КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.АмортизацияРегл + ТаблицаОС.АмортизацияЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРегл
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание остаточной стоимости
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВложенныйЗапрос.Статья КАК Статья,
	|	ВложенныйЗапрос.АналитикаДоходов КАК АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|	ВложенныйЗапрос.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	ВложенныйЗапрос.КорПодразделение КАК КорПодразделение,
	|	ВложенныйЗапрос.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВложенныйЗапрос.КорСтатья КАК КорСтатья,
	|	ВложенныйЗапрос.КорАналитикаДоходов КАК КорАналитикаДоходов,
	|	ВложенныйЗапрос.КорАналитикаРасходов КАК КорАналитикаРасходов,
	|	ВложенныйЗапрос.КорАналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	|	ВложенныйЗапрос.КорГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА СУММА(ВложенныйЗапрос.Сумма) 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА СУММА(ВложенныйЗапрос.СуммаУпр) 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА СУММА(ВложенныйЗапрос.СуммаРегл) 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРегл
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Период                           КАК Период,
	|		&ХозяйственнаяОперацияСтоимость   КАК ХозяйственнаяОперация,
	|		&Организация                      КАК Организация,
	|
	|		&Подразделение                    КАК Подразделение,
	|		ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		&СтатьяАП_ОС                      КАК Статья,
	|		НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|		НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|		ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|		ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|		&Подразделение                    КАК КорПодразделение,
	|		ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|		&СтатьяРасходов                   КАК КорСтатья,
	|		НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|		&АналитикаРасходов                КАК КорАналитикаРасходов,
	|		НЕОПРЕДЕЛЕНО                      КАК КорАналитикаАктивовПассивов,
	|		ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|		ТаблицаОС.Стоимость               КАК Сумма,
	|		ТаблицаОС.Стоимость               КАК СуммаУпр,
	|		ТаблицаОС.СтоимостьРегл + ТаблицаОС.СтоимостьЦФ КАК СуммаРегл
	|	ИЗ
	|		втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		&Период                           КАК Период,
	|		&ХозяйственнаяОперацияСтоимость КАК ХозяйственнаяОперация,
	|		&Организация                      КАК Организация,
	|
	|		&Подразделение                    КАК Подразделение,
	|		ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		&СтатьяАП_ОС                      КАК Статья,
	|		НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|		НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|		ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|		ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|		&Подразделение                    КАК КорПодразделение,
	|		ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|		&СтатьяРасходов                   КАК КорСтатья,
	|		НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|		&АналитикаРасходов                КАК КорАналитикаРасходов,
	|		НЕОПРЕДЕЛЕНО                      КАК КорАналитикаАктивовПассивов,
	|		ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|		ТаблицаОС.Амортизация             КАК Сумма,
	|		ТаблицаОС.Амортизация             КАК СуммаУпр,
	|		ТаблицаОС.АмортизацияРегл + ТаблицаОС.АмортизацияЦФ КАК СуммаРегл
	|	ИЗ
	|		втАмортизацияСписанияОС КАК ТаблицаОС) ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ХозяйственнаяОперация,
	|	ВложенныйЗапрос.Организация,
	|
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.Статья,
	|	ВложенныйЗапрос.АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаРасходов,
	|	ВложенныйЗапрос.АналитикаАктивовПассивов,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета,
	|
	|	ВложенныйЗапрос.КорПодразделение,
	|	ВложенныйЗапрос.КорНаправлениеДеятельности,
	|	ВложенныйЗапрос.КорСтатья,
	|	ВложенныйЗапрос.КорАналитикаДоходов,
	|	ВложенныйЗапрос.КорАналитикаРасходов,
	|	ВложенныйЗапрос.КорАналитикаАктивовПассивов,
	|	ВложенныйЗапрос.КорГруппаФинансовогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Признание доходов от целевого финансирования
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДвижениеДоходовЗаСчетАктивовПассивов) КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаОС.СтатьяДоходов           КАК Статья,
	|	ТаблицаОС.АналитикаДоходов        КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ВЫБОР КОГДА ТаблицаОС.СтоимостьЦФ = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаОС.Стоимость * ТаблицаОС.СтоимостьРегл / ТаблицаОС.СтоимостьЦФ
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ТаблицаОС.СтоимостьЦФ = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаОС.Стоимость * ТаблицаОС.СтоимостьРегл / ТаблицаОС.СтоимостьЦФ
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаОС.СтоимостьЦФ             КАК СуммаРегл
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.СтоимостьЦФ <> 0
	|	И &ОтражатьВРеглУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание резерва переоценки амортизации ОС
	|ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                            КАК Статья,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство              КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                          КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбытки                 КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета        КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаОС.РезервПереоценкиАмортизации   КАК Сумма,
	|	ТаблицаОС.РезервПереоценкиАмортизации   КАК СуммаУпр,
	|	0                                       КАК СуммаРегл
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиАмортизации <> 0
	|	И &ОтражатьВУпрУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание резерва переоценки стоимости ОС
	|ВЫБРАТЬ
	|	&Период                               КАК Период,
	|	&ХозяйственнаяОперацияРезервСтоимость КАК ХозяйственнаяОперация,
	|	&Организация                          КАК Организация,
	|
	|	&Подразделение                        КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбытки               КАК Статья,
	|	НЕОПРЕДЕЛЕНО                          КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                          КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                          КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                        КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                          КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                          КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                          КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство            КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаОС.РезервПереоценкиСтоимости   КАК Сумма,
	|	ТаблицаОС.РезервПереоценкиСтоимости   КАК СуммаУпр,
	|	0                                     КАК СуммаРегл
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиСтоимости <> 0
	|	И &ОтражатьВУпрУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса)

	ИмяТаблицы = "втСтоимостьСписанияОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	&Ссылка                               КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство            КАК ОсновноеСредство,
	|
	|	АмортизацияОС.Амортизация             КАК Стоимость,
	|	АмортизацияОС.АмортизацияРегл         КАК СтоимостьРегл,
	|	АмортизацияОС.АмортизацияНУ           КАК СтоимостьНУ,
	|	АмортизацияОС.АмортизацияПР           КАК СтоимостьПР,
	|	АмортизацияОС.АмортизацияВР           КАК СтоимостьВР,
	|	АмортизацияОС.АмортизацияЦФ           КАК СтоимостьЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ         КАК СтоимостьНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ         КАК СтоимостьПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ         КАК СтоимостьВРЦФ,
	|
	|	СтоимостьОС.АмортизационнаяПремия         КАК АмортизационнаяПремия,
	|	СтоимостьОС.РезервПереоценкиСтоимости     КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|
	|	1                                     КАК ДоляСписания,
	|	1                                     КАК ДоляСписанияБУ,
	|	1                                     КАК ДоляСписанияЦФ,
	|	1                                     КАК ДоляСписанияНУ,
	|	1                                     КАК ДоляСписанияНУЦФ,
	|	1                                     КАК ДоляСписанияПР,
	|	1                                     КАК ДоляСписанияПРЦФ,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ТаблицаОС.НачислятьИзнос              КАК НачислятьИзнос,
	|	ТаблицаОС.СтатьяДоходов               КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов            КАК АналитикаДоходов
	|
	|ПОМЕСТИТЬ втСтоимостьСписанияОС
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОбъектУчета
	|		И СтоимостьОС.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО ТаблицаОС.ОсновноеСредство = АмортизацияОС.ОбъектУчета
	|		И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание частичной стоимости
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка                    КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ТаблицаОС.СуммаСписанияУУ  КАК Стоимость,
	|	ТаблицаОС.СуммаСписанияБУ - ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ КАК СтоимостьРегл,
	|	ТаблицаОС.СуммаСписанияНУ - ТаблицаОС.СуммаСписанияНУ * СтоимостьОС.ДоляЦФНУ КАК СтоимостьНУ,
	|	ТаблицаОС.СуммаСписанияПР - ТаблицаОС.СуммаСписанияПР * СтоимостьОС.ДоляЦФБУ КАК СтоимостьПР,
	|
	|	ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияБУ - ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияНУ - ТаблицаОС.СуммаСписанияНУ * СтоимостьОС.ДоляЦФНУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияПР - ТаблицаОС.СуммаСписанияПР * СтоимостьОС.ДоляЦФБУ) КАК ЧИСЛО(31,2)) КАК СтоимостьВР,
	|
	|	ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ КАК СтоимостьЦФ,
	|	ТаблицаОС.СуммаСписанияНУ * СтоимостьОС.ДоляЦФНУ КАК СтоимостьНУЦФ,
	|	ТаблицаОС.СуммаСписанияПР * СтоимостьОС.ДоляЦФБУ КАК СтоимостьПРЦФ,
	|
	|	ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияНУ * СтоимостьОС.ДоляЦФНУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияПР * СтоимостьОС.ДоляЦФБУ) КАК ЧИСЛО(31,2)) КАК СтоимостьВРЦФ,
	|
	|	СтоимостьОС.АмортизационнаяПремия КАК АмортизационнаяПремия,
	|
	|	СтоимостьОС.РезервПереоценкиСтоимости
	|	* ВЫБОР КОГДА СтоимостьОС.Стоимость ЕСТЬ NULL ИЛИ СтоимостьОС.Стоимость = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаОС.СуммаСписанияУУ / СтоимостьОС.Стоимость
	|	КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ (ТаблицаОС.СуммаСписанияБУ - ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ) / СтоимостьОС.СтоимостьРегл
	|	КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаОС.СуммаСписанияУУ / СтоимостьОС.Стоимость КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписания,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияБУ - ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ) / СтоимостьОС.СтоимостьРегл КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияБУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияБУ * СтоимостьОС.ДоляЦФБУ) / СтоимостьОС.СтоимостьЦФ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияНУ - ТаблицаОС.СуммаСписанияНУ * СтоимостьОС.ДоляЦФНУ) / СтоимостьОС.СтоимостьНУ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияНУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияНУ * СтоимостьОС.ДоляЦФНУ) / СтоимостьОС.СтоимостьНУЦФ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияНУЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияПР - ТаблицаОС.СуммаСписанияПР * СтоимостьОС.ДоляЦФБУ) / СтоимостьОС.СтоимостьРегл КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияПР,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаОС.СуммаСписанияПР * СтоимостьОС.ДоляЦФБУ) / СтоимостьОС.СтоимостьЦФ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияПРЦФ,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаОС.НачислятьИзнос КАК НачислятьИзнос,
	|	ТаблицаОС.СтатьяДоходов               КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов            КАК АналитикаДоходов
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОбъектУчета
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса)

	ИмяТаблицы = "втАмортизацияСписанияОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	&Ссылка                                   КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство                КАК ОсновноеСредство,
	|
	|	АмортизацияОС.Амортизация                 КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации     КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация         КАК ХозяйственнаяОперация,
	|	ТаблицаОС.НачислятьИзнос                  КАК НачислятьИзнос
	|
	|ПОМЕСТИТЬ втАмортизацияСписанияОС
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание частичной амортизации
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка                                КАК Ссылка,
	|	СтоимостьСписанияОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	// Амортизация
	|	ВЫБОР
	|		КОГДА АмортизацияОС.Амортизация = ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|			ТОГДА СтоимостьСписанияОС.Стоимость
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.Амортизация * СтоимостьСписанияОС.ДоляСписания КАК ЧИСЛО(31,2))     
	|	КОНЕЦ КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияРегл = ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияРегл * СтоимостьСписанияОС.ДоляСписанияБУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияНУ = ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьНУ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияНУ * СтоимостьСписанияОС.ДоляСписанияНУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияПР = ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьПР
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияПР * СтоимостьСписанияОС.ДоляСписанияПР КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияПР,
	|
	// АмортизацияВР
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияРегл = ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияРегл * СтоимостьСписанияОС.ДоляСписанияБУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияОС.АмортизацияНУ = ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
	|					ТОГДА СтоимостьСписанияОС.СтоимостьНУ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияНУ * СтоимостьСписанияОС.ДоляСписанияНУ КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияОС.АмортизацияПР = ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0)
	|					ТОГДА СтоимостьСписанияОС.СтоимостьПР
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияПР * СтоимостьСписанияОС.ДоляСписанияПР КАК ЧИСЛО(31,2))
	|			КОНЕЦ КАК АмортизацияВР,
	|
	// АмортизацияЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияЦФ = ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияЦФ  * СтоимостьСписанияОС.ДоляСписанияЦФ  КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияЦФ,
	|
	
	// АмортизацияНУЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияЦФ = ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияНУЦФ * СтоимостьСписанияОС.ДоляСписанияНУЦФ  КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияНУЦФ,
	|
	
	// АмортизацияПРЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияПРЦФ = ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьПРЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияПРЦФ * СтоимостьСписанияОС.ДоляСписанияПРЦФ  КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|
	// АмортизацияВРЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияОС.АмортизацияЦФ = ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияОС.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияЦФ  * СтоимостьСписанияОС.ДоляСписанияЦФ  КАК ЧИСЛО(31,2))
	|	КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияОС.АмортизацияЦФ = ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|					ТОГДА СтоимостьСписанияОС.СтоимостьЦФ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияНУЦФ * СтоимостьСписанияОС.ДоляСписанияНУЦФ  КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияОС.АмортизацияПРЦФ = ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0)
	|					ТОГДА СтоимостьСписанияОС.СтоимостьПРЦФ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияОС.АмортизацияПРЦФ * СтоимостьСписанияОС.ДоляСписанияПРЦФ  КАК ЧИСЛО(31,2))
	|			КОНЕЦ КАК АмортизацияВРЦФ,
	|
	|	ВЫРАЗИТЬ(АмортизацияОС.РезервПереоценкиАмортизации * СтоимостьСписанияОС.ДоляСписания КАК ЧИСЛО(31,2))         КАК РезервПереоценкиАмортизации,
	|	ВЫРАЗИТЬ(АмортизацияОС.РезервПереоценкиАмортизацииРегл * СтоимостьСписанияОС.ДоляСписанияБУ КАК ЧИСЛО(31,2))   КАК РезервПереоценкиАмортизацииРегл,
	|
	|	СтоимостьСписанияОС.ГруппаФинансовогоУчета                 КАК ГруппаФинансовогоУчета,
	|	СтоимостьСписанияОС.НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация                          КАК ХозяйственнаяОперация,
	|	СтоимостьСписанияОС.НачислятьИзнос                         КАК НачислятьИзнос
	|ИЗ
	|	ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияОС КАК СтоимостьСписанияОС
	|		ПО АмортизацияОС.ОбъектУчета = СтоимостьСписанияОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = СтоимостьСписанияОС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО АмортизацияОС.ОбъектУчета = СтоимостьОС.ОбъектУчета
	|			И АмортизацияОС.Ссылка = СтоимостьОС.Ссылка
	|ГДЕ
	|	&ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса) Экспорт

	ИмяТаблицы = "втОстаточнаяСтоимостьОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Ссылка                                                   КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство                                КАК ОсновноеСредство,
	|
	|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)         КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) КАК СтоимостьРегл,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0)     КАК СтоимостьПР,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьВР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВР, 0)     КАК СтоимостьВР,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ЕСТЬNULL(СтоимостьОС.АмортизационнаяПремия, 0)            КАК АмортизационнаяПремия,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета                          КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|	ТаблицаОС.СтатьяДоходов                                   КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов                                КАК АналитикаДоходов
	|
	|ПОМЕСТИТЬ втОстаточнаяСтоимостьОС
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОбъектУчета
	|		И СтоимостьОС.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО ТаблицаОС.ОсновноеСредство = АмортизацияОС.ОбъектУчета
	|		И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|	И (ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьВР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВР, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Частичное списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Ссылка                                                 КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство                              КАК ОсновноеСредство,
	|
	|	ТаблицаОС.Стоимость - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)         КАК Стоимость,
	|	ТаблицаОС.СтоимостьРегл - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) КАК СтоимостьРегл,
	|	ТаблицаОС.СтоимостьНУ - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|	ТаблицаОС.СтоимостьПР - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0)     КАК СтоимостьПР,
	|	ТаблицаОС.СтоимостьВР - ЕСТЬNULL(АмортизацияОС.АмортизацияВР, 0)     КАК СтоимостьВР,
	|	ТаблицаОС.СтоимостьЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ТаблицаОС.СтоимостьНУЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ТаблицаОС.СтоимостьПРЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ТаблицаОС.СтоимостьВРЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ТаблицаОС.АмортизационнаяПремия                         КАК АмортизационнаяПремия,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета                        КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                       КАК НаправлениеДеятельности,
	|	ТаблицаОС.СтатьяДоходов                                 КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов                              КАК АналитикаДоходов
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияОС КАК АмортизацияОС
	|		ПО ТаблицаОС.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|	И (ТаблицаОС.Стоимость - ЕСТЬNULL(АмортизацияОС.Амортизация, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьРегл - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьНУ - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьПР - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьВР - ЕСТЬNULL(АмортизацияОС.АмортизацияВР, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьНУЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьПРЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ ТаблицаОС.СтоимостьВРЦФ - ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СписаниеОСЛокализация.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаМестонахождениеОС(ТекстыЗапроса, ПолноеИмяОбъекта());
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, ПолноеИмяОбъекта());
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаОС.НомерСтроки                  КАК НомерСтроки,
		|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
		|
		|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
		|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
		|	ПорядокУчета.ПоказательНаработки       КАК ПоказательНаработки,
		|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
		|
		|	МестонахождениеОС.Организация          КАК Организация,
		|	МестонахождениеОС.МОЛ                  КАК МОЛ,
		|	МестонахождениеОС.Местонахождение      КАК Местонахождение,
		|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
		|
		|	ЛОЖЬ                                   КАК НачислятьИзнос,
		|
		|	НЕОПРЕДЕЛЕНО                           КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО                           КАК АналитикаДоходов,
		|
		|	ТаблицаОС.СуммаСписанияУУ              КАК СуммаСписанияУУ,
		|	ТаблицаОС.СуммаСписанияБУ              КАК СуммаСписанияБУ,
		|	ТаблицаОС.СуммаСписанияНУ              КАК СуммаСписанияНУ,
		|	ТаблицаОС.СуммаСписанияПР              КАК СуммаСписанияПР,
		|	ТаблицаОС.СуммаСписанияВР              КАК СуммаСписанияВР
		|	
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчета
		|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
		|		ПО ТаблицаОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
		|
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Ссылка";
	
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ПолноеИмяОбъекта()

	Возврат "Документ.СписаниеОС2_4";

КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат СписаниеОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат СписаниеОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СписаниеОСЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Документ.СписаниеОС2_4";
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Документ.СписаниеОС2_4");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если ДанныеОбъекта = Неопределено Тогда
				ЗафиксироватьТранзакцию();
 				Продолжить;
 			КонецЕсли;
			
			Если ДанныеОбъекта.УдалитьХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеОСпоИнвентаризации Тогда
				ДанныеОбъекта.УдалитьХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПустаяСсылка();
				ДанныеОбъекта.ДокументНаОсновании = Истина;
			КонецЕсли;
			
			Если ДанныеОбъекта.Модифицированность() Тогда
	 			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
	 			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДанныеОбъекта);
			КонецЕсли; 
			
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеОС2_4.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СписаниеОС2_4 КАК СписаниеОС2_4
	|ГДЕ
	|	СписаниеОС2_4.УдалитьХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеОСпоИнвентаризации)";
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
