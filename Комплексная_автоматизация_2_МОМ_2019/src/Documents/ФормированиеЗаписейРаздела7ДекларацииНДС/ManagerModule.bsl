#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 23, 0);
	
КонецФункции

// Подготавливает данные для заполнения документа
//
// Параметры:
//   СтруктураПараметров - Струкутра - структура входных параметров с ключами:
//     * Организация           - СправочникСсылка.Организация - организация, для которой будут получены данные для
//                                                              заполнения документа.
//     * Дата                  - Дата - дата, на которую будут получены данные для заполнения документа.
//   АдресХранилища - Строка - адрес временного хранилища для помещения результата выполнения.
Процедура ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища) Экспорт
	
	ДанныеДляЗаполнения = Новый Структура;
	НеоблагаемыеНДСОперации = НовыйНеоблагаемыеНДСОперации();
	ПодтверждающиеДокументы = НовыйПодтверждающиеДокументы();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(СтруктураПараметров.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("ПустойКод", Справочники.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор КАК Регистратор,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель
	|ПОМЕСТИТЬ ПродажиБезНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияПоЗаймамВыданным.Регистратор КАК Регистратор,
	|	НачисленияПоЗаймамВыданным.СуммаРеглОборот КАК Сумма,
	|	ВЫБОР
	|		КОГДА НачисленияПоЗаймамВыданным.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА НачисленияПоЗаймамВыданным.Договор.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ КАК КодОперации,
	|	НачисленияПоЗаймамВыданным.Контрагент КАК Контрагент,
	|	НачисленияПоЗаймамВыданным.Договор КАК Договор
	|ПОМЕСТИТЬ НачисленияПоЗаймам
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентДоходыРасходы.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)) КАК НачисленияПоЗаймамВыданным
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленияПоЗаймамВыданным.Регистратор,
	|	НачисленияПоЗаймамВыданным.СуммаРеглОборот,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(НачисленияПоЗаймамВыданным.АналитикаДоходов КАК Справочник.ДоговорыКредитовИДепозитов).КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВЫРАЗИТЬ(НачисленияПоЗаймамВыданным.АналитикаДоходов КАК Справочник.ДоговорыКредитовИДепозитов).КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	НачисленияПоЗаймамВыданным.Контрагент,
	|	ВЫРАЗИТЬ(НачисленияПоЗаймамВыданным.АналитикаДоходов КАК Справочник.ДоговорыКредитовИДепозитов)
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)) КАК НачисленияПоЗаймамВыданным
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор КАК Регистратор,
	|	СУММА(ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиРеглОборот) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ КАК КодОперации,
	|	ВыручкаИСебестоимостьПродажОбороты.Договор КАК Договор,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВыручкаИСебестоимостьПродажОбороты.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|					ИЛИ ВыручкаИСебестоимостьПродажОбороты.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕстьТовары
	|ПОМЕСТИТЬ ПродажиПоКодам
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериода, &КонецПериода, Регистратор, АналитикаУчетаПоПартнерам.Организация = &Организация) КАК ВыручкаИСебестоимостьПродажОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиБезНДС КАК ПродажиБезНДС
	|		ПО ВыручкаИСебестоимостьПродажОбороты.Регистратор = ПродажиБезНДС.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор,
	|	ВЫБОР
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	ВыручкаИСебестоимостьПродажОбороты.Договор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияПрочихАктивов.Регистратор,
	|	РеализацияПрочихАктивов.СуммаРеглБезНДСОборот,
	|	ВЫБОР
	|		КОГДА РеализацияПрочихАктивов.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА РеализацияПрочихАктивов.Договор.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	РеализацияПрочихАктивов.Договор,
	|	0
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентДоходыРасходы.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК РеализацияПрочихАктивов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиБезНДС КАК ПродажиБезНДС
	|		ПО РеализацияПрочихАктивов.Регистратор = ПродажиБезНДС.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗакупкиОбороты.Регистратор,
	|	ЗакупкиОбороты.СуммаРеглБезНДСОборот,
	|	ВЫБОР
	|		КОГДА ЗакупкиОбороты.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ЗакупкиОбороты.Договор.КодРаздел7ДекларацииНДС
	|		КОГДА ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	ЗакупкиОбороты.Договор,
	|	1
	|ИЗ
	|	РегистрНакопления.Закупки.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК ЗакупкиОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиБезНДС КАК ПродажиБезНДС
	|		ПО ЗакупкиОбороты.Регистратор = ПродажиБезНДС.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиПоКодам.Регистратор КАК Регистратор,
	|	СУММА(ПродажиПоКодам.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ИтогиПоДокументам
	|ИЗ
	|	ПродажиПоКодам КАК ПродажиПоКодам
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиПоКодам.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(ПродажиПоКодам.Сумма) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВложенныйЗапрос.СуммаПриобретения) КАК СуммаПриобретения,
	|	СУММА(ВложенныйЗапрос.НДС) КАК НДС,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ПрямыеРасходы
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровОрганизаций.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииТоваровОрганизаций.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК СуммаПриобретения,
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровОрганизаций.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииТоваровОрганизаций.НДСРегл
	|			КОНЕЦ) КАК НДС,
	|		ПартииТоваровОрганизаций.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК ПартииТоваровОрганизаций
	|	ГДЕ
	|		ПартииТоваровОрганизаций.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииТоваровОрганизаций.Активность
	|		И ПартииТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииТоваровОрганизаций.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииЗатратНаВыпуск.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииЗатратНаВыпуск.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииЗатратНаВыпуск.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииЗатратНаВыпуск.НДСРегл
	|			КОНЕЦ),
	|		ПартииЗатратНаВыпуск.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК ПартииЗатратНаВыпуск
	|	ГДЕ
	|		ПартииЗатратНаВыпуск.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииЗатратНаВыпуск.Активность
	|		И ПартииЗатратНаВыпуск.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииЗатратНаВыпуск.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииРасходовНаСебестоимостьТоваров.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииРасходовНаСебестоимостьТоваров.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииРасходовНаСебестоимостьТоваров.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииРасходовНаСебестоимостьТоваров.НДСРегл
	|			КОНЕЦ),
	|		ПартииРасходовНаСебестоимостьТоваров.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ПартииРасходовНаСебестоимостьТоваров
	|	ГДЕ
	|		ПартииРасходовНаСебестоимостьТоваров.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииРасходовНаСебестоимостьТоваров.Активность
	|		И ПартииРасходовНаСебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииРасходовНаСебестоимостьТоваров.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПрочихРасходов.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииПрочихРасходов.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПрочихРасходов.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииПрочихРасходов.НДСРегл
	|			КОНЕЦ),
	|		ПартииПрочихРасходов.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|	ГДЕ
	|		ПартииПрочихРасходов.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииПрочихРасходов.Активность
	|		И ПартииПрочихРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииПрочихРасходов.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПроизводственныхЗатрат.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииПроизводственныхЗатрат.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПроизводственныхЗатрат.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииПроизводственныхЗатрат.НДСРегл
	|			КОНЕЦ),
	|		ПартииПроизводственныхЗатрат.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК ПартииПроизводственныхЗатрат
	|	ГДЕ
	|		ПартииПроизводственныхЗатрат.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииПроизводственныхЗатрат.Активность
	|		И ПартииПроизводственныхЗатрат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииПроизводственныхЗатрат.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровПереданныеНаКомиссию.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииТоваровПереданныеНаКомиссию.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровПереданныеНаКомиссию.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииТоваровПереданныеНаКомиссию.НДСРегл
	|			КОНЕЦ),
	|		ПартииТоваровПереданныеНаКомиссию.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ПартииТоваровПереданныеНаКомиссию
	|	ГДЕ
	|		ПартииТоваровПереданныеНаКомиссию.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииТоваровПереданныеНаКомиссию.Активность
	|		И ПартииТоваровПереданныеНаКомиссию.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииТоваровПереданныеНаКомиссию.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ДетализацияПартийТоваровДляНДСиУСН.СтоимостьБезНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ДетализацияПартийТоваровДляНДСиУСН.НДС
	|			КОНЕЦ),
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	ИЗ
	|		РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН КАК ДетализацияПартийТоваровДляНДСиУСН
	|	ГДЕ
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ДетализацияПартийТоваровДляНДСиУСН.Активность
	|		И ДетализацияПартийТоваровДляНДСиУСН.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаДокументаПоступления.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА ДетализацияПартийТоваровДляНДСиУСН.СтоимостьБезНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаДокументаПоступления.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ДетализацияПартийТоваровДляНДСиУСН.НДС
	|			КОНЕЦ),
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	ИЗ
	|		РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4 КАК ДетализацияПартийТоваровДляНДСиУСН
	|	ГДЕ
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ДетализацияПартийТоваровДляНДСиУСН.Активность
	|		И ДетализацияПартийТоваровДляНДСиУСН.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиБезНДС.Покупатель КАК Контрагент,
	|	ПродажиБезНДС.Регистратор КАК ДокументРеализации,
	|	СУММА(ЕСТЬNULL(ПродажиБезНДС.Сумма * ПродажиПоКодам.Сумма / ИтогиПоДокументам.Сумма, ПродажиБезНДС.Сумма)) КАК СуммаРеализации,
	|	ЕСТЬNULL(ПродажиПоКодам.КодОперации, &ПустойКод) КАК КодОперации,
	|	СУММА(ЕСТЬNULL(ПрямыеРасходы.СуммаПриобретения, 0)) КАК СуммаПриобретения,
	|	СУММА(ЕСТЬNULL(ПрямыеРасходы.НДС, 0)) КАК НДС,
	|	ПродажиПоКодам.Договор КАК Договор,
	|	ПродажиПоКодам.ЕстьТовары КАК ЕстьТовары
	|ПОМЕСТИТЬ Реализация
	|ИЗ
	|	ПродажиБезНДС КАК ПродажиБезНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогиПоДокументам КАК ИтогиПоДокументам
	|		ПО ПродажиБезНДС.Регистратор = ИтогиПоДокументам.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПродажиПоКодам КАК ПродажиПоКодам
	|		ПО ПродажиБезНДС.Регистратор = ПродажиПоКодам.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПрямыеРасходы КАК ПрямыеРасходы
	|		ПО ПродажиБезНДС.Регистратор = ПрямыеРасходы.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиБезНДС.Покупатель,
	|	ПродажиБезНДС.Регистратор,
	|	ЕСТЬNULL(ПродажиПоКодам.КодОперации, &ПустойКод),
	|	ПродажиПоКодам.Договор,
	|	ПродажиПоКодам.ЕстьТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленияПоЗаймам.Контрагент,
	|	НачисленияПоЗаймам.Регистратор,
	|	СУММА(НачисленияПоЗаймам.Сумма),
	|	НачисленияПоЗаймам.КодОперации,
	|	0,
	|	0,
	|	НачисленияПоЗаймам.Договор,
	|	0
	|ИЗ
	|	НачисленияПоЗаймам КАК НачисленияПоЗаймам
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияПоЗаймам.Контрагент,
	|	НачисленияПоЗаймам.Регистратор,
	|	НачисленияПоЗаймам.КодОперации,
	|	НачисленияПоЗаймам.Договор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Реализация.ДокументРеализации КАК ДокументРеализации,
	|	Реализация.Контрагент КАК Контрагент,
	|	Реализация.Договор КАК Договор,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.СправкаРасчет)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетКомитенту
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.АктВыполненныхРабот
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Акт)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ВозвратТоваровПоставщику
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ВыкупВозвратнойТарыКлиентом
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Накладная)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.НачисленияКредитовИДепозитов
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ЗаписьКнигиПродаж
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.ПустаяСсылка)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетКомиссионера
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.КорректировкаРеализации
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|			ТОГДА ВЫБОР
	|					КОГДА Реализация.ЕстьТовары > 0
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Накладная)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Акт)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.ПустаяСсылка)
	|	КОНЕЦ КАК ТипДокумента,
	|	Реализация.ЕстьТовары КАК ЕстьТовары
	|ПОМЕСТИТЬ ПодтверждающиеДокументы
	|ИЗ
	|	Реализация КАК Реализация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО Реализация.ДокументРеализации = ДанныеПервичныхДокументов.Документ
	|			И ДанныеПервичныхДокументов.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Реализация.ДокументРеализации,
	|	Реализация.Контрагент,
	|	Реализация.Договор,
	|	Реализация.Договор.Номер,
	|	Реализация.Договор.Дата,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Договор),
	|	Реализация.ЕстьТовары
	|ИЗ
	|	Реализация КАК Реализация
	|ГДЕ
	|	НЕ Реализация.Договор.Номер ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРеализации,
	|	Контрагент,
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реализация.Контрагент КАК Контрагент,
	|	Реализация.ДокументРеализации КАК ДокументРеализации,
	|	Реализация.СуммаРеализации КАК СуммаРеализации,
	|	Реализация.КодОперации КАК КодОперации,
	|	Реализация.СуммаПриобретения КАК СуммаПриобретения,
	|	Реализация.НДС КАК НДС,
	|	ПодтверждающиеДокументы.НомерДокумента КАК НомерДокумента,
	|	ПодтверждающиеДокументы.ДатаДокумента КАК ДатаДокумента,
	|	ПодтверждающиеДокументы.ТипДокумента КАК ТипДокумента,
	|	0 КАК ПорядокДокументов,
	|	Реализация.КодОперации.Код КАК КодОперацииКод,
	|	Реализация.КодОперации.Наименование КАК КодОперацииНаименование,
	|	ВЫБОР
	|		КОГДА Реализация.КодОперации.ВключаетсяВРеестрПодтверждающихДокументов
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВключаетсяВРеестрСортировка,
	|	ВЫБОР
	|		КОГДА Реализация.КодОперации.ОперацияНеПодлежитНалогообложению
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СТ149Сортировка
	|ИЗ
	|	Реализация КАК Реализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО Реализация.ДокументРеализации = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
	|		ПО Реализация.Контрагент = ПодтверждающиеДокументы.Контрагент
	|			И Реализация.ДокументРеализации = ПодтверждающиеДокументы.ДокументРеализации
	|			И Реализация.Договор = ПодтверждающиеДокументы.Договор
	|			И Реализация.ЕстьТовары = ПодтверждающиеДокументы.ЕстьТовары
	|ГДЕ
	|	ДанныеПервичныхДокументов.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	СТ149Сортировка,
	|	ВключаетсяВРеестрСортировка,
	|	КодОперацииКод,
	|	КодОперацииНаименование,
	|	ДанныеПервичныхДокументов.ДатаРегистратора,
	|	ПорядокДокументов
	|ИТОГИ
	|	МАКСИМУМ(СуммаРеализации),
	|	МАКСИМУМ(СуммаПриобретения),
	|	МАКСИМУМ(НДС),
	|	МАКСИМУМ(НомерДокумента),
	|	МАКСИМУМ(ДатаДокумента),
	|	МАКСИМУМ(ТипДокумента)
	|ПО
	|	КодОперации,
	|	Контрагент,
	|	Реализация.Договор,
	|	ДокументРеализации";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		ДанныеДляЗаполнения.Вставить("НеоблагаемыеНДСОперации",         НеоблагаемыеНДСОперации);
		ДанныеДляЗаполнения.Вставить("ПодтверждающиеДокументы",         ПодтверждающиеДокументы);
		ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	ВыборкаПоКодам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоКодам.Следующий() Цикл
		
		ВыборкаПоКонтрагентам = ВыборкаПоКодам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоКонтрагентам.Следующий() Цикл
			
			ВыборкаПоДоговорам = ВыборкаПоКонтрагентам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоДоговорам.Следующий() Цикл
			
				ВыборкаПоДокументам = ВыборкаПоДоговорам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоДокументам.Следующий() Цикл
					
					КлючСтроки = ПолучитьНовыйКлючСтроки(НеоблагаемыеНДСОперации);
					
					СтрокаНеоблагаемыеНДСОперации = НеоблагаемыеНДСОперации.Добавить();
					СтрокаНеоблагаемыеНДСОперации.КодОперации = ВыборкаПоДокументам.КодОперации;
					СтрокаНеоблагаемыеНДСОперации.ДокументРеализации = ВыборкаПоДокументам.ДокументРеализации;
					СтрокаНеоблагаемыеНДСОперации.Контрагент = ВыборкаПоДокументам.Контрагент;
					СтрокаНеоблагаемыеНДСОперации.СуммаРеализации = ВыборкаПоДокументам.СуммаРеализации;
					СтрокаНеоблагаемыеНДСОперации.СуммаПриобретения = ВыборкаПоДокументам.СуммаПриобретения;
					СтрокаНеоблагаемыеНДСОперации.НДСПрямой= ВыборкаПоДокументам.НДС;
					СтрокаНеоблагаемыеНДСОперации.КлючСтроки = КлючСтроки;
					
					ВыборкаПоСтрокам = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаПоСтрокам.Следующий() Цикл
						Если ЗначениеЗаполнено(ВыборкаПоСтрокам.ТипДокумента) Тогда 
							СтрокаПодтверждающиеДокументы = ПодтверждающиеДокументы.Добавить();
							СтрокаПодтверждающиеДокументы.ТипДокумента = ВыборкаПоСтрокам.ТипДокумента;
							СтрокаПодтверждающиеДокументы.НомерДокумента = ВыборкаПоСтрокам.НомерДокумента;
							СтрокаПодтверждающиеДокументы.ДатаДокумента= ВыборкаПоСтрокам.ДатаДокумента;
							СтрокаПодтверждающиеДокументы.КлючСтроки = КлючСтроки;
						КонецЕсли;
					КонецЦикла;
					
				КонецЦикла;
			
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Отдельно распределим включенный в стоимость НДС по кодам операций
	МассивКоэффициентов = НеоблагаемыеНДСОперации.ВыгрузитьКолонку("СуммаРеализации");
	РаспределитьНДСПоКодамРаздела7(СтруктураПараметров, НеоблагаемыеНДСОперации, МассивКоэффициентов);
	
	ДанныеДляЗаполнения.Вставить("НеоблагаемыеНДСОперации",         НеоблагаемыеНДСОперации);
	ДанныеДляЗаполнения.Вставить("ПодтверждающиеДокументы",         ПодтверждающиеДокументы);
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);
	
КонецПроцедуры

Функция ПолучитьНовыйКлючСтроки(НеоблагаемыеНДСОперации) Экспорт
	
	Если НеоблагаемыеНДСОперации.Количество() = 0 Тогда
		Возврат 1;
	КонецЕсли;
	
	СписокКлючей = Новый СписокЗначений;
	СписокКлючей.ЗагрузитьЗначения(НеоблагаемыеНДСОперации.ВыгрузитьКолонку("КлючСтроки"));
	СписокКлючей.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	МаксимальныйКлюч = СписокКлючей[0].Значение;
	
	Возврат МаксимальныйКлюч + 1;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйНеоблагаемыеНДСОперации()
	
	НеоблагаемыеНДСОперации = Новый ТаблицаЗначений;
	
	НеоблагаемыеНДСОперации.Колонки.Добавить("КодОперации", Новый ОписаниеТипов("СправочникСсылка.КодыОперацийРаздела7ДекларацииПоНДС"));
	НеоблагаемыеНДСОперации.Колонки.Добавить("ДокументРеализации", Документы.ТипВсеСсылки());
	НеоблагаемыеНДСОперации.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты,СправочникСсылка.Организации"));
	НеоблагаемыеНДСОперации.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	НеоблагаемыеНДСОперации.Колонки.Добавить("СуммаРеализации", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	НеоблагаемыеНДСОперации.Колонки.Добавить("СуммаПриобретения", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	НеоблагаемыеНДСОперации.Колонки.Добавить("НДСПрямой", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	НеоблагаемыеНДСОперации.Колонки.Добавить("НДСРаспределенный", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	НеоблагаемыеНДСОперации.Колонки.Добавить("КлючСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Возврат НеоблагаемыеНДСОперации;
	
КонецФункции

Функция НовыйПодтверждающиеДокументы()
	
	ПодтверждающиеДокументы = Новый ТаблицаЗначений;
	
	ПодтверждающиеДокументы.Колонки.Добавить("ТипДокумента", Новый ОписаниеТипов("СправочникСсылка.ТипыДокументовПодтверждающихЛьготуНДС"));
	ПодтверждающиеДокументы.Колонки.Добавить("НомерДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ПодтверждающиеДокументы.Колонки.Добавить("ДатаДокумента", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ПодтверждающиеДокументы.Колонки.Добавить("КлючСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	
	Возврат ПодтверждающиеДокументы;
	
КонецФункции

Процедура РаспределитьНДСПоКодамРаздела7(СтруктураПараметров, НеоблагаемыеНДСОперации, МассивКоэффициентов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(СтруктураПараметров.Дата));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ПартииПрочихРасходов.НДСРегл) КАК НДС
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|ГДЕ
	|	ПартииПрочихРасходов.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПартииПрочихРасходов.Организация = &Организация
	|	И ПартииПрочихРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ПартииПрочихРасходов.Активность
	|	И ПартииПрочихРасходов.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииПрочихРасходов.Регистратор ССЫЛКА Документ.РаспределениеНДС";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.НДС <> Null Тогда 
		НДСКРаспределению = Выборка.НДС;
		Если НеоблагаемыеНДСОперации.Количество() > 0 Тогда 
			МассивРаспределеннаяСуммаНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(НДСКРаспределению, МассивКоэффициентов);
			Для Инд = 0 По НеоблагаемыеНДСОперации.Количество() - 1 Цикл
				НеоблагаемыеНДСОперации[Инд].НДСРаспределенный = МассивРаспределеннаяСуммаНДС[Инд];
			КонецЦикла;
		КонецЕсли;
	Иначе
		Для Инд = 0 По НеоблагаемыеНДСОперации.Количество() - 1 Цикл
			НеоблагаемыеНДСОперации[Инд].НДСРаспределенный = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ПроведениеДокумента

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаНДСЗаписиРаздела7Декларации(Запрос, ТекстыЗапроса, Регистры);
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",      Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	
КонецПроцедуры

Функция ТекстЗапросаНДСЗаписиРаздела7Декларации(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "НДСЗаписиРаздела7Декларации";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации,
	|	СУММА(ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.СуммаРеализации) КАК СуммаРеализации,
	|	СУММА(ВЫБОР
	|			КОГДА КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению
	|				ТОГДА ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.СуммаПриобретения
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПриобретения,
	|	СУММА(ВЫБОР
	|			КОГДА КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению
	|				ТОГДА ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.НДСПрямой + ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.НДСРаспределенный
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДС,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата КАК Период,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка КАК Регистратор,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация
	|ИЗ
	|	Документ.ФормированиеЗаписейРаздела7ДекларацииНДС.НеоблагаемыеНДСОперации КАК ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
	|		ПО ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации = КодыОперацийРаздела7ДекларацииПоНДС.Ссылка
	|ГДЕ
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.КодОперации,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Дата,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Ссылка,
	|	ФормированиеЗаписейРаздела7ДекларацииНДСНеоблагаемыеНДСОперации.Ссылка.Организация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
		
КонецФункции

// Формирование записей раздела 7 Декларации НДС
Процедура ОтразитьЗаписиРаздела7ДекларацииНДС(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	ТаблицаНДСЗаписиРаздела7Декларации = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаНДСЗаписиРаздела7Декларации;
	
	Если Отказ ИЛИ ТаблицаНДСЗаписиРаздела7Декларации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияНДСЗаписиРаздела7Декларации = Движения.НДСЗаписиРаздела7Декларации;
	ДвиженияНДСЗаписиРаздела7Декларации.Записывать = Истина;
	ДвиженияНДСЗаписиРаздела7Декларации.Загрузить(ТаблицаНДСЗаписиРаздела7Декларации);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
