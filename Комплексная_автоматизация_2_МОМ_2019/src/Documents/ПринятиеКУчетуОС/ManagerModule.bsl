#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура обновляет движения документа принятия к учету после отражения в регл. учете
// Вызывается при отражения в регл. учете из функции "РеглУчетПроведениеСервер.ДополнительнаяОбработкаПриОтраженииДокумента".
//
// Параметры:
// 		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджера временных таблиц проводок в регл. учете.
//
Процедура ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПринятиеКУчетуОС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ПринятиеКУчетуОС.Ссылка = ДокументыКОтражению.Ссылка";
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	
	Если Не РеквизитыДокумента.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, РеквизитыДокумента.Ссылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Неопределено);
	ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Неопределено);
	ПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Неопределено);
	ПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Неопределено);
	
	ТаблицыДляДвижений = Новый Структура;
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина, Ложь, Истина);

	СтруктураВременныеТаблицы = Новый Структура;
	СтруктураВременныеТаблицы.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	
	НачатьТранзакцию();
	
	Попытка
		
		// БлокировкаДанных
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПрочиеРасходы.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыАмортизацииОСНалоговыйУчет.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет.НаборЗаписей");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Регистратор", РеквизитыДокумента.Ссылка);
		
		Блокировка.Заблокировать();
		
		// Запись движений
		
		НаборЗаписей = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ПрочиеРасходы);
		ДобавитьПараметрыДляПроведения(НаборЗаписей, СтруктураВременныеТаблицы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ПараметрыАмортизацииОСБухгалтерскийУчет);
		ДобавитьПараметрыДляПроведения(НаборЗаписей, СтруктураВременныеТаблицы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПараметрыАмортизацииОСНалоговыйУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ПараметрыАмортизацииОСНалоговыйУчет);
		ДобавитьПараметрыДляПроведения(НаборЗаписей, СтруктураВременныеТаблицы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ПервоначальныеСведенияОСБухгалтерскийУчет);
		ДобавитьПараметрыДляПроведения(НаборЗаписей, СтруктураВременныеТаблицы);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ПервоначальныеСведенияОСНалоговыйУчет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(РеквизитыДокумента.Ссылка);
		НаборЗаписей.Загрузить(ТаблицыДляДвижений.ПервоначальныеСведенияОСНалоговыйУчет);
		ДобавитьПараметрыДляПроведения(НаборЗаписей, СтруктураВременныеТаблицы);
		НаборЗаписей.Записать();
		
		ВнеоборотныеАктивы.СформироватьЗаписиРегистровЗаданий(РеквизитыДокумента.Ссылка, СтруктураВременныеТаблицы);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Заполнение регистров ОС после отражения документа в регламентированном учете'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Принятие к учету ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании	 - ТаблицаЗначений	 - Таблица с командами создания на основании. Для изменения.
//  	См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//  Порядок						 - Число			 - Порядок команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПринятиеКУчетуОС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПринятиеКУчетуОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПринятиеКУчетуОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивы2_2";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
 
#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПринятиеКУчетуОС";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам" Тогда
		
		ТекстЗапроса = СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаОС";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	ГрафикиАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	МестонахождениеОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	СобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	СостоянияОСОрганизаций(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	СчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	ПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.СобытиеОС КАК СобытиеОС,
	|	ДанныеДокумента.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ДанныеДокумента.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ДанныеДокумента.СчетУчета КАК СчетУчета,
	|	ДанныеДокумента.СчетАмортизации КАК СчетАмортизации,
	|	ДанныеДокумента.Местонахождение КАК Местонахождение,
	|	ДанныеДокумента.МОЛ КАК МОЛ,
	|	ДанныеДокумента.АдресМестонахождения КАК АдресМестонахождения,
	|	ДанныеДокумента.ГруппаОС КАК ГруппаОС,
	|	ДанныеДокумента.КодПоОКОФ КАК КодПоОКОФ,
	|	ДанныеДокумента.ШифрПоЕНАОФ КАК ШифрПоЕНАОФ,
	|	ДанныеДокумента.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ДанныеДокумента.СпособПоступления КАК СпособПоступления,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ДанныеДокумента.НачислятьАмортизациюБУ,
	|	ДанныеДокумента.МетодНачисленияАмортизацииБУ,
	|	ДанныеДокумента.СрокИспользованияБУ,
	|	ДанныеДокумента.ГрафикАмортизации,
	|	ДанныеДокумента.ГодоваяНормаАмортизацииБУ,
	|	ДанныеДокумента.КоэффициентУскоренияБУ,
	|	ДанныеДокумента.НачислятьАмортизациюНУ,
	|	ДанныеДокумента.СрокИспользованияНУ,
	|	ДанныеДокумента.СпециальныйКоэффициентНУ,
	|	ДанныеДокумента.ВключитьАмортизационнуюПремиюВСоставРасходов,
	|	ДанныеДокумента.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ,
	|	ДанныеДокумента.СтатьяРасходовАмортизационнойПремии,
	|	ДанныеДокумента.АналитикаРасходовАмортизационнойПремии,
	|	ДанныеДокумента.СтатьяРасходовНалог,
	|	ДанныеДокумента.АналитикаРасходовНалог,
	|	ДанныеДокумента.ПередаватьРасходыВДругуюОрганизацию,
	|	ДанныеДокумента.ОрганизацияПолучательРасходов,
	|	ДанныеДокумента.СчетПередачиРасходов,
	|	ДанныеДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ДанныеДокумента.НаправлениеДеятельности,
	|	ДанныеДокумента.ДокументНаОснованииИнвентаризации,
	|	ДанныеДокумента.СчетУчетаЦФ,
	|	ДанныеДокумента.СчетАмортизацииЦФ,
	|	ДанныеДокумента.СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ ДанныеДокумента.ДокументНаОснованииИнвентаризации
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтоимостьЗаданаДокументом
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ЗаполнитьСпособыОтраженияРасходовПоИмущественнымНалогам", ЗначениеЗаполнено(Реквизиты.СтатьяРасходовНалог));
	Запрос.УстановитьПараметр("ГраницаКонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонецДня", КонецДня(Реквизиты.Период));
	
	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(Реквизиты.Организация, Реквизиты.Период);
	Запрос.УстановитьПараметр("ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи", 
		ПараметрыУчетнойПолитики <> Неопределено И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПринятиеКУчетуОС"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Принятие к учету ОС'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ВременнаяТаблицаПоТабличнойЧастиДокумента(ТекстыЗапроса)
	
	ИмяТаблицы = "ТабличнаяЧасть";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	ТаблицаОС.СуммаЦелевыхСредств,
	|	ВЫБОР
	|		КОГДА &СтоимостьЗаданаДокументом
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &СтоимостьЗаданаДокументом
	|			ТОГДА ТаблицаОС.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|	ВЫБОР
	|		КОГДА &СтоимостьЗаданаДокументом
	|			ТОГДА ТаблицаОС.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПР,
	|	ВЫБОР
	|		КОГДА &СтоимостьЗаданаДокументом
	|			ТОГДА ТаблицаОС.СтоимостьВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьВР
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
	Возврат Текст;
	
КонецФункции

Функция ДанныеДокументаИУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧастиДокумента(ТекстыЗапроса);
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВтСчетаВложенияВоВнеоборотныеАктивы(Ложь, ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	ТаблицаОС.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств,
	|	ПервоначальныеСведения.ДоговорЛизинга КАК ДоговорЛизинга,
	|	СУММА(ТаблицаОС.СтоимостьБУ + ЕСТЬNULL(ДанныеУчета.СуммаОстаток, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	СУММА(ВЫБОР
	|			КОГДА &СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|					И &ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|				ТОГДА ТаблицаОС.СтоимостьНУ
	|			ИНАЧЕ ТаблицаОС.СтоимостьНУ + ЕСТЬNULL(ДанныеУчета.СуммаНУОстаток, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьНУ,
	|	СУММА(ВЫБОР
	|			КОГДА &СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|					И &ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОС.СтоимостьПР + ЕСТЬNULL(ДанныеУчета.СуммаПРОстаток, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьПР,
	|	СУММА(ВЫБОР
	|			КОГДА &СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|					И &ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|				ТОГДА ТаблицаОС.СтоимостьБУ + ЕСТЬNULL(ДанныеУчета.СуммаОстаток, 0) - ТаблицаОС.СтоимостьНУ
	|			ИНАЧЕ ТаблицаОС.СтоимостьВР + ЕСТЬNULL(ДанныеУчета.СуммаВРОстаток, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьВР
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	ТабличнаяЧасть КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&ПериодКонецДня,
	|				ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.ОсновноеСредство
	|					ИЗ
	|						ТабличнаяЧасть КАК Т)) КАК ПервоначальныеСведения
	|		ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|			И (НЕ &СтоимостьЗаданаДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ГраницаКонецМесяца,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.Счет
	|					ИЗ
	|						втСчетаВложенияВоВнеоборотныеАктивы КАК Т),
	|				,
	|				Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И НаправлениеДеятельности = &НаправлениеДеятельности
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							ТабличнаяЧасть КАК Т)) КАК ДанныеУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ДанныеУчета.Субконто1
	|			И (НЕ &СтоимостьЗаданаДокументом)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	ТаблицаОС.СуммаЦелевыхСредств,
	|	ПервоначальныеСведения.ДоговорЛизинга"; 
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
	Возврат Текст;
	
КонецФункции

Процедура ДанныеДокументаИУчетаПриОтраженииРегл(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДокументаИУчета";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧастиДокумента(ТекстыЗапроса);
	
	Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	ТаблицаОС.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств,
	|	ПервоначальныеСведения.ДоговорЛизинга КАК ДоговорЛизинга,
	|	СУММА(ТаблицаОС.СтоимостьБУ + ЕСТЬNULL(ДанныеУчета.Сумма, 0)) КАК ПервоначальнаяСтоимостьБУ,
	|	СУММА(ВЫБОР
	|			КОГДА &СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|					И &ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|				ТОГДА ТаблицаОС.СтоимостьНУ
	|			ИНАЧЕ ТаблицаОС.СтоимостьНУ + ЕСТЬNULL(ДанныеУчета.СуммаНУКт, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьНУ,
	|	СУММА(ВЫБОР
	|			КОГДА &СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|					И &ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаОС.СтоимостьПР + ЕСТЬNULL(ДанныеУчета.СуммаПРКт, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьПР,
	|	СУММА(ВЫБОР
	|			КОГДА &СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|					И &ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|				ТОГДА ТаблицаОС.СтоимостьБУ + ЕСТЬNULL(ДанныеУчета.Сумма, 0) - ТаблицаОС.СтоимостьНУ
	|			ИНАЧЕ ТаблицаОС.СтоимостьВР + ЕСТЬNULL(ДанныеУчета.СуммаВРКт, 0)
	|		КОНЕЦ) КАК ПервоначальнаяСтоимостьВР
	|ПОМЕСТИТЬ втДанныеДокументаИУчета
	|ИЗ
	|	ТабличнаяЧасть КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&ПериодКонецДня,
	|				ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.ОсновноеСредство
	|					ИЗ
	|						ТабличнаяЧасть КАК Т)) КАК ПервоначальныеСведения
	|		ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|			И (НЕ &СтоимостьЗаданаДокументом)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				,
	|				,
	|				Регистратор = &Ссылка
	|					И СчетДт В (&СчетУчета, &СчетУчетаЦФ),
	|				,
	|				) КАК ДанныеУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ДанныеУчета.СубконтоДт1
	|			И (НЕ &СтоимостьЗаданаДокументом)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработки,
	|	ТаблицаОС.СуммаЦелевыхСредств,
	|	ПервоначальныеСведения.ДоговорЛизинга"; 
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеДокументаИУчета(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	втДанныеДокументаИУчета.Ссылка,
	|	втДанныеДокументаИУчета.НомерСтроки,
	|	втДанныеДокументаИУчета.ОсновноеСредство,
	|	втДанныеДокументаИУчета.ИнвентарныйНомер,
	|	втДанныеДокументаИУчета.ПоказательНаработки,
	|	втДанныеДокументаИУчета.ОбъемНаработки,
	|	втДанныеДокументаИУчета.ДоговорЛизинга КАК ДоговорЛизинга,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Неиспользуется)
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьНУ
	|		КОГДА втДанныеДокументаИУчета.СуммаЦелевыхСредств > втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР - втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьНУ - втДанныеДокументаИУчета.СуммаЦелевыхСредств + втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР + втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|		ИНАЧЕ втДанныеДокументаИУчета.СуммаЦелевыхСредств
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНУ,
	|	ВЫБОР
	|		КОГДА &ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Неиспользуется)
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР
	|		КОГДА &ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|			ТОГДА втДанныеДокументаИУчета.ПервоначальнаяСтоимостьБУ
	|		КОГДА втДанныеДокументаИУчета.СуммаЦелевыхСредств > втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР - втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|			ТОГДА втДанныеДокументаИУчета.СуммаЦелевыхСредств - втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР
	|		ИНАЧЕ втДанныеДокументаИУчета.ПервоначальнаяСтоимостьПР
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьПР,
	|	втДанныеДокументаИУчета.ПервоначальнаяСтоимостьВР КАК ПервоначальнаяСтоимостьВР,
	|	втДанныеДокументаИУчета.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	втДанныеДокументаИУчета КАК втДанныеДокументаИУчета";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
	Возврат Текст;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Местонахождение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	- ТаблицаОС.ПервоначальнаяСтоимостьНУ КАК ВременнаяРазница,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|	И НЕ &ДокументНаОснованииИнвентаризации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Местонахождение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	0 КАК СуммаСНДСРегл,
	|	0 КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	- ТаблицаОС.СтоимостьНУ КАК ВременнаяРазница,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|	И &ДокументНаОснованииИнвентаризации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	0 КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	- ТаблицаОС.ПервоначальнаяСтоимостьНУ КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|	И НЕ &ДокументНаОснованииИнвентаризации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	0 КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	- ТаблицаОС.СтоимостьНУ КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|	И &ДокументНаОснованииИнвентаризации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ГрафикиАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикиАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	&ГрафикАмортизации КАК ГрафикАмортизации
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ГрафикАмортизации <> ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура МестонахождениеОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&МОЛ КАК МОЛ,
	|	&Местонахождение КАК Местонахождение,
	|	&АдресМестонахождения КАК АдресМестонахождения
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	&НачислятьАмортизациюБУ КАК НачислятьАмортизацию
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ВЫБОР КОГДА &ПорядокУчетаНУ В (ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации), ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена))
	|	ТОГДА &НачислятьАмортизациюНУ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизацию
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	&СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ПорядокУчетаНУ В (ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации), ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&СрокИспользованияБУ КАК СрокПолезногоИспользования,
	|	ТаблицаОС.ОбъемНаработки КАК ОбъемПродукцииРабот,
	|	&СрокИспользованияБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемНаработки КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	&ГодоваяНормаАмортизацииБУ КАК КоэффициентАмортизации,
	|	&КоэффициентУскоренияБУ КАК КоэффициентУскорения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ВЫБОР КОГДА &ПорядокУчетаНУ В (ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации), ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена))
	|		ТОГДА &СрокИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользования,
	|	0 КАК КоэффициентАмортизации,
	|	ТаблицаОС.ПервоначальнаяСтоимостьПР КАК ПРДляВычисленияАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	&СпособПоступления КАК СпособПоступления,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	&МетодНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	ТаблицаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	&ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
	|	&ГруппаОС КАК ГруппаОС,
	|	&КодПоОКОФ КАК КодПоОКОФ,
	|	&ШифрПоЕНАОФ КАК ШифрПоЕНАОФ,
	|	&АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ТаблицаОС.ДоговорЛизинга КАК ДоговорЛизинга
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	0 КАК АмортизацияДо2002,
	|	0 КАК АмортизацияДо2009,
	|	ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) КАК МетодНачисленияАмортизации,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	&ПорядокУчетаНУ КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	0 КАК СтоимостьДо2002,
	|	0 КАК ФактическийСрокИспользованияДо2009
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
	Возврат Текст;
	
КонецФункции

Процедура СобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента,
	|	&СобытиеОС КАК Событие
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СостоянияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СостоянияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	&Период КАК ДатаСостояния,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние
	|
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	ЛОЖЬ КАК СпособОтраженияРасходовЗаданДокументом,
	|	НЕОПРЕДЕЛЕНО КАК СпособОтраженияРасходов,
	|	&ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	&ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	&СчетПередачиРасходов КАК СчетПередачиРасходов
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается)
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество) КАК ВидНалога,
	|	&СтатьяРасходовНалог КАК СтатьяРасходов,
	|	&АналитикаРасходовНалог КАК АналитикаРасходов
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И НЕ &ГруппаОС В (
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог) КАК ВидНалога,
	|	&СтатьяРасходовНалог КАК СтатьяРасходов,
	|	&АналитикаРасходовНалог КАК АналитикаРасходов
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И &ГруппаОС В (
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства),
	|			ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МашиныИОборудование)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог) КАК ВидНалога,
	|	&СтатьяРасходовНалог КАК СтатьяРасходов,
	|	&АналитикаРасходовНалог КАК АналитикаРасходов
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &СтатьяРасходовНалог <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И &ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СчетаБухгалтерскогоУчетаОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&СчетУчета КАК СчетУчета,
	|	&СчетАмортизации КАК СчетНачисленияАмортизации
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения,
	|	
	|	&Организация КАК Организация,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыЦелевогоФинансированияОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныеСредства(ТекстыЗапроса);
	
	Текст =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ИСТИНА КАК ПрименениеЦелевогоФинансирования,
	|	&СчетУчетаЦФ КАК СчетУчета,
	|	&СчетАмортизацииЦФ КАК СчетАмортизации,
	|	&СтатьяДоходов КАК СтатьяДоходов,
	|	&АналитикаДоходов КАК АналитикаДоходов
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|ГДЕ
	|	&ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное))";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&Местонахождение                        КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Местонахождение                        КАК Подразделение,
	|	&Организация                            КАК Организация,
	|	&Проведен                               КАК Проведен,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                                    КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ВременнаяТаблицаДанныеДокумента());
	ТекстыОтражения.Добавить(ВнеоборотныеАктивыЛокализация.ТекстЗапросаВтСчетаВложенияВоВнеоборотныеАктивы(Ложь, Неопределено));
	ТекстыОтражения.Добавить(ВременнаяТаблицаОстаткиСчетаКапитализации());
	ТекстыОтражения.Добавить(ВременнаяТаблицаЦелевоеФинансирование());
	ТекстыОтражения.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
КонецФункции

Функция ВременнаяТаблицаДанныеДокумента()
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.Местонахождение КАК Местонахождение,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.СпособПоступления КАК СпособПоступления,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.СчетАмортизации,
	|	ТаблицаДокумента.ПорядокУчетаБУ,
	|	ТаблицаДокумента.ПорядокУчетаНУ,
	|	ТаблицаДокумента.ВключитьАмортизационнуюПремиюВСоставРасходов,
	|	ТаблицаДокумента.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ,
	|	ТаблицаДокумента.СтатьяРасходов,
	|	ТаблицаДокумента.АналитикаРасходов,
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ТабличнаяЧасть.СуммаЦелевыхСредств КАК СуммаЦелевыхСредств,
	|	ТабличнаяЧасть.СтоимостьНУ КАК СтоимостьНУ,
	|	ТаблицаДокумента.СчетУчетаЦФ,
	|	ТаблицаДокумента.СчетАмортизацииЦФ,
	|	ТаблицаДокумента.СтатьяДоходов,
	|	ТаблицаДокумента.АналитикаДоходов,
	|	ТаблицаДокумента.ДокументНаОснованииИнвентаризации
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТабличнаяЧасть
	|		ПО ТаблицаДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
КонецФункции

Функция ВременнаяТаблицаОстаткиСчетаКапитализации()
	
	Возврат
	"ВЫБРАТЬ
	|	ДанныеУчета.Счет                 КАК Счет,
	|	ДанныеУчета.Субконто1            КАК ОсновноеСредство,
	|	СУММА(ДанныеУчета.СуммаОборот)   КАК СуммаБУ,
	|	СУММА(ДанныеУчета.СуммаНУОборот) КАК СуммаНУ,
	|	СУММА(ДанныеУчета.СуммаПРОборот) КАК СуммаПР,
	|	СУММА(ДанныеУчета.СуммаВРОборот) КАК СуммаВР
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&ГраницаМесяцОкончание,
	|			Регистратор,
	|			Счет В (ВЫБРАТЬ
	|						Т.Счет
	|					ИЗ втСчетаВложенияВоВнеоборотныеАктивы КАК Т),
	|			,
	|			(Организация, Подразделение, НаправлениеДеятельности, Субконто1) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.НаправлениеДеятельности,
	|					Т.ОсновноеСредство
	|				ИЗ
	|					втДанныеДокумента КАК Т),
	|			,
	|			) КАК ДанныеУчета
	|ГДЕ
	|	ДанныеУчета.Регистратор <> &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.Субконто1,
	|	ДанныеУчета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.ОсновноеСредство,
	|	СУММА(втОстатки.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втОстатки.СуммаНУ) КАК СуммаНУ,
	|	СУММА(втОстатки.СуммаПР) КАК СуммаПР,
	|	СУММА(втОстатки.СуммаВР) КАК СуммаВР
	|ПОМЕСТИТЬ втОстаткиСгруппированные
	|ИЗ
	|	втОстатки КАК втОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	втОстатки.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.ОсновноеСредство,
	|	ВЫРАЗИТЬ(втДанныеДокумента.СуммаЦелевыхСредств * (втОстатки.СуммаБУ / втОстаткиСгруппированные.СуммаБУ) КАК ЧИСЛО(31,2)) КАК Сумма
	|ПОМЕСТИТЬ втСуммыЦелевыхСредствПоСчетам
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаткиСгруппированные КАК втОстаткиСгруппированные
	|		ПО втОстатки.ОсновноеСредство = втОстаткиСгруппированные.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.ОсновноеСредство = втДанныеДокумента.ОсновноеСредство
	|ГДЕ
	|	втДанныеДокумента.СуммаЦелевыхСредств <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(втСуммыЦелевыхСредствПоСчетам.Счет) КАК Счет,
	|	втСуммыЦелевыхСредствПоСчетам.ОсновноеСредство,
	|	СУММА(втСуммыЦелевыхСредствПоСчетам.Сумма) КАК Сумма
	|ПОМЕСТИТЬ втСуммыЦелевыхСредствПоСчетамСгруппированные
	|ИЗ
	|	втСуммыЦелевыхСредствПоСчетам КАК втСуммыЦелевыхСредствПоСчетам
	|
	|СГРУППИРОВАТЬ ПО
	|	втСуммыЦелевыхСредствПоСчетам.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСуммыЦелевыхСредствПоСчетам.Счет КАК Счет,
	|	втСуммыЦелевыхСредствПоСчетам.ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА втДанныеДокумента.СуммаЦелевыхСредств = втСуммыЦелевыхСредствПоСчетамСгруппированные.Сумма
	|			ТОГДА втСуммыЦелевыхСредствПоСчетам.Сумма
	|		КОГДА втДанныеДокумента.СуммаЦелевыхСредств <> втСуммыЦелевыхСредствПоСчетамСгруппированные.Сумма
	|				И втСуммыЦелевыхСредствПоСчетам.Счет = втСуммыЦелевыхСредствПоСчетамСгруппированные.Счет
	|			ТОГДА втСуммыЦелевыхСредствПоСчетам.Сумма + (втДанныеДокумента.СуммаЦелевыхСредств - втСуммыЦелевыхСредствПоСчетамСгруппированные.Сумма)
	|		ИНАЧЕ втСуммыЦелевыхСредствПоСчетам.Сумма
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ втСуммыЦелевыхСредств
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыЦелевыхСредствПоСчетам КАК втСуммыЦелевыхСредствПоСчетам
	|		ПО втДанныеДокумента.ОсновноеСредство = втСуммыЦелевыхСредствПоСчетам.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыЦелевыхСредствПоСчетамСгруппированные КАК втСуммыЦелевыхСредствПоСчетамСгруппированные
	|		ПО втДанныеДокумента.ОсновноеСредство = втСуммыЦелевыхСредствПоСчетамСгруппированные.ОсновноеСредство
	|ГДЕ
	|	втДанныеДокумента.СуммаЦелевыхСредств <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.ОсновноеСредство,
	|	втСуммыЦелевыхСредств.Сумма КАК СуммаБУЦФ,
	|	втСуммыЦелевыхСредств.Сумма
	|	- ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втСуммыЦелевыхСредств.Сумма
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втОстатки.СуммаПР
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|	- ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА 0
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаВР > втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втОстатки.СуммаВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУЦФ,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втСуммыЦелевыхСредств.Сумма
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма
	|			ТОГДА втОстатки.СуммаПР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПРЦФ,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаПР > 0 И втОстатки.СуммаПР > втСуммыЦелевыхСредств.Сумма
	|			ТОГДА 0
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаВР > втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|		КОГДА втОстатки.СуммаВР > 0 И втОстатки.СуммаПР <= втСуммыЦелевыхСредств.Сумма - втОстатки.СуммаПР
	|			ТОГДА втОстатки.СуммаВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВРЦФ
	|ПОМЕСТИТЬ втЦелевоеФинансированиеСтоимости
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыЦелевыхСредств КАК втСуммыЦелевыхСредств
	|		ПО втОстатки.ОсновноеСредство = втСуммыЦелевыхСредств.ОсновноеСредство
	|			И втОстатки.Счет = втСуммыЦелевыхСредств.Счет
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Счет КАК Счет,
	|	втОстатки.ОсновноеСредство,
	|	втОстатки.СуммаБУ - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаБУЦФ, 0) КАК СуммаБУ,
	|	втОстатки.СуммаНУ - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаНУЦФ, 0) КАК СуммаНУ,
	|	втОстатки.СуммаПР - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаПРЦФ, 0) КАК СуммаПР,
	|	втОстатки.СуммаВР - ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаВРЦФ, 0) КАК СуммаВР,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаБУЦФ, 0) КАК СуммаБУЦФ,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаНУЦФ, 0) КАК СуммаНУЦФ,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаПРЦФ, 0) КАК СуммаПРЦФ,
	|	ЕСТЬNULL(втЦелевоеФинансированиеСтоимости.СуммаВРЦФ, 0) КАК СуммаВРЦФ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализации
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеДокумента КАК втДанныеДокумента
	|		ПО втОстатки.ОсновноеСредство = втДанныеДокумента.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦелевоеФинансированиеСтоимости КАК втЦелевоеФинансированиеСтоимости
	|		ПО втОстатки.ОсновноеСредство = втЦелевоеФинансированиеСтоимости.ОсновноеСредство
	|			И втОстатки.Счет = втЦелевоеФинансированиеСтоимости.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.ОсновноеСредство,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУ) КАК СуммаБУ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаНУ) КАК СуммаНУ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаПР) КАК СуммаПР,
	|	СУММА(втДанныеСчетаКапитализации.СуммаВР) КАК СуммаВР,
	|	СУММА(втДанныеСчетаКапитализации.СуммаБУЦФ) КАК СуммаБУЦФ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаНУЦФ) КАК СуммаНУЦФ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаПРЦФ) КАК СуммаПРЦФ,
	|	СУММА(втДанныеСчетаКапитализации.СуммаВРЦФ) КАК СуммаВРЦФ
	|ПОМЕСТИТЬ втДанныеСчетаКапитализацииСгруппированные
	|ИЗ
	|	втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.ОсновноеСредство";
	
КонецФункции

Функция ВременнаяТаблицаЦелевоеФинансирование()
	
	Возврат
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.Счет,
	|	ТабличнаяЧасть.Субконто1,
	|	ТабличнаяЧасть.Субконто2,
	|	ТабличнаяЧасть.Субконто3,
	|	ТабличнаяЧасть.Сумма,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.ВариантПримененияЦелевогоФинансирования,
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Дата,
	|	ТабличнаяЧасть.НомерСтроки
	|ПОМЕСТИТЬ втТабличнаяЧастьЦелевоеФинансирование
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ЦелевоеФинансирование КАК ТабличнаяЧасть
	|		ПО (ТабличнаяЧасть.Ссылка = ТаблицаДокумента.Ссылка)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТабличнаяЧастьЦелевоеФинансирование.Счет,
	|	втТабличнаяЧастьЦелевоеФинансирование.Субконто1,
	|	втТабличнаяЧастьЦелевоеФинансирование.Субконто2,
	|	втТабличнаяЧастьЦелевоеФинансирование.Субконто3,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстаток, втТабличнаяЧастьЦелевоеФинансирование.Сумма) КАК Сумма,
	|	втТабличнаяЧастьЦелевоеФинансирование.Организация,
	|	втТабличнаяЧастьЦелевоеФинансирование.Подразделение,
	|	втТабличнаяЧастьЦелевоеФинансирование.НаправлениеДеятельности,
	|	втТабличнаяЧастьЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования,
	|	втТабличнаяЧастьЦелевоеФинансирование.Ссылка,
	|	втТабличнаяЧастьЦелевоеФинансирование.Дата,
	|	втТабличнаяЧастьЦелевоеФинансирование.НомерСтроки
	|ПОМЕСТИТЬ втИсточникиЦелевогоФинансирования
	|ИЗ
	|	втТабличнаяЧастьЦелевоеФинансирование КАК втТабличнаяЧастьЦелевоеФинансирование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&Дата,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.Счет
	|					ИЗ
	|						втТабличнаяЧастьЦелевоеФинансирование КАК Т),
	|				,
	|				(Организация, Подразделение, НаправлениеДеятельности) В
	|					(ВЫБРАТЬ
	|						втТабличнаяЧастьЦелевоеФинансирование.Организация,
	|						втТабличнаяЧастьЦелевоеФинансирование.Подразделение,
	|						втТабличнаяЧастьЦелевоеФинансирование.НаправлениеДеятельности
	|					ИЗ
	|						втТабличнаяЧастьЦелевоеФинансирование КАК втТабличнаяЧастьЦелевоеФинансирование)) КАК ХозрасчетныйОстатки
	|		ПО (втТабличнаяЧастьЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное))
	|			И втТабличнаяЧастьЦелевоеФинансирование.Счет = ХозрасчетныйОстатки.Счет
	|			И втТабличнаяЧастьЦелевоеФинансирование.Субконто1 = ХозрасчетныйОстатки.Субконто1
	|			И втТабличнаяЧастьЦелевоеФинансирование.Субконто2 = ХозрасчетныйОстатки.Субконто2
	|			И втТабличнаяЧастьЦелевоеФинансирование.Субконто3 = ХозрасчетныйОстатки.Субконто3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(втИсточникиЦелевогоФинансирования.Сумма) КАК Сумма,
	|	КОЛИЧЕСТВО(втИсточникиЦелевогоФинансирования.Счет) КАК Количество
	|ПОМЕСТИТЬ втИсточникиЦелевогоФинансированияСгруппированные
	|ИЗ
	|	втИсточникиЦелевогоФинансирования КАК втИсточникиЦелевогоФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеСчетаКапитализации.ОсновноеСредство,
	|	СУММА(ВЫБОР
	|			КОГДА втДанныеДокумента.ВариантПримененияЦелевогоФинансирования = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|				ТОГДА втДанныеСчетаКапитализации.СуммаБУ
	|			ИНАЧЕ втДанныеСчетаКапитализации.СуммаБУЦФ
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ втЦелевоеФинансированиеОС
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации КАК втДанныеСчетаКапитализации
	|		ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеСчетаКапитализации.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансированиеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	втИсточникиЦелевогоФинансирования.Счет КАК Счет,
	|	втИсточникиЦелевогоФинансирования.Субконто1 КАК Субконто1,
	|	втИсточникиЦелевогоФинансирования.Субконто2 КАК Субконто2,
	|	втИсточникиЦелевогоФинансирования.Субконто3 КАК Субконто3,
	|	ВЫБОР
	|		КОГДА втИсточникиЦелевогоФинансированияСгруппированные.Сумма = 0
	|			ТОГДА ВЫРАЗИТЬ(втЦелевоеФинансированиеОС.Сумма / втИсточникиЦелевогоФинансированияСгруппированные.Количество КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ВЫРАЗИТЬ(втЦелевоеФинансированиеОС.Сумма * (втИсточникиЦелевогоФинансирования.Сумма / втИсточникиЦелевогоФинансированияСгруппированные.Сумма) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма,
	|	втИсточникиЦелевогоФинансирования.Организация,
	|	втИсточникиЦелевогоФинансирования.Подразделение,
	|	втИсточникиЦелевогоФинансирования.НаправлениеДеятельности,
	|	втИсточникиЦелевогоФинансирования.ВариантПримененияЦелевогоФинансирования,
	|	втИсточникиЦелевогоФинансирования.Ссылка,
	|	втИсточникиЦелевогоФинансирования.Дата,
	|	втИсточникиЦелевогоФинансирования.НомерСтроки,
	|	втЦелевоеФинансированиеОС.Сумма КАК ПолнаяСуммаПоОС
	|ПОМЕСТИТЬ втЦелевоеФинансирование
	|ИЗ
	|	втЦелевоеФинансированиеОС КАК втЦелевоеФинансированиеОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсточникиЦелевогоФинансирования КАК втИсточникиЦелевогоФинансирования
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсточникиЦелевогоФинансированияСгруппированные КАК втИсточникиЦелевогоФинансированияСгруппированные
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансирование.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(втЦелевоеФинансирование.Сумма) КАК Сумма,
	|	МАКСИМУМ(втЦелевоеФинансирование.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ втЦелевоеФинансированиеСгруппированное
	|ИЗ
	|	втЦелевоеФинансирование КАК втЦелевоеФинансирование
	|
	|СГРУППИРОВАТЬ ПО
	|	втЦелевоеФинансирование.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЦелевоеФинансирование.ОсновноеСредство,
	|	втЦелевоеФинансирование.Счет,
	|	втЦелевоеФинансирование.Субконто1,
	|	втЦелевоеФинансирование.Субконто2,
	|	втЦелевоеФинансирование.Субконто3,
	|	ВЫБОР
	|		КОГДА НЕ втЦелевоеФинансированиеСгруппированное.ОсновноеСредство ЕСТЬ NULL 
	|				И втЦелевоеФинансированиеСгруппированное.Сумма <> втЦелевоеФинансирование.ПолнаяСуммаПоОС
	|			ТОГДА втЦелевоеФинансирование.Сумма + втЦелевоеФинансирование.ПолнаяСуммаПоОС - втЦелевоеФинансированиеСгруппированное.Сумма
	|		ИНАЧЕ втЦелевоеФинансирование.Сумма
	|	КОНЕЦ КАК Сумма,
	|	втЦелевоеФинансирование.Организация,
	|	втЦелевоеФинансирование.Подразделение,
	|	втЦелевоеФинансирование.НаправлениеДеятельности,
	|	втЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования,
	|	втЦелевоеФинансирование.Ссылка,
	|	втЦелевоеФинансирование.Дата
	|ПОМЕСТИТЬ втЦелевоеФинансированиеИтог
	|ИЗ
	|	втЦелевоеФинансирование КАК втЦелевоеФинансирование
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦелевоеФинансированиеСгруппированное КАК втЦелевоеФинансированиеСгруппированное
	|		ПО втЦелевоеФинансирование.ОсновноеСредство = втЦелевоеФинансированиеСгруппированное.ОсновноеСредство
	|			И втЦелевоеФинансирование.НомерСтроки = втЦелевоеФинансированиеСгруппированное.НомерСтроки";
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ПринятиеКУчетуОС());
	ТекстыОтражения.Добавить(КорректировкаСтоимостиАрендованногоОС());
	ТекстыОтражения.Добавить(ПринятиеКУчетуОСЦелевыеСредства());
	ТекстыОтражения.Добавить(ПринятиеКУчетуОСЧастичныеЦелевыеСредства());
	ТекстыОтражения.Добавить(АмортизационнаяПремияДтКВ());
	ТекстыОтражения.Добавить(СписаниеПриПринятииКУчетуНУ());
	ТекстыОтражения.Добавить(СписаниеПриПринятииКУчетуНУПоРезультатамИнвентаризации());
	ТекстыОтражения.Добавить(ПринятиеКУчетуАмортизиторованногоОС());
	ТекстыОтражения.Добавить(ПринятиеКУчетуПоРезультатамИнвентаризации());
	ТекстыОтражения.Добавить(СписаниеСЗабалансовогоУчета());
	ТекстыОтражения.Добавить(ПризнаниеДоходовБудущихПериодов());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция ПринятиеКУчетуОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчета:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втДанныеДокумента.СчетУчета КАК СчетДт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		втДанныеДокумента.СтоимостьНУ
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаНУ 
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		0
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаПР 
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		втДанныеСчетаКапитализации.СуммаБУ - втДанныеДокумента.СтоимостьНУ
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаВР 
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		втДанныеДокумента.СтоимостьНУ
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаНУ 
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		0
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаПР 
	|	КОНЕЦ КАК СуммаПРКт,
	|	ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		втДанныеСчетаКапитализации.СуммаБУ - втДанныеДокумента.СтоимостьНУ
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаВР 
	|	КОНЕЦ КАК СуммаВРКт,
	|	""Принятие к учету ОС"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		втДанныеДокумента.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И втДанныеДокумента.Организация = УчетнаяПолитикаОрганизаций.Организация
	|
	|ГДЕ
	|	втДанныеДокумента.ПорядокУчетаБУ В(
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету)
	|	)
	|	И втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.НеИспользуется),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.ПустаяСсылка)
	|	)
	|	И НЕ втДанныеДокумента.ДокументНаОснованииИнвентаризации
	|";
	
КонецФункции

Функция КорректировкаСтоимостиАрендованногоОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт 01.К:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетДт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втДанныеСчетаКапитализации.СуммаБУ - втДанныеДокумента.СтоимостьНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-(втДанныеСчетаКапитализации.СуммаБУ - втДанныеДокумента.СтоимостьНУ) КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаБУ - втДанныеДокумента.СтоимостьНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-(втДанныеСчетаКапитализации.СуммаБУ - втДанныеДокумента.СтоимостьНУ) КАК СуммаВРКт,
	|	""Корректировка стоимости арендованного имущества по НУ"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		втДанныеДокумента.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И втДанныеДокумента.Организация = УчетнаяПолитикаОрганизаций.Организация
	|
	|ГДЕ
	|	втДанныеДокумента.ПорядокУчетаБУ В(
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету)
	|	)
	|	И втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.НеИспользуется),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.ПустаяСсылка)
	|	)
	|	И НЕ втДанныеДокумента.ДокументНаОснованииИнвентаризации
	|	И УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|	И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|";
	
КонецФункции

Функция ПринятиеКУчетуОСЦелевыеСредства()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчетаЦФ:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втДанныеДокумента.СчетУчетаЦФ КАК СчетДт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	втДанныеСчетаКапитализации.СуммаНУ + втДанныеСчетаКапитализации.СуммаПР + втДанныеСчетаКапитализации.СуммаВР КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУКт,
	|	втДанныеСчетаКапитализации.СуммаПР КАК СуммаПРКт,
	|	втДанныеСчетаКапитализации.СуммаВР КАК СуммаВРКт,
	|	""Принятие к учету ОС"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|ГДЕ
	|	втДанныеДокумента.ПорядокУчетаБУ В(
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету)
	|	)
	|	И втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное)
	|	)
	|	И НЕ втДанныеДокумента.ДокументНаОснованииИнвентаризации
	|";
	
КонецФункции

Функция ПринятиеКУчетуОСЧастичныеЦелевыеСредства()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчета:: Кт 08.04.2, 08.03)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеСчетаКапитализации.СуммаБУЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втДанныеДокумента.СчетУчетаЦФ КАК СчетДт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	втДанныеСчетаКапитализации.СуммаНУЦФ + втДанныеСчетаКапитализации.СуммаПРЦФ + втДанныеСчетаКапитализации.СуммаВРЦФ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДокумента.Подразделение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеСчетаКапитализации.Счет КАК СчетКт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУЦФ КАК СуммаНУКт,
	|	втДанныеСчетаКапитализации.СуммаПРЦФ КАК СуммаПРКт,
	|	втДанныеСчетаКапитализации.СуммаВРЦФ КАК СуммаВРКт,
	|	""Принятие к учету ОС"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|ГДЕ
	|	втДанныеДокумента.ПорядокУчетаБУ В(
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается),
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету)
	|	)
	|	И втДанныеДокумента.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное)
	|	)
	|	И НЕ втДанныеДокумента.ДокументНаОснованииИнвентаризации
	|";
	
КонецФункции

Функция АмортизационнаяПремияДтКВ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизационная премия (Дт КВ :: Кт <ПустаяСсылка>)
	|//
	|ВЫБРАТЬ
	|
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения) КАК СчетДт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоДт1,
	|	втДанныеДокумента.Ссылка КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	ВЫРАЗИТЬ(ВЫБОР КОГДА УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|					И втДанныеДокумента.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) ТОГДА
	|		втДанныеДокумента.СтоимостьНУ
	|	ИНАЧЕ
	|		втДанныеСчетаКапитализации.СуммаНУ 
	|	КОНЕЦ * (втДанныеДокумента.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ / 100) КАК ЧИСЛО(31,2))  КАК СуммаНУДт,
	|
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Амортизационная премия"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализацииСгруппированные КАК втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО втДанныеДокумента.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И втДанныеДокумента.Организация = УчетнаяПолитикаОрганизаций.Организация
	|ГДЕ
	|	втДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|	И втДанныеДокумента.ВключитьАмортизационнуюПремиюВСоставРасходов
	|";
	
КонецФункции

Функция СписаниеПриПринятииКУчетуНУ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Включение в расходы при принятии к учету(Дт СчетЗатрат:: СчетУчетаОС)
	|//
	|ВЫБРАТЬ
	|	
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Период,
	|	втДанныеДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	втДанныеДокумента.СтатьяРасходов КАК АналитикаУчетаДт,
	|	втДанныеДокумента.Местонахождение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеДт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ КАК СчетДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		втДанныеДокумента.АналитикаРасходов // Объект строительства
	|	ИНАЧЕ
	|		втДанныеДокумента.СтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		втДанныеДокумента.СтатьяРасходов
	|	ИНАЧЕ
	|		втДанныеДокумента.Ссылка
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	- втДанныеСчетаКапитализации.СуммаНУ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	втДанныеДокумента.Местонахождение КАК ПодразделениеКт,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	втДанныеДокумента.СчетУчета КАК СчетКт,
	|	
	|	втДанныеДокумента.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втДанныеСчетаКапитализации.СуммаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	- втДанныеСчетаКапитализации.СуммаНУ КАК СуммаВРКт,
	|	""Включение в расходы при принятии к учету"" КАК Содержание
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеСчетаКапитализации
	|	ПО втДанныеДокумента.ОсновноеСредство = втДанныеСчетаКапитализации.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО втДанныеДокумента.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	втДанныеДокумента.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|";
	
КонецФункции

Функция СписаниеПриПринятииКУчетуНУПоРезультатамИнвентаризации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Включение в расходы при принятии к учету(Дт СчетЗатрат:: СчетУчетаОС)
	|//
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Местонахождение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Местонахождение КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	КОНЕЦ КАК СчетДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Операция.АналитикаРасходов // Объект строительства
	|	ИНАЧЕ
	|		Операция.СтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Операция.СтатьяРасходов
	|	ИНАЧЕ
	|		Операция.Ссылка
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	- Строки.СтоимостьНУ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Местонахождение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Операция.СчетУчета КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-Строки.СтоимостьНУ КАК СуммаВРКт,
	|	""Включение в расходы при принятии к учету"" КАК Содержание
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Операция.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|	И Операция.ДокументНаОснованииИнвентаризации";
	
КонецФункции

Функция ПринятиеКУчетуАмортизиторованногоОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету самортизизрованного ОС (Дт СчетУчета:: Кт СчетАмортизации)
	|//
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Местонахождение КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Операция.СчетУчета КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Местонахождение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	Операция.СчетАмортизации КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	""Принятие к учету самортизированного ОС"" КАК Содержание
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ПорядокУчетаБУ В(
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|	)
	|";
	
КонецФункции

Функция ПринятиеКУчетуПоРезультатамИнвентаризации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету самортизизрованного ОС (Дт СчетУчета:: Кт СчетДоходов)
	|//
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Местонахождение КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Операция.СчетУчета КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Операция.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Операция.Местонахождение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Местонахождение КАК ПодразделениеКт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Операция.СтатьяДоходов КАК СубконтоКт1,
	|	Операция.АналитикаДоходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	""Принятие к учету по результатам инвентаризации"" КАК Содержание
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ДокументНаОснованииИнвентаризации
	|";
	
КонецФункции

Функция СписаниеСЗабалансовогоУчета()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выбытие с забалансового учета арендуемых ОС (Дт ---:: Кт СчетЗабалансовогоУчета)
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ОстаткиПоСчетуУчета.СуммаОстатокДт КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	ОстаткиПоСчетуУчета.Субконто2 КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выбытие с забалансового учета арендуемых ОС"" КАК Содержание
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|			&Дата,
	|			Регистратор <> &Ссылка
	|			И (Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					Т.Ссылка.Организация,
	|					Т.ОсновноеСредство
	|				ИЗ
	|					Документ.ПринятиеКУчетуОС.ОС КАК Т
	|				ГДЕ Т.Ссылка = &Ссылка)) КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В 
	|				(ВЫБРАТЬ СчетаОтражения.СчетУчета
	|				ИЗ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|					&Дата,
	|					Регистратор <> &Ссылка
	|					И (Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Ссылка.Организация,
	|							Т.ОсновноеСредство
	|						ИЗ
	|							Документ.ПринятиеКУчетуОС.ОС КАК Т
	|						ГДЕ Т.Ссылка = &Ссылка)) КАК СчетаОтражения),
	|				&ВидыСубконтоХозрасчетныеОсновныеСредстваКонтрагенты,
	|			(Организация, Подразделение, Субконто1) В
	|				(ВЫБРАТЬ
	|					ТабличнаяЧастьОС.Ссылка.Организация, ТабличнаяЧастьОС.Ссылка.Подразделение, ТабличнаяЧастьОС.ОсновноеСредство
	|				ИЗ
	|					Документ.ПринятиеКУчетуОС.ОС КАК ТабличнаяЧастьОС
	|				ГДЕ
	|					ТабличнаяЧастьОС.Ссылка = &Ссылка)
	|			) КАК ОстаткиПоСчетуУчета
	|		ПО Строки.ОсновноеСредство = ОстаткиПоСчетуУчета.Субконто1
	|			И СчетаОтражения.СчетУчета = ОстаткиПоСчетуУчета.Счет
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ПорядокУчетаБУ В(
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|	)
	|	И ОстаткиПоСчетуУчета.СуммаОстатокДт <> 0
	|";
	
КонецФункции

Функция ПризнаниеДоходовБудущихПериодов()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов будущих периодов (Дт 86:: Кт 98)
	|//
	|ВЫБРАТЬ
	|	
	|	втЦелевоеФинансирование.Ссылка КАК Ссылка,
	|	втЦелевоеФинансирование.Дата КАК Период,
	|	втЦелевоеФинансирование.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втЦелевоеФинансирование.Сумма КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втЦелевоеФинансирование.Подразделение КАК ПодразделениеДт,
	|	втЦелевоеФинансирование.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	втЦелевоеФинансирование.Счет КАК СчетДт,
	|	
	|	втЦелевоеФинансирование.Субконто1 КАК СубконтоДт1,
	|	втЦелевоеФинансирование.Субконто2 КАК СубконтоДт2,
	|	втЦелевоеФинансирование.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втЦелевоеФинансирование.Сумма КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втЦелевоеФинансирование.Подразделение ПодразделениеКт,
	|	втЦелевоеФинансирование.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетКт,
	|	
	|	втЦелевоеФинансирование.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втЦелевоеФинансирование.Сумма КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Признание доходов будущих периодов"" КАК Содержание
	|ИЗ
	|	втЦелевоеФинансированиеИтог КАК втЦелевоеФинансирование
	|	
	|ГДЕ
	|	втЦелевоеФинансирование.ВариантПримененияЦелевогоФинансирования В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное)
	|	)
	|";
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// ОС-1 (Акт о приеме-передаче ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОС1";
	КомандаПечати.Идентификатор = "ОС1";
	КомандаПечати.Представление = НСтр("ru = 'ОС-1 (Акт о приеме-передаче ОС)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр(
		"ИспользоватьУчетДрагоценныхМатериалов",
		ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов"));
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Шапка.Ссылка КАК Ссылка,
	|	Шапка.Номер КАК НомерДокумента,
	|	Шапка.Дата КАК ДатаДокумента,
	|	Шапка.Дата КАК ДатаВвода,
	|	НЕОПРЕДЕЛЕНО КАК ДатаПередачи,
	|	Шапка.Организация.Префикс КАК Префикс,
	|	НЕОПРЕДЕЛЕНО КАК Сдатчик,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеСдатчика,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетСдатчика,
	|	Шапка.Организация КАК Получатель,
	|	Шапка.Местонахождение КАК ПодразделениеПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК Руководитель,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК ГлавныйБухгалтер,
	|	НЕОПРЕДЕЛЕНО КАК СдалМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьСдалМОЛ,
	|	Шапка.МОЛ КАК ПринялМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьПринялМОЛ
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ПО Шапка.Ссылка = ТаблицаДанныхДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.ОсновноеСредство.НаименованиеПолное = """"
	|			ТОГДА ТабличнаяЧасть.ОсновноеСредство
	|		ИНАЧЕ ТабличнаяЧасть.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК НаименованиеОС,
	|	ТабличнаяЧасть.Ссылка.ГруппаОС КАК ГруппаОС,
	|	ЕСТЬNULL(ТабличнаяЧасть.Ссылка.АмортизационнаяГруппа.Порядок + 1, 0) КАК АмортизационнаяГруппа,
	|	ТабличнаяЧасть.Ссылка.КодПоОКОФ.Код КАК КодОКОФ,
	|	ТабличнаяЧасть.ИнвентарныйНомер КАК НомерИнвентарный,
	|	ОбъектыЭксплуатации.ЗаводскойНомер КАК НомерЗаводской,
	|	НЕОПРЕДЕЛЕНО КАК ДатаНачалаСтроительства,
	|	ОбъектыЭксплуатации.ДатаВыпуска КАК ДатаВыпуска,
	|	ОбъектыЭксплуатации.Изготовитель КАК Изготовитель,
	|	ТабличнаяЧасть.Ссылка.Дата КАК ДатаВводаВЭксплуатацию,
	|	НЕОПРЕДЕЛЕНО КАК ДатаМодернизации,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКапитальногоРемонта,
	|	0 КАК СрокИспользованияФакт,
	|	0 КАК СрокИспользования,
	|	ТабличнаяЧасть.Ссылка.СрокИспользованияБУ КАК СрокИспользованияРаздел2,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ПервоначальнаяСтоимость КАК СтоимостьПервоначальная,
	|	0 КАК СтоимостьОстаточная,
	|	ТабличнаяЧасть.Ссылка.СчетУчета КАК СчетУчетаОС,
	|	0 КАК ЦенаПродажи,
	|	0 КАК АмортизацияСумма,
	|	ТабличнаяЧасть.Ссылка.МетодНачисленияАмортизацииБУ КАК АмортизацияСпособ,
	|	ТабличнаяЧасть.Ссылка.ГодоваяНормаАмортизацииБУ КАК АмортизацияНорма,
	|	ИСТИНА КАК СведенияАктуальны
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ПО ТабличнаяЧасть.Ссылка = ТаблицаДанныхДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО ТабличнаяЧасть.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ТабличнаяЧасть.Ссылка = ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор
	|			И ТабличнаяЧасть.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТабличнаяЧасть.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Ссылка КАК Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал КАК ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ДрагоценныеМатериалы.Количество) КАК Количество
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ПО ТабличнаяЧасть.Ссылка = ТаблицаДанныхДокументов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации.ДрагоценныеМатериалы КАК ДрагоценныеМатериалы
	|		ПО ТабличнаяЧасть.ОсновноеСредство = ДрагоценныеМатериалы.Ссылка
	|ГДЕ
	|	&ИспользоватьУчетДрагоценныхМатериалов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабличнаяЧасть.Ссылка
	|ИТОГИ ПО
	|	Ссылка";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатПоДрагоценнымМатериалам",
		РезультатыЗапроса[0],
		РезультатыЗапроса[1],
		РезультатыЗапроса[2]);
	
КонецФункции

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ДобавитьПараметрыДляПроведения(НаборЗаписей, СтруктураВременныеТаблицы)

	НаборЗаписей.ДополнительныеСвойства.Вставить("ДляПроведения", 
		Новый Структура("СтруктураВременныеТаблицы",
			СтруктураВременныеТаблицы));
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
