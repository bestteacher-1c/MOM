#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Команды

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.СправкаРасчетАмортизацииОС2_4.ДобавитьКомандуОтчетаПоДокументуАмортизацииОС(КомандыОтчетов);
	
	АмортизацияОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	АмортизацияОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
	АмортизацияОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'Амортизация ОС %1 за %2'"), Данные.Номер, Формат(Данные.Дата, "ДФ='ММММ гггг'"));
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет расчет амортизации в заданном месяце и записывает его с помощью проведения документа "Амортизация ОС".
//
// Параметры:
//  ИсходныеПараметрыРасчета - Структура - Содержит параметры расчета (см. НовыеПараметрыРасчетаАмортизации()).
//
Процедура РассчитатьАмортизацию(ИсходныеПараметрыРасчета, АдресХранилища) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ЗакрытиеМесяца.НачислениеАмортизацииОС");
	
	ПараметрыРасчета = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ИсходныеПараметрыРасчета);
	ВнеоборотныеАктивыСлужебный.ДополнитьПараметрамиУчетаВнеоборотныхАктивов(ПараметрыРасчета);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИсходныеПараметрыРасчета.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Основной расчет амортизации выполняется с помощью запроса
	ТекстЗапроса = ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);
	
	КоличествоОбработанныхДанных = 0;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		// Расчет доначисления амортизации за прошлый месяц
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(РезультатЗапроса, Неопределено);
		
		// Расчет амортизации за текущий месяц
		Запрос.Текст = ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(
									РезультатЗапроса[РезультатЗапроса.ВГраница()], 
									РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выгрузить());
		
		Запрос.Текст = ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		КоличествоОбработанныхДанных = Выборка.КоличествоДанных;
		
		ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		
		АмортизацияОСЛокализация.ПослеРасчетаАмортизации(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц);
		
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц,, "ВТ_НачисленнаяАмортизация,НомераПакетовСФилиалами");
		
		Если ПараметрыРасчета.ЗаписатьДанные Тогда
			ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		КонецЕсли;
	
	Исключение
		
		ПараметрыРасчета.ЕстьОшибки = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета.ЭтапРасчета, 
			ПараметрыРасчета.Период, 
			ПараметрыРасчета.ПакетыАмортизации.ВыгрузитьКолонку("Организация"),
			ТекстОшибки);
			
	КонецПопытки; 
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОбработанныхДанных / 100);
	
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		ПоместитьВоВременноеХранилище(ПараметрыРасчета.ЕстьОшибки, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Определяет номер пакета амортизации.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - Организация.
//
// Возвращаемое значение:
//  Число - Номер пакета.
//
Функция НомерПакетаПоУмолчанию(Организация) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПакетыАмортизацииОС.НомерПакета) КАК НомерПакета,
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПакетыАмортизацииОС.НомерПакета), 0) КАК КоличествоНомеров
	|ИЗ
	|	РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|ГДЕ
	|	ПакетыАмортизацииОС.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыАмортизацииОС.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НомерПакета = 0;
	Если Выборка.Следующий() Тогда
		Если Выборка.КоличествоНомеров = 1 Тогда
			НомерПакета = Выборка.НомерПакета;
		КонецЕсли; 
	КонецЕсли;

	Возврат НомерПакета;
	
КонецФункции

// Добавляет в список запросов формирование временной таблицы ВТ_НачисленнаяАмортизация.
//  Используется для универсального начисления амортизации.
//  Созданная временная таблица может использоваться для формирования движений по регистрам.
//
// Параметры:
//  СписокЗапросов			 - Массив	 - Список запросов.
//  ТекстПодзапроса			 - Строка	 - Текст запроса, содержащий результат расчета амортизации.
//  ОпределитьНомерПакета	 - Булеов	 - Истина, если нужно определить номер пакета.
//
Процедура ТекстЗапросаВтНачисленнаяАмортизация(СписокЗапросов, ТекстПодзапроса, ОпределитьНомерПакета) Экспорт

	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаВтНачисленнаяАмортизация();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НачисленнаяАмортизация.ПериодРасчета,
		|	НачисленнаяАмортизация.Организация,
		|	&НомерПакета КАК НомерПакета,
		|	НачисленнаяАмортизация.Подразделение,
		|	НачисленнаяАмортизация.КорПодразделение,
		|	НачисленнаяАмортизация.ОсновноеСредство,
		|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
		|	НачисленнаяАмортизация.НаправлениеДеятельности,
		|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
		|	НачисленнаяАмортизация.СтатьяРасходов,
		|	НачисленнаяАмортизация.АналитикаРасходов,
		|	СУММА(НачисленнаяАмортизация.СуммаУУ) КАК СуммаУУ,
		|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС) КАК ХозяйственнаяОперация,
		|	НачисленнаяАмортизация.ЭтоДоначисление,
		|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод) КАК РесурсВыработанныйЗаПрошлыйПериод,
		|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод) КАК РесурсВыработанныйЗаТекущийПериод,
		|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации) КАК СтоимостьДляВычисленияАмортизации
		|ПОМЕСТИТЬ ВТ_НачисленнаяАмортизация
		|ИЗ
		|	(&ТекстПодзапроса) КАК НачисленнаяАмортизация
		|	//ТекстСоединения//
		|
		|СГРУППИРОВАТЬ ПО
		|	НачисленнаяАмортизация.ПериодРасчета,
		|	НачисленнаяАмортизация.Организация,
		|	&НомерПакета,
		|	НачисленнаяАмортизация.Подразделение,
		|	НачисленнаяАмортизация.КорПодразделение,
		|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
		|	НачисленнаяАмортизация.НаправлениеДеятельности,
		|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
		|	НачисленнаяАмортизация.ОсновноеСредство,
		|	НачисленнаяАмортизация.СтатьяРасходов,
		|	НачисленнаяАмортизация.АналитикаРасходов,
		|	НачисленнаяАмортизация.ЭтоДоначисление
		|
		|ИМЕЮЩИЕ
		|	СУММА(НачисленнаяАмортизация.СуммаУУ) <> 0
		|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаБУ) <> 0";
	
	КонецЕсли; 
	
	Если ОпределитьНомерПакета Тогда
		
		ТекстСоединения = 
			"	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
			|		ПО ПакетыАмортизацииОС.Организация = НачисленнаяАмортизация.Организация
			|			И ПакетыАмортизацииОС.ОсновноеСредство = НачисленнаяАмортизация.ОсновноеСредство";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстСоединения//", ТекстСоединения);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НомерПакета", "ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0)");
		
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", ТекстПодзапроса);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.АмортизацияОС2_4";
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаПартииПрочихРасходов_Амортизация(Запрос, ТекстыЗапроса, Регистры);
	
	АмортизацияОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Дата             КАК Период,
	|	ДанныеДокумента.Номер            КАК Номер,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления  КАК ПометкаУдаления,
	|	ДанныеДокумента.Комментарий      КАК Комментарий,
	|	ДанныеДокумента.Ответственный    КАК Ответственный,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.НомерПакета      КАК НомерПакета
	|ИЗ
	|	Документ.АмортизацияОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.АмортизацияОС2_4"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация",  Перечисления.ХозяйственныеОперации.АмортизацияОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаНУ    КАК СтоимостьНУ,
	|	-НачисленнаяАмортизация.СуммаНУ   КАК СтоимостьВР,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизационнаяПремия,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.Подразделение                 КАК КорПодразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос
	|ИЗ
	|	ВТ_НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.ЭтоАмортизационнаяПремия
	|	И НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|	КОНЕЦ                                           КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Амортизация,
	|	НачисленнаяАмортизация.СуммаБУ    КАК АмортизацияРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизацияНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК АмортизацияПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК АмортизацияВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК АмортизацияЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК АмортизацияНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК АмортизацияВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление
	|ИЗ
	|	ВТ_НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НЕ НачисленнаяАмортизация.ЭтоАмортизационнаяПремия
	|	И НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период  КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	СписокЗапросовОбъединение = Новый Массив;
	
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПрочихРасходах());
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область РасчетАмортизации

Процедура ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц)

	НачатьТранзакцию();
	
	Попытка
	
		// Нужно заблокировать документы, чтобы не создать дублей
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("Документ.АмортизацияОС2_4");
		ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(НачалоМесяца(ПараметрыРасчета.Период),КонецМесяца(ПараметрыРасчета.Период)));
		ЭлементБлокировки.ИсточникДанных = ПараметрыРасчета.ПакетыАмортизации;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НомерПакета", "НомерПакета");
		
		Блокировка.Заблокировать(); 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаНеПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.ПометкаУдаления, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаУдален
		|ИЗ
		|	НомераПакетовСФилиалами КАК НомераПакетовСФилиалами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АмортизацияОС2_4 КАК РегламентныйДокумент
		|		ПО (РегламентныйДокумент.Организация = НомераПакетовСФилиалами.Организация)
		|			И (РегламентныйДокумент.НомерПакета = НомераПакетовСФилиалами.НомерПакета)
		|			И (РегламентныйДокумент.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца)
		|ГДЕ
		|	НомераПакетовСФилиалами.Организация В(&СписокОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НомерПакета = ПараметрыРасчета.НомерПакета Тогда
				
				// Расчет выполняется из формы документа.
				Если ЗначениеЗаполнено(ПараметрыРасчета.Ссылка) Тогда
					ДокументОбъект = ПараметрыРасчета.Ссылка.ПолучитьОбъект();
				Иначе 
					ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
						Выборка.СсылкаПроведен, Неопределено, Неопределено, Документы.АмортизацияОС2_4);
				КонецЕсли;
				
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Комментарий   = ПараметрыРасчета.Комментарий;
				ДокументОбъект.Ответственный = ПараметрыРасчета.Ответственный;
				
			Иначе	
				
				ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
					Выборка.СсылкаПроведен, Выборка.СсылкаНеПроведен, Выборка.СсылкаУдален, Документы.АмортизацияОС2_4);
					
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
			КонецЕсли; 
				
			ДокументОбъект.Дата = КонецМесяца(ПараметрыРасчета.Период);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			ВнеоборотныеАктивыСлужебный.УничтожитьВременныеТаблицыПослеЗаписиРасчетаАмортизации(МенеджерВременныхТаблиц);
			
		КонецЦикла; 
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
КонецПроцедуры

Функция РассчитатьАмортизациюИзменений(РезультатЗапроса, ТаблицаРазукомплектации)

	АмортизацияИзменений = РезультатЗапроса.Выгрузить();
	АмортизацияИзменений.Сортировать("ОсновноеСредство,Период,Приоритет");
	
	ТекущееОС = Неопределено;
	ОстаточнаяСтоимость = 0;
	ОстаточнаяСтоимостьРегл = 0;
	НачисленнаяАмортизацияЗаМесяц = 0;
	НачисленнаяАмортизацияЗаМесяцРегл = 0;
	РесурсНазначенный = 0;
	
	Для каждого ДанныеСтроки Из АмортизацияИзменений Цикл
		
		Если РесурсНазначенный <> ДанныеСтроки.РесурсНазначенный Тогда
			РесурсНазначенный = ДанныеСтроки.РесурсНазначенный;
		КонецЕсли;
		
		Если ДанныеСтроки.ОсновноеСредство <> ТекущееОС Тогда
			ОстаточнаяСтоимость = ДанныеСтроки.Стоимость - ДанныеСтроки.Амортизация;
			ОстаточнаяСтоимостьРегл = ДанныеСтроки.СтоимостьРегл - ДанныеСтроки.АмортизацияРегл;
			НачисленнаяАмортизацияЗаМесяц = 0;
			НачисленнаяАмортизацияЗаМесяцРегл = 0;
			ТекущееОС = ДанныеСтроки.ОсновноеСредство;
		КонецЕсли; 
		
		Если ДанныеСтроки.ЭтоИзменения Тогда

			Если ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.ПереоценкаОС2_4") Тогда
				
				// Нужно расчитать остаточную стоимость так как это делает переоценка.
				
				ОстаточнаяСтоимость = ДанныеСтроки.СтоимостьИзменение;
				ОстаточнаяСтоимостьРегл = ДанныеСтроки.СтоимостьРеглИзменение;
				
				Если ДанныеСтроки.Стоимость <> 0 Тогда
					
					НоваяАмортизация = (ДанныеСтроки.Амортизация + НачисленнаяАмортизацияЗаМесяц) 
												* ДанныеСтроки.СтоимостьИзменение 
												/ ДанныеСтроки.Стоимость;
												
					Если ДанныеСтроки.СтоимостьРегл <> 0 Тогда
						НоваяАмортизацияРегл = (ДанныеСтроки.АмортизацияРегл + НачисленнаяАмортизацияЗаМесяцРегл) 
													* ДанныеСтроки.СтоимостьРеглИзменение 
													/ ДанныеСтроки.СтоимостьРегл;
					Иначе
						НоваяАмортизацияРегл = 0;
					КонецЕсли;
												
					ОстаточнаяСтоимость = ОстаточнаяСтоимость - Окр(НоваяАмортизация, 2); 
					ОстаточнаяСтоимостьРегл = ОстаточнаяСтоимостьРегл - Окр(НоваяАмортизацияРегл, 2); 
					
				КонецЕсли; 

			ИначеЕсли ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.СписаниеОС2_4") Тогда
				
				Если ДанныеСтроки.НачислятьАмортизацию Тогда
					
					// Нужно расчитать остаточную стоимость так как это делает списание.
					
					// Уменьшение стоимости на сумму списания.
					ОстаточнаяСтоимость = ОстаточнаяСтоимость + ДанныеСтроки.СтоимостьИзменение;
					ОстаточнаяСтоимостьРегл = ОстаточнаяСтоимостьРегл + ДанныеСтроки.СтоимостьРеглИзменение;
					
					// Увеличение стоимости на сумму списанной амортизации.
					ДоляСписания = ?(ДанныеСтроки.Стоимость <> 0, Окр(-ДанныеСтроки.СтоимостьИзменение / ДанныеСтроки.Стоимость, 16), 1);
					ОстаточнаяСтоимость = ОстаточнаяСтоимость + (ДанныеСтроки.Амортизация + НачисленнаяАмортизацияЗаМесяц) * ДоляСписания;
					
					Если ДанныеСтроки.СтоимостьРегл <> 0 Тогда
						ДоляСписания = ?(ДанныеСтроки.СтоимостьРегл <> 0, Окр(-ДанныеСтроки.СтоимостьРеглИзменение / ДанныеСтроки.СтоимостьРегл, 16), 1);
						ОстаточнаяСтоимостьРегл = ОстаточнаяСтоимостьРегл + (ДанныеСтроки.АмортизацияРегл + НачисленнаяАмортизацияЗаМесяцРегл) * ДоляСписания;
					КонецЕсли; 
					
				Иначе	
					ОстаточнаяСтоимость = 0;
					ОстаточнаяСтоимостьРегл = 0;
				КонецЕсли; 
				
			Иначе
				ОстаточнаяСтоимость = ОстаточнаяСтоимость + ДанныеСтроки.СтоимостьИзменение;
				ОстаточнаяСтоимостьРегл = ОстаточнаяСтоимостьРегл + ДанныеСтроки.СтоимостьРеглИзменение;
			КонецЕсли; 
			
			Если ТаблицаРазукомплектации <> Неопределено Тогда
				
				Если ДанныеСтроки.ЭтоРазукомплектация Тогда
					
					СтруктураПоиска = Новый Структура("ИсходноеОсновноеСредство", ДанныеСтроки.ОсновноеСредство);
					СписокСтрок = ТаблицаРазукомплектации.НайтиСтроки(СтруктураПоиска);
					
					СтоимостьНовыхОС = 0;
					СтоимостьНовыхОСРегл = 0;
					Для каждого ПолученныйОС Из СписокСтрок Цикл
						СтоимостьНовыхОС = СтоимостьНовыхОС + ПолученныйОС.Стоимость;
						СтоимостьНовыхОСРегл = СтоимостьНовыхОСРегл + ПолученныйОС.СтоимостьРегл;
					КонецЦикла; 
					
					Для каждого ПолученныйОС Из СписокСтрок Цикл
						
						ПолученныйОС.Амортизация = (ДанныеСтроки.Амортизация + ДанныеСтроки.СуммаУУ + НачисленнаяАмортизацияЗаМесяц) 
														* ПолученныйОС.Стоимость / (ДанныеСтроки.Стоимость + ДанныеСтроки.СтоимостьИзменение + СтоимостьНовыхОС);
						ПолученныйОС.Амортизация = Окр(ПолученныйОС.Амортизация, 2);
						ОстаточнаяСтоимость = ОстаточнаяСтоимость + ПолученныйОС.Амортизация; 
						
						//
						Если (ДанныеСтроки.СтоимостьРегл + ДанныеСтроки.СтоимостьРеглИзменение + СтоимостьНовыхОСРегл) <> 0 Тогда
							ПолученныйОС.АмортизацияРегл = (ДанныеСтроки.АмортизацияРегл + ДанныеСтроки.СуммаБУ + НачисленнаяАмортизацияЗаМесяцРегл) 
															* ПолученныйОС.СтоимостьРегл / (ДанныеСтроки.СтоимостьРегл + ДанныеСтроки.СтоимостьРеглИзменение + СтоимостьНовыхОСРегл);
							ПолученныйОС.АмортизацияРегл = Окр(ПолученныйОС.АмортизацияРегл, 2);
							ОстаточнаяСтоимостьРегл = ОстаточнаяСтоимостьРегл + ПолученныйОС.АмортизацияРегл;
						КонецЕсли; 
						
					КонецЦикла; 
					
				ИначеЕсли ДанныеСтроки.ПолученПриРазукомплектации Тогда
					
					СтруктураПоиска = Новый Структура("ИсходноеОсновноеСредство", ДанныеСтроки.ОсновноеСредство);
					ПолученныйОС = ТаблицаРазукомплектации.Найти(ДанныеСтроки.ОсновноеСредство, "ОсновноеСредство");
					Если ПолученныйОС <> Неопределено Тогда
						ОстаточнаяСтоимость = ОстаточнаяСтоимость - ПолученныйОС.Амортизация;
						ОстаточнаяСтоимостьРегл = ОстаточнаяСтоимостьРегл - ПолученныйОС.АмортизацияРегл;
					КонецЕсли; 
					
				КонецЕсли;
				
			КонецЕсли; 
			
			ДанныеСтроки.СтоимостьДляВычисленияАмортизации = ОстаточнаяСтоимость;
			
			ДанныеСтроки.СуммаУУ = РассчитатьСуммуАмортизацииУУ(
										ДанныеСтроки, 
										ОстаточнаяСтоимость, 
										ДанныеСтроки.ЛиквидационнаяСтоимость, 
										НачисленнаяАмортизацияЗаМесяц);
			
			ДанныеСтроки.СуммаБУ = РассчитатьСуммуАмортизацииУУ(
										ДанныеСтроки, 
										ОстаточнаяСтоимостьРегл, 
										ДанныеСтроки.ЛиквидационнаяСтоимостьРегл, 
										НачисленнаяАмортизацияЗаМесяцРегл);
			
		КонецЕсли;
		
		НачисленнаяАмортизацияЗаМесяц = НачисленнаяАмортизацияЗаМесяц + ДанныеСтроки.СуммаУУ;
		НачисленнаяАмортизацияЗаМесяцРегл = НачисленнаяАмортизацияЗаМесяцРегл + ДанныеСтроки.СуммаБУ;
		
	КонецЦикла; 
	
	Возврат АмортизацияИзменений;

КонецФункции

Функция РассчитатьСуммуАмортизацииУУ(ДанныеСтроки, ОстаточнаяСтоимость, ЛиквидационнаяСтоимость, НачисленнаяАмортизацияЗаМесяц)

	СуммаУУ = 0;
	
	ОстаточнаяСтоимостьПослеНачисленияАмортизации =
		(ОстаточнаяСтоимость
			- НачисленнаяАмортизацияЗаМесяц 
			- ЛиквидационнаяСтоимость);
				
	Если НЕ ДанныеСтроки.НачислятьАмортизацию 
		ИЛИ ОстаточнаяСтоимостьПослеНачисленияАмортизации <= 0
		ИЛИ ДанныеСтроки.РесурсНазначенный <= 0 Тогда
		
		СуммаУУ = 0;
		
	ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
		
		СуммаУУ = ОстаточнаяСтоимость - ЛиквидационнаяСтоимость;
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.Линейный
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка
			И (ДанныеСтроки.РесурсНазначенный
				- ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод
				- ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) <= 12 Тогда
		
		СуммаУУ = (ОстаточнаяСтоимость - ЛиквидационнаяСтоимость)
					* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка Тогда
		
		СуммаУУ = ОстаточнаяСтоимость
					* ДанныеСтроки.КоэффициентУскорения
					* ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.КоэффициентУскорения 
							*(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод - ДанныеСтроки.ОтработанныхМесяцевПолныхЛет));
	КонецЕсли; 
	
	Если СуммаУУ > ОстаточнаяСтоимостьПослеНачисленияАмортизации
		ИЛИ (ОстаточнаяСтоимостьПослеНачисленияАмортизации - СуммаУУ) < ВнеоборотныеАктивыСлужебный.МинимальнаяОстаточнаяСтоимость() Тогда
		СуммаУУ = Макс(ОстаточнаяСтоимостьПослеНачисленияАмортизации, 0);
	КонецЕсли;

	Возврат Окр(СуммаУУ, 2);
	
КонецФункции
 
Процедура ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц) Экспорт
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).Наименование КАК ОбъектУчетаПредставление,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ) КАК НеУказанСрокИспользованияУУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанРесурсВыработанный) КАК НеУказанРесурсВыработанный,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанОбъемНаработки) КАК НеУказанОбъемНаработки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроверкаПараметровРасчета.Организация КАК Организация,
	|		ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
	|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки
	|	ИЗ
	|		ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|	ГДЕ
	|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПроверкаПараметровРасчета.Организация КАК Организация,
	|		ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|		ПроверкаПараметровРасчета.НеУказанРесурсВыработанный КАК НеУказанРесурсВыработанный,
	|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки
	|	ИЗ
	|		ВТ_ПроверкаПараметровРасчетаИзменения_УУ КАК ПроверкаПараметровРасчета
	|	ГДЕ
	|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
	|
	|) КАК ПроверкаПараметровРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверкаПараметровРасчета.ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонОбъемНаработки      = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан предполагаемый объем наработки (продукции, работ).'");
	ШаблонСрокИспользованияУУ = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан срок использования в управленческом учете.'");
	ШаблонРесурсВыработанный  = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не удалось определить выработанный ресурс в управленческом учете.'");
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеУказанОбъемНаработки Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработки, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанРесурсВыработанный Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонРесурсВыработанный, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанСрокИспользованияУУ Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСрокИспользованияУУ, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
	
	КонецЦикла;
	
	АмортизацияОСЛокализация.ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
	
КонецПроцедуры

#Область ТекстыЗапросовДляРасчетаАмортизации

Функция ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запросов, в которых подготавливаются параметры для расчета
	ТекстЗапросаПакетыАмортизации(СписокЗапросов);
	ТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета);
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета);
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов);
	
	// Выборка должна быть последней
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПакетыАмортизации(СписокЗапросов)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетов
	|ИЗ
	|	&ПакетыАмортизации КАК ПакетыАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераПакетов.Организация КАК Организация,
	|	НомераПакетов.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетовСФилиалами
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Филиалы.Ссылка,
	|	НомераПакетов.НомерПакета
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Филиалы
	|		ПО (Филиалы.ГоловнаяОрганизация = НомераПакетов.Организация)
	|			И (НЕ Филиалы.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НомерПакета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.ОсновноеСредство КАК ОбъектУчета
	|ПОМЕСТИТЬ ПакетыАмортизации
	|ИЗ
	|	НомераПакетовСФилиалами КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
	|		ПО (ПакетыАмортизации.Организация = НомераПакетов.Организация)
	|			И (ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаСписокОбъектовУчета(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокОС.НачалоМесяца                        КАК НачалоМесяца,
		|	СписокОС.КонецМесяца                         КАК КонецМесяца,
		|	СписокОС.Организация                         КАК Организация,
		|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
		|	СписокОС.СпособОтраженияРасходовУУ           КАК СпособОтраженияРасходов,
		|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета
		|ИЗ
		|	ВТ_СписокОбъектовУчета_УУ КАК СписокОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
		|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
		|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Формирует тексты запросов, которые создают таблицы объектов учета, по которым требуется расчет амортизации.
//
// Параметры:
//  СписокЗапросов	 - Массив	 - Список запросов, в который нужно добавить текст запроса.
//  ПараметрыРасчета - Структура - Содержит параметры расчета амортизации.
//  ПрефиксТаблицы	 - Строка	 - Используется для добавления префикса к созданным таблицам.
//
Процедура ТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы = "") Экспорт
	
	#Область ВТ_СписокОбъектовУчета_Доначисление_УУ
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьОС.Организация      КАК Организация,
		|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
		|	МИНИМУМ(СтоимостьОС.Период)  КАК Период
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_Доначисление_УУ
		|ИЗ
		|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|ГДЕ
		|	СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьОС.Активность
		|	И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И СтоимостьОС.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
		|	И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьОС.Организация,
		|	СтоимостьОС.ОсновноеСредство
		|
		|ИМЕЮЩИЕ
		|	СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0
		|		ИЛИ НЕ &ВедетсяРегламентированныйУчетВНА 
		|			И СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
		
		Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_Доначисление_УУ", "ВТ_СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
		КонецЕсли; 
		
		СписокЗапросов.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	#Область ВТ_СписокОбъектовУчета_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	&КонецМесяца                    КАК КонецМесяца,
		|	ЛОЖЬ                            КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_УУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоПредыдущегоМесяца        КАК НачалоМесяца,
		|	&КонецПредыдущегоМесяца         КАК КонецМесяца,
		|	ИСТИНА                          КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&КонецПозапрошлогоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)) КАК ПорядокУчетаОС
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	&КонецМесяца                    КАК КонецМесяца,
		|	ЛОЖЬ                            КАК ЭтоДоначисление,
		|	ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) = ТИП(Документ.РазукомплектацияОС) КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОС
		|ГДЕ
		|	ПорядокУчетаОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (
		|				ТИП(Документ.ПринятиеКУчетуОС2_4),
		|				ТИП(Документ.РазукомплектацияОС))
		|	И (ПорядокУчетаОС.Организация, ПорядокУчетаОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоПредыдущегоМесяца        КАК НачалоМесяца,
		|	&КонецПредыдущегоМесяца         КАК КонецМесяца,
		|	ИСТИНА                          КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОС
		|ГДЕ
		|	ПорядокУчетаОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (
		|				ТИП(Документ.ПринятиеКУчетуОС2_4),
		|				ТИП(Документ.РазукомплектацияОС))
		|	И (Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	&КонецМесяца                    КАК КонецМесяца,
		|	ЛОЖЬ                            КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|	
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_УУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
		|ГДЕ
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство, 
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_УУ", "ВТ_СписокОбъектовУчета_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_Доначисление_УУ", "ВТ_СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы);
	
КонецПроцедуры

// Получает все необходимые параметры для расчета.
//
Процедура ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета)

	ОбъектУчета = "ОсновноеСредство";
	ДобавитьТекстДляДоначисления = ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"МестонахождениеОС", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПервоначальныеСведенияОС", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПорядокУчетаОС", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ТекстПоляДатаПоследнегоИзменения =
	"ВЫБОР 
	|	КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|		ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаРегистра.ДатаПоследнегоИзменения, МЕСЯЦ, 1), МЕСЯЦ)
	|	ИНАЧЕ ТаблицаРегистра.ДатаПоследнегоИзменения
	|КОНЕЦ";
	ВычислениеПолей = Новый Структура;
	ВычислениеПолей.Вставить("ДатаПоследнегоИзменения", ТекстПоляДатаПоследнегоИзменения);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПараметрыАмортизацииОСУУ", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов, ВычислениеПолей);
	
	#Область СтоимостьОСНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(СтоимостьОС.Стоимость) КАК Стоимость,
	|	СУММА(СтоимостьОС.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(СтоимостьОС.СтоимостьНУ) КАК СтоимостьНУ,
	|	СУММА(СтоимостьОС.СтоимостьПР) КАК СтоимостьПР,
	|	СУММА(СтоимостьОС.СтоимостьВР) КАК СтоимостьВР,
	|	СУММА(СтоимостьОС.СтоимостьЦФ) КАК СтоимостьЦФ,
	|	СУММА(СтоимостьОС.СтоимостьНУЦФ) КАК СтоимостьНУЦФ,
	|	СУММА(СтоимостьОС.СтоимостьПРЦФ) КАК СтоимостьПРЦФ,
	|	СУММА(СтоимостьОС.СтоимостьВРЦФ) КАК СтоимостьВРЦФ,
	|	СУММА(СтоимостьОС.АмортизационнаяПремия) КАК АмортизационнаяПремия
	|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоМесяца
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СтоимостьОС.СтоимостьОстаток КАК Стоимость,
	|		СтоимостьОС.СтоимостьРеглОстаток КАК СтоимостьРегл,
	|		СтоимостьОС.СтоимостьНУОстаток КАК СтоимостьНУ,
	|		СтоимостьОС.СтоимостьПРОстаток КАК СтоимостьПР,
	|		СтоимостьОС.СтоимостьВРОстаток КАК СтоимостьВР,
	|		СтоимостьОС.СтоимостьЦФОстаток КАК СтоимостьЦФ,
	|		СтоимостьОС.СтоимостьНУЦФОстаток КАК СтоимостьНУЦФ,
	|		СтоимостьОС.СтоимостьПРЦФОстаток КАК СтоимостьПРЦФ,
	|		СтоимостьОС.СтоимостьВРЦФОстаток КАК СтоимостьВРЦФ,
	|		СтоимостьОС.АмортизационнаяПремияОстаток КАК АмортизационнаяПремия
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС.Остатки(
	|				&КонецПредыдущегоМесяцаГраница,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						СписокОС.Организация,
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						ВТ_СписокОбъектовУчета КАК СписокОС)) КАК СтоимостьОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтоимостьОС.ОсновноеСредство,
	|		СтоимостьОС.Стоимость,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|	ГДЕ
	|		СтоимостьОС.Активность
	|		И СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И СтоимостьОС.Регистратор ССЫЛКА Документ.РазукомплектацияОС
	|		И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					ВТ_СписокОбъектовУчета КАК СписокОС)) КАК СтоимостьОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СтоимостьИАмортизацияНаНачалоГода
	
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаВтСписокОСУменьшаемогоОстатка(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СписокОС.ОсновноеСредство
		|ПОМЕСТИТЬ втСписокОСУменьшаемогоОстатка
		|ИЗ
		|	ВТ_ПараметрыАмортизацииОСУУ КАК СписокОС
		|ГДЕ
		|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
		|	И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаСтоимостьИАмортизацияНаНачалоГода(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&НачалоМесяца                            КАК НачалоМесяца,
		|	СтоимостьОС.ОсновноеСредство             КАК ОсновноеСредство,
		|	СтоимостьОС.СтоимостьОстаток             КАК Стоимость,
		|	СтоимостьОС.СтоимостьРеглОстаток         КАК СтоимостьРегл,
		|	СтоимостьОС.СтоимостьЦФОстаток           КАК СтоимостьЦФ
		|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоГода
		|ИЗ
		|	РегистрНакопления.СтоимостьОС.Остатки(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК СтоимостьОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтоимостьОС.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&НачалоМесяца                          КАК НачалоМесяца,
		|	АмортизацияОС.ОсновноеСредство         КАК ОсновноеСредство,
		|	-АмортизацияОС.АмортизацияОстаток      КАК Амортизация,
		|	-АмортизацияОС.АмортизацияРеглОстаток  КАК АмортизацияРегл,
		|	-АмортизацияОС.АмортизацияЦФОстаток    КАК АмортизацияЦФ
		|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоГода
		|ИЗ
		|	РегистрНакопления.АмортизацияОС.Остатки(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК АмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АмортизацияОС.ОсновноеСредство";
		
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПредварительнаяСтоимостьОС
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                                    КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство                     КАК ОсновноеСредство,
	|	СУММА(СтоимостьОС.ПредварительнаяСтоимость)      КАК Стоимость,
	|	СУММА(СтоимостьОС.ПредварительнаяСтоимостьРегл)  КАК СтоимостьРегл
	|ПОМЕСТИТЬ ВТ_ПредварительнаяСтоимостьОС
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|ГДЕ
	|	СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И (СтоимостьОС.ПредварительнаяСтоимость <> 0
	|		ИЛИ СтоимостьОС.ПредварительнаяСтоимостьРегл <> 0)
	|	И СтоимостьОС.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации))
	|	И СтоимостьОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОбъектовУчета КАК СписокОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство";
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НачалоПредыдущегоМесяца              КАК НачалоМесяца,
		|	СтоимостьОС.ОсновноеСредство          КАК ОсновноеСредство,
		|	ВЫБОР 
		|		КОГДА СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0 
		|			ТОГДА СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0
		|			ТОГДА СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) 
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СтоимостьРегл
		|ИЗ
		|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|ГДЕ
		|	СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьОС.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга), 
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
		|	И СтоимостьОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьОС.ОсновноеСредство
		|
		|ИМЕЮЩИЕ 
		|	СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0
		|		ИЛИ СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство";
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизацияОСНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                          КАК НачалоМесяца,
	|	АмортизацияОС.Организация              КАК Организация,
	|	АмортизацияОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	-АмортизацияОС.АмортизацияОстаток      КАК Амортизация,
	|	-АмортизацияОС.АмортизацияРеглОстаток  КАК АмортизацияРегл,
	|	-АмортизацияОС.АмортизацияНУОстаток    КАК АмортизацияНУ,
	|	-АмортизацияОС.АмортизацияПРОстаток    КАК АмортизацияПР,
	|	-АмортизацияОС.АмортизацияВРОстаток    КАК АмортизацияВР,
	|	-АмортизацияОС.АмортизацияЦФОстаток    КАК АмортизацияЦФ,
	|	-АмортизацияОС.АмортизацияНУЦФОстаток  КАК АмортизацияНУЦФ,
	|	-АмортизацияОС.АмортизацияПРЦФОстаток  КАК АмортизацияПРЦФ,
	|	-АмортизацияОС.АмортизацияВРЦФОстаток  КАК АмортизацияВРЦФ
	|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.АмортизацияОС.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					ВТ_СписокОбъектовУчета КАК СписокОС)) КАК АмортизацияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АмортизацияОС.ОсновноеСредство,
	|	АмортизацияОС.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВыработкаОС
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ) = МЕСЯЦ(&НачалоМесяца) - 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ ВТ_СписокОС_Выработка
	|ИЗ
	|	ВТ_СписокОбъектовУчета КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|ГДЕ
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В (
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции), 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега))
	|	ИЛИ ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаНачалоМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					ВТ_СписокОС_Выработка КАК СписокОС)) КАК ВыработкаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки,
	|	СписокОС.ПринятоКУчетуВПрошломМесяце КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаКонецМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					ВТ_СписокОС_Выработка КАК СписокОС)) КАК ВыработкаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокОС_Выработка КАК СписокОС
	|		ПО (СписокОС.ОсновноеСредство = ВыработкаОС.ОбъектЭксплуатации)
	|			И (СписокОС.ПоказательНаработки = ВыработкаОС.ПоказательНаработки)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.ПоказательНаработки
	|ПОМЕСТИТЬ ВТ_СписокОС_ПринятоКУчетуВПрошломМесяце
	|ИЗ
	|	ВТ_СписокОС_Выработка КАК СписокОС
	|ГДЕ
	|	СписокОС.ПринятоКУчетуВПрошломМесяце
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецПозапрошлогоМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					ВТ_СписокОС_ПринятоКУчетуВПрошломМесяце КАК СписокОС)) КАК ВыработкаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ВыработкаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	СУММА(ВыработкаОС.ОбъемВыработкиБУ) КАК ОбъемВыработкиБУ,
	|	СУММА(ВыработкаОС.ОбъемВыработкиУУ) КАК ОбъемВыработкиУУ,
	|	СУММА(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца) КАК ОбъемВыработкиНаНачалоМесяца
	|ПОМЕСТИТЬ ВТ_ВыработкаОС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство) КАК ОсновноеСредство,
	|		ЕСТЬNULL(НаработкиПериодНачало.ПоказательНаработки, НаработкиПериодОкончание.ПоказательНаработки) КАК ПоказательНаработки,
	|		0 КАК ОбъемВыработкиБУ,
	|		ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0) КАК ОбъемВыработкиУУ,
	|		ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0) КАК ОбъемВыработкиНаНачалоМесяца
	|	ИЗ
	|		ВТ_ВыработкаОС_НаНачалоМесяца КАК НаработкиПериодНачало
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|			ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|				И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|	ГДЕ
	|		&ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство),
	|		ЕСТЬNULL(НаработкиПериодНачало.ПоказательНаработки, НаработкиПериодОкончание.ПоказательНаработки),
	|		ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0),
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_ВыработкаОС_НаНачалоМесяца КАК НаработкиПериодНачало
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|			ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|				И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|	ГДЕ
	|		НЕ НаработкиПериодОкончание.ПринятоКУчетуВПрошломМесяце
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство),
	|		ЕСТЬNULL(НаработкиПериодНачало.ПоказательНаработки, НаработкиПериодОкончание.ПоказательНаработки),
	|		ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0),
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца КАК НаработкиПериодНачало
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|			ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|				И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|	ГДЕ
	|		НаработкиПериодОкончание.ПринятоКУчетуВПрошломМесяце) КАК ВыработкаОС
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаОС.ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки";
	
	Если НЕ ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СписокОС_Выработка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыработкаОС_НаНачалоМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыработкаОС_НаКонецМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца";
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область Изменения_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	ПараметрыАмортизацииОСУУ.Период КАК Период,
		|	ПараметрыАмортизацииОСУУ.МоментВремени,
		|	ПараметрыАмортизацииОСУУ.Регистратор,
		|	ПараметрыАмортизацииОСУУ.ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.Организация,
		|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
		|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
		|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки,
		|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения,
		|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл
		|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУИзменения
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|ГДЕ
		|	ПараметрыАмортизацииОСУУ.Активность
		|	И ПараметрыАмортизацииОСУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПараметрыАмортизацииОСУУ.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	ПараметрыАмортизацииОСУУ.Период КАК Период,
		|	ПараметрыАмортизацииОСУУ.МоментВремени,
		|	ПараметрыАмортизацииОСУУ.Регистратор,
		|	ПараметрыАмортизацииОСУУ.ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.Организация,
		|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
		|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
		|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки,
		|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения,
		|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	ПараметрыАмортизацииОСУУ.Активность
		|	И ПараметрыАмортизацииОСУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПараметрыАмортизацииОСУУ.Период >= Доначисление.Период
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПараметрыАмортизацииОСУУ.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// ПорядокУчетаОС
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	ПорядокУчетаОС.Период КАК Период,
		|	ПорядокУчетаОС.МоментВремени,
		|	ПорядокУчетаОС.Регистратор,
		|	ПорядокУчетаОС.ОсновноеСредство,
		|	РеестрДокументов.Организация,
		|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОС.НаправлениеДеятельности,
		|	ПорядокУчетаОС.ПоказательНаработки,
		|	ПорядокУчетаОС.ОбъемНаработки
		|ПОМЕСТИТЬ ПорядокУчетаОСИзменения
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО РеестрДокументов.Ссылка = ПорядокУчетаОС.Регистратор
		|			И НЕ РеестрДокументов.ДополнительнаяЗапись
		|ГДЕ
		|	ПорядокУчетаОС.Активность
		|	И ПорядокУчетаОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПорядокУчетаОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	ПорядокУчетаОС.Период КАК Период,
		|	ПорядокУчетаОС.МоментВремени,
		|	ПорядокУчетаОС.Регистратор,
		|	ПорядокУчетаОС.ОсновноеСредство,
		|	РеестрДокументов.Организация,
		|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОС.НаправлениеДеятельности,
		|	ПорядокУчетаОС.ПоказательНаработки,
		|	ПорядокУчетаОС.ОбъемНаработки
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО ПорядокУчетаОС.ОсновноеСредство = Доначисление.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО РеестрДокументов.Ссылка = ПорядокУчетаОС.Регистратор
		|			И НЕ РеестрДокументов.ДополнительнаяЗапись
		|ГДЕ
		|	ПорядокУчетаОС.Активность
		|	И ПорядокУчетаОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПорядокУчетаОС.Период >= Доначисление.Период
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПорядокУчетаОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// ПорядокУчетаОСУУ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	ПорядокУчетаОСУУ.Период КАК Период,
		|	ПорядокУчетаОСУУ.МоментВремени,
		|	ПорядокУчетаОСУУ.Регистратор,
		|	ПорядокУчетаОСУУ.ОсновноеСредство,
		|	ПорядокУчетаОСУУ.Организация,
		|	ПорядокУчетаОСУУ.Состояние,
		|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ,
		|	ПорядокУчетаОСУУ.СпособОтраженияРасходов,
		|	ПорядокУчетаОСУУ.СтатьяРасходов,
		|	ПорядокУчетаОСУУ.АналитикаРасходов,
		|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
		|ПОМЕСТИТЬ ПорядокУчетаОСУУИзменения
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
		|ГДЕ
		|	ПорядокУчетаОСУУ.Активность
		|	И ПорядокУчетаОСУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И (ПорядокУчетаОСУУ.Организация, ПорядокУчетаОСУУ.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	ПорядокУчетаОСУУ.Период КАК Период,
		|	ПорядокУчетаОСУУ.МоментВремени,
		|	ПорядокУчетаОСУУ.Регистратор,
		|	ПорядокУчетаОСУУ.ОсновноеСредство,
		|	ПорядокУчетаОСУУ.Организация,
		|	ПорядокУчетаОСУУ.Состояние,
		|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ,
		|	ПорядокУчетаОСУУ.СпособОтраженияРасходов,
		|	ПорядокУчетаОСУУ.СтатьяРасходов,
		|	ПорядокУчетаОСУУ.АналитикаРасходов,
		|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО ПорядокУчетаОСУУ.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	ПорядокУчетаОСУУ.Активность
		|	И ПорядокУчетаОСУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПорядокУчетаОСУУ.Период >= Доначисление.Период
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И (ПорядокУчетаОСУУ.Организация, ПорядокУчетаОСУУ.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// МестонахождениеОС
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	МестонахождениеОС.Период КАК Период,
		|	МестонахождениеОС.МоментВремени,
		|	МестонахождениеОС.Регистратор,
		|	МестонахождениеОС.ОсновноеСредство,
		|	МестонахождениеОС.Организация,
		|	МестонахождениеОС.Местонахождение
		|ПОМЕСТИТЬ МестонахождениеОСИзменения
		|ИЗ
		|	РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|ГДЕ
		|	МестонахождениеОС.Активность
		|	И МестонахождениеОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И МестонахождениеОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	МестонахождениеОС.Период КАК Период,
		|	МестонахождениеОС.МоментВремени,
		|	МестонахождениеОС.Регистратор,
		|	МестонахождениеОС.ОсновноеСредство,
		|	МестонахождениеОС.Организация,
		|	МестонахождениеОС.Местонахождение
		|ИЗ
		|	РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО МестонахождениеОС.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	МестонахождениеОС.Активность
		|	И МестонахождениеОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И МестонахождениеОС.Период >= Доначисление.Период
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И МестонахождениеОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// СтоимостьОСИзменения
		|ВЫБРАТЬ
		|	СтоимостьОС.НачалоМесяца КАК НачалоМесяца,
		|	СтоимостьОС.Период КАК Период,
		|	СтоимостьОС.Регистратор КАК Регистратор,
		|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтоимостьОС.Организация КАК Организация,
		|	СУММА(СтоимостьОС.Стоимость) КАК Стоимость,
		|	СУММА(СтоимостьОС.СтоимостьРегл) КАК СтоимостьРегл
		|ПОМЕСТИТЬ СтоимостьОСИзменения
		|ИЗ
		|	(ВЫБРАТЬ
		|		&НачалоМесяца КАК НачалоМесяца,
		|		СтоимостьОС.Период КАК Период,
		|		СтоимостьОС.Регистратор КАК Регистратор,
		|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
		|		СтоимостьОС.Организация КАК Организация,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(СтоимостьОС.Регистратор) = ТИП(Документ.МодернизацияОС2_4)
		|				ТОГДА СтоимостьОС.ПредварительнаяСтоимость
		|			ИНАЧЕ -СтоимостьОС.Стоимость
		|		КОНЕЦ КАК Стоимость,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(СтоимостьОС.Регистратор) = ТИП(Документ.МодернизацияОС2_4)
		|				ТОГДА СтоимостьОС.ПредварительнаяСтоимостьРегл
		|			ИНАЧЕ -СтоимостьОС.СтоимостьРегл
		|		КОНЕЦ КАК СтоимостьРегл
		|	ИЗ
		|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|	ГДЕ
		|		СтоимостьОС.Активность
		|		И СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|		И (СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
		|				ИЛИ СтоимостьОС.Регистратор ССЫЛКА Документ.РазукомплектацияОС
		|					И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|		И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК СписокОС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&НачалоМесяца,
		|		СписаниеОС2_4.Дата,
		|		СтоимостьОС.Ссылка,
		|		СтоимостьОС.ОсновноеСредство,
		|		СписаниеОС2_4.Организация,
		|		-СтоимостьОС.СуммаСписанияУУ,
		|		-СтоимостьОС.СуммаСписанияБУ
		|	ИЗ
		|		Документ.СписаниеОС2_4 КАК СписаниеОС2_4
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ОС КАК СтоимостьОС
		|			ПО (СтоимостьОС.Ссылка = СписаниеОС2_4.Ссылка)
		|	ГДЕ
		|		СписаниеОС2_4.Проведен
		|		И СписаниеОС2_4.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
		|		И СписаниеОС2_4.ОтражатьВУпрУчете
		|		И СтоимостьОС.СуммаСписанияУУ <> 0
		|		И (СписаниеОС2_4.Организация, СтоимостьОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК СписокОС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&НачалоМесяца,
		|		ПереоценкаОС.Дата,
		|		СтоимостьОС.Ссылка,
		|		СтоимостьОС.ОсновноеСредство,
		|		ПереоценкаОС.Организация,
		|		СтоимостьОС.СтоимостьУУ,
		|		СтоимостьОС.СтоимостьБУ
		|	ИЗ
		|		Документ.ПереоценкаОС2_4 КАК ПереоценкаОС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаОС2_4.ОС КАК СтоимостьОС
		|			ПО (СтоимостьОС.Ссылка = ПереоценкаОС.Ссылка)
		|	ГДЕ
		|		ПереоценкаОС.Проведен
		|		И ПереоценкаОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
		|		И ПереоценкаОС.ОтражатьВУпрУчете
		|		И СтоимостьОС.СтоимостьУУ <> 0
		|		И (ПереоценкаОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК СписокОС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&НачалоПредыдущегоМесяца,
		|		СтоимостьОС.Период,
		|		СтоимостьОС.Регистратор,
		|		СтоимостьОС.ОсновноеСредство,
		|		СтоимостьОС.Организация,
		|		СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость,
		|		СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл
		|	ИЗ
		|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|			ПО СтоимостьОС.ОсновноеСредство = Доначисление.ОсновноеСредство
		|	ГДЕ
		|		СтоимостьОС.Активность
		|		И СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|		И СтоимостьОС.Период >= Доначисление.Период
		|		И СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
		|		И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)) КАК СтоимостьОС
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьОС.НачалоМесяца,
		|	СтоимостьОС.Период,
		|	СтоимостьОС.Регистратор,
		|	СтоимостьОС.ОсновноеСредство,
		|	СтоимостьОС.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаИзменений.Организация КАК Организация,
		|	МАКСИМУМ(ТаблицаИзменений.Период) КАК Период,
		|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаИзменений.Период, МЕСЯЦ, 1), МЕСЯЦ)) КАК НачалоСледующегоМесяца,
		|	МАКСИМУМ(ПараметрыАмортизацииОСУУПоследнее.Период) КАК ПараметрыАмортизацииОСУУПоследнееПериод,
		|	МАКСИМУМ(ПорядокУчетаОСИзмененияПоследнее.Период) КАК ПорядокУчетаОСИзмененияПоследнееПериод,
		|	МАКСИМУМ(ПорядокУчетаОСУУИзмененияПоследнее.Период) КАК ПорядокУчетаОСУУИзмененияПоследнееПериод,
		|	МАКСИМУМ(МестонахождениеОСИзмененияПоследнее.Период) КАК МестонахождениеОСИзмененияПоследнееПериод
		|ПОМЕСТИТЬ ТаблицаИзменений
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|		ТаблицаИзменений.Регистратор КАК Регистратор,
		|		ТаблицаИзменений.Период КАК Период,
		|		ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|		ТаблицаИзменений.Организация КАК Организация
		|	ИЗ
		|		ПараметрыАмортизацииОСУУИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		ПорядокУчетаОСИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		ПорядокУчетаОСУУИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		МестонахождениеОСИзменения КАК ТаблицаИзменений
		|
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		СтоимостьОСИзменения КАК ТаблицаИзменений
		|
		|	) КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУИзменения КАК ПараметрыАмортизацииОСУУПоследнее
		|		ПО (ПараметрыАмортизацииОСУУПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПараметрыАмортизацииОСУУПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСИзменения КАК ПорядокУчетаОСИзмененияПоследнее
		|		ПО (ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУИзменения КАК ПорядокУчетаОСУУИзмененияПоследнее
		|		ПО (ПорядокУчетаОСУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСИзменения КАК МестонахождениеОСИзмененияПоследнее
		|		ПО (МестонахождениеОСИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (МестонахождениеОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (МестонахождениеОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (МестонахождениеОСИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство,
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	МИНИМУМ(ТаблицаИзмененийСледующее.Период) КАК ПериодСледующее
		|ПОМЕСТИТЬ ВТ_ТаблицаИзмененийСледующее
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзменений КАК ТаблицаИзмененийСледующее
		|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийСледующее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ТаблицаИзмененийСледующее.Период > ТаблицаИзменений.Период
		|				ИЛИ ТаблицаИзмененийСледующее.Период = ТаблицаИзменений.Период
		|						И ТаблицаИзмененийСледующее.Регистратор > ТаблицаИзменений.Регистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство,
		|	МИНИМУМ(ТаблицаИзменений.Период) КАК ПериодСледующее
		|ПОМЕСТИТЬ ВТ_ТаблицаИзмененийВЭтомМесяце
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	(ТаблицаИзменений.НачалоМесяца = &НачалоМесяца)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	КОНЕЦПЕРИОДА(ТаблицаИзменений.НачалоМесяца, МЕСЯЦ) КАК КонецМесяца,
		|	КОНЕЦПЕРИОДА(ТаблицаИзменений.Период, ДЕНЬ) КАК ДатаИзменений,
		|	ТаблицаИзменений.Регистратор КАК Регистратор,
		|	ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Регистратор) = ТИП(Документ.РазукомплектацияОС) КАК ЭтоРазукомплектация,
		|	ВЫБОР 
		|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
		|			ТОГДА ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
		|		ИНАЧЕ ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийСледующее.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
		|	КОНЕЦ КАК Период,
		|	РАЗНОСТЬДАТ(ТаблицаИзменений.Период, ЕСТЬNULL(ТаблицаИзмененийСледующее.ПериодСледующее, ТаблицаИзменений.НачалоСледующегоМесяца), ДЕНЬ) КАК СрокДействия,
		|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаИзменений.Организация КАК Организация,
		|	ЕСТЬNULL(СтоимостьОСИзменения.Стоимость, 0) КАК СтоимостьИзменение, // Изменение стоимости документом
		|	ЕСТЬNULL(СтоимостьОСИзменения.СтоимостьРегл, 0) КАК СтоимостьРеглИзменение, // Изменение стоимости документом
		|	ПараметрыАмортизацииОСУУПоследнее.ДатаПоследнегоИзменения,
		|	ПараметрыАмортизацииОСУУПоследнее.СрокИспользования,
		|	ПараметрыАмортизацииОСУУПоследнее.ОбъемНаработки,
		|	ПараметрыАмортизацииОСУУПоследнее.КоэффициентУскорения,
		|	ПараметрыАмортизацииОСУУПоследнее.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииОСУУПоследнее.ЛиквидационнаяСтоимость,
		|	ПараметрыАмортизацииОСУУПоследнее.ЛиквидационнаяСтоимостьРегл,
		|	ПорядокУчетаОСИзмененияПоследнее.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОСИзмененияПоследнее.НаправлениеДеятельности,
		|	ПорядокУчетаОСИзмененияПоследнее.ПоказательНаработки,
		|	ПорядокУчетаОСУУИзмененияПоследнее.Состояние,
		|	ПорядокУчетаОСУУИзмененияПоследнее.НачислятьАмортизациюУУ,
		|	ПорядокУчетаОСУУИзмененияПоследнее.СпособОтраженияРасходов,
		|	ПорядокУчетаОСУУИзмененияПоследнее.СтатьяРасходов,
		|	ПорядокУчетаОСУУИзмененияПоследнее.АналитикаРасходов,
		|	ПорядокУчетаОСУУИзмененияПоследнее.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаОСУУИзмененияПоследнее.ОрганизацияПолучательРасходов,
		|	МестонахождениеОСИзмененияПоследнее.Местонахождение
		|ПОМЕСТИТЬ ВТ_Изменения_УУ
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзмененийСледующее КАК ТаблицаИзмененийСледующее
		|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийСледующее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ТаблицаИзмененийСледующее.Регистратор = ТаблицаИзменений.Регистратор)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзмененийВЭтомМесяце КАК ТаблицаИзмененийВЭтомМесяце
		|		ПО (ТаблицаИзмененийВЭтомМесяце.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийВЭтомМесяце.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУИзменения КАК ПараметрыАмортизацииОСУУПоследнее
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУИзменения КАК ПараметрыАмортизацииОСУУСледующее
		|			ПО (ПараметрыАмортизацииОСУУСледующее.Организация = ПараметрыАмортизацииОСУУПоследнее.Организация)
		|				И (ПараметрыАмортизацииОСУУСледующее.ОсновноеСредство = ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство)
		|				И (ПараметрыАмортизацииОСУУСледующее.НачалоМесяца = ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца)
		|				И (ПараметрыАмортизацииОСУУСледующее.Период = ПараметрыАмортизацииОСУУПоследнее.Период)
		|				И (ПараметрыАмортизацииОСУУСледующее.МоментВремени > ПараметрыАмортизацииОСУУПоследнее.МоментВремени)
		|		ПО (ПараметрыАмортизацииОСУУПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПараметрыАмортизацииОСУУПоследнее.Период = ТаблицаИзменений.ПараметрыАмортизацииОСУУПоследнееПериод)
		|			И (ПараметрыАмортизацииОСУУСледующее.Регистратор ЕСТЬ NULL)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСИзменения КАК ПорядокУчетаОСИзмененияПоследнее
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСИзменения КАК ПорядокУчетаОСИзмененияСледующее
		|			ПО (ПорядокУчетаОСИзмененияСледующее.Организация = ПорядокУчетаОСИзмененияПоследнее.Организация)
		|				И (ПорядокУчетаОСИзмененияСледующее.ОсновноеСредство = ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство)
		|				И (ПорядокУчетаОСИзмененияСледующее.НачалоМесяца = ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца)
		|				И (ПорядокУчетаОСИзмененияСледующее.Период = ПорядокУчетаОСИзмененияПоследнее.Период)
		|				И (ПорядокУчетаОСИзмененияСледующее.МоментВремени > ПорядокУчетаОСИзмененияПоследнее.МоментВремени)
		|		ПО (ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаОСИзмененияПоследнееПериод)
		|			И (ПорядокУчетаОСИзмененияСледующее.Регистратор ЕСТЬ NULL)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУИзменения КАК ПорядокУчетаОСУУИзмененияПоследнее
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУИзменения КАК ПорядокУчетаОСУУИзмененияСледующее
		|			ПО (ПорядокУчетаОСУУИзмененияСледующее.Организация = ПорядокУчетаОСУУИзмененияПоследнее.Организация)
		|				И (ПорядокУчетаОСУУИзмененияСледующее.ОсновноеСредство = ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство)
		|				И (ПорядокУчетаОСУУИзмененияСледующее.НачалоМесяца = ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца)
		|				И (ПорядокУчетаОСУУИзмененияСледующее.Период = ПорядокУчетаОСУУИзмененияПоследнее.Период)
		|				И (ПорядокУчетаОСУУИзмененияСледующее.МоментВремени > ПорядокУчетаОСУУИзмененияПоследнее.МоментВремени)
		|		ПО (ПорядокУчетаОСУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаОСУУИзмененияПоследнееПериод)
		|			И (ПорядокУчетаОСУУИзмененияСледующее.Регистратор ЕСТЬ NULL)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСИзменения КАК МестонахождениеОСИзмененияПоследнее
		|			ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСИзменения КАК МестонахождениеОСИзмененияСледующее
		|			ПО (МестонахождениеОСИзмененияСледующее.Организация = МестонахождениеОСИзмененияПоследнее.Организация)
		|				И (МестонахождениеОСИзмененияСледующее.ОсновноеСредство = МестонахождениеОСИзмененияПоследнее.ОсновноеСредство)
		|				И (МестонахождениеОСИзмененияСледующее.НачалоМесяца = МестонахождениеОСИзмененияПоследнее.НачалоМесяца)
		|				И (МестонахождениеОСИзмененияСледующее.Период = МестонахождениеОСИзмененияПоследнее.Период)
		|				И (МестонахождениеОСИзмененияСледующее.МоментВремени > МестонахождениеОСИзмененияПоследнее.МоментВремени)
		|		ПО (МестонахождениеОСИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (МестонахождениеОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (МестонахождениеОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (МестонахождениеОСИзмененияПоследнее.Период = ТаблицаИзменений.МестонахождениеОСИзмененияПоследнееПериод)
		|			И (МестонахождениеОСИзмененияСледующее.Регистратор ЕСТЬ NULL)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОСИзменения КАК СтоимостьОСИзменения
		|		ПО (СтоимостьОСИзменения.Организация = ТаблицаИзменений.Организация)
		|			И (СтоимостьОСИзменения.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (СтоимостьОСИзменения.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (СтоимостьОСИзменения.Регистратор = ТаблицаИзменений.Регистратор)";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК НачалоМесяца,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК КонецМесяца,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзменений,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	0    КАК СрокДействия,
		|	ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка) КАК ОсновноеСредство,
		|	NULL КАК Организация,
		|	0    КАК СтоимостьИзменение,
		|	0    КАК СтоимостьРеглИзменение,
		|	NULL КАК Регистратор,
		|	ЛОЖЬ КАК ЭтоРазукомплектация,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоследнегоИзменения,
		|	0    КАК СрокИспользования,
		|	0    КАК ОбъемНаработки,
		|	0    КАК КоэффициентУскорения,
		|	NULL КАК МетодНачисленияАмортизации,
		|	0    КАК ЛиквидационнаяСтоимость,
		|	0    КАК ЛиквидационнаяСтоимостьРегл,
		|	NULL КАК ГруппаФинансовогоУчета,
		|	NULL КАК НаправлениеДеятельности,
		|	NULL КАК ПоказательНаработки,
		|	NULL КАК Состояние,
		|	ЛОЖЬ КАК НачислятьАмортизациюУУ,
		|	ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) КАК СпособОтраженияРасходов,
		|	NULL КАК СтатьяРасходов,
		|	NULL КАК АналитикаРасходов,
		|	ЛОЖЬ КАК ПередаватьРасходыВДругуюОрганизацию,
		|	NULL КАК ОрганизацияПолучательРасходов,
		|	NULL КАК Местонахождение
		|ПОМЕСТИТЬ ВТ_Изменения_УУ
		|
		|ГДЕ
		|	ЛОЖЬ";
	
	КонецЕсли; 

	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПериодИзменения_УУ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения_УУ.НачалоМесяца           КАК НачалоМесяца,
	|	Изменения_УУ.ОсновноеСредство       КАК ОсновноеСредство,
	|	Изменения_УУ.Организация            КАК Организация,
	|	МИНИМУМ(Изменения_УУ.ДатаИзменений) КАК Период
	|ПОМЕСТИТЬ ВТ_ПериодИзменения_УУ
	|ИЗ
	|	ВТ_Изменения_УУ КАК Изменения_УУ
	|
	|СГРУППИРОВАТЬ ПО
	|	Изменения_УУ.НачалоМесяца,
	|	Изменения_УУ.ОсновноеСредство,
	|	Изменения_УУ.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОССПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА ПериодИзмененияВТекущемМесяце.Период ЕСТЬ NULL
	|			ТОГДА СписокОС.ПериодНачисления
	|		КОГДА НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, МЕСЯЦ)
	|			ТОГДА ПериодИзмененияВТекущемМесяце.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ, -1)
	|	КОНЕЦ КАК Период,
	|	СписокОС.ЭтоДоначисление,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.Организация,
	|
	|	ПорядокУчетаОСНаНачалоМесяца.ГруппаФинансовогоУчета,
	|	ПорядокУчетаОСНаНачалоМесяца.НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаОС.ОбъемНаработки, 0)
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|			ТОГДА ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации, 0)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|			ТОГДА ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизации
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|			ТОГДА ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизацииРегл
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	
	// СтоимостьНаНачалоГода
	|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.Стоимость, 0)
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.Амортизация, 0) КАК СтоимостьНаНачалоГода,
	|
	
	// СтоимостьРеглНаНачалоГода
	|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.СтоимостьРегл, 0)
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.АмортизацияРегл, 0) КАК СтоимостьРеглНаНачалоГода,
	
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫБОР 
	|					КОГДА НЕ ПериодИзменения_УУ.Период ЕСТЬ NULL
	|						ТОГДА ВЫБОР 
	|								КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|									ТОГДА (РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|									ИНАЧЕ (РАЗНОСТЬДАТ(СписокОС.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|							КОНЕЦ / ДЕНЬ(СписокОС.КонецМесяца)
	|					ИНАЧЕ 1
	|				КОНЕЦ * ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0)
	
			// Если расчет выполняется в месяце принятия к учету то ДеньПринятия/ВсегоДнейВМесяце
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|			ТОГДА (РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокОС.КонецМесяца)
	
			// В остальных случаях КоличествоДнейДоПервогоИзмененияПараметров/ВсегоДнейВМесяце
	|		ИНАЧЕ (РАЗНОСТЬДАТ(СписокОС.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокОС.КонецМесяца)
	|	КОНЕЦ КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0)
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				ИЛИ ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СписокОС.НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ)
	|			ТОГДА (РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))
	
			// Расшифровка формулы: [кол-во отработаннных дней в мес принятия] / [всего дней в мес принятия] + [кол-во месяцев после принятия до начала месяца]
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ, 1), МЕСЯЦ), СписокОС.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) <> ГОД(СписокОС.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)
	|				+ 12 * (ГОД(СписокОС.НачалоМесяца) - Год(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	СписокОС.НачислятьАмортизациюУУ,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|
	// ОстаточнаяСтоимость
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
	|				И НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|
	// ОстаточнаяСтоимостьРегл
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
	|				И НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0)                   КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)               КАК СтоимостьРегл,
	|	ЕСТЬNULL(АмортизацияОС.Амортизация, 0)               КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)           КАК АмортизацияРегл,
	|
	|	МестонахождениеНаНачалоМесяца.Местонахождение        КАК Местонахождение,
	|
	|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0)            КАК ОбъемВыработки,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования           КАК СрокИспользованияУУ,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения        КАК КоэффициентУскоренияУУ,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации  КАК МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения     КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ВТ_СписокОССПараметрами_УУ
	|ИЗ
	|	ВТ_СписокОбъектовУчета_УУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодИзменения_УУ КАК ПериодИзменения_УУ
	|		ПО (ПериодИзменения_УУ.Организация = СписокОС.Организация)
	|			И (ПериодИзменения_УУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПериодИзменения_УУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодИзменения_УУ КАК ПериодИзмененияВТекущемМесяце
	|		ПО (ПериодИзмененияВТекущемМесяце.Организация = СписокОС.Организация)
	|			И (ПериодИзмененияВТекущемМесяце.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПериодИзмененияВТекущемМесяце.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОСНаНачалоМесяца
	|		ПО (ПорядокУчетаОСНаНачалоМесяца.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОСНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоГода КАК СтоимостьОСНаНачалоГода
	|		ПО (СтоимостьОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоГода КАК АмортизацияОСНаНачалоГода
	|		ПО (АмортизацияОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
	|		ПО (ПредварительнаяСтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПредварительнаяСтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = СписокОС.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (МестонахождениеОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеНаНачалоМесяца
	|		ПО (МестонахождениеНаНачалоМесяца.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (МестонахождениеНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|ГДЕ
	|	(НЕ СписокОС.ЭтоДоначисление
	|		ИЛИ СписокОС.НачалоМесяца = НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ))
	|	И СписокОС.НачислятьАмортизациюУУ
	|	И НЕ СписокОС.ПолученПриРазукомплектации";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОССИзмененнымиПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Изменения_УУ.Период,
	|	Изменения_УУ.Регистратор,
	|	Изменения_УУ.ОсновноеСредство КАК ОсновноеСредство,
	|	Изменения_УУ.Организация,
	|	(Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца) КАК ЭтоДоначисление,
	|
	|	Изменения_УУ.ЭтоРазукомплектация И НЕ ЕСТЬNULL(СписокОС.ПолученПриРазукомплектации, ЛОЖЬ) КАК ЭтоРазукомплектация,
	|	ЕСТЬNULL(СписокОС.ПолученПриРазукомплектации, ЛОЖЬ) КАК ПолученПриРазукомплектации, 
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ЕСТЬNULL(АмортизацияОС.Амортизация, 0) КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) КАК АмортизацияРегл,
	|
	|	Изменения_УУ.СтоимостьИзменение КАК СтоимостьИзменение,
	|	Изменения_УУ.СтоимостьРеглИзменение КАК СтоимостьРеглИзменение,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА ПорядокУчетаОСНаНачалоМесяца.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.ГруппаФинансовогоУчета, ПорядокУчетаОС.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ЕСТЬNULL(Изменения_УУ.НаправлениеДеятельности, ПорядокУчетаОС.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(Изменения_УУ.ПоказательНаработки,     ПорядокУчетаОС.ПоказательНаработки)     КАК ПоказательНаработки,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(Изменения_УУ.ОбъемНаработки, ПорядокУчетаОС.ОбъемНаработки)
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0)
	|		ИНАЧЕ 1
	|	КОНЕЦ * Изменения_УУ.СрокДействия / ДЕНЬ(Изменения_УУ.КонецМесяца) КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.ОсновноеСредство ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0)
	|
	|		КОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА 0
	
	// 		Расшифровка формулы: [Кол-во отработаннных дней в мес. принятия] / [всего дней в мес.принятия] + [кол-во мес. после принятия до начала месяца расчета]
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), КОНЕЦПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ, 1), МЕСЯЦ), Изменения_УУ.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	// ОтработанныхМесяцевПолныхЛет
	|	ВЫБОР
	|		КОГДА ГОД(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)) <> ГОД(Изменения_УУ.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения))
	|				+ 12 * (ГОД(Изменения_УУ.НачалоМесяца) - Год(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	ЕСТЬNULL(Изменения_УУ.НачислятьАмортизациюУУ, ЕСТЬNULL(СписокОС.НачислятьАмортизациюУУ, ЛОЖЬ)) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.СтатьяРасходов, СписокОС.СтатьяРасходовУУ) КАК СтатьяРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.АналитикаРасходов, СписокОС.АналитикаРасходовУУ) КАК АналитикаРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.СпособОтраженияРасходов, СписокОС.СпособОтраженияРасходовУУ) КАК СпособОтраженияРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.ПередаватьРасходыВДругуюОрганизацию, ЕСТЬNULL(СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ, ЛОЖЬ)) КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.ОрганизацияПолучательРасходов, СписокОС.ОрганизацияПолучательРасходовУУ) КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимость, ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0)) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимостьРегл, ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл, 0)) КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА МестонахождениеНаНачалоМесяца.Местонахождение
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.Местонахождение, МестонахождениеОС.Местонахождение) 
	|	КОНЕЦ КАК Местонахождение,
	|
	|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0) КАК ОбъемВыработки,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользованияУУ,
	|	ЕСТЬNULL(Изменения_УУ.КоэффициентУскорения, ПараметрыАмортизацииОСУУ.КоэффициентУскорения) КАК КоэффициентУскоренияУУ,
	|	ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации
	|ПОМЕСТИТЬ ВТ_СписокОССИзмененнымиПараметрами_УУ
	|ИЗ
	|	ВТ_Изменения_УУ КАК Изменения_УУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_УУ КАК СписокОС
	|		ПО (СписокОС.Организация = Изменения_УУ.Организация)
	|			И (СписокОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (СписокОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОСНаНачалоМесяца
	|		ПО (ПорядокУчетаОСНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПорядокУчетаОСНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
	|		ПО (ПредварительнаяСтоимостьОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПредварительнаяСтоимостьОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = Изменения_УУ.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (МестонахождениеОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеНаНачалоМесяца
	|		ПО (МестонахождениеНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (МестонахождениеНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = Изменения_УУ.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = Изменения_УУ.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|ГДЕ
	|	ЕСТЬNULL(Изменения_УУ.НачислятьАмортизациюУУ, ЕСТЬNULL(СписокОС.НачислятьАмортизациюУУ, ЛОЖЬ))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоДоначисление, 
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СпособыОтраженияРасходов
	
	ТекстЗапроса =
	"ВЫБРАТЬ 
	|	СписокВсехОС.СпособОтраженияРасходов КАК Ссылка
	|ПОМЕСТИТЬ втСпособыОтраженияРасходов
	|ИЗ 
	|	ВТ_СписокОбъектовУчета КАК СписокВсехОС
	|ГДЕ 
	|	СписокВсехОС.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ 
	|	СписокВсехОС.СпособОтраженияРасходов
	|ИЗ 
	|	ВТ_Изменения_УУ КАК СписокВсехОС
	|ГДЕ 
	|	СписокВсехОС.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
	|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
	|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
	|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизация.Коэффициент                          КАК База,
	|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
	|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизации КАК СуммаБазы,
	|	ЛОЖЬ                                                    КАК ЭтоАмортизационнаяПремия
	|ПОМЕСТИТЬ ВТ_СпособыОтраженияРасходов
	|ИЗ
	|	Документ.ИзменениеПараметровОС2_4.Амортизация КАК ТаблицаАмортизация
	|ГДЕ
	|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходов КАК СпособыОтраженияРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
	|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
	|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
	|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизация.Коэффициент                          КАК База,
	|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
	|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизационнойПремии КАК СуммаБазы,
	|	ИСТИНА                                                  КАК ЭтоАмортизационнаяПремия
	|ИЗ
	|	Документ.ИзменениеПараметровОС2_4.АмортизационнаяПремия КАК ТаблицаАмортизация
	|ГДЕ
	|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходов КАК СпособыОтраженияРасходов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов,
	|	ЭтоАмортизационнаяПремия";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаПараметрыРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
	// НеУказанСрокИспользованияУУ
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|		И СписокОС.РесурсНазначенный = 0 КАК НеУказанСрокИспользованияУУ,
	|
	// НеУказанОбъемНаработки
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И СписокОС.РесурсНазначенный = 0 КАК НеУказанОбъемНаработки
	|
	|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчета_УУ
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.НачислятьАмортизациюУУ 
	|	И СписокОС.ОстаточнаяСтоимость > 0
	|	И (СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|			И СписокОС.РесурсНазначенный = 0
	|		ИЛИ СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И СписокОС.РесурсНазначенный = 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	//
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
	// НеУказанСрокИспользованияУУ
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|		И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0 КАК НеУказанСрокИспользованияУУ,
	|
	// НеУказанОбъемНаработки
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0 КАК НеУказанОбъемНаработки,
	|
	// НеУказанРесурсВыработанный
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод ЕСТЬ NULL
	|		ИЛИ СписокОС.РесурсВыработанныйЗаПрошлыйПериод ЕСТЬ NULL КАК НеУказанРесурсВыработанный
	|
	|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчетаИзменения_УУ
	|ИЗ
	|	ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.НачислятьАмортизациюУУ 
	|	И (СписокОС.Стоимость - СписокОС.Амортизация) > 0
	|	И (СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|			И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0
	|		ИЛИ СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0
	|		ИЛИ СписокОС.РесурсВыработанныйЗаТекущийПериод ЕСТЬ NULL
	|		ИЛИ СписокОС.РесурсВыработанныйЗаПрошлыйПериод ЕСТЬ NULL)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаПроверкаПараметровРасчета(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Местонахождение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - (&СуммаАмортизацииУУ)) > СписокОС.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - СписокОС.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА СписокОС.ОстаточнаяСтоимость - СписокОС.ЛиквидационнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР 
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - (&СуммаАмортизацииБУ)) > СписокОС.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - СписокОС.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА СписокОС.ОстаточнаяСтоимостьРегл - СписокОС.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокОС.Стоимость,
	|	СписокОС.СтоимостьРегл,
	|	СписокОС.Амортизация,
	|	СписокОС.АмортизацияРегл,
	|	СписокОС.ОстаточнаяСтоимость,
	|	СписокОС.ОстаточнаяСтоимостьРегл,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскоренияУУ КАК КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|	СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(
	    "СписокОС.СтоимостьДляВычисленияАмортизации",
	    "СписокОС.ОстаточнаяСтоимость",
	    "СписокОС.ЛиквидационнаяСтоимость",
	    "СписокОС.СтоимостьНаНачалоГода",
		"0", 
		ПараметрыРасчета);
		
	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(
	    "СписокОС.СтоимостьДляВычисленияАмортизацииРегл",
	    "СписокОС.ОстаточнаяСтоимостьРегл",
	    "СписокОС.ЛиквидационнаяСтоимостьРегл",
	    "СписокОС.СтоимостьРеглНаНачалоГода",
		"0", 
		ПараметрыРасчета);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(СписокОС.СуммаУУ) КАК СуммаУУ,
	|	СУММА(СписокОС.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Доначисление КАК СписокОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Местонахождение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	ВЫБОР 
	|		КОГДА СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ) - СписокОС.ЛиквидационнаяСтоимость < &МинимальнаяОстаточнаяСтоимость
	|				И &ВалютаРегламентированногоУчета = &ВалютаУправленческогоУчета
	|				И &ВедетсяРегламентированныйУчетВНА
	|				И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|
	|			ТОГДА СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость
	|
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ)) > СписокОС.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость)
	|
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР 
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - (&СуммаАмортизацииБУ)) > СписокОС.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокОС.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА (СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокОС.ЛиквидационнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокОС.Стоимость,
	|	СписокОС.СтоимостьРегл,
	|	СписокОС.Амортизация,
	|	СписокОС.АмортизацияРегл,
	|	СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) КАК ОстаточнаяСтоимость,
	|	СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) КАК ОстаточнаяСтоимостьРегл,
	|	СписокОС.СтоимостьДляВычисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскоренияУУ КАК КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Предварительный
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	НЕ СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(
	    "СписокОС.СтоимостьДляВычисленияАмортизации",
	    "СписокОС.ОстаточнаяСтоимость",
	    "СписокОС.ЛиквидационнаяСтоимость",
	    "СписокОС.СтоимостьНаНачалоГода",
		"ЕСТЬNULL(Доначисление.СуммаУУ, 0)", 
		ПараметрыРасчета);
		
	
	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(
	    "СписокОС.СтоимостьДляВычисленияАмортизацииРегл",
	    "СписокОС.ОстаточнаяСтоимостьРегл",
	    "СписокОС.ЛиквидационнаяСтоимостьРегл",
	    "СписокОС.СтоимостьРеглНаНачалоГода",
		"ЕСТЬNULL(Доначисление.СуммаБУ, 0)", 
		ПараметрыРасчета);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РазукомплектацияОС.ОсновноеСредство КАК ИсходноеОсновноеСредство,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ТабличнаяЧасть.СтоимостьУУ КАК Стоимость,
	|	ТабличнаяЧасть.СтоимостьБУ КАК СтоимостьРегл,
	|	0 КАК Амортизация,
	|	0 КАК АмортизацияРегл
	|ИЗ
	|	Документ.РазукомплектацияОС КАК РазукомплектацияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТабличнаяЧасть
	|		ПО ТабличнаяЧасть.Ссылка = РазукомплектацияОС.Ссылка
	|ГДЕ
	|	РазукомплектацияОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И РазукомплектацияОС.Проведен
	|	И РазукомплектацияОС.ОтражатьВУпрУчете
	|	И (РазукомплектацияОС.Организация, ТабличнаяЧасть.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	1 КАК Коэффициент,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ КАК ЭтоРазукомплектация,
	|	ЛОЖЬ КАК ПолученПриРазукомплектации, 
	|	СписокОС.Стоимость КАК Стоимость,
	|	СписокОС.СтоимостьРегл КАК СтоимостьРегл,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК СтоимостьРеглИзменение,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ ) = &НачалоМесяца
	|			ТОГДА СписокОС.Амортизация + ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокОС.Амортизация
	|	КОНЕЦ КАК Амортизация,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ ) = &НачалоМесяца
	|			ТОГДА СписокОС.АмортизацияРегл + ЕСТЬNULL(Доначисление.СуммаБУ, 0) 
	|		ИНАЧЕ СписокОС.АмортизацияРегл
	|	КОНЕЦ КАК АмортизацияРегл,
	|	СписокОС.Стоимость - СписокОС.Амортизация КАК СтоимостьДляВычисленияАмортизации,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Предварительный КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|ГДЕ
	|	СписокОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СписокОС.ПолученПриРазукомплектации
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет,
	|	СписокОС.Период,
	|	СписокОС.Регистратор,
	|	СписокОС.Организация,
	|	СписокОС.Местонахождение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	1,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскоренияУУ,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ,
	|	СписокОС.ЭтоРазукомплектация КАК ЭтоРазукомплектация,
	|	СписокОС.ПолученПриРазукомплектации КАК ПолученПриРазукомплектации, 
	|	СписокОС.Стоимость КАК Стоимость,
	|	СписокОС.СтоимостьРегл КАК СтоимостьРегл,
	|	СписокОС.СтоимостьИзменение КАК СтоимостьИзменение,
	|	СписокОС.СтоимостьРеглИзменение КАК СтоимостьРеглИзменение,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ ) = &НачалоМесяца
	|			ТОГДА СписокОС.Амортизация + ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокОС.Амортизация
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ ) = &НачалоМесяца
	|			ТОГДА СписокОС.АмортизацияРегл + ЕСТЬNULL(Доначисление.СуммаБУ, 0) 
	|		ИНАЧЕ СписокОС.АмортизацияРегл
	|	КОНЕЦ,
	|	СписокОС.Стоимость - СписокОС.Амортизация,
	|	ИСТИНА
	|ИЗ
	|	ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчетаИзменения_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	НЕ СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	1 КАК Коэффициент,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ КАК ЭтоРазукомплектация,
	|	ЛОЖЬ КАК ПолученПриРазукомплектации, 
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК СтоимостьРеглИзменение,
	|	0 КАК Амортизация,
	|	0 КАК АмортизацияРегл,
	|	0 КАК СтоимостьДляВычисленияАмортизации,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокОС
	|ГДЕ
	|	СписокОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	СписокОС.Период,
	|	СписокОС.Регистратор,
	|	СписокОС.Организация,
	|	СписокОС.Местонахождение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	1,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскоренияУУ,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ,
	|	ЛОЖЬ,
	|	ЛОЖЬ, 
	|	СписокОС.Стоимость,
	|	СписокОС.СтоимостьРегл,
	|	СписокОС.СтоимостьИзменение,
	|	СписокОС.СтоимостьРеглИзменение,
	|	СписокОС.Амортизация,
	|	СписокОС.АмортизацияРегл,
	|	0,
	|	ИСТИНА
	|ИЗ
	|	ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_АмортизацияИзменений_УУ_Доначисление
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Доначисление
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Период КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.СуммаБУ КАК СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.Период,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ,
	|		СписокОС.СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации
	|	ИЗ
	|		ВТ_АмортизацияИзменений_УУ_Доначисление КАК СписокОС
	|
	|	) КАК Амортизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ВТ_АмортизацияИзменений_УУ
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.Подразделение КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	Амортизация.ЭтоДоначисление КАК ЭтоДоначисление
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Период КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.СуммаБУ КАК СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Предварительный КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.ПериодРасчета КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.СуммаБУ КАК СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		ИСТИНА КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Доначисление КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.Период,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ,
	|		СписокОС.СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_АмортизацияИзменений_УУ КАК СписокОС
	|
	|	) КАК Амортизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета)

	СписокПодзапросов = Новый Массив;
	
	ТекстПодзапроса =  
	"	ВЫБРАТЬ
	|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
	|		РасчетАмортизации.Организация КАК Организация,
	|		РасчетАмортизации.Подразделение КАК Подразделение,
	|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
	|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|		РасчетАмортизации.ГруппаФинансовогоУчета,
	|		РасчетАмортизации.НаправлениеДеятельности,
	|		РасчетАмортизации.КорНаправлениеДеятельности,
	|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|		РасчетАмортизации.СуммаУУ КАК СуммаУУ,
	|		ВЫБОР 
	|			КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|				ТОГДА РасчетАмортизации.СуммаБУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаБУ,
	|		0 КАК СуммаНУ,
	|		0 КАК СуммаПР,
	|		0 КАК СуммаВР,
	|		0 КАК СуммаЦФ,
	|		0 КАК СуммаНУЦФ,
	|		0 КАК СуммаПРЦФ,
	|		0 КАК СуммаВРЦФ,
	|		РасчетАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		РасчетАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		ЛОЖЬ КАК НачислятьИзнос,
	|		ЛОЖЬ КАК ЭтоАмортизационнаяПремия,
	|		РасчетАмортизации.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		РасчетАмортизации.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		РасчетАмортизации.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		РасчетАмортизации.ЭтоДоначисление КАК ЭтоДоначисление
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ КАК РасчетАмортизации";
	СписокПодзапросов.Добавить(ТекстПодзапроса);
	
	АмортизацияОСЛокализация.ДополнитьТекстПодзапросаНачисленнаяАмортизация(СписокПодзапросов, ПараметрыРасчета);
	
	ТекстПодзапроса = СтрСоединить(СписокПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапросаВтНачисленнаяАмортизация(СписокЗапросов, ТекстПодзапроса, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета); 
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =  
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(НачисленнаяАмортизация.ОсновноеСредство), 0) КАК КоличествоДанных
		|ИЗ
		|	ВТ_РасчетАмортизации_УУ КАК НачисленнаяАмортизация";
		
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ТекстЗапросаСуммаАмортизацииУУ(ТекстСтоимостьДляВычисленияАмортизации, ТекстОстаточнаяСтоимость, ТекстЛиквидационнаяСтоимость, ТекстСтоимостьНаНачалоГода, ТекстСуммаДоначисления, ПараметрыРасчета)
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = 
		"ВЫБОР 
		|	КОГДА НЕ СписокОС.НачислятьАмортизациюУУ ИЛИ (&ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 ТОГДА
		|		0
		|	КОГДА (СписокОС.РесурсВыработанныйЗаПрошлыйПериод + СписокОС.РесурсВыработанныйЗаТекущийПериод) >= СписокОС.РесурсНазначенный ТОГДА
		|		&ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость
		|
		// Линейный или ПропорциональноОбъемуПродукции
		|	КОГДА СписокОС.МетодНачисленияАмортизации В (
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)) 
		|			ИЛИ (СписокОС.РесурсНазначенный
		|					- СписокОС.РесурсВыработанныйЗаПрошлыйПериод
		|					- СписокОС.РесурсВыработанныйЗаТекущийПериод) <= 12 
		|				И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
		|
		|		ВЫБОР 
		|			КОГДА СписокОС.ЭтоДоначисление
		|				ТОГДА ВЫРАЗИТЬ((&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления)
		|								* ((СписокОС.РесурсВыработанныйЗаТекущийПериод) / (СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод)) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ ВЫРАЗИТЬ((&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления - &ЛиквидационнаяСтоимость) 
		|								* ((СписокОС.РесурсВыработанныйЗаТекущийПериод) / (СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод)) КАК ЧИСЛО(31,2))
		|		КОНЕЦ
		|
			// УменьшаемогоОстатка
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
		|   
		|		ВЫРАЗИТЬ((&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления)
		|					* СписокОС.КоэффициентУскоренияУУ
		|					* СписокОС.РесурсВыработанныйЗаТекущийПериод
		|					/ (СписокОС.РесурсНазначенный 
		|						- СписокОС.КоэффициентУскоренияУУ * (СписокОС.РесурсВыработанныйЗаПрошлыйПериод - СписокОС.ОтработанныхМесяцевПолныхЛет)) КАК ЧИСЛО(31,2))
		|КОНЕЦ"; 
			
	Иначе
		
		ТекстЗапроса = 
		"ВЫБОР 
		|	КОГДА НЕ СписокОС.НачислятьАмортизациюУУ ИЛИ (&ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 ТОГДА
		|		0
		// Линейный
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокОС.РесурсНазначенный = 0
		|				ТОГДА 0
		|			ИНАЧЕ
		|				ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации / СписокОС.РесурсНазначенный КАК ЧИСЛО(31,2))
		|		КОНЕЦ
		|   
		// ПропорциональноОбъемуПродукции
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокОС.РесурсВыработанныйЗаТекущийПериод <> 0 И СписокОС.РесурсНазначенный <> 0 
		|				ТОГДА ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации 
		|									* СписокОС.РесурсВыработанныйЗаТекущийПериод 
		|									/ СписокОС.РесурсНазначенный КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	
		// УменьшаемогоОстатка
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокОС.РесурсНазначенный = 0
		|				ТОГДА 0
		|			ИНАЧЕ
		|				ВЫБОР
		|					КОГДА ГОД(СписокОС.ДатаПоследнегоИзменения) = ГОД(&КонецМесяца) 
		|						ТОГДА ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации 
		|										/ СписокОС.РесурсНазначенный 
		|										* СписокОС.КоэффициентУскоренияУУ КАК ЧИСЛО(31,2))
		|					ИНАЧЕ
		|						ВЫРАЗИТЬ(&СтоимостьНаНачалоГода 
		|									/ СписокОС.РесурсНазначенный 
		|									* СписокОС.КоэффициентУскоренияУУ КАК ЧИСЛО(31,2))
		|				КОНЕЦ
		|		КОНЕЦ 
		|КОНЕЦ"; 
				
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьДляВычисленияАмортизации", ТекстСтоимостьДляВычисленияАмортизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстаточнаяСтоимость", ТекстОстаточнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЛиквидационнаяСтоимость", ТекстЛиквидационнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьНаНачалоГода", ТекстСтоимостьНаНачалоГода);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаДоначисления", ТекстСуммаДоначисления);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
