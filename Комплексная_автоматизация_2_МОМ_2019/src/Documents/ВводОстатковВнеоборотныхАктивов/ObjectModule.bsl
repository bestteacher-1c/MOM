#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга Тогда
		ТаблицаКопирования = РасчетыПоДоговорамЛизинга.Выгрузить();
		ТаблицаКопирования.ЗаполнитьЗначения(Неопределено, "ДокументАванса");
		РасчетыПоДоговорамЛизинга.Загрузить(ТаблицаКопирования);
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеСерверУТ.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	(ВЫРАЗИТЬ(НМАДанныеДокумента.НомерСтроки КАК ЧИСЛО)) - 1 КАК ИндексСтроки,
	|	ВЫРАЗИТЬ(НМАДанныеДокумента.НематериальныйАктив КАК Справочник.НематериальныеАктивы) КАК НематериальныйАктив,
	|	ВЫРАЗИТЬ(НМАДанныеДокумента.НачислятьАмортизациюНУ КАК БУЛЕВО) КАК НачислятьАмортизациюНУ,
	|	ВЫРАЗИТЬ(НМАДанныеДокумента.ПорядокСписанияНИОКРНаРасходыНУ КАК Перечисление.ПорядокСписанияНИОКРНУ) КАК ПорядокСписанияНИОКРНаРасходыНУ
	|ПОМЕСТИТЬ НМАДанныеДокумента
	|ИЗ
	|	&НМАДанныеДокумента КАК НМАДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НМАДанныеДокумента.ИндексСтроки КАК ИндексСтроки,
	|	НЕ НМАДанныеДокумента.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ
	|ИЗ
	|	НМАДанныеДокумента КАК НМАДанныеДокумента
	|ГДЕ
	|	НМАДанныеДокумента.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|	И ВЫБОР
	|			КОГДА НМАДанныеДокумента.ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.Равномерно)
	|				ТОГДА НЕ НМАДанныеДокумента.НачислятьАмортизациюНУ
	|			ИНАЧЕ НМАДанныеДокумента.НачислятьАмортизациюНУ
	|		КОНЕЦ";
	Запрос.УстановитьПараметр("НМАДанныеДокумента", НМА.Выгрузить(, "НомерСтроки, НематериальныйАктив, НачислятьАмортизациюНУ, ПорядокСписанияНИОКРНаРасходыНУ"));
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НМА[Выборка.ИндексСтроки].НачислятьАмортизациюНУ = Выборка.НачислятьАмортизациюНУ;
		КонецЦикла;
	КонецЕсли;
	
	Если ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиОС
		И ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС
		И ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС
		И ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе
		И ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе
		И ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом Тогда
		
		ОС.Очистить();
	Иначе
		Для Каждого Строка Из ОС Цикл
			ОчиститьНеИспользуемыеРеквизитыОС(Строка);
		КонецЦикла;
	КонецЕсли;
	
	Если ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиНМА Тогда
		НМА.Очистить();
	КонецЕсли;
	
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга Тогда
		ЗаполнитьЗависимыеРеквизитыРасчетовПоДоговорамЛизинга();
		СоздатьУдалитьДокументыАвансовПередЗаписью();
	Иначе
		РасчетыПоДоговорамЛизинга.Очистить();
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если НЕ РегистрыСведений.УчетнаяПолитикаОрганизаций.РаздельныйУчетТоваровПоНалогообложениюНДС(Организация, Дата)
		И (ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиОС
			Или ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе) Тогда
		
		ЗначенияЗаполнения.Вставить("ВариантРаздельногоУчетаНДС", Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента);
		ЗначенияЗаполнения.Вставить("НалогообложениеНДС", Справочники.Организации.ЗакупкаПодДеятельность(Организация, , Дата));
		
	КонецЕсли;
	
	Для Каждого Строка Из ОС Цикл
		Если ЗначенияЗаполнения.Количество() <> 0 Тогда
			ЗаполнитьЗначенияСвойств(Строка, ЗначенияЗаполнения);
		КонецЕсли;
		
		Если Строка.ПорядокУчетаБУ <> Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации Тогда
			
			Строка.МетодНачисленияАмортизацииБУ = Неопределено;
			Строка.КоэффициентУскоренияБУ = 0;
			Если Строка.ПорядокУчетаБУ <> Перечисления.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ Тогда
				Строка.НачислятьАмортизациюБУ = Ложь;
				Строка.СрокИспользованияБУ = 0;
				Строка.КоэффициентАмортизацииБУ = 0;
			КонецЕсли;
			
			Строка.СтатьяРасходовАмортизации = Неопределено;
			Строка.АналитикаРасходовАмортизации = Неопределено;
			Строка.ПередаватьРасходыВДругуюОрганизацию = Ложь;
			Строка.ОрганизацияПолучательРасходов = Неопределено;
			Строка.СчетПередачиРасходов = Неопределено;
			
		КонецЕсли;
		
		Если Строка.ПорядокУчетаНУ <> Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации Тогда
			Строка.НачислятьАмортизациюНУ = Ложь;
			
			Строка.СтатьяРасходовАмортизационнойПремии = Неопределено;
			Строка.АналитикаРасходовАмортизационнойПремии = Неопределено;
			Строка.ВключитьАмортизационнуюПремиюВСоставРасходов = Ложь;
			Строка.СуммаКапитальныхВложенийВключаемыхВРасходыНУ = 0;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга Тогда
		СоздатьУдалитьДокументыАвансовПриЗаписи();
	КонецЕсли;
	
	Если Не Отказ И ДополнительныеСвойства.РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
		Документы.ВводОстатковВнеоборотныхАктивов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, "РеестрДокументов,ДокументыПоОС,ДокументыПоНМА");
		РегистрыСведений.РеестрДокументов.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
		РегистрыСведений.ДокументыПоОС.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
		РегистрыСведений.ДокументыПоНМА.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ВнеоборотныеАктивы.ПроверитьСоответствиеДатыВерсииУчета(ЭтотОбъект, Ложь, Отказ);
	
	МассивНепроверяемыхРеквизитов.Добавить("РасчетыПоДоговорамЛизинга.СуммаРегл");
	
	Если ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС
		И ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОС.Контрагент");
	КонецЕсли;
	
	Если ТипОперации <> Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОС.ДоговорЛизинга");
	КонецЕсли;
	
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиОС
		Или ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС
		Или ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС
		Или ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе
		Или ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе
		Или ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом Тогда
		
		ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", "ОсновноеСредство", Отказ);
		
		ПроверкаОСНеПринятыКУчетуВДругихОрганизациях(Отказ);
		
		ПроверкаТабличнойЧастиОС(Отказ);
		
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ОС");
	КонецЕсли;
	
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиНМА Тогда
		
		ВнеоборотныеАктивы.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "НМА", "НематериальныйАктив", Отказ);
		
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("НМА");
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("РасчетыПоДоговорамЛизинга.СуммаРегл");
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга Тогда
		ПроверкаТабличнойЧастиРасчетовПоДоговорамЛизинга(Отказ);
		
		МассивНепроверяемыхРеквизитов.Добавить("Подразделение");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("РасчетыПоДоговорамЛизинга");
	КонецЕсли;
	
	ТабличныеЧасти = Новый Структура;
	ТабличныеЧасти.Вставить(
		"ОС",
		"СтатьяРасходовАмортизации, АналитикаРасходовАмортизации, СтатьяРасходовАмортизационнойПремии, АналитикаРасходовАмортизационнойПремии");
	ТабличныеЧасти.Вставить("НМА", "СтатьяРасходовАмортизации, АналитикаРасходовАмортизации");
	ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(ЭтотОбъект, ТабличныеЧасти, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ВводОстатковВнеоборотныхАктивов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	РегистрыСведений.РеестрДокументов.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
	РегистрыСведений.ДокументыПоОС.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
	РегистрыСведений.ДокументыПоНМА.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
	
	ПроведениеСерверУТ.ЗагрузитьТаблицыДвижений(ДополнительныеСвойства, Движения);
	
	РеглУчетПроведениеСервер.ЗарегистрироватьКОтражению(ЭтотОбъект, ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	ПроведениеСерверУТ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	Если Не Отказ Тогда
		РеглУчетПроведениеСервер.ОтразитьДокумент(Новый Структура("Ссылка, Дата, Организация", Ссылка, Дата));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	УчетОСВызовСервера.ПолучитьНачислениеАмортизацииДляФормированияЗаданийКЗакрытиюМесяца(
		ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы, Ссылка);
	
	РасчетИмущественныхНалоговУП.ПолучитьСостоянияОСОрганизацийДляФормированияЗаданийКЗакрытиюМесяца(
		ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы, Ссылка);
	
	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУТ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()
	Массив = Новый Массив;
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);
КонецПроцедуры

Процедура ПроверкаТабличнойЧастиОС(Отказ)
	
	Для Каждого Строка Из ОС Цикл 
		
		Если Строка.ПорядокУчетаНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету  Тогда
			
			// Запрет некоторых движений если ОС списано при принятии
			Если Строка.НачислятьАмортизациюНУ Тогда
				Отказ = Истина;
				СтрокаСообщение = НСтр("ru='(НУ) по ОС не может начисляться амортизация, если оно списано на затраты при принятии к учету'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщение);
				Прервать;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Строка.НакопленнаяАмортизацияНУ) Тогда
				Отказ = Истина;
				СтрокаСообщение = НСтр("ru='(НУ) накопленная амортизация по ОС должна быть равна 0, если оно списано на затраты при принятии к учету'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщение);
				Прервать;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Строка.ТекущаяСтоимостьНУ) Тогда
				Отказ = Истина;
				СтрокаСообщение = НСтр("ru='(НУ) текущая стоимость ОС должна быть равна 0, если оно списано на затраты при принятии к учету'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщение);
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаТабличнойЧастиРасчетовПоДоговорамЛизинга(Отказ)
	
	РеквизитыДоговоров = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РасчетыПоДоговорамЛизинга.ВыгрузитьКолонку("Договор"), "ВалютаВзаиморасчетов");
	
	ВалютаРегУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Для Каждого Строка Из РасчетыПоДоговорамЛизинга Цикл
		
		Если РеквизитыДоговоров.Получить(Строка.Договор).ВалютаВзаиморасчетов = ВалютаРегУчета Тогда
			Строка.СуммаРегл = Строка.Сумма;
		ИначеЕсли Не ЗначениеЗаполнено(Строка.СуммаРегл) Тогда
			ТекстОшибки = НСтр("ru='Не заполнено поле ""Сумма (регл.)"" в строке %НомерСтроки% списка ""Расчеты по договорам лизинга""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", Строка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РасчетыПоДоговорамЛизинга", Строка.НомерСтроки, "СуммаРегл"),
				,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаОСНеПринятыКУчетуВДругихОрганизациях(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТаблицаОС", ОС.Выгрузить());
	
	Запрос.Текст = "
	|// Таблица ОС
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫРАЗИТЬ(ТаблицаОС.ОсновноеСредство КАК Справочник.ОбъектыЭксплуатации) КАК ОсновноеСредство,
	|	ТаблицаОС.ДатаПринятияКУчету КАК ДатаПринятияКУчетуРегл
	|ПОМЕСТИТЬ
	|	ВТ_ТаблицаОС
	|ИЗ
	|	&ТаблицаОС КАК ТаблицаОС
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство,
	|	ДатаПринятияКУчетуРегл
	|;
	|
	|// Все принятия к учету ОС по другим организациям
	|ВЫБРАТЬ
	|	Рег.ОсновноеСредство КАК ОсновноеСредство,
	|	Рег.Организация КАК Организация,
	|	Рег.ДатаСостояния КАК ДатаСостояния,
	|	Рег.Состояние
	|ПОМЕСТИТЬ ВТ_ПринятияКУчету
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК Рег
	|ГДЕ
	|	Рег.ОсновноеСредство В (ВЫБРАТЬ ВТ_ТаблицаОС.ОсновноеСредство ИЗ ВТ_ТаблицаОС)
	|	И Рег.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	И Рег.Организация <> &Организация
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство,
	|	ДатаСостояния
	|;
	|
	|// Все списания ОС по другим организациям
	|ВЫБРАТЬ
	|	Рег.ОсновноеСредство КАК ОсновноеСредство,
	|	Рег.Организация КАК Организация,
	|	Рег.ДатаСостояния КАК ДатаСостояния,
	|	Рег.Состояние
	|ПОМЕСТИТЬ ВТ_СнятияСУчета
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК Рег
	|ГДЕ
	|	Рег.ОсновноеСредство В (ВЫБРАТЬ ВТ_ТаблицаОС.ОсновноеСредство ИЗ ВТ_ТаблицаОС)
	|	И Рег.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|	И Рег.Организация <> &Организация
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство,
	|	ДатаСостояния
	|;
	|
	|// Поиск ОС, которые в других организациях были приняты к  учету, но не списаны, т.е. еще находятся в учете
	|ВЫБРАТЬ
	|	ВТ_ТаблицаОС.Организация                   КАК Организация,
	|	ВТ_ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ВТ_ТаблицаОС.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
	|	ВТ_ТаблицаОС.ОсновноеСредство.Код          КАК КодОсновногоСредства,
	|	ВТ_ТаблицаОС.ДатаПринятияКУчетуРегл        КАК ДатаПринятияКУчетуРегл,
	|	ЕСТЬNULL(Рег_ПринятиеКУчету.ДатаСостояния, ДАТАВРЕМЯ(1,1,1))                           КАК ДатаПринятияКУчетуВДругойОрганизации,
	|	ЕСТЬNULL(Рег_ПринятиеКУчету.Организация,ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ОрганизацияПринятияКУчету,
	|	ЕСТЬNULL(Рег_СнятияСУчета.ДатаСостояния, ДАТАВРЕМЯ(3999,12,31))                        КАК ДатаСнятияСУчетаВДругойОрганизации,
	|	ЕСТЬNULL(Рег_СнятияСУчета.Организация,ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))   КАК ОрганизацияСнятияСУчета
	|
	|ИЗ
	|	ВТ_ТаблицаОС
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(
	|		ВЫБРАТЬ
	|			МАКСИМУМ(Рег.ДатаСостояния),
	|			Рег.ОсновноеСредство КАК ОсновноеСредство,
	|			Рег.Организация КАК Организация
	|		ИЗ
	|		ВТ_ПринятияКУчету КАК Рег
	|		СГРУППИРОВАТЬ ПО
	|			Рег.ОсновноеСредство,
	|			Рег.Организация
	|	) КАК Рег_ПринятиеКУчету
	|	ПО
	|		ВТ_ТаблицаОС.Организация <> Рег_ПринятиеКУчету.Организация
	|		И ВТ_ТаблицаОС.ОсновноеСредство = Рег_ПринятиеКУчету.ОсновноеСредство
	|		И ВТ_ТаблицаОС.ДатаПринятияКУчетуРегл >= Рег_ПринятиеКУчету.ДатаСостояния
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|	(
	|		ВЫБРАТЬ
	|			МАКСИМУМ(Рег.ДатаСостояния),
	|			Рег.ОсновноеСредство КАК ОсновноеСредство,
	|			Рег.Организация КАК Организация
	|		ИЗ
	|		ВТ_СнятияСУчета КАК Рег
	|		СГРУППИРОВАТЬ ПО
	|			Рег.ОсновноеСредство,
	|			Рег.Организация
	|	) КАК Рег_СнятияСУчета
	|	ПО
	|		Рег_ПринятиеКУчету.Организация = Рег_СнятияСУчета.Организация
	|		И ВТ_ТаблицаОС.ОсновноеСредство = Рег_СнятияСУчета.ОсновноеСредство
	|		И ВТ_ТаблицаОС.ДатаПринятияКУчетуРегл >= Рег_СнятияСУчета.ДатаСостояния
	|
	|// Надо выбрать только те ОС, по которым в других организациях есть принятие к учету и нет списания, или списание раньше
	|ГДЕ
	|	ЕСТЬNULL(Рег_СнятияСУчета.ДатаСостояния, ДАТАВРЕМЯ(3999,12,31)) > ДатаПринятияКУчетуРегл
	|	И НЕ Рег_ПринятиеКУчету.Организация ЕСТЬ NULL
	|";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока Выборка.Следующий() Цикл
			НайденнаяСтрока = ОС.Найти(Выборка.ОсновноеСредство, "ОсновноеСредство");
			Если НайденнаяСтрока <> Неопределено Тогда
				Префикс = "ОС[" + Формат(НайденнаяСтрока.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='В строках %1 Основное средство %2 (%3) %4 уже было принято к учету в организации %5'"),
					НайденнаяСтрока.НомерСтроки,
					Выборка.ОсновноеСредствоНаименование,
					Выборка.КодОсновногоСредства,
					Формат(Выборка.ДатаПринятияКУчетуВДругойОрганизации, "ДЛФ=DD"),
					Выборка.ОрганизацияПринятияКУчету);
				Поле = Префикс + "ОсновноеСредство";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗависимыеРеквизитыРасчетовПоДоговорамЛизинга()
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютыДоговоров = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		РасчетыПоДоговорамЛизинга.ВыгрузитьКолонку("Договор"),
		"ВалютаВзаиморасчетов");
	ВалютыДоговоров.Вставить(Справочники.ДоговорыЛизинга.ПустаяСсылка(), ВалютаРегл);
	
	Для Каждого Строка Из РасчетыПоДоговорамЛизинга Цикл
		Если ВалютыДоговоров[Строка] = ВалютаРегл Тогда
			Строка.СуммаРегл = Строка.Сумма;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьУдалитьДокументыАвансовПередЗаписью()
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить("Организация", Организация);
	ЗначенияРеквизитов.Вставить("Дата", Дата);
	ЗначенияРеквизитов.Вставить("ДатаПроведенияБанком", Дата);
	ЗначенияРеквизитов.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ОплатаЛизингодателю);
	ЗначенияРеквизитов.Вставить("ТипПлатежаПоЛизингу", Перечисления.ТипыПлатежейПоЛизингу.ОбеспечительныйПлатеж);
	ЗначенияРеквизитов.Вставить("ПроведеноБанком", Истина);
	
	Запрос = Новый Запрос;
	Для Каждого КлючИЗначение Из ЗначенияРеквизитов Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ВалютаРегл", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("УдалитьСозданные", (ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения));
	Запрос.УстановитьПараметр("ТаблицаДокумента", РасчетыПоДоговорамЛизинга.Выгрузить(, "НомерСтроки, Контрагент, Договор, Сумма, СуммаРегл, ТипЗадолженности, ДокументАванса"));
	#Область ТекстЗапроса
	Запрос.Текст =
	"ВЫБРАТЬ
	|	(ВЫРАЗИТЬ(Т.НомерСтроки КАК ЧИСЛО)) - 1 КАК ИндексСтроки,
	|	ВЫРАЗИТЬ(Т.Контрагент КАК Справочник.Контрагенты) КАК Контрагент,
	|	ВЫРАЗИТЬ(Т.Договор КАК Справочник.ДоговорыЛизинга) КАК Договор,
	|	ВЫРАЗИТЬ(Т.ТипЗадолженности КАК Перечисление.ТипыПлатежейПоЛизингу) КАК ТипЗадолженности,
	|	ВЫРАЗИТЬ(Т.Сумма КАК ЧИСЛО) КАК Сумма,
	|	ВЫРАЗИТЬ(Т.СуммаРегл КАК ЧИСЛО) КАК СуммаРегл,
	|	ВЫРАЗИТЬ(Т.ДокументАванса КАК Документ.СписаниеБезналичныхДенежныхСредств) КАК ДокументАванса
	|ПОМЕСТИТЬ втТаблицаДокумента
	|ИЗ
	|	&ТаблицаДокумента КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Ссылка
	|ИЗ
	|	втТаблицаДокумента КАК втТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|		ПО втТаблицаДокумента.ДокументАванса = СписаниеБезналичныхДенежныхСредств.Ссылка
	|ГДЕ
	|	НЕ СписаниеБезналичныхДенежныхСредств.ПометкаУдаления
	|	И (&УдалитьСозданные
	|			ИЛИ втТаблицаДокумента.ТипЗадолженности <> ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ОбеспечительныйПлатеж))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписаниеБезналичныхДенежныхСредств.Ссылка
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов.РасчетыПоДоговорамЛизинга КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|		ПО Т.ДокументАванса = СписаниеБезналичныхДенежныхСредств.Ссылка
	|ГДЕ
	|	Т.Ссылка = &Ссылка
	|	И НЕ Т.ДокументАванса В
	|				(ВЫБРАТЬ
	|					втТаблицаДокумента.ДокументАванса
	|				ИЗ
	|					втТаблицаДокумента КАК втТаблицаДокумента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаДокумента.ИндексСтроки,
	|	втТаблицаДокумента.Контрагент,
	|	втТаблицаДокумента.Договор,
	|	втТаблицаДокумента.Сумма,
	|	втТаблицаДокумента.СуммаРегл,
	|	втТаблицаДокумента.ДокументАванса,
	|	ВЫБОР
	|		КОГДА втТаблицаДокумента.Договор.ВалютаВзаиморасчетов = &ВалютаРегл
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоговорВВалютеРегл,
	|	втТаблицаДокумента.Договор.ВалютаВзаиморасчетов КАК Валюта
	|ПОМЕСТИТЬ втЗначимыеСтроки
	|ИЗ
	|	втТаблицаДокумента КАК втТаблицаДокумента
	|ГДЕ
	|	втТаблицаДокумента.ТипЗадолженности = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоЛизингу.ОбеспечительныйПлатеж)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Дата,
	|	Операция.Контрагент КАК Контрагент,
	|	Операция.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Операция.Организация КАК Организация,
	|	Операция.СуммаДокумента КАК СуммаДокумента,
	|	Операция.Валюта КАК Валюта,
	|	ТабличнаяЧасть.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ТабличнаяЧасть.ТипПлатежаПоЛизингу КАК ТипПлатежаПоЛизингу,
	|	ТабличнаяЧасть.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СуммыДокументовВВалютеРегл.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	Операция.Проведен КАК Проведен,
	|	Операция.ПроведеноБанком КАК ПроведеноБанком,
	|	Операция.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ втДанныеДокументов
	|ИЗ
	|	втЗначимыеСтроки КАК втЗначимыеСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК Операция
	|		ПО втЗначимыеСтроки.ДокументАванса = Операция.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТабличнаяЧасть
	|		ПО втЗначимыеСтроки.ДокументАванса = ТабличнаяЧасть.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО (НЕ втЗначимыеСтроки.ДоговорВВалютеРегл)
	|			И (ТабличнаяЧасть.Ссылка = СуммыДокументовВВалютеРегл.Регистратор)
	|			И (ТабличнаяЧасть.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втДанныеДокументов.Ссылка ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СоздатьДокумент,
	|	втЗначимыеСтроки.ДокументАванса КАК ДокументАванса,
	|	втЗначимыеСтроки.Контрагент КАК Контрагент,
	|	втЗначимыеСтроки.Договор КАК ДоговорЛизинга,
	|	втЗначимыеСтроки.ДоговорВВалютеРегл КАК ДоговорВВалютеРегл,
	|	втЗначимыеСтроки.Валюта КАК Валюта,
	|	втЗначимыеСтроки.Сумма КАК СуммаДокумента,
	|	втЗначимыеСтроки.Сумма КАК Сумма,
	|	втЗначимыеСтроки.Сумма КАК СуммаВзаиморасчетов,
	|	втЗначимыеСтроки.Валюта КАК ВалютаВзаиморасчетов,
	|	ИСТИНА КАК ПроведеноБанком,
	|	&Дата КАК Период,
	|	втЗначимыеСтроки.Сумма КАК СуммаБезНДС,
	|	втЗначимыеСтроки.СуммаРегл КАК СуммаБезНДСРегл,
	|	втЗначимыеСтроки.ИндексСтроки
	|ИЗ
	|	втЗначимыеСтроки КАК втЗначимыеСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеДокументов КАК втДанныеДокументов
	|		ПО втЗначимыеСтроки.ДокументАванса = втДанныеДокументов.Ссылка
	|ГДЕ
	|	(втДанныеДокументов.Ссылка ЕСТЬ NULL 
	|			ИЛИ НЕ втДанныеДокументов.Проведен
	|			ИЛИ втДанныеДокументов.ПометкаУдаления
	|			ИЛИ втДанныеДокументов.Дата <> &Дата
	|			ИЛИ втДанныеДокументов.ХозяйственнаяОперация <> &ХозяйственнаяОперация
	|			ИЛИ втДанныеДокументов.Организация <> &Организация
	|			ИЛИ втДанныеДокументов.ПроведеноБанком <> &ПроведеноБанком
	|			ИЛИ втДанныеДокументов.Контрагент <> втЗначимыеСтроки.Контрагент
	|			ИЛИ втДанныеДокументов.ДоговорЛизинга <> втЗначимыеСтроки.Договор
	|			ИЛИ втДанныеДокументов.Валюта <> втЗначимыеСтроки.Валюта
	|			ИЛИ втДанныеДокументов.СуммаДокумента <> втЗначимыеСтроки.Сумма
	|			ИЛИ втДанныеДокументов.СуммаБезНДСРегл <> втЗначимыеСтроки.СуммаРегл
	|			ИЛИ втДанныеДокументов.ТипПлатежаПоЛизингу <> &ТипПлатежаПоЛизингу)";
	#КонецОбласти
	
	ПоляДокументаАванса = "СоздатьДокумент, ДокументАванса,
	|Организация, Дата, ДатаПроведенияБанком, Период, ХозяйственнаяОперация, ТипПлатежаПоЛизингу, ПроведеноБанком,
	|Контрагент, ДоговорЛизинга, ДоговорВВалютеРегл, Валюта, ВалютаВзаиморасчетов,
	|СуммаДокумента, Сумма, СуммаБезНДС, СуммаБезНДСРегл, СуммаВзаиморасчетов,
	|Комментарий";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	// Удаление не используемых документов аванса
	Выборка = Пакет[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Пометить на удаление документ аванса
		ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектДокумента.ПометкаУдаления = Истина;
		ОбъектДокумента.Проведен = Ложь;
		ОбъектДокумента.Записать(РежимЗаписиДокумента.Запись);
		
		// Стереть записи в регистре сумм в валюте регл. учета
		НаборЗаписей = РегистрыСведений.СуммыДокументовВВалютеРегл.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Ссылка);
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	МассивОбновляемыхДокументов = Новый Массив;
	Выборка = Пакет[4].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РеквизитыДокументаАванса = Новый Структура(ПоляДокументаАванса);
		ЗаполнитьЗначенияСвойств(РеквизитыДокументаАванса, ЗначенияРеквизитов);
		ЗаполнитьЗначенияСвойств(РеквизитыДокументаАванса, Выборка);
		
		Если Выборка.СоздатьДокумент Тогда
			РеквизитыДокументаАванса.ДокументАванса = Документы.СписаниеБезналичныхДенежныхСредств.ПолучитьСсылку();
			РасчетыПоДоговорамЛизинга[Выборка.ИндексСтроки].ДокументАванса = РеквизитыДокументаАванса.ДокументАванса;
		КонецЕсли;
		
		МассивОбновляемыхДокументов.Добавить(РеквизитыДокументаАванса);
		
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("МассивОбновляемыхДокументов", МассивОбновляемыхДокументов);
	
КонецПроцедуры

Процедура СоздатьУдалитьДокументыАвансовПриЗаписи()
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись
		Или Не ДополнительныеСвойства.Свойство("МассивОбновляемыхДокументов") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	КомментарийДокументаАванса = НСтр("ru = 'Создан автоматически при вводе начальных остатков взаиморасчетов по договору лизинга документом ""%1"". Используется для формирования счетов-фактур на аванс по обеспечительному платежу.'");
	КомментарийДокументаАванса = СтрШаблон(КомментарийДокументаАванса, ЭтотОбъект.Ссылка);
	
	Для Каждого ОписаниеДокумента Из ДополнительныеСвойства.МассивОбновляемыхДокументов Цикл
		
		ОбъектДокумента = Неопределено;
		Если ОписаниеДокумента.СоздатьДокумент Тогда
			ОбъектДокумента = Документы.СписаниеБезналичныхДенежныхСредств.СоздатьДокумент();
			ОбъектДокумента.УстановитьСсылкуНового(ОписаниеДокумента.ДокументАванса);
		Иначе
			ОбъектДокумента = ОписаниеДокумента.ДокументАванса.ПолучитьОбъект();
			
		КонецЕсли;
		
		ОбъектДокумента.Проведен = Истина;
		ОбъектДокумента.ПометкаУдаления = Ложь;
		ЗаполнитьЗначенияСвойств(ОбъектДокумента, ОписаниеДокумента);
		
		ОбъектДокумента.РасшифровкаПлатежа.Очистить();
		СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, ОписаниеДокумента);
		СтрокаРасшифровки.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
		ОбъектДокумента.Комментарий = КомментарийДокументаАванса;
		
		ОбъектДокумента.Записать(РежимЗаписиДокумента.Запись);
		
		НаборЗаписей = РегистрыСведений.СуммыДокументовВВалютеРегл.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ОбъектДокумента.Ссылка);
		Если Не ОписаниеДокумента.ДоговорВВалютеРегл Тогда
			СтрокаНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНабора, ОписаниеДокумента);
			СтрокаНабора.Регистратор = ОбъектДокумента.Ссылка;
		КонецЕсли;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьНеИспользуемыеРеквизитыОС(Описание)
	
	Если Не Описание.ПрименениеЦелевогоФинансирования Тогда
		Описание.СчетУчетаЦФ = Неопределено;
		Описание.СчетАмортизацииЦФ = Неопределено;
		Описание.СтатьяДоходов = Неопределено;
		Описание.АналитикаДоходов = Неопределено;
		
		Описание.ТекущаяСтоимостьБУЦФ = 0;
		Описание.ТекущаяСтоимостьНУЦФ = 0;
		Описание.ТекущаяСтоимостьПРЦФ = 0;
		
		Описание.НакопленнаяАмортизацияБУЦФ = 0;
		Описание.НакопленнаяАмортизацияНУЦФ = 0;
		Описание.НакопленнаяАмортизацияПРЦФ = 0;
	КонецЕсли;
	
	Если Описание.ПорядокУчетаБУ = Перечисления.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается
		И Описание.ПорядокУчетаНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.СтоимостьНеВключаетсяВРасходы Тогда
		
		Описание.СчетАмортизации = Неопределено;
		
		Описание.НакопленнаяАмортизацияБУ = 0;
		Описание.НакопленнаяАмортизацияНУ = 0;
		Описание.НакопленнаяАмортизацияПР = 0;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли