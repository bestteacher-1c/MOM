#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаСписка";
	КонецЕсли;
	
	СписаниеНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Списание НМА".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеНМА2_4) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СписаниеНМА2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СписаниеНМА2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	
	СписаниеНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	СписаниеНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.СписаниеНМА2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса);
	ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	СписаниеНМАЛокализация.ДополнитьТекстыЗапросаОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДляДвижений = Новый Структура;
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

Функция ПредставлениеРеквизитов() Экспорт

	ПредставлениеРеквизитов = Новый Соответствие;

	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ВедетсяРегламентированныйУчетВНА = ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	Если ВедетсяРегламентированныйУчетВНА Тогда
		
		ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияБУ", СтрШаблон(НСтр("ru = 'Сумма регл. (%1)'"), Строка(ВалютаРегл)));
		ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияУУ", СтрШаблон(НСтр("ru = 'Сумма упр. (%1)'"), Строка(ВалютаУпр)));
		
	Иначе
		
		Если ВалютаРегл = ВалютаУпр Тогда
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияБУ", НСтр("ru = 'Сумма'"));
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияУУ", НСтр("ru = 'Сумма'"));
		Иначе
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияБУ", СтрШаблон(НСтр("ru = 'Сумма (%1)'"), Строка(ВалютаРегл)));
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияУУ", СтрШаблон(НСтр("ru = 'Сумма (%1)'"), Строка(ВалютаУпр)));
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат ПредставлениеРеквизитов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.СписаниеНМА2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры);
	
	СписаниеНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Ссылка,
	|	ДанныеДокумента.Дата                       КАК Период,
	|	ДанныеДокумента.Номер                      КАК Номер,
	|	ДанныеДокумента.Проведен                   КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления            КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете         КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете          КАК ОтражатьВУпрУчете,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМА)            КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиНМА)   КАК ХозяйственнаяОперацияСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеАмортизацииНМА) КАК ХозяйственнаяОперацияАмортизация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиСтоимостиНМА)   КАК ХозяйственнаяОперацияРезервСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиАмортизацииНМА) КАК ХозяйственнаяОперацияРезервАмортизация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы) КАК СтатьяАП_НМА,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК СтатьяАП_ПрибыльУбытки,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ДанныеДокумента.ЧастичнаяЛиквидация        КАК ЧастичнаяЛиквидация,
	|	ДанныеДокумента.СтатьяРасходов             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов          КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	ДанныеДокумента.Ответственный              КАК Ответственный,
	|	ДанныеДокумента.Комментарий                КАК Комментарий
	|ИЗ
	|	Документ.СписаниеНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КонецДня", Новый Граница(КонецДня(Реквизиты.Период),ВидГраницы.Включая));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СписаниеНМА2_4"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.СписаниеНМА);
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Списание НМА'"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив   КАК НематериальныйАктив,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&Период                          КАК Дата,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&Проведен                        КАК Проведен,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете
	|ИЗ
	|	Документ.СписаниеНМА2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.СписаниеНМА2_4.НМА КАК ТаблицаНМА
	|		ПО
	|			ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка                        КАК Ссылка,
	|	&Период                        КАК ДатаДокументаИБ,
	|	&Номер                         КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных       КАК ТипСсылки,
	|	&Организация                   КАК Организация,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                   КАК НаправлениеДеятельности,
	|	&Подразделение                 КАК Подразделение,
	|	&Ответственный                 КАК Ответственный,
	|	&Комментарий                   КАК Комментарий,
	|	&Проведен                      КАК Проведен,
	|	&ПометкаУдаления               КАК ПометкаУдаления,
	|	ЛОЖЬ                           КАК ДополнительнаяЗапись,
	|	&Период                        КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                 КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                            КАК Период,
	|	&Организация                                                       КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив                                     КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМА.СпособПоступления                        КАК СпособПоступления,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ - ТаблицаНМА.СуммаСписанияУУ                
	|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ                                                              КАК ПервоначальнаяСтоимостьУУ,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ - ТаблицаНМА.СуммаСписанияБУ                
	|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ                                                              КАК ПервоначальнаяСтоимостьБУ,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ - ТаблицаНМА.СуммаСписанияНУ                
	|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьНУ
	|	КОНЕЦ                                                              КАК ПервоначальнаяСтоимостьНУ,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьПР - ТаблицаНМА.СуммаСписанияПР                
	|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьПР
	|	КОНЕЦ                                                              КАК ПервоначальнаяСтоимостьПР,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьВР - ТаблицаНМА.СуммаСписанияВР                
	|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьВР
	|	КОНЕЦ                                                              КАК ПервоначальнаяСтоимостьВР,
	|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ             КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииНУ             КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияНМА.Коэффициент                              КАК Коэффициент,
	|	ПервоначальныеСведенияНМА.АмортизацияДо2002                        КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияНМА.АмортизацияДо2009                        КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияНМА.СтоимостьДо2002                          КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияНМА.ФактическийСрокИспользованияДо2009       КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияНМА.ДатаПриобретения                         КАК ДатаПриобретения,
	|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ                     КАК ДатаПринятияКУчетуУУ,
	|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ                     КАК ДатаПринятияКУчетуБУ,
	|	ПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ                 КАК ДокументПринятияКУчетуУУ,
	|	ПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ                 КАК ДокументПринятияКУчетуБУ,
	|	ПервоначальныеСведенияНМА.ПорядокУчетаНУ			               КАК ПорядокУчетаНУ,
	|	ВЫБОР
	|		КОГДА НЕ &ЧастичнаяЛиквидация
	|			ТОГДА &Ссылка                
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                              КАК ДокументСписания
	|ИЗ
	|	Документ.СписаниеНМА2_4.НМА КАК ТаблицаНМА
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
	|			&КонецДня,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И НематериальныйАктив В (ВЫБРАТЬ Т.НематериальныйАктив ИЗ втСписокНМА КАК Т)
	|		) КАК ПервоначальныеСведенияНМА
	|	ПО
	|		ПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан) КАК Состояние
	|ИЗ
	|	Документ.СписаниеНМА2_4.НМА КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И НЕ &ЧастичнаяЛиквидация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	&Ссылка                                    КАК Регистратор,
	|	&Период                                    КАК Период,
	|	&Организация                               КАК Организация,
	|	&Подразделение                             КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК Амортизация,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияПР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияВР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияПРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК АмортизацияВРЦФ,
	|	0                                          КАК РезервПереоценкиАмортизации,
	|	0                                          КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                            КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                         КАК КорАналитикаРасходов,
	|	&ХозяйственнаяОперацияАмортизация          КАК ХозяйственнаяОперация
	|ИЗ
	|	втАмортизацияСписанияНМА КАК ТаблицаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	&Ссылка                                    КАК Регистратор,
	|	&Период                                    КАК Период,
	|	&Организация                               КАК Организация,
	|	&Подразделение                             КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	0                                          КАК Амортизация,
	|	0                                          КАК АмортизацияРегл,
	|	0                                          КАК АмортизацияНУ,
	|	0                                          КАК АмортизацияПР,
	|	0                                          КАК АмортизацияВР,
	|	0                                          КАК АмортизацияЦФ,
	|	0                                          КАК АмортизацияНУЦФ,
	|	0                                          КАК АмортизацияПРЦФ,
	|	0                                          КАК АмортизацияВРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК РезервПереоценкиАмортизации,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизацииРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                            КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                         КАК КорАналитикаРасходов,
	|	&ХозяйственнаяОперацияРезервАмортизация    КАК ХозяйственнаяОперация
	|ИЗ
	|	втАмортизацияСписанияНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ОтражатьВУпрУчете И ТаблицаНМА.РезервПереоценкиАмортизации <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ТаблицаНМА.РезервПереоценкиАмортизацииРегл <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК Стоимость,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВРЦФ,
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация      КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                        КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                     КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втАмортизацияСписанияНМА КАК ТаблицаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание остаточной стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК Стоимость,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьПРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                  КАК СтоимостьВРЦФ,
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияСтоимость        КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                        КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                     КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	&Организация                             КАК Организация,
	|	&Подразделение                           КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив           КАК НематериальныйАктив,
	|
	|	0                                        КАК Стоимость,
	|	0                                        КАК СтоимостьРегл,
	|	0                                        КАК СтоимостьНУ,
	|	0                                        КАК СтоимостьПР,
	|	0                                        КАК СтоимостьВР,
	|	0                                        КАК СтоимостьЦФ,
	|	0                                        КАК СтоимостьНУЦФ,
	|	0                                        КАК СтоимостьПРЦФ,
	|	0                                        КАК СтоимостьВРЦФ,
	|	0                                        КАК ПредварительнаяСтоимость,
	|	0                                        КАК ПредварительнаяСтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                    КАК РезервПереоценкиСтоимости,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимостиРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                    КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияРезервСтоимость    КАК ХозяйственнаяОперация,
	|	&СтатьяРасходов                          КАК КорСтатьяРасходов,
	|	&АналитикаРасходов                       КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                      КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                       КАК ОтражатьВУпрУчете
	|ИЗ
	|	втСтоимостьСписанияНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ОтражатьВУпрУчете И ТаблицаНМА.РезервПереоценкиСтоимости <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ТаблицаНМА.РезервПереоценкиСтоимостиРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание предварительной стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	&Период                                  КАК Период,
	|	&Организация                             КАК Организация,
	|	&Подразделение                           КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив           КАК НематериальныйАктив,
	|
	|	0                                        КАК Стоимость,
	|	0                                        КАК СтоимостьРегл,
	|	0                                        КАК СтоимостьНУ,
	|	0                                        КАК СтоимостьПР,
	|	0                                        КАК СтоимостьВР,
	|	0                                        КАК СтоимостьЦФ,
	|	0                                        КАК СтоимостьНУЦФ,
	|	0                                        КАК СтоимостьПРЦФ,
	|	0                                        КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ПредварительнаяСтоимость     КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл КАК ПредварительнаяСтоимостьРегл,
	|	0                                        КАК РезервПереоценкиСтоимости,
	|	0                                        КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                   КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                             КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                             КАК КорАналитикаРасходов,
	|	&ОтражатьВРеглУчете                      КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                       КАК ОтражатьВУпрУчете
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.ОбъектУчета
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|	И &ОтражатьВУпрУчете
	|	И (СтоимостьНМА.ПредварительнаяСтоимость <> 0
	|		ИЛИ СтоимостьНМА.ПредварительнаяСтоимостьРегл <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() +
	"ВЫБРАТЬ
	|	&Период                                           КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	&Организация                                      КАК Организация,
	|	&Подразделение                                    КАК Подразделение,
	|	&СтатьяРасходов                                   КАК СтатьяРасходов,
	|	&АналитикаРасходов                                КАК АналитикаРасходов,
	|	ТаблицаНМА.НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                      КАК ВидДеятельностиНДС,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДС,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл + ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл + ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПР + ТаблицаНМА.СтоимостьПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВР + ТаблицаНМА.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ВременнаяРазница,
	|	
	|	&ХозяйственнаяОперацияСтоимость                   КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                      КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов();
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() +
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	ТаблицаРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ТаблицаРасходы.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете
	|			ТОГДА ТаблицаРасходы.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|
	|	0 КАК НДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаРасходы.СтоимостьРегл + ТаблицаРасходы.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаРасходы.СтоимостьПР + ТаблицаРасходы.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаРасходы.СтоимостьВР + ТаблицаРасходы.СтоимостьВРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	втОстаточнаяСтоимостьНМА КАК ТаблицаРасходы";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияСтоимость         КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяРасходов                         КАК Статья,
	|	&АналитикаАктивовПассивов               КАК Аналитика,
	|
	|	ТаблицаНМА.Стоимость                    КАК Сумма
	|ИЗ
	|	втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.Стоимость <> 0
	|	И ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Доходы от целевого финансирования
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияСтоимость         КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                           КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив          КАК Аналитика,
	|
	|	ТаблицаНМА.АмортизацияЦФ                КАК Сумма
	|ИЗ
	|	втАмортизацияСписанияНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.АмортизацияЦФ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                           КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив          КАК Аналитика,
	|
	|	ТаблицаНМА.РезервПереоценкиАмортизации  КАК Сумма
	|ИЗ
	|	втАмортизацияСписанияНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиАмортизации <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияРезервСтоимость   КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                           КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив          КАК Аналитика,
	|
	|	ТаблицаНМА.РезервПереоценкиСтоимости    КАК Сумма
	|ИЗ
	|	втСтоимостьСписанияНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиСтоимости <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	
		ТекстЗапроса =
		"// Списание амортизации
		|ВЫБРАТЬ
		|	&Период                            КАК Период,
		|	&ХозяйственнаяОперацияАмортизация  КАК ХозяйственнаяОперация,
		|	&Организация                       КАК Организация,
		|
		|	&Подразделение                     КАК Подразделение,
		|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	&СтатьяРасходов                    КАК Статья,
		|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
		|	&АналитикаРасходов                 КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО                       КАК АналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
		|
		|	&Подразделение                     КАК КорПодразделение,
		|	ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	&СтатьяАП_НМА                      КАК КорСтатья,
		|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
		|	ТаблицаНМА.НематериальныйАктив     КАК КорАналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.Амортизация 
		|		ИНАЧЕ 0 
		|	КОНЕЦ                              КАК Сумма,
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.Амортизация 
		|		ИНАЧЕ 0 
		|	КОНЕЦ                              КАК СуммаУпр,
		|	ВЫБОР 
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА ТаблицаНМА.АмортизацияРегл + ТаблицаНМА.АмортизацияЦФ 
		|		ИНАЧЕ 0 
		|	КОНЕЦ                              КАК СуммаРегл
		|ИЗ
		|	втАмортизацияСписанияНМА КАК ТаблицаНМА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Списание остаточной стоимости
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВложенныйЗапрос.Статья КАК Статья,
		|	ВложенныйЗапрос.АналитикаДоходов КАК АналитикаДоходов,
		|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
		|	ВложенныйЗапрос.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
		|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|
		|	ВложенныйЗапрос.КорПодразделение КАК КорПодразделение,
		|	ВложенныйЗапрос.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	ВложенныйЗапрос.КорСтатья КАК КорСтатья,
		|	ВложенныйЗапрос.КорАналитикаДоходов КАК КорАналитикаДоходов,
		|	ВложенныйЗапрос.КорАналитикаРасходов КАК КорАналитикаРасходов,
		|	ВложенныйЗапрос.КорАналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
		|	ВложенныйЗапрос.КорГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
		|
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА СУММА(ВложенныйЗапрос.Сумма) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР 
		|		КОГДА &ОтражатьВУпрУчете 
		|			ТОГДА СУММА(ВложенныйЗапрос.СуммаУпр) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР 
		|		КОГДА &ОтражатьВРеглУчете 
		|			ТОГДА СУММА(ВложенныйЗапрос.СуммаРегл) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаРегл
		|ИЗ
		|	(ВЫБРАТЬ
		|		&Период                            КАК Период,
		|		&ХозяйственнаяОперацияСтоимость    КАК ХозяйственнаяОперация,
		|		&Организация                       КАК Организация,
		|
		|		&Подразделение                     КАК Подразделение,
		|		ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		&СтатьяАП_НМА                      КАК Статья,
		|		НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
		|		НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
		|		ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
		|		ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
		|
		|		&Подразделение                     КАК КорПодразделение,
		|		ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		&СтатьяРасходов                    КАК КорСтатья,
		|		НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
		|		&АналитикаРасходов                 КАК КорАналитикаРасходов,
		|		НЕОПРЕДЕЛЕНО                       КАК КорАналитикаАктивовПассивов,
		|		ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
		|
		|		ТаблицаНМА.Стоимость               КАК Сумма,
		|		ТаблицаНМА.Стоимость               КАК СуммаУпр,
		|		ТаблицаНМА.СтоимостьРегл + ТаблицаНМА.СтоимостьЦФ КАК СуммаРегл
		|	ИЗ
		|		втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		&Период                            КАК Период,
		|		&ХозяйственнаяОперацияСтоимость    КАК ХозяйственнаяОперация,
		|		&Организация                       КАК Организация,
		|
		|		&Подразделение                     КАК Подразделение,
		|		ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		&СтатьяАП_НМА                      КАК Статья,
		|		НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
		|		НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
		|		ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
		|		ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
		|
		|		&Подразделение                     КАК КорПодразделение,
		|		ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		&СтатьяРасходов                    КАК КорСтатья,
		|		НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
		|		&АналитикаРасходов                 КАК КорАналитикаРасходов,
		|		НЕОПРЕДЕЛЕНО                       КАК КорАналитикаАктивовПассивов,
		|		ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
		|
		|		ТаблицаНМА.Амортизация             КАК Сумма,
		|		ТаблицаНМА.Амортизация             КАК СуммаУпр,
		|		ТаблицаНМА.АмортизацияРегл + ТаблицаНМА.АмортизацияЦФ КАК СуммаРегл
		|	ИЗ
		|		втАмортизацияСписанияНМА КАК ТаблицаНМА) КАК ВложенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.ХозяйственнаяОперация,
		|	ВложенныйЗапрос.Организация,
		|
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности,
		|	ВложенныйЗапрос.Статья,
		|	ВложенныйЗапрос.АналитикаДоходов,
		|	ВложенныйЗапрос.АналитикаРасходов,
		|	ВложенныйЗапрос.АналитикаАктивовПассивов,
		|	ВложенныйЗапрос.ГруппаФинансовогоУчета,
		|
		|	ВложенныйЗапрос.КорПодразделение,
		|	ВложенныйЗапрос.КорНаправлениеДеятельности,
		|	ВложенныйЗапрос.КорСтатья,
		|	ВложенныйЗапрос.КорАналитикаДоходов,
		|	ВложенныйЗапрос.КорАналитикаРасходов,
		|	ВложенныйЗапрос.КорАналитикаАктивовПассивов,
		|	ВложенныйЗапрос.КорГруппаФинансовогоУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Списание резерва переоценки амортизации НМА
		|ВЫБРАТЬ
		|	&Период                                 КАК Период,
		|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
		|	&Организация                            КАК Организация,
		|
		|	&Подразделение                          КАК Подразделение,
		|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
		|	&СтатьяАП_НМА                           КАК Статья,
		|	НЕОПРЕДЕЛЕНО                            КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
		|	ТаблицаНМА.НематериальныйАктив          КАК АналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
		|
		|	&Подразделение                          КАК КорПодразделение,
		|	ТаблицаНМА.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
		|	&СтатьяАП_ПрибыльУбытки                 КАК КорСтатья,
		|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
		|
		|	ТаблицаНМА.РезервПереоценкиАмортизации  КАК Сумма,
		|	ТаблицаНМА.РезервПереоценкиАмортизации  КАК СуммаУпр,
		|	0                                       КАК СуммаРегл
		|ИЗ
		|	втАмортизацияСписанияНМА КАК ТаблицаНМА
		|ГДЕ
		|	ТаблицаНМА.РезервПереоценкиАмортизации <> 0
		|	И &ОтражатьВУпрУчете
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Списание резерва переоценки стоимости НМА
		|ВЫБРАТЬ
		|	&Период                               КАК Период,
		|	&ХозяйственнаяОперацияРезервСтоимость КАК ХозяйственнаяОперация,
		|	&Организация                          КАК Организация,
		|
		|	&Подразделение                        КАК Подразделение,
		|	ТаблицаНМА.НаправлениеДеятельности    КАК НаправлениеДеятельности,
		|	&СтатьяАП_ПрибыльУбытки               КАК Статья,
		|	НЕОПРЕДЕЛЕНО                          КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                          КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО                          КАК АналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
		|
		|	&Подразделение                        КАК КорПодразделение,
		|	ТаблицаНМА.НаправлениеДеятельности    КАК КорНаправлениеДеятельности,
		|	&СтатьяАП_НМА                         КАК КорСтатья,
		|	НЕОПРЕДЕЛЕНО                          КАК КорАналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                          КАК КорАналитикаРасходов,
		|	ТаблицаНМА.НематериальныйАктив        КАК КорАналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета     КАК КорГруппаФинансовогоУчета,
		|
		|	ТаблицаНМА.РезервПереоценкиСтоимости  КАК Сумма,
		|	ТаблицаНМА.РезервПереоценкиСтоимости  КАК СуммаУпр,
		|	0                                     КАК СуммаРегл
		|ИЗ
		|	втСтоимостьСписанияНМА КАК ТаблицаНМА
		|ГДЕ
		|	ТаблицаНМА.РезервПереоценкиСтоимости <> 0
		|	И &ОтражатьВУпрУчете";
	
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса)

	ИмяТаблицы = "втСтоимостьСписанияНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	&Ссылка                                    КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	АмортизацияНМА.Амортизация                 КАК Стоимость,
	|	АмортизацияНМА.АмортизацияРегл             КАК СтоимостьРегл,
	|	АмортизацияНМА.АмортизацияНУ               КАК СтоимостьНУ,
	|	АмортизацияНМА.АмортизацияПР               КАК СтоимостьПР,
	|	АмортизацияНМА.АмортизацияВР               КАК СтоимостьВР,
	|	АмортизацияНМА.АмортизацияЦФ               КАК СтоимостьЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ             КАК СтоимостьНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ             КАК СтоимостьПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ             КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.РезервПереоценкиСтоимости     КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|
	|	1                                          КАК ДоляСписания,
	|	1                                          КАК ДоляСписанияБУ,
	|	1                                          КАК ДоляСписанияЦФ,
	|	1                                          КАК ДоляСписанияНУ,
	|	1                                          КАК ДоляСписанияНУЦФ,
	|	1                                          КАК ДоляСписанияПР,
	|	1                                          КАК ДоляСписанияПРЦФ,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов                   КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов                КАК АналитикаДоходов
	|
	|ПОМЕСТИТЬ втСтоимостьСписанияНМА
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.ОбъектУчета
	|		И СтоимостьНМА.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = АмортизацияНМА.ОбъектУчета
	|		И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание частичной стоимости
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка                    КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ТаблицаНМА.СуммаСписанияУУ  КАК Стоимость,
	|	ТаблицаНМА.СуммаСписанияБУ - ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ КАК СтоимостьРегл,
	|	ТаблицаНМА.СуммаСписанияНУ - ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ КАК СтоимостьНУ,
	|	ТаблицаНМА.СуммаСписанияПР - ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФБУ КАК СтоимостьПР,
	|
	|	ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияБУ - ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияНУ - ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияПР - ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФБУ) КАК ЧИСЛО(31,2)) КАК СтоимостьВР,
	|
	|	ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ КАК СтоимостьЦФ,
	|	ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ КАК СтоимостьНУЦФ,
	|	ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФБУ КАК СтоимостьПРЦФ,
	|
	|	ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФБУ) КАК ЧИСЛО(31,2)) КАК СтоимостьВРЦФ,
	|	
	|	СтоимостьНМА.РезервПереоценкиСтоимости
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаНМА.СуммаСписанияУУ / СтоимостьНМА.Стоимость
	|	КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаНМА.СуммаСписанияУУ / СтоимостьНМА.Стоимость
	|	КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияУУ / СтоимостьНМА.Стоимость КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписания,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияБУ - ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ) / СтоимостьНМА.СтоимостьРегл КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияБУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ) / СтоимостьНМА.СтоимостьЦФ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияНУ - ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ) / СтоимостьНМА.СтоимостьНУ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияНУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ) / СтоимостьНМА.СтоимостьНУЦФ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияНУЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияПР - ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФБУ) / СтоимостьНМА.СтоимостьРегл КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияПР,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФБУ) / СтоимостьНМА.СтоимостьЦФ КАК ЧИСЛО(38, 16))
	|	КОНЕЦ КАК ДоляСписанияПРЦФ,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов               КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов            КАК АналитикаДоходов
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.ОбъектУчета
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса)

	ИмяТаблицы = "втАмортизацияСписанияНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	&Ссылка                                        КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                 КАК НематериальныйАктив,
	|
	|	АмортизацияНМА.Амортизация                     КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл                 КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ                   КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР                   КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР                   КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ                   КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ                 КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ                 КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ                 КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации     КАК РезервПереоценкиАмортизации,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета              КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности             КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация              КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ втАмортизацияСписанияНМА
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание частичной амортизации
	|ВЫБРАТЬ
	|	&Ссылка                                   КАК Ссылка,
	|	СтоимостьСписанияНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|
	// Амортизация
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.Амортизация = ЕСТЬNULL(СтоимостьНМА.Стоимость, 0)
	|			ТОГДА СтоимостьСписанияНМА.Стоимость
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.Амортизация * СтоимостьСписанияНМА.ДоляСписания КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияРегл = ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияРегл * СтоимостьСписанияНМА.ДоляСписанияБУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияНУ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьНУ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУ * СтоимостьСписанияНМА.ДоляСписанияНУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияПР = ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьПР
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПР * СтоимостьСписанияНМА.ДоляСписанияПР КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияПР,
	|
	// АмортизацияВР
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияРегл = ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияРегл * СтоимостьСписанияНМА.ДоляСписанияБУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияНУ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьНУ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУ * СтоимостьСписанияНМА.ДоляСписанияНУ КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияПР = ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьПР
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПР * СтоимостьСписанияНМА.ДоляСписанияПР КАК ЧИСЛО(31,2))
	|			КОНЕЦ КАК АмортизацияВР,
	|
	// АмортизацияЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияЦФ * СтоимостьСписанияНМА.ДоляСписанияЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияЦФ,
	|
	// АмортизацияНУЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияНУЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьНУЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУЦФ * СтоимостьСписанияНМА.ДоляСписанияНУЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияНУЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияПРЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьПРЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПРЦФ * СтоимостьСписанияНМА.ДоляСписанияПРЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|
	// АмортизацияВРЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияЦФ * СтоимостьСписанияНМА.ДоляСписанияЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияНУЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьНУЦФ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУЦФ * СтоимостьСписанияНМА.ДоляСписанияНУЦФ КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияПРЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьПРЦФ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПРЦФ * СтоимостьСписанияНМА.ДоляСписанияПРЦФ КАК ЧИСЛО(31,2))
	|			КОНЕЦ КАК АмортизацияВРЦФ,
	|
	|	АмортизацияНМА.РезервПереоценкиАмортизации                   КАК РезервПереоценкиАмортизации,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл               КАК РезервПереоценкиАмортизацииРегл,
	|
	|	СтоимостьСписанияНМА.ГруппаФинансовогоУчета                  КАК ГруппаФинансовогоУчета,
	|	СтоимостьСписанияНМА.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация                            КАК ХозяйственнаяОперация
	|ИЗ
	|	ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК СтоимостьСписанияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = СтоимостьСписанияНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = СтоимостьСписанияНМА.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО АмортизацияНМА.ОбъектУчета = СтоимостьНМА.ОбъектУчета
	|			И АмортизацияНМА.Ссылка = СтоимостьНМА.Ссылка
	|ГДЕ
	|	&ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса) Экспорт

	ИмяТаблицы = "втОстаточнаяСтоимостьНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Ссылка                                                     КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                              КАК НематериальныйАктив,
	|
	|	ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)         КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) КАК СтоимостьРегл,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)     КАК СтоимостьПР,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьВР, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияВР, 0)     КАК СтоимостьВР,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета                           КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности                          КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов                                    КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов                                 КАК АналитикаДоходов
	|
	|ПОМЕСТИТЬ втОстаточнаяСтоимостьНМА
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = СтоимостьНМА.ОбъектУчета
	|		И СтоимостьНМА.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = АмортизацияНМА.ОбъектУчета
	|		И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|	И (ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьВР, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияВР, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Частичное списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Ссылка                                                   КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                            КАК НематериальныйАктив,
	|
	|	ТаблицаНМА.Стоимость - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)         КАК Стоимость,
	|	ТаблицаНМА.СтоимостьРегл - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) КАК СтоимостьРегл,
	|	ТаблицаНМА.СтоимостьНУ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|	ТаблицаНМА.СтоимостьПР - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)     КАК СтоимостьПР,
	|	ТаблицаНМА.СтоимостьВР - ЕСТЬNULL(АмортизацияНМА.АмортизацияВР, 0)     КАК СтоимостьВР,
	|	ТаблицаНМА.СтоимостьЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ТаблицаНМА.СтоимостьНУЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ТаблицаНМА.СтоимостьПРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ТаблицаНМА.СтоимостьВРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета                         КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности                        КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов                                  КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов                               КАК АналитикаДоходов
	|ИЗ
	|	втСтоимостьСписанияНМА КАК ТаблицаНМА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК АмортизацияНМА
	|		ПО ТаблицаНМА.НематериальныйАктив = АмортизацияНМА.НематериальныйАктив
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|	И (ТаблицаНМА.Стоимость - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьРегл - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьНУ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьПР - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьВР - ЕСТЬNULL(АмортизацияНМА.АмортизацияВР, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьНУЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьПРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьВРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПорядокУчетаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаМестоУчетаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, ПолноеИмяОбъекта());
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаНМА.НомерСтроки                 КАК НомерСтроки,
		|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
		|
		|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
		|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
		|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
		|
		|	МестоУчетаНМА.Организация              КАК Организация,
		|	МестоУчетаНМА.Подразделение            КАК Подразделение,
		|
		|	НЕОПРЕДЕЛЕНО                           КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО                           КАК АналитикаДоходов,
		|
		|	ТаблицаНМА.СуммаСписанияУУ             КАК СуммаСписанияУУ,
		|	ТаблицаНМА.СуммаСписанияБУ             КАК СуммаСписанияБУ,
		|	ТаблицаНМА.СуммаСписанияНУ             КАК СуммаСписанияНУ,
		|	ТаблицаНМА.СуммаСписанияПР             КАК СуммаСписанияПР,
		|	ТаблицаНМА.СуммаСписанияВР             КАК СуммаСписанияВР
		|	
		|ПОМЕСТИТЬ втТаблицаНМА
		|ИЗ
		|	Документ.СписаниеНМА2_4.НМА КАК ТаблицаНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК ПорядокУчета
		|		ПО ТаблицаНМА.НематериальныйАктив = ПорядокУчета.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втМестоУчетаНМА КАК МестоУчетаНМА
		|		ПО ТаблицаНМА.НематериальныйАктив = МестоУчетаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииУУ
		|		ПО ТаблицаНМА.НематериальныйАктив = ПараметрыАмортизацииУУ.НематериальныйАктив
		|
		|ГДЕ
		|	ТаблицаНМА.Ссылка = &Ссылка";
	
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ПолноеИмяОбъекта()

	Возврат "Документ.СписаниеНМА2_4";

КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат СписаниеНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат СписаниеНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
