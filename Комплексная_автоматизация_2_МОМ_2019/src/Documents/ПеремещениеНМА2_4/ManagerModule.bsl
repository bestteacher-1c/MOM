#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровНМА2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ПеремещениеНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Перемещение НМА".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПеремещениеНМА2_4) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПеремещениеНМА2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПеремещениеНМА2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ПеремещениеНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.ПеремещениеНМА2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	ПеремещениеНМАЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДляДвижений = Новый Структура;
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПеремещениеНМА2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоОтправителю",  """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоПолучателю", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМестоУчетаНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры);
	
	ПеремещениеНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.Дата                            КАК Период,
	|	ДанныеДокумента.Номер                           КАК Номер,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.ОрганизацияПолучатель           КАК ОрганизацияПолучатель,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ДанныеДокумента.ПодразделениеПолучатель         КАК ПодразделениеПолучатель,
	|	ДанныеДокумента.Ответственный                   КАК Ответственный,
	|	ДанныеДокумента.Комментарий                     КАК Комментарий,
	|	ДанныеДокумента.Проведен                        КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления                 КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеСтоимостиНМАвДругуюОрганизацию)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперацияСтоимостьОтправитель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеСтоимостиНМАизДругойОрганизации)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперацияСтоимостьПолучатель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеАмортизацииНМАвДругуюОрганизацию)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперацияАмортизацияОтправитель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеАмортизацииНМАизДругойОрганизации)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперацияАмортизацияПолучатель,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы) КАК СтатьяАП_НМА,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК СтатьяАП_ПрибыльУбытки
	|ИЗ
	|	Документ.ПеремещениеНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("НазваниеДокумента", НСтр("ru='Перемещение НМА'"));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПеремещениеНМА2_4"));

	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		
		ВнеоборотныеАктивыСлужебный.УстановитьЗначенияПараметровПроведенияПриПеремещении(Реквизиты, ЗначенияПараметровПроведения);
		
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаПервоначальныеСведенияНМАСрезПоследних(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                                     КАК Регистратор,
	|	&Период                                                     КАК Период,
	|	&ОрганизацияПолучатель                                      КАК Организация,
	|	ПервоначальныеСведения.НематериальныйАктив                  КАК НематериальныйАктив,
	|	ПервоначальныеСведения.СпособПоступления                    КАК СпособПоступления,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьУУ            КАК ПервоначальнаяСтоимостьУУ,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьБУ            КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ            КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьПР            КАК ПервоначальнаяСтоимостьПР,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьВР            КАК ПервоначальнаяСтоимостьВР,
	|	ПервоначальныеСведения.МетодНачисленияАмортизацииБУ         КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведения.МетодНачисленияАмортизацииНУ         КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведения.Коэффициент                          КАК Коэффициент,
	|	ПервоначальныеСведения.АмортизацияДо2002                    КАК АмортизацияДо2002,
	|	ПервоначальныеСведения.АмортизацияДо2009                    КАК АмортизацияДо2009,
	|	ПервоначальныеСведения.СтоимостьДо2002                      КАК СтоимостьДо2002,
	|	ПервоначальныеСведения.ФактическийСрокИспользованияДо2009   КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведения.ДатаПриобретения                     КАК ДатаПриобретения,
	|	ПервоначальныеСведения.ДатаПринятияКУчетуУУ                 КАК ДатаПринятияКУчетуУУ,
	|	ПервоначальныеСведения.ДатаПринятияКУчетуБУ                 КАК ДатаПринятияКУчетуБУ,
	|	ПервоначальныеСведения.ДокументПринятияКУчетуУУ             КАК ДокументПринятияКУчетуУУ,
	|	ПервоначальныеСведения.ДокументПринятияКУчетуБУ             КАК ДокументПринятияКУчетуБУ,
	|	ПервоначальныеСведения.ПорядокУчетаНУ                       КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведения.ДокументСписания                     КАК ДокументСписания
	|ИЗ
	|	втПервоначальныеСведенияНМАСрезПоследних КАК ПервоначальныеСведения
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив   КАК НематериальныйАктив,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&Период                          КАК Дата,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	ЛОЖЬ                             КАК ДополнительнаяЗапись,
	|	&Проведен                        КАК Проведен,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете
	|ИЗ
	|	Документ.ПеремещениеНМА2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеНМА2_4.НМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив   КАК НематериальныйАктив,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель           КАК Организация,
	|	&ПодразделениеПолучатель         КАК Подразделение,
	|	&Период                          КАК Дата,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	ИСТИНА                           КАК ДополнительнаяЗапись,
	|	&Проведен                        КАК Проведен,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете
	|ИЗ
	|	Документ.ПеремещениеНМА2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеНМА2_4.НМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	&ИнформацияПоПолучателю КАК Дополнительно,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ДанныеДокумента.Дата   КАК ДатаОтраженияВУчете
	|ИЗ
	|	Документ.ПеремещениеНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	&ИдентификаторМетаданных,
	|	ДанныеДокумента.ОрганизацияПолучатель,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.ПодразделениеПолучатель,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ИСТИНА,
	|	&ИнформацияПоОтправителю,
	|	ДанныеДокумента.Дата,
	|	&НомерНаПечать,
	|	ДанныеДокумента.Дата
	|ИЗ
	|	Документ.ПеремещениеНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)
	|	И ДанныеДокумента.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаМестоУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестоУчетаНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка                        КАК Регистратор,
	|	&Период                        КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	&Организация                   КАК Организация,
	|	&ПодразделениеПолучатель       КАК Подразделение,
	|	ТаблицаНМА.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|	ТаблицаНМА.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Ссылка                        КАК Регистратор,
	|	&Период                        КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	&ОрганизацияПолучатель         КАК Организация,
	|	&ПодразделениеПолучатель       КАК Подразделение,
	|	ТаблицаНМА.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|	ТаблицаНМА.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка                                 КАК Регистратор,
	|	&Период                                 КАК Период,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	&ОрганизацияПолучатель                  КАК Организация,
	|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ТаблицаНМА.ОбъемНаработки               КАК ОбъемНаработки,
	|	ТаблицаНМА.ОтражатьВРеглУчете           КАК ОтражатьВРеглУчете,
	|	ТаблицаНМА.ОтражатьВУпрУчете            КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка                                 КАК Регистратор,
	|	&Период                                 КАК Период,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	&Организация                            КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету) КАК Состояние,
	|	ЛОЖЬ                                    КАК НачислятьАмортизациюУУ,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	ПорядокУчета.СпособОтраженияРасходов                 КАК СпособОтраженияРасходов,
	|	ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию     КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчета.ОрганизацияПолучательРасходов           КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	Документ.ПеремещениеНМА2_4.НМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И НематериальныйАктив В (ВЫБРАТЬ Т.НематериальныйАктив ИЗ ВтТаблицаНМА КАК Т)
	|			) КАК ПорядокУчета
	|		ПО
	|			ПорядокУчета.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Ссылка                                              КАК Регистратор,
	|	&Период                                              КАК Период,
	|	ТаблицаНМА.НематериальныйАктив                       КАК НематериальныйАктив,
	|	&ОрганизацияПолучатель                               КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету) КАК Состояние,
	|	ПорядокУчета.НачислятьАмортизациюУУ                  КАК НачислятьАмортизациюУУ,
	|	ПорядокУчета.СтатьяРасходов                          КАК СтатьяРасходов,
	|	ПорядокУчета.АналитикаРасходов                       КАК АналитикаРасходов,
	|	ПорядокУчета.СпособОтраженияРасходов                 КАК СпособОтраженияРасходов,
	|	ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию     КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчета.ОрганизацияПолучательРасходов           КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	Документ.ПеремещениеНМА2_4.НМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И НематериальныйАктив В (ВЫБРАТЬ Т.НематериальныйАктив ИЗ ВтТаблицаНМА КАК Т)
	|			) КАК ПорядокУчета
	|		ПО
	|			ПорядокУчета.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииНМАУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка                                          КАК Регистратор,
	|	&Период                                          КАК Период,
	|	ТаблицаНМА.НематериальныйАктив                   КАК НематериальныйАктив,
	|	&ОрганизацияПолучатель                           КАК Организация,
	|	ПараметрыАмортизации.СрокИспользования           КАК СрокИспользования,
	|	ПараметрыАмортизации.КоэффициентУскорения        КАК КоэффициентУскорения,
	|	ПараметрыАмортизации.МетодНачисленияАмортизации  КАК МетодНачисленияАмортизации,
	|	ПараметрыАмортизации.ЛиквидационнаяСтоимость     КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизации.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл
	|ИЗ
	|	Документ.ПеремещениеНМА2_4.НМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ПараметрыАмортизацииНМАУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И НематериальныйАктив В (ВЫБРАТЬ Т.НематериальныйАктив ИЗ ВтТаблицаНМА КАК Т)
	|			) КАК ПараметрыАмортизации
	|		ПО
	|			ПараметрыАмортизации.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Ссылка                                      КАК Регистратор,
	|	&Период                                      КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьОтправитель   КАК ХозяйственнаяОперация,
	|	&Организация                                 КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив               КАК НематериальныйАктив,
	|	&Подразделение                               КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК Контрагент,
	|	СтоимостьНМА.Стоимость                       КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРегл                   КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУ                     КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПР                     КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВР                     КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФ                     КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФ                   КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФ                   КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФ                   КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ЗалоговаяСтоимость              КАК ЗалоговаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимость        КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл    КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьНМА.РезервПереоценкиСтоимости       КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл   КАК РезервПереоценкиСтоимостиРегл,
	|	&ОрганизацияПолучатель                       КАК КорОрганизация,
	|	&ПодразделениеПолучатель                     КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорСтатьяРасходов,
	|	Неопределено                                 КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета            КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Ссылка                                      КАК Регистратор,
	|	&Период                                      КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьПолучатель    КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель                       КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив               КАК НематериальныйАктив,
	|	&ПодразделениеПолучатель                     КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК Контрагент,
	|	СтоимостьНМА.Стоимость                       КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРегл                   КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУ                     КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПР                     КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВР                     КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФ                     КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФ                   КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФ                   КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФ                   КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ЗалоговаяСтоимость              КАК ЗалоговаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимость        КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл    КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьНМА.РезервПереоценкиСтоимости       КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл   КАК РезервПереоценкиСтоимостиРегл,
	|	&Организация                                 КАК КорОрганизация,
	|	&Подразделение                               КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорСтатьяРасходов,
	|	Неопределено                                 КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета            КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)           КАК ВидДвижения,
	|	&Ссылка                                          КАК Регистратор,
	|	&Период                                          КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияОтправитель     КАК ХозяйственнаяОперация,
	|	&Организация                                     КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив                   КАК НематериальныйАктив,
	|	&Подразделение                                   КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                     КАК Контрагент,
	|	АмортизацияНМА.Амортизация                       КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл                   КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ                     КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР                     КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР                     КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ                     КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ                   КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ                   КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ                   КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|	&ОрганизацияПолучатель                           КАК КорОрганизация,
	|	&ПодразделениеПолучатель                         КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности               КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                     КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                     КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета                КАК КорГруппаФинансовогоУчета
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)           КАК ВидДвижения,
	|	&Ссылка                                          КАК Регистратор,
	|	&Период                                          КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияПолучатель      КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель                           КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив                   КАК НематериальныйАктив,
	|	&ПодразделениеПолучатель                         КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                     КАК Контрагент,
	|	АмортизацияНМА.Амортизация                       КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл                   КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ                     КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР                     КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР                     КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ                     КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ                   КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ                   КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ                   КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|	&Организация                                     КАК КорОрганизация,
	|	&Подразделение                                   КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности               КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                     КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                     КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета                КАК КорГруппаФинансовогоУчета
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"// Перемещение амортизации для отправителя
	|ВЫБРАТЬ
	|	&Период                            КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияОтправитель  КАК ХозяйственнаяОперация,
	|	&Организация                       КАК Организация,
	|
	|	&Подразделение                     КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&ПодразделениеПолучатель           КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК КорАналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&ОрганизацияПолучатель             КАК КорОрганизация,
	|
	|	АмортизацияНМА.Амортизация         КАК Сумма,
	|	АмортизацияНМА.Амортизация         КАК СуммаУпр,
	|	АмортизацияНМА.АмортизацияРегл + АмортизацияНМА.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение амортизации для получателя
	|ВЫБРАТЬ
	|	&Период                            КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияПолучатель КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель             КАК Организация,
	|
	|	&ПодразделениеПолучатель           КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                     КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК КорАналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                       КАК КорОрганизация,
	|
	|	АмортизацияНМА.Амортизация         КАК Сумма,
	|	АмортизацияНМА.Амортизация         КАК СуммаУпр,
	|	АмортизацияНМА.АмортизацияРегл + АмортизацияНМА.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости для отправителя
	|ВЫБРАТЬ
	|	&Период                            КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьОтправитель КАК ХозяйственнаяОперация,
	|	&Организация                       КАК Организация,
	|
	|	&Подразделение                     КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&ПодразделениеПолучатель           КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК КорАналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&ОрганизацияПолучатель             КАК КорОрганизация,
	|
	|	СтоимостьНМА.Стоимость             КАК Сумма,
	|	СтоимостьНМА.Стоимость             КАК СуммаУпр,
	|	СтоимостьНМА.СтоимостьРегл + СтоимостьНМА.СтоимостьЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости для получателя
	|ВЫБРАТЬ
	|	&Период                            КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьПолучатель КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель             КАК Организация,
	|
	|	&ПодразделениеПолучатель           КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                     КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив     КАК КорАналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                       КАК КорОрганизация,
	|
	|	СтоимостьНМА.Стоимость             КАК Сумма,
	|	СтоимостьНМА.Стоимость             КАК СуммаУпр,
	|	СтоимостьНМА.СтоимостьРегл + СтоимостьНМА.СтоимостьЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеНМАвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПервоначальныеСведенияНМАСрезПоследних(ТекстыЗапроса)

	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса("втПервоначальныеСведенияНМАСрезПоследних", ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли; 
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокНМА(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПервоначальныеСведения.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьПР КАК ПервоначальнаяСтоимостьПР,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьВР КАК ПервоначальнаяСтоимостьВР,
	|	ПервоначальныеСведения.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведения.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведения.Коэффициент КАК Коэффициент,
	|	ПервоначальныеСведения.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведения.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведения.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведения.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведения.ДатаПриобретения КАК ДатаПриобретения,
	|	ПервоначальныеСведения.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
	|	ПервоначальныеСведения.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
	|	ПервоначальныеСведения.ДокументПринятияКУчетуУУ КАК ДокументПринятияКУчетуУУ,
	|	ПервоначальныеСведения.ДокументПринятияКУчетуБУ КАК ДокументПринятияКУчетуБУ,
	|	ПервоначальныеСведения.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведения.ДокументСписания КАК ДокументСписания,
	|	ПервоначальныеСведения.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ втПервоначальныеСведенияНМАСрезПоследних
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						втСписокНМА КАК СписокНМА)) КАК ПервоначальныеСведения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "втПервоначальныеСведенияНМАСрезПоследних");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолноеИмяОбъекта()

	Возврат "Документ.ПеремещениеНМА2_4";

КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ПеремещениеНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ПеремещениеНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ПеремещениеНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
