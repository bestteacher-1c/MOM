
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.РеализацияУслугПрочихАктивов.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ПодготовкаКПередачеОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Подготовка к передаче ОС".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПодготовкаКПередачеОС2_4) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПодготовкаКПередачеОС2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПодготовкаКПередачеОС2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	
	
	ПодготовкаКПередачеОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.ПеремещениеОС2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	Запрос.УстановитьПараметр("ЧастичнаяЛиквидация", Ложь);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	ТаблицыДляДвижений = Новый Структура;
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПодготовкаКПередачеОС2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
	
	ПодготовкаКПередачеОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПодготовкаКПередачеОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС)   КАК ХозяйственнаяОперацияСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеАмортизацииОС) КАК ХозяйственнаяОперацияАмортизация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиСтоимостиОС)   КАК ХозяйственнаяОперацияРезервСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиАмортизацииОС) КАК ХозяйственнаяОперацияРезервАмортизация,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства) КАК СтатьяАП_ОС,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК СтатьяАП_ПрибыльУбытки,
	|	ДанныеДокумента.Ответственный          КАК Ответственный,
	|	ДанныеДокумента.Комментарий            КАК Комментарий,
	|	ДанныеДокумента.Проведен               КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления        КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете
	|ИЗ
	|	Документ.ПодготовкаКПередачеОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КонецДня", Новый Граница(КонецДня(Реквизиты.Период),ВидГраницы.Включая));
	
	СпособыНачисленияПоЕНАОФ = Новый Массив;
	СпособыНачисленияПоЕНАОФ.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПоЕНАОФ);
	СпособыНачисленияПоЕНАОФ.Добавить(Перечисления.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега);
	
	Запрос.УстановитьПараметр("НачисленияПоЕНАОФ", СпособыНачисленияПоЕНАОФ);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПодготовкаКПередачеОС2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Подготовка к передаче ОС'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПодготовкаКПередачеОС);
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПодготовкаКПередачеОС2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                         КАК Ссылка,
	|	&Период                         КАК ДатаДокументаИБ,
	|	&Номер                          КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных        КАК ТипСсылки,
	|	&Организация                    КАК Организация,
	|	&ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                    КАК НаправлениеДеятельности,
	|	&Подразделение                  КАК Подразделение,
	|	&Ответственный                  КАК Ответственный,
	|	&Комментарий                    КАК Комментарий,
	|	&Проведен                       КАК Проведен,
	|	&ПометкаУдаления                КАК ПометкаУдаления,
	|	ЛОЖЬ                            КАК ДополнительнаяЗапись,
	|	&Период                         КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                  КАК НомерПервичногоДокумента,
	|	&Период   КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимостьУУ,
	|	ПервоначальныеСведенияОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	&Ссылка КАК ДокументСнятияСУчета,
	|	ПервоначальныеСведенияОС.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования
	|ИЗ
	|	Документ.ПодготовкаКПередачеОС2_4.ОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|		&КонецДня,
	|		Регистратор <> &Ссылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)
	|	) КАК ПервоначальныеСведенияОС
	|	ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|		И ПервоначальныеСведенияОС.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ                       КАК НачислятьАмортизациюУУ
	|ИЗ
	|	Документ.ПодготовкаКПередачеОС2_4.ОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|		&КонецДня,
	|		Регистратор <> &Ссылка 
	|		И (ОсновноеСредство, Организация) В (ВЫБРАТЬ СписокОС.ОсновноеСредство, СписокОС.Организация ИЗ втСписокОС КАК СписокОС)) КАК ПорядокУчета
	|	ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Ссылка                                КАК Регистратор,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	ТаблицаОС.Амортизация                  КАК Амортизация,
	|	ТаблицаОС.АмортизацияРегл              КАК АмортизацияРегл,
	|	ТаблицаОС.АмортизацияНУ                КАК АмортизацияНУ,
	|	ТаблицаОС.АмортизацияПР                КАК АмортизацияПР,
	|	ТаблицаОС.АмортизацияВР                КАК АмортизацияВР,
	|	ТаблицаОС.АмортизацияЦФ                КАК АмортизацияЦФ,
	|	ТаблицаОС.АмортизацияНУЦФ              КАК АмортизацияНУЦФ,
	|	ТаблицаОС.АмортизацияПРЦФ              КАК АмортизацияПРЦФ,
	|	ТаблицаОС.АмортизацияВРЦФ              КАК АмортизацияВРЦФ,
	|	0                                      КАК РезервПереоценкиАмортизации,
	|	0                                      КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация      КАК ХозяйственнаяОперация,
	|	ТаблицаОС.НачислятьИзнос               КАК НачислятьИзнос
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Ссылка                                 КАК Регистратор,
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	0                                       КАК Амортизация,
	|	0                                       КАК АмортизацияРегл,
	|	0                                       КАК АмортизацияНУ,
	|	0                                       КАК АмортизацияПР,
	|	0                                       КАК АмортизацияВР,
	|	0                                       КАК АмортизацияЦФ,
	|	0                                       КАК АмортизацияНУЦФ,
	|	0                                       КАК АмортизацияПРЦФ,
	|	0                                       КАК АмортизацияВРЦФ,
	|	ТаблицаОС.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	ТаблицаОС.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
	|	ТаблицаОС.НачислятьИзнос                КАК НачислятьИзнос
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	(ТаблицаОС.РезервПереоценкиАмортизации <> 0
	|		ИЛИ ТаблицаОС.РезервПереоценкиАмортизацииРегл <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание первоначальной стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	ТаблицаОС.Стоимость                    КАК Стоимость,
	|	ТаблицаОС.СтоимостьРегл                КАК СтоимостьРегл,
	|	ТаблицаОС.СтоимостьНУ                  КАК СтоимостьНУ,
	|	ТаблицаОС.СтоимостьПР                  КАК СтоимостьПР,
	|	ТаблицаОС.СтоимостьВР                  КАК СтоимостьВР,
	|	ТаблицаОС.СтоимостьЦФ                  КАК СтоимостьЦФ,
	|	ТаблицаОС.СтоимостьНУЦФ                КАК СтоимостьНУЦФ,
	|	ТаблицаОС.СтоимостьПРЦФ                КАК СтоимостьПРЦФ,
	|	ТаблицаОС.СтоимостьВРЦФ                КАК СтоимостьВРЦФ,
	|	ТаблицаОС.АмортизационнаяПремия        КАК АмортизационнаяПремия,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	СтоимостьСписания.ОсновноеСредство     КАК ОсновноеСредство,
	|
	|	СтоимостьСписания.Стоимость 
	|		- ЕСТЬNULL(АмортизацияСписания.Амортизация, 0) КАК Стоимость,
	|
	|	СтоимостьСписания.СтоимостьРегл
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияРегл, 0)
	|		КОНЕЦ КАК СтоимостьРегл,
	|
	|	СтоимостьСписания.СтоимостьНУ
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияНУ, 0) 
	|		КОНЕЦ                  КАК СтоимостьНУ,
	|
	|	СтоимостьСписания.СтоимостьПР
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияПР, 0) 
	|		КОНЕЦ                  КАК СтоимостьПР,
	|
	|	СтоимостьСписания.СтоимостьВР
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияВР, 0) 
	|		КОНЕЦ                  КАК СтоимостьВР,
	|
	|	СтоимостьСписания.СтоимостьЦФ
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияЦФ, 0) 
	|		КОНЕЦ                  КАК СтоимостьЦФ,
	|
	|	СтоимостьСписания.СтоимостьНУЦФ
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияНУЦФ, 0) 
	|		КОНЕЦ                КАК СтоимостьНУЦФ,
	|
	|	СтоимостьСписания.СтоимостьПРЦФ
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияПРЦФ, 0) 
	|		КОНЕЦ                КАК СтоимостьПРЦФ,
	|
	|	СтоимостьСписания.СтоимостьВРЦФ
	|		- ВЫБОР КОГДА АмортизацияСписания.НачислятьИзнос ТОГДА 0 
	|				ИНАЧЕ ЕСТЬNULL(АмортизацияСписания.АмортизацияВРЦФ, 0) 
	|		КОНЕЦ                КАК СтоимостьВРЦФ,
	|
	|	0                                      КАК АмортизационнаяПремия,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	СтоимостьСписания.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	СтоимостьСписания.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втСтоимостьСписанияОС КАК СтоимостьСписания
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияОС КАК АмортизацияСписания
	|		ПО АмортизацияСписания.ОсновноеСредство = СтоимостьСписания.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	0                                      КАК Стоимость,
	|	0                                      КАК СтоимостьРегл,
	|	0                                      КАК СтоимостьНУ,
	|	0                                      КАК СтоимостьПР,
	|	0                                      КАК СтоимостьВР,
	|	0                                      КАК СтоимостьЦФ,
	|	0                                      КАК СтоимостьНУЦФ,
	|	0                                      КАК СтоимостьПРЦФ,
	|	0                                      КАК СтоимостьВРЦФ,
	|	0                                      КАК АмортизационнаяПремия,
	|	ТаблицаОС.РезервПереоценкиСтоимости     КАК РезервПереоценкиСтоимости,
	|	ТаблицаОС.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияРезервСтоимость  КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	(ТаблицаОС.РезервПереоценкиСтоимости <> 0
	|		ИЛИ ТаблицаОС.РезервПереоценкиСтоимостиРегл <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание Стоимости
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперация            КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаОС.Стоимость               КАК Сумма,
	|	ТаблицаОС.Стоимость               КАК СуммаУпр,
	|	ТаблицаОС.СтоимостьРегл + ТаблицаОС.СтоимостьЦФ КАК СуммаРегл
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание амортизации
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияАмортизация КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаОС.Амортизация             КАК Сумма,
	|	ТаблицаОС.Амортизация             КАК СуммаУпр,
	|	ТаблицаОС.АмортизацияРегл + ТаблицаОС.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиАмортизации = 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Признание доходов от целевого финансирования
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияСтоимость   КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаОС.СтатьяДоходов           КАК Статья,
	|	ТаблицаОС.АналитикаДоходов        КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	ВЫБОР КОГДА ТаблицаОС.СтоимостьЦФ = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаОС.Стоимость * ТаблицаОС.СтоимостьРегл / ТаблицаОС.СтоимостьЦФ
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ТаблицаОС.СтоимостьЦФ = 0
	|		ТОГДА 1
	|		ИНАЧЕ ТаблицаОС.Стоимость * ТаблицаОС.СтоимостьРегл / ТаблицаОС.СтоимостьЦФ
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаОС.СтоимостьЦФ             КАК СуммаРегл
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.СтоимостьЦФ <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание резерва переоценки амортизации ОС
	|ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&ХозяйственнаяОперацияРезервАмортизация КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|
	|	&Подразделение                          КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                            КАК Статья,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство              КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                          КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбытки                 КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета        КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаОС.РезервПереоценкиАмортизации   КАК Сумма,
	|	ТаблицаОС.РезервПереоценкиАмортизации   КАК СуммаУпр,
	|	0                                       КАК СуммаРегл
	|ИЗ
	|	втАмортизацияСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиАмортизации <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание резерва переоценки стоимости ОС
	|ВЫБРАТЬ
	|	&Период                               КАК Период,
	|	&ХозяйственнаяОперацияРезервСтоимость КАК ХозяйственнаяОперация,
	|	&Организация                          КАК Организация,
	|
	|	&Подразделение                        КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&СтатьяАП_ПрибыльУбытки               КАК Статья,
	|	НЕОПРЕДЕЛЕНО                          КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                          КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                          КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                        КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                          КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                          КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                          КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство            КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
	|
	|	ТаблицаОС.РезервПереоценкиСтоимости   КАК Сумма,
	|	ТаблицаОС.РезервПереоценкиСтоимости   КАК СуммаУпр,
	|	0                                     КАК СуммаРегл
	|ИЗ
	|	втСтоимостьСписанияОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.РезервПереоценкиСтоимости <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСтоимостьСписанияОС(ТекстыЗапроса)

	ИмяТаблицы = "втСтоимостьСписанияОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	&Ссылка                               КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство            КАК ОсновноеСредство,
	|
	|	СтоимостьОС.Стоимость                 КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл             КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ               КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР               КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР               КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ               КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ             КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ             КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ             КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремия     КАК АмортизационнаяПремия,
	|	СтоимостьОС.РезервПереоценкиСтоимости     КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ТаблицаОС.НачислятьИзнос              КАК НачислятьИзнос,
	|	ТаблицаОС.СтатьяДоходов               КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов            КАК АналитикаДоходов
	|
	|ПОМЕСТИТЬ втСтоимостьСписанияОС
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОбъектУчета
	|		И СтоимостьОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтАмортизацияСписанияОС(ТекстыЗапроса)

	ИмяТаблицы = "втАмортизацияСписанияОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание амортизации
	|ВЫБРАТЬ
	|	&Ссылка                                   КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство                КАК ОсновноеСредство,
	|
	|	АмортизацияОС.Амортизация                 КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации     КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	&ХозяйственнаяОперацияАмортизация         КАК ХозяйственнаяОперация,
	|	ТаблицаОС.НачислятьИзнос                  КАК НачислятьИзнос
	|
	|ПОМЕСТИТЬ втАмортизацияСписанияОС
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса)

	ИмяТаблицы = "втОстаточнаяСтоимостьОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицавтТаблицаОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"//Списание остаточной стоимости
	|ВЫБРАТЬ
	|	&Ссылка                                                   КАК Ссылка,
	|	ТаблицаОС.ОсновноеСредство                                КАК ОсновноеСредство,
	|
	|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)         КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) КАК СтоимостьРегл,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0)     КАК СтоимостьПР,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьВР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВР, 0)     КАК СтоимостьВР,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ЕСТЬNULL(СтоимостьОС.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|	ЕСТЬNULL(СтоимостьОС.АмортизационнаяПремия, 0)            КАК АмортизационнаяПремия,
	|	0                                                         КАК РезервПереоценкиСтоимости,
	|	
	|	ТаблицаОС.ГруппаФинансовогоУчета                          КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|	ТаблицаОС.СтатьяДоходов                                   КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов                                КАК АналитикаДоходов
	|
	|ПОМЕСТИТЬ втОстаточнаяСтоимостьОС
	|ИЗ
	|	втТаблицаОС КАК ТаблицаОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОбъектУчета
	|		И СтоимостьОС.Ссылка = &Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО ТаблицаОС.ОсновноеСредство = АмортизацияОС.ОбъектУчета
	|		И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	(ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьВР, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВР, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияВРЦФ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ПодготовкаКПередачеОСЛокализация.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
		|
		|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
		|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
		|	ПорядокУчета.ПоказательНаработки       КАК ПоказательНаработки,
		|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
		|
		|	ЛОЖЬ КАК НачислятьИзнос,
		|
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов
		|	
		|ПОМЕСТИТЬ втТаблицаОС
		|ИЗ
		|	Документ.ПодготовкаКПередачеОС2_4.ОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчета
		|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Ссылка";
		
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Функция ПолноеИмяОбъекта()

	Возврат "Документ.ПодготовкаКПередачеОС2_4";

КонецФункции

#КонецОбласти

#Область ПроведениеРеглУчет

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ПодготовкаКПередачеОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ПодготовкаКПередачеОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ПодготовкаКПередачеОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт

	Возврат ПодготовкаКПередачеОСЛокализация.ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов);

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
