//////////////////////////////////////////////////////////////////////////////////////////////
// УчетнаяПолитикаПереопределяемый: переопределяемый механизм отвечающий за получение данных учетной политики.
//  
//////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбщиеПараметрыУчетнойПолитики

Функция Существует(Организация, Период, ВыводитьСообщениеОбОтсутствииУчетнойПолитики = Ложь, ДокументСсылка = Неопределено) Экспорт

	Возврат Истина;

КонецФункции

//++ НЕ УТ

// Возвращает признак, является ли организация плательщиком налога на прибыль.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется необходимость оплаты налога на прибыль;
//	Период - Дата - дата, на начало месяца которой определяется необходимость оплаты налога на прибыль.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату является плательщиком налога на прибыль. В противном случае - ложь.
//
Функция ПлательщикНалогаНаПрибыль(Организация, Период) Экспорт
	
	Результат = ПолучитьФункциональнуюОпцию("ПлательщикНалогаНаПрибыль", 
		Новый Структура("Организация, Период", Организация, НачалоМесяца(Период)));
	
	Возврат Результат;
	
КонецФункции 

// Возвращает признак необходимости применения для организации ПБУ18.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется необходимость применения ПБУ18;
//	Период - Дата - дата, на начало месяца которой определяется необходимость применения ПБУ18.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату применяет ПБУ18. В противном случае - ложь.
//
Функция ПоддержкаПБУ18(Организация, Период) Экспорт
	
	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(Организация, НачалоМесяца(Период));
	Результат = ПараметрыУчетнойПолитики.ПрименяетсяПБУ18;
	
	Возврат Результат;
	
КонецФункции 

// Заполняет структуру признаков, относящихся к применению ПБУ 18 для организации.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется необходимость применения ПБУ18;
//	Период - Дата - дата, на начало месяца которой определяется необходимость применения ПБУ18.
//	ПараметрыПБУ18 - Структура - см. УчетнаяПолитика.ПараметрыПБУ18()
//
Процедура ЗаполнитьПараметрыПБУ18(Организация, Период, ПараметрыПБУ18) Экспорт
	
	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(Организация, НачалоМесяца(Период));
	
	ЗаполнитьЗначенияСвойств(ПараметрыПБУ18, ПараметрыУчетнойПолитики);
	
КонецПроцедуры

// Возвращает признак ведения учета постоянных разниц.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак;
//	Период - Дата - дата, на начало месяца которой определяется признак.
//
// Возвращаемое значение:
//	Булево - Истина, если организация ведет учет постоянных разниц.
//
Функция ВедетсяУчетПостоянныхИВременныхРазниц(Организация, Период) Экспорт
	
	ПараметрыФункциональнойОпции = Новый Структура;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФункциональнойОпции.Вставить("Организация", Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Период) Тогда
		ПериодКеша = НачалоМесяца(Период);
		ПараметрыФункциональнойОпции.Вставить("Период", ПериодКеша);
	КонецЕсли;
	
	Результат = ПолучитьФункциональнуюОпцию("ВедетсяУчетПостоянныхИВременныхРазниц", ПараметрыФункциональнойОпции);
	
	Возврат Результат;
	
КонецФункции

// Возвращает метод начисления амортизации (Линейный, Нелинейный, ПоЕНАОФ).
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется метод начисления;
//	Период - Дата - дата, на которую определяется метод начисления.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.МетодыНачисленияАмортизации - метод начисления амортизации налогового учета, применяемый для данной организации на указанную дату.
//	Значение по умолчанию: Линейный.
//
Функция МетодНачисленияАмортизацииНУ(Организация, Период) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЕСТЬNULL(Т.УчетнаяПолитика.МетодНачисленияАмортизацииНУ, ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)) КАК МетодНачисленияАмортизацииНУ
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Период, Организация = &Организация) КАК Т");
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Новый Граница(Период, ВидГраницы.Включая));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.МетодНачисленияАмортизацииНУ;
		
	КонецЕсли;
	
	Возврат Перечисления.МетодыНачисленияАмортизации.Линейный;
	
КонецФункции

// Возвращает базу распределения косвенных расходов: доходы от реализации или доходы от реализации и внереализационные.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется база распределения;
//	Период - Дата - дата, на начало месяца которой определяется база распределения.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.БазыРаспределенияКосвенныхРасходовПоВидамДеятельности - база распределения косвенных расходов, применяемая для данной организации на указанную дату.
//	Значение по умолчанию: Доходы от реализации.
//
Функция БазаРаспределенияКосвенныхРасходовПоВидамДеятельности(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(
		Организация,
		НачалоМесяца(Период));
	Если ПараметрыУчетнойПолитики <> Неопределено Тогда
		Результат = ПараметрыУчетнойПолитики.БазаРаспределенияКосвенныхРасходовПоВидамДеятельности;
	Иначе
		Результат = Перечисления.БазыРаспределенияКосвенныхРасходовПоВидамДеятельности.ДоходыОтРеализации;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//-- НЕ УТ

// Возвращает систему налогообложения применяемую организацией: общая или упрощенная.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется система налогообложения;
//	Период - Дата - дата, на начало месяца которой определяется система налогообложения.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.СистемыНалогообложения - система налогообложения, применяемая для данной организации на указанную дату.
//	Значение по умолчанию: Общая.
//
Функция СистемаНалогообложения(Организация, Период) Экспорт

	Если ПрименяетсяУСН(Организация, Период) Тогда
		Возврат Перечисления.СистемыНалогообложения.Упрощенная;
	Иначе
		Возврат Перечисления.СистемыНалогообложения.Общая;
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область ПараметрыУчетнойПолитикиПоНДС

// Параметры учетной политики по НДС

// Возвращает признак, является ли организация плательщиком налога на добавленную стоимость (НДС).
// Определяется на основании того, применяет ли организация упрощенную систему налогообложения. Если применяет - то не является плательщиком НДС.
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак оплаты НДС;
//	Период - Дата - дата, на начало месяца которой определяется необходимость оплаты НДС.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату является плательщиком налога на добавленную стоимость. В противном случае - ложь.
//
Функция ПлательщикНДС(Организация, Период) Экспорт

	Возврат НЕ ПрименяетсяУСН(Организация, Период);

КонецФункции 

// Возвращает признак освобождения организации от уплаты НДС.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак освобождения от уплаты НДС;
//	Период - Дата - дата, на которую определяется признак освобождения от уплаты НДС.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату освобождается от уплаты НДС. Всегда возвращается Ложь.
//
Функция ПрименяетсяОсвобождениеОтУплатыНДС(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(
		Организация,
		НачалоМесяца(Период));
	Результат = Не ПараметрыУчетнойПолитики = Неопределено И ПараметрыУчетнойПолитики.ПрименяетсяОсвобождениеОтУплатыНДС;

	Возврат Результат;

КонецФункции

//++ НЕ УТ

// Возвращает признак учета 5% порога выручки не облагаемой НДС от общей выручки для организации на начало месяца указанной даты.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак учета 5% порога;
//	Период - Дата - дата, на которую определяется признак учета 5% порога.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату учитывает 5% порог. В противном случае - ложь.
//
Функция Учитывать5ПроцентныйПорог(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(
		Организация,
		НачалоМесяца(Период));
	Результат = Не ПараметрыУчетнойПолитики = Неопределено И ПараметрыУчетнойПолитики.Учитывать5ПроцентныйПорог;

	Возврат Результат;

КонецФункции

//-- НЕ УТ

// Возвращает правило отбора авансов для регистрации счетов-фактур
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой необходимо определить правило отбора авансов
//	Период - Дата - дата, на которую определяется правило отбора авансов.
//
// Возвращаемое значение:
//	Результат - ПеречислениеСсылка.ПорядокРегистрацииСчетовФактурНаАванс - Правило отбора авансов
//
Функция ПравилоОтбораАвансовДляРегистрацииСчетовФактур(Организация, Период) Экспорт
	
	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(
		Организация,
		НачалоМесяца(Период));
		
	Если ПараметрыУчетнойПолитики = Неопределено Тогда
		Результат = Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.КромеЗачтенныхВТечениеДня; 
	Иначе 
		Результат = ПараметрыУчетнойПолитики.ПравилоОтбораАвансовДляРегистрацииСчетовФактур;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

//++ НЕ УТ 

#Область ПараметрыУчетнойПолитикиПоЕНВД

// Параметры учетной политики по ЕНВД

// Возвращает признак, применяет ли организация единый налог на вмененный доход (ЕНВД).
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется применяет ли она ЕНВД;
//	Период - Дата - дата, на которую определяется применение ЕНВД.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату является плательщиком единого налога на вмененный доход. В противном случае - ложь.
//
Функция ПлательщикЕНВД(Организация, Период) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизации.ПлательщикЕНВД КАК ПлательщикЕНВД
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаОрганизации");
	
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Возврат РезультатЗапроса.Выгрузить()[0].ПлательщикЕНВД;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

// Определяет, являлась ли организация плательщиком единого налога на вмененный доход (ЕНВД) в течении указанного периода.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется применение ЕНВД;
//	НачалоПериода - Дата - начало периода, для которого определяется применение организацией ЕНВД;
//	КонецПериода - Дата - конец периода, для которого определяется применение организацией ЕНВД.
//
// Возвращаемое значение:
//	Булево - Истина, если организация в течении указанного периода являлась плательщиком ЕНВД (хотя бы раз). В противном случае - ложь.
//
Функция ПлательщикЕНВДЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт

	МассивЗначений = ЗначенияРесурсаУчетнойПолитикиЗаПериод(
		"ПлательщикЕНВД", Организация, НачалоПериода, КонецПериода);
	
	Результат = Ложь;
	Для Каждого Значение Из МассивЗначений Цикл
		Результат = Результат ИЛИ Значение;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

//-- НЕ УТ

#Область ПараметрыУчетнойПолитикиПоУСН

// Параметры учетной политики по УСН

// Возвращает признак применения для организации упрошенной системы налогообложения.
// Параметры:
//	Организация - Массив, СправочникСсылка.Организации - организация или массив организаций, для которых необходимо определить применение УСН;
//	Период - Дата - дата, на начало месяца которой определяется признак применения УСН.
//
// Возвращаемое значение:
//	Булево - Истина, если для организации (или если для хотя бы одной организации из списка) на дату
//				применяется упрощенная система налогообложения. В противном случае - ложь.
//
Функция ПрименяетсяУСН(Знач Организация, Период) Экспорт

	Если ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		Организация = Организация.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("Массив") И Организация.Количество() = 1 Тогда
		 Организация = Организация.Получить(0);
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		Результат = ПолучитьФункциональнуюОпцию("ПрименятьУСН", Новый Структура("Организация, Период", Организация, НачалоМесяца(Период)));
	ИначеЕсли ТипЗнч(Организация) = Тип("Массив") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
		|			&Период,
		|			(Организация В (&СписокОрганизаций)
		|				ИЛИ &ВсеОрганизации)
		|				И ПрименяетсяУСН) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
		Запрос.УстановитьПараметр("Период", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", Организация);
		Запрос.УстановитьПараметр("ВсеОрганизации", Организация.Количество() = 0);
		
		РезультатЗапроса = Запрос.Выполнить();
		Результат = Не РезультатЗапроса.Пустой();
		
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак применения объекта налогообложения "Доходы, уменьшенные на величину расходов",
//	который используется для организации на упрощенной системе налогообложения.
// Параметры:
//	Организация - Массив, СправочникСсылка.Организации - организация или массив организаций,
//					для которых необходимо определить применение объекта налогообложения "ДоходыМинусРасходы";
//	Период - Дата - дата, на которую анализируется учетная политика организации.
//
// Возвращаемое значение:
//	Булево - Истина, если для организации (или если для хотя бы одной организации из списка) на дату
//				применяется объект налогообложения "ДоходыМинусРасходы". В противном случае - ложь.
//
Функция ПрименяетсяУСНДоходыМинусРасходы(Знач Организация, Период) Экспорт

	Если ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		Организация = Организация.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("Массив") И Организация.Количество() = 1 Тогда
		 Организация = Организация.Получить(0);
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		Результат = ПолучитьФункциональнуюОпцию("ПрименяетсяУСНДоходыМинусРасходы", Новый Структура("Организация, Период", Организация, НачалоМесяца(Период)));
	ИначеЕсли ТипЗнч(Организация) = Тип("Массив") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
		|			&Период,
		|			(Организация В (&СписокОрганизаций)
		|				ИЛИ &ВсеОрганизации)
		|				И ПрименяетсяУСН
		|				И ПрименяетсяУСНДоходыМинусРасходы) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
		Запрос.УстановитьПараметр("Период", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", Организация);
		Запрос.УстановитьПараметр("ВсеОрганизации", Организация.Количество() = 0);
		
		РезультатЗапроса = Запрос.Выполнить();
		Результат = Не РезультатЗапроса.Пустой();
		
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

// Возвращает признак применения объекта налогообложения "Доходы",
//	который используется для организации на упрощенной системе налогообложения.
// Параметры:
//	Организация - Массив, СправочникСсылка.Организации - организация или массив организаций,
//					для которых необходимо определить применение объекта налогообложения "Доходы";
//	Период - Дата - дата, на которую анализируется учетная политика организации.
//
// Возвращаемое значение:
//	Булево - Истина, если для организации (или если для хотя бы одной организации из списка) на дату
//				применяется объект налогообложения "Доходы". В противном случае - ложь.
//
Функция ПрименяетсяУСНДоходы(Знач Организация, Период) Экспорт

	Если ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		Организация = Организация.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("Массив") И Организация.Количество() = 1 Тогда
		 Организация = Организация.Получить(0);
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		ПараметрыФО = Новый Структура("Организация, Период", Организация, НачалоМесяца(Период));
		Результат = ПолучитьФункциональнуюОпцию("ПрименятьУСН", ПараметрыФО) И Не ПолучитьФункциональнуюОпцию("ПрименяетсяУСНДоходыМинусРасходы", ПараметрыФО);
	ИначеЕсли ТипЗнч(Организация) = Тип("Массив") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
		|			&Период,
		|			(Организация В (&СписокОрганизаций)
		|				ИЛИ &ВсеОрганизации)
		|				И ПрименяетсяУСН
		|				И Не ПрименяетсяУСНДоходыМинусРасходы) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
		Запрос.УстановитьПараметр("Период", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", Организация);
		Запрос.УстановитьПараметр("ВсеОрганизации", Организация.Количество() = 0);
		
		РезультатЗапроса = Запрос.Выполнить();
		Результат = Не РезультатЗапроса.Пустой();
		
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Возвращает массив организаций, находящихся на УСН за определенный период.
// Параметры:
//	НачалоПериода - Дата - дата, на начало месяца которой определяется список организаций на УСН;
//	КонецПериода - Дата - дата, на конец месяца которой определяется список организаций на УСН.
//
// Возвращаемое значение:
//	Массив - СправочникСсылка.Организации - все организации, применяющие УСН в течении указанного периода.
//
Функция ОрганизацииНаУСНЗаПериод(НачалоПериода, КонецПериода) Экспорт
	
	СледующийМесяц = НачалоМесяца(ДобавитьМесяц(НачалоПериода, 1));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("СледующийМесяц",   СледующийМесяц);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(КонецПериода));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&НачалоПериода, ПрименяетсяУСН И НЕ Организация.Ссылка ЕСТЬ NULL) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
	
	Если СледующийМесяц < КонецМесяца(КонецПериода) Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|"+
		"ВЫБРАТЬ
		|	УчетнаяПолитикаОрганизаций.Организация
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
		|ГДЕ
		|	УчетнаяПолитикаОрганизаций.ПрименяетсяУСН
		|	И НЕ Организация.Ссылка ЕСТЬ NULL
		|	И УчетнаяПолитикаОрганизаций.Период МЕЖДУ &СледующийМесяц И &ОкончаниеПериода";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;
	
КонецФункции

// Возвращает признак применения для организации упрошенной системы налогообложения за определенный период.
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой необходимо определить применение УСН с объектом налогообложения "Доходы минус расходы";
//	НачалоПериода - Дата - дата, на начало месяца которой определяется признак применения УСН с объектом налогообложения "Доходы минус расходы";
//	КонецПериода - Дата - дата, на конец месяца которой определяется признак применения УСН с объектом налогообложения "Доходы минус расходы";
//	ОбъектНалогообложения - ПеречислениеСсылка.ОбъектыНалогообложенияПоУСН - ссылка на объект налогообложения, если требуется получить признак
//		применения конкретного объекта налогообложения, если не заполнен - получает признак непосредственного применения УСН.
//
// Возвращаемое значение:
//	Булево - Истина, если для организации в какой-либо из отрезков указанного периода применялась упрощенная система налогообложения с объектом налогообложения "Доходы минус расходы".
//				В противном случае - ложь.
//
Функция ПрименяетсяУСНЗаПериод(Организация, НачалоПериода, КонецПериода, ОбъектНалогообложения = Неопределено) Экспорт

	СледующийМесяц = НачалоМесяца(ДобавитьМесяц(НачалоПериода, 1));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("СледующийМесяц",   СледующийМесяц);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(КонецПериода));
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Данные.ПрименяетсяУСН И &УсловиеНаОбъектНалогообложения КАК Значение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК Данные";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	Если СледующийМесяц < КонецМесяца(КонецПериода) Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Данные.ПрименяетсяУСН И &УсловиеНаОбъектНалогообложения
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК Данные
		|ГДЕ
		|	Данные.Организация = &Организация
		|	И Данные.Период МЕЖДУ &СледующийМесяц И &ОкончаниеПериода";
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	УсловиеНаОбъектНалогообложения = "ЛОЖЬ";
	Если ОбъектНалогообложения = Неопределено Тогда
		УсловиеНаОбъектНалогообложения = "ИСТИНА";
	ИначеЕсли ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы Тогда
		УсловиеНаОбъектНалогообложения = "Данные.ПрименяетсяУСНДоходыМинусРасходы";
	ИначеЕсли ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда
		УсловиеНаОбъектНалогообложения = "НЕ Данные.ПрименяетсяУСНДоходыМинусРасходы";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаОбъектНалогообложения", УсловиеНаОбъектНалогообложения);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");
	
	Результат = Ложь;
	Для Каждого Значение Из РезультатЗапроса Цикл
		Результат = Результат ИЛИ Значение;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

//++ НЕ УТ

// Определяет ставку налога УСН для организации на конкретную дату.
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, для которой получается ставка налога УСН;
//	Период - Дата - дата, на начало которой определяется ставка налога УСН.
//
// Возвращаемое значение:
//	Число - ставка налога УСН, указанная для учетной политики организации на переданную дату.
//
Функция СтавкаНалогаУСН(Организация, Период) Экспорт

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СпрУчетнаяПолитика.СтавкаНалогаУСН КАК СтавкаНалогаУСН
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
	|		&Период, Организация = &Организация
	|		) КАК Настройка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК СпрУчетнаяПолитика
	|	ПО Настройка.УчетнаяПолитика = СпрУчетнаяПолитика.Ссылка
	|	   И СпрУчетнаяПолитика.СистемаНалогообложения = &СистемаУСН");
	
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СистемаУСН", ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СтавкаНалогаУСН;
	КонецЕсли;

КонецФункции

// Определяет признак применения налоговых каникул для организации на УСН.
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, из учетной политики которой берется требуемое значение;
//	Период - Дата - дата, на которую анализируется учетная политика организации.
//
// Возвращаемое значение:
//	Булево - признак применения налоговых каникул УСН.
//
Функция НалоговыеКаникулыУСН(Организация, Период) Экспорт
	
	Результат = Ложь;
	
	Возврат Результат;
	
КонецФункции

// Возвращает объект налогообложения который используется для организации на упрощенной системе налогообложения.
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, из учетной политики которой берется требуемое значение;
//	Период - Дата - дата, на которую анализируется учетная политика организации.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ОбъектыНалогообложенияПоУСН - объект налогообложения, указанный для учетной политики организации на переданную дату.
//
Функция ОбъектНалогообложенияУСН(Организация, Период) Экспорт

	Если ПрименяетсяУСНДоходыМинусРасходы(Организация, Период) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы");
	КонецЕсли;

КонецФункции 

// Возвращает признак применения труда наемных работников, который используется для организации на упрощенной системе налогообложения.
// Параметры:
//	Организация - СправочникСсылка.Организации - организация, из учетной политики которой берется требуемое значение;
//	Период - Дата - дата, на которую анализируется учетная политика организации.
//
// Возвращаемое значение:
//	Булево - признак использования труда наемных работников, указанный для учетной политики организации на переданную дату.
//
Функция ИспользуетсяТрудНаемныхРаботников(Организация, Период) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпрУчетнаяПолитика.ИспользуетсяТрудНаемныхРаботников КАК ИспользуетсяТрудНаемныхРаботников
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
	|		&Период, Организация = &Организация) КАК Настройка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК СпрУчетнаяПолитика
	|	ПО Настройка.УчетнаяПолитика = СпрУчетнаяПолитика.Ссылка
	|		И СпрУчетнаяПолитика.СистемаНалогообложения = &СистемаУСН";
	
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СистемаУСН", ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Истина;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ИспользуетсяТрудНаемныхРаботников;
	КонецЕсли;

КонецФункции 

//-- НЕ УТ

#КонецОбласти

// Параметры учетной политики по производству

//++ НЕ УТ

#Область ПараметрыУчетнойПолитикиПоРезервамИСомнительнымДолгам

// Параметры учетной политики по резервам по сомнительным долгам

// Возвращает признак формирования резервов по сомнительным долгам для организации в бухгалтерском учете.
// Для определения используется функция "ПараметрыУчетнойПолитики" модуля менеджера регистра сведений "УчетнаяПолитикаОрганизаций".
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак формирования резервов по сомнительным долгам;
//	Период - Дата - дата, на начало месяца которой определяется формирования резервов по сомнительным долгам.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату формирует резервы по сомнительным долгам в бухгалтерском учете. В противном случае - ложь.
//
Функция ФормироватьРезервыПоСомнительнымДолгамБУ(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(
		Организация,
		НачалоМесяца(Период));
	Результат = Не ПараметрыУчетнойПолитики = Неопределено И ПараметрыУчетнойПолитики.ФормироватьРезервыПоСомнительнымДолгамБУ;

	Возврат Результат;

КонецФункции 

// Возвращает признак формирования резервов по сомнительным долгам для организации в налоговом учете.
// Для определения используется функция "ПараметрыУчетнойПолитики" модуля менеджера регистра сведений "УчетнаяПолитикаОрганизаций".
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак формирования резервов по сомнительным долгам;
//	Период - Дата - дата, на начало месяца которой определяется формирования резервов по сомнительным долгам.
//
// Возвращаемое значение:
//	Булево - Истина, если организация на дату формирует резервы по сомнительным долгам в налоговом учете. В противном случае - ложь.
//
Функция ФормироватьРезервыПоСомнительнымДолгамНУ(Организация, Период) Экспорт

	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(
		Организация,
		НачалоМесяца(Период));
	Результат = Не ПараметрыУчетнойПолитики = Неопределено И ПараметрыУчетнойПолитики.ФормироватьРезервыПоСомнительнымДолгамНУ;

	Возврат Результат;

КонецФункции 

#КонецОбласти

#Область ПараметрыУчетнойПолитикиПоТорговомуСбору

// Возвращает признак, является ли организация плательщиком торгового сбора.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак оплаты торгового сбора;
//	Период - Дата - дата, на которую определяется признак оплаты торгового сбора.
//
// Возвращаемое значение:
//	Булево - Истина, если организация имеет активные (не снятые с учета) торговые точки. В противном случае - ложь.
// (см. функцию ТорговыйСбор.ИспользуютсяТорговыеТочки)
//
Функция ПлательщикТорговогоСбора(Организация, Период) Экспорт

	Возврат ТорговыйСбор.ИспользуютсяТорговыеТочки(Организация, Период);

КонецФункции

// Определяет, являлась ли организация плательщиком торгового сбора в течении указанного периода.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - организация, для которой определяется признак оплаты торгового сбора;
//	НачалоПериода - Дата - начало периода, для которого определяется признак оплаты торгового сбора;
//	КонецПериода - Дата - конец периода, для которого определяется признак оплаты торгового сбора.
//
// Возвращаемое значение:
//	Булево - Истина, если организация в течении указанного периода являлась плательщиком торгового сбора (хотя бы раз). В противном случае - ложь.
// (см. функцию ТорговыйСбор.ИспользуютсяТорговыеТочкиЗаПериод)
//
Функция ПлательщикТорговогоСбораЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт

	Возврат ТорговыйСбор.ИспользуютсяТорговыеТочкиЗаПериод(Организация, НачалоПериода, КонецПериода);

КонецФункции

#КонецОбласти

//-- НЕ УТ

// Определяет, применялся ли для организации признак, указанный в первом параметре, в течении указанного периода.
//
// Параметры:
//	ИмяРесурса - Строка - имя ресурса регистра сведений "УчетныеПолитикиОрганизаций" с типом "Булево";
//	Организация - СправочникСсылка.Организация - организация, для которой определяется значение ресурса;
//	НачалоПериода - Дата - начало периода, для которого определяется значение ресурса;
//	КонецПериода - Дата - конец периода, для которого определяется значение ресурса.
//
// Возвращаемое значение:
//	Булево - Истина, если для организации в течении указанного периода, значение ресурса хотя бы раз принимало значение "Истина". В противном случае - ложь.
//
Функция ЗначенияРесурсаУчетнойПолитикиЗаПериод(ИмяРесурса, Организация, НачалоПериода, ОкончаниеПериода) Экспорт
	
	СледующийМесяц = НачалоМесяца(ДобавитьМесяц(НачалоПериода, 1));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",      Организация);
	Запрос.УстановитьПараметр("НачалоПериода",    НачалоМесяца(НачалоПериода));
	Запрос.УстановитьПараметр("СледующийМесяц",   СледующийМесяц);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(ОкончаниеПериода));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизацийСрезПоследних.СистемаНалогообложения КАК Значение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
	
	Если СледующийМесяц < КонецМесяца(ОкончаниеПериода) Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|"+
		"ВЫБРАТЬ
		|	УчетнаяПолитикаОрганизаций.СистемаНалогообложения
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
		|ГДЕ
		|	УчетнаяПолитикаОрганизаций.Организация = &Организация
		|	И УчетнаяПолитикаОрганизаций.Период МЕЖДУ &СледующийМесяц И &ОкончаниеПериода";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СистемаНалогообложения", ИмяРесурса);
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти