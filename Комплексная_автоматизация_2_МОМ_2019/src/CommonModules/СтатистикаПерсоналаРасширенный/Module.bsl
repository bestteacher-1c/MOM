////////////////////////////////////////////////////////////////////////////////
// Подсистема "Статистика персонала".
// Процедуры и функции, предназначенные для форм статистической отчетности.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область Должности

Процедура ГруппаКатегорииДополнитьФорму(Форма, ТолькоПросмотр = Ложь) Экспорт
	
	НастройкиСтатистики = НастройкиСтатистикиПерсонала();
	Если НастройкиСтатистики.ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы 
		И ЗначениеЗаполнено(НастройкиСтатистики.ФормаОтчетностиМониторингаРаботниковСоциальнойСферы) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы",
			"Заголовок",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Строка отчетности %1'"), 
				НастройкиСтатистики.ФормаОтчетностиМониторингаРаботниковСоциальнойСферы));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы",
			"Подсказка",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Сведения по численности и оплате труда работников этой должности будут включены в указанную строку отчета по статистическому наблюдению по форме %1'"), 
				НастройкиСтатистики.ФормаОтчетностиМониторингаРаботниковСоциальнойСферы));
				
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы",
			"ТолькоПросмотр",
			Форма.ТолькоПросмотр ИЛИ ТолькоПросмотр);
				
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы",
			"Видимость",
			Ложь);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ГруппаКатегорииДополнитьФорму(Форма, ТолькоПросмотр);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДополнительныеДанныеДолжности(Форма, Должность, ЗначениеКопирования = Неопределено) Экспорт	
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы) Тогда
		МенеджерЗаписи = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Должность = Должность;
		МенеджерЗаписи.Прочитать();
		
		Если ЗначениеЗаполнено(ЗначениеКопирования) Тогда
			Выборка = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Выбрать(Новый Структура("Должность", ЗначениеКопирования));
			Если Выборка.Следующий() Тогда 
				МенеджерЗаписи.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы = Выборка.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы;
			КонецЕсли;
		КонецЕсли;
		
		ЗначениеВДанныеФормы(МенеджерЗаписи, Форма.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ПрочитатьДополнительныеДанныеДолжностиСтатистика(Форма, Должность, ЗначениеКопирования);
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаписатьДополнительныеДанныеДолжности(Форма, Ссылка) Экспорт
	
	Если  ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы")
		И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы) Тогда
		МенеджерЗаписи = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Форма.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы);
		МенеджерЗаписи.Должность = Ссылка;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ЗаписатьДополнительныеДанныеДолжностиСтатистика(Форма, Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ШтатноеРасписание

Процедура ПриПолученииВнешнихДанныхПозицииШтатногоРасписания(Форма, ДанныеПозиции) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Должность = ДанныеПозиции.ПозицияШтатногоРасписания; 
	МенеджерЗаписи.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы = ДанныеПозиции.КатегорияПерсонала; 

	ЗначениеВДанныеФормы(МенеджерЗаписи, Форма.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы);
	
КонецПроцедуры

Процедура ПоместитьКатегориюПерсоналаВОписаниеПозиции(Форма, ДанныеПозиции) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПозиции.Вставить("КатегорияПерсонала", Форма.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы);
		
КонецПроцедуры

Функция ТекстЗапросаВТКатегорииПерсоналаПоПозициям(ИмяВТ = "ВТВсеПозицииНаДатуАктуальности", ИмяПоляПозиция = "ПозицияШтатногоРасписания") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 0
		|	NULL КАК Позиция,
		|	NULL КАК КатегорияПерсонала
		|ПОМЕСТИТЬ ВТКатегорииПерсоналаПоПозициям";
		
	Иначе
		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КатегорииПерсоналаПоПозициям.Должность КАК Позиция,
			|	КатегорииПерсоналаПоПозициям.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы КАК КатегорияПерсонала
			|ПОМЕСТИТЬ ВТКатегорииПерсоналаПоПозициям
			|ИЗ
			|	РегистрСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы КАК КатегорииПерсоналаПоПозициям
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеПозицииНаДатуАктуальности КАК ВсеПозицииНаДатуАктуальности
			|		ПО (КатегорииПерсоналаПоПозициям.Должность = &ИмяПоляПозиция)";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПозиция", "ВсеПозицииНаДатуАктуальности." + ИмяПоляПозиция);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТВсеПозицииНаДатуАктуальности", ИмяВТ);
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьКатегориюПерсоналаПоДолжности(Форма, Должность) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		Возврат;
	КонецЕсли;

	МенеджерЗаписи = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Должность = Должность;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Должность = Форма.Объект.Ссылка;
		ЗаполнитьЗначенияСвойств(Форма.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы, МенеджерЗаписи, "СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы");
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписьюДокументовИзмененияШтатногоРасписания(СтрокиСозданныхПозиций) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаПозиции Из СтрокиСозданныхПозиций Цикл
		
		МенеджерЗаписи = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Должность = СтрокаПозиции.Должность;
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда
			
			МенеджерЗаписиПозиции = РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиПозиции, МенеджерЗаписи);
			
			МенеджерЗаписиПозиции.Должность = СтрокаПозиции.Позиция;
			МенеджерЗаписиПозиции.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

// См. УправлениеШтатнымРасписанием.ОписаниеИсточниковСведенийОШтатномРасписании.
Процедура ДополнитьОписаниеИсточниковСведенийОШтатномРасписании(ОписаниеИсточников) Экспорт
	
	// Добавление описания сведений категории персонала.
	
	ИдентификаторИсточника = "РегистрСведенийСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы";
	ОписаниеИсточника = УправлениеШтатнымРасписанием.ОписаниеИсточникаСведений(ОписаниеИсточников, ИдентификаторИсточника);
	
	ИмяДанныхШР = "КатегорияПерсонала";
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		
		УправлениеШтатнымРасписанием.ДобавитьОписаниеСведенийШтатногоРасписания(ОписаниеИсточников, ИмяДанныхШР, ИдентификаторИсточника,
			"РегистрСведенийСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы");
		
		Если ОписаниеИсточников.ЗначенияОпций.ИспользоватьИсториюИзмененияШтатногоРасписания Тогда
			
			ОписаниеИсточника.ТекстСоединений = "
				|	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы КАК РегистрСведенийСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы
				|		ПО РегистрСведенийИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания = РегистрСведенийСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Должность}";
			
		Иначе
			
			ОписаниеИсточника.ТекстСоединений = "
				|	{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы КАК РегистрСведенийСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы
				|		ПО СправочникШтатноеРасписание.Ссылка = РегистрСведенийСтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Должность}";
			
		КонецЕсли;
		
	Иначе
		УправлениеШтатнымРасписанием.ДобавитьОписаниеСведенийШтатногоРасписания(ОписаниеИсточников, ИмяДанныхШР, ИдентификаторИсточника, "NULL");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РегламентированнаяОтчетность

// Функция заполняет выходной параметр ДанныеЧисленности значениями численности сотрудников для 
// переданной организации за переданный период
// Функция рассчитывает численность всех работников, женщин и инвалидов.
// При этом за переданный период считается среднесписочная численность, 
// а на конец заданного периода - списочная численность.
// см. также КадровыйУчет.СреднесписочнаяЧисленностьРаботающих
// Параметры:
//		Организация		- СправочникСсылка.Организации
//		НачалоПериода	- Дата
//		КонецПериода	- Дата
//		ДанныеЧисленности - структура с численностью (см. СтатистикаПерсонала.ДанныеЧисленности()).
//
Процедура СреднесписочнаяЧисленностьРаботающих(Организация, НачалоПериода, КонецПериода, ДанныеЧисленности) Экспорт

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация", Организация);
	ПараметрыОтчета.Вставить("ДатаНачалаПериодаОтчета", НачалоПериода);
	ПараметрыОтчета.Вставить("ДатаКонцаПериодаОтчета", КонецПериода);
	
	// Получение данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4",
		ПараметрыОтчета, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(Организация, "Организация");
	
	Если СтрокаОрганизации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЧисленности.ЧисленностьЖенщин = ?(Не ЗначениеЗаполнено(СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаЖенщинСписочногоСостава), 0, Окр(Окр(СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаЖенщинСписочногоСостава, 5)));
	ДанныеЧисленности.ЧисленностьИнвалидов = ?(Не ЗначениеЗаполнено(СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаИнвалидовСписочногоСостава), 0,Окр(Окр(СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаИнвалидовСписочногоСостава, 5)));
	ДанныеЧисленности.ЧисленностьРаботников = ?(Не ЗначениеЗаполнено(СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаСписочногоСостава), 0, Окр(Окр(СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаСписочногоСостава, 5)));
	ДанныеЧисленности.СреднесписочнаяЧисленностьЖенщин = ?(Не ЗначениеЗаполнено(СтрокаОрганизации.СредняяЧисленностьЗаПериодЖенщинСписочногоСостава), 0, Окр(Окр(СтрокаОрганизации.СредняяЧисленностьЗаПериодЖенщинСписочногоСостава, 5)));
	ДанныеЧисленности.СреднесписочнаяЧисленностьИнвалидов = ?(Не ЗначениеЗаполнено(СтрокаОрганизации.СредняяЧисленностьЗаПериодИнвалидовСписочногоСостава), 0,Окр(Окр(СтрокаОрганизации.СредняяЧисленностьЗаПериодИнвалидовСписочногоСостава, 5)));
	ДанныеЧисленности.СреднесписочнаяЧисленностьРаботников = ?(Не ЗначениеЗаполнено(СтрокаОрганизации.СредняяЧисленностьЗаПериодСписочногоСостава), 0, Окр(Окр(СтрокаОрганизации.СредняяЧисленностьЗаПериодСписочногоСостава, 5)));
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОПоказателяхОтчета.
Процедура ПриПолученииСведенийОПоказателяхОтчета(ПоказателиОтчета, ИДОтчета, ИДРедакцииОтчета, ПараметрыОтчета) Экспорт
	
	Если ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПЗдрав" Тогда
		
		ДобавитьПоказателиФормыФинмониторинга(ПоказателиОтчета, "РегламентированныйОтчетЗПЗдрав_2013");
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПКультура" Тогда
		
		ДобавитьПоказателиФормыФинмониторинга(ПоказателиОтчета, "РегламентированныйОтчетЗПКультура_2013");
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПНаука" Тогда
		
		ДобавитьПоказателиФормыФинмониторинга(ПоказателиОтчета, "РегламентированныйОтчетЗПНаука_2013");
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПОбразование" Тогда
		
		ДобавитьПоказателиФормыФинмониторинга(ПоказателиОтчета, "РегламентированныйОтчетЗПОбразование_2013");
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПСоц" Тогда
		
		ДобавитьПоказателиФормыФинмониторинга(ПоказателиОтчета, "РегламентированныйОтчетЗПСоц_2013");
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма1ТГМС" Тогда
		
		Если ИДРедакцииОтчета >= "ФормаОтчета2020Кв1" Тогда
			ДобавитьПоказателиФормы1ТГМС_2019(ПоказателиОтчета);
		Иначе
			ДобавитьПоказателиФормы1ТГМС_2015(ПоказателиОтчета);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма57Т" Тогда
		
		ДобавитьПоказателиФормы57Т_2017(ПоказателиОтчета);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма1" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2016Кв1" Тогда
			ДобавитьПоказателиФормы1_2016(ПоказателиОтчета);
		ИначеЕсли ИДРедакцииОтчета = "ФормаОтчета2019Кв1" Тогда
			ДобавитьПоказателиФормы1_2019(ПоказателиОтчета);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// См. РегламентированнаяОтчетностьПереопределяемый.ЗаполнитьОтчет.
Процедура ПриЗаполненииРегламентированногоОтчета(ИДОтчета, ИДРедакцииОтчета, ПараметрыОтчета, Контейнер) Экспорт
	
	Если ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПЗдрав" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПЗдрав_2013", 2013);
		Иначе
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПЗдрав_2013", 2015);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПКультура" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПКультура_2013", 2013);
		Иначе
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПКультура_2013", 2015);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПНаука" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПНаука_2013", 2013);
		Иначе
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПНаука_2013", 2015);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПОбразование" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПОбразование_2013", 2013);
		Иначе
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПОбразование_2013", 2015);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПСоц" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2013Кв1" Или ИДРедакцииОтчета = "ФормаОтчета2014Кв1" Тогда
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПСоц_2013", 2013);
		Иначе
			ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, "РегламентированныйОтчетЗПСоц_2013", 2015);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма1ТГМС" Тогда
		
		Если ИДРедакцииОтчета >= "ФормаОтчета2020Кв1" Тогда
			ЗаполнитьПоказателиФормы1ТГМС_2019(ПараметрыОтчета, Контейнер);
		Иначе
			ЗаполнитьПоказателиФормы1ТГМС_2015(ПараметрыОтчета, Контейнер);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма57Т" Тогда
		
		ЗаполнитьПоказателиФормы57Т_2017(ПараметрыОтчета, Контейнер);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма1" Тогда
		
		Если ИДРедакцииОтчета = "ФормаОтчета2016Кв1" Тогда
			ЗаполнитьПоказателиФормы1_2016(ПараметрыОтчета, Контейнер);
		ИначеЕсли ИДРедакцииОтчета = "ФормаОтчета2019Кв1" Тогда
			ЗаполнитьПоказателиФормы1_2019(ПараметрыОтчета, Контейнер);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит расшифровку в табличный документ на основе параметров формы, либо включает флажок Отказ.
Процедура ПриФормированииРасшифровкиРегламентированногоОтчета(Форма, ТабличныйДокумент, Отказ) Экспорт
	ПараметрыОтчета = Форма.Параметры.ПараметрыОтчета;
	
	Если Не ЗначениеЗаполнено(ПараметрыОтчета.ИДИменПоказателей) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИДОтчета          = ПараметрыОтчета.ИДОтчета;
	ИДРедакцииОтчета  = ПараметрыОтчета.ИДРедакцииОтчета;
	ИмяПоказателя     = ПараметрыОтчета.ИДИменПоказателей[0];
	
	ПараметрыОтчета.Вставить("ВидОтчета");
	ПараметрыОтчета.Вставить("ИмяСКД", "Расшифровка");
	
	Если ИДОтчета = "РегламентированныйОтчетСтатистикаФормаП4" Тогда
		
		Если ИДРедакцииОтчета < "ФормаОтчета2013Кв1" Тогда
			Отказ = Истина;
			Возврат; // 2011.
		ИначеЕсли ИДРедакцииОтчета <= "ФормаОтчета2014Кв1" Тогда // 2013 и 2014.
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетП4_2013";
		Иначе // > 2014.
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетП4_2017";
		КонецЕсли;
		
		Показатель = РазобратьИмяПоказателяРегламентированногоОтчета(ИмяПоказателя, "П----СССКК");
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтчета, Показатель, Истина);
		Если Не ЗначениеЗаполнено(ПараметрыОтчета.НомерСтроки)
			Или Не ЗначениеЗаполнено(ПараметрыОтчета.НомерКолонки)
			Или ПараметрыОтчета.НомерСтроки = "001"
			Или ПараметрыОтчета.НомерКолонки = "А1"
			Или ПараметрыОтчета.НомерКолонки = "Б1"
			Или ПараметрыОтчета.НомерКолонки = "В1"
			Или ПараметрыОтчета.НомерКолонки = "01"
			Или ПараметрыОтчета.НомерКолонки = "07" Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Форма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Строка %1, графа %2'"),
			Формат(Число(ПараметрыОтчета.НомерСтроки), "ЧГ="),
			Формат(Число(ПараметрыОтчета.НомерКолонки), "ЧГ="));
		
		ПараметрыОтчета.Вставить("ТолькоГражданские", Истина);
		Если ПараметрыОтчета.НомерКолонки = "05" Или ПараметрыОтчета.НомерКолонки = "06" Или ПараметрыОтчета.НомерКолонки = "11" Тогда
			ПараметрыОтчета.Вставить("ДатаНачалаПериодаВыборки", НачалоГода(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
		КонецЕсли;
		Если НачалоМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета) <> ПараметрыОтчета.ДатаНачалаПериодаОтчета Тогда
			ПараметрыОтчета.Вставить("СНачалаГода", Истина);
		КонецЕсли;
		
		СформироватьОтчетРасшифровку(ПараметрыОтчета, ТабличныйДокумент);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаП4НЗ" Тогда
		
		Если ИДРедакцииОтчета < "ФормаОтчета2015Кв1" Тогда
			Отказ = Истина;
			Возврат; // 2013 и 2014.
		КонецЕсли;
		
		Показатель = РазобратьИмяПоказателяРегламентированногоОтчета(ИмяПоказателя, "П------ССКК");
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтчета, Показатель, Истина);
		Если Не ЗначениеЗаполнено(ПараметрыОтчета.НомерСтроки)
			Или Не ЗначениеЗаполнено(ПараметрыОтчета.НомерКолонки)
			Или ПараметрыОтчета.НомерСтроки = "01"
			Или ПараметрыОтчета.НомерСтроки = "04"
			Или ПараметрыОтчета.НомерСтроки = "11"
			Или ПараметрыОтчета.НомерСтроки = "14"
			Или ПараметрыОтчета.НомерСтроки = "18"
			Или ПараметрыОтчета.НомерСтроки = "19"
			Или ПараметрыОтчета.НомерСтроки = "21"
			Или ПараметрыОтчета.НомерСтроки = "22" Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Форма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Строка %1, графа %2'"),
			Формат(Число(ПараметрыОтчета.НомерСтроки), "ЧГ="),
			Формат(Число(ПараметрыОтчета.НомерКолонки), "ЧГ="));
		
		ПараметрыОтчета.Вставить("ВидОтчета", "РегламентированныйОтчетП4НЗ_2015");
		ПараметрыОтчета.Вставить("ТолькоОсновноеМестоРаботы", Истина);
		
		СформироватьОтчетРасшифровку(ПараметрыОтчета, ТабличныйДокумент);
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФорма1ТГМС" Тогда
		
		Показатель = РазобратьИмяПоказателяРегламентированногоОтчета(ИмяПоказателя, "П--------ССКК");
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтчета, Показатель, Истина);
		Если Не ЗначениеЗаполнено(ПараметрыОтчета.НомерСтроки)
			Или Не ЗначениеЗаполнено(ПараметрыОтчета.НомерКолонки)
			Или ПараметрыОтчета.НомерСтроки = "05" Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		Форма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Строка %1, графа %2'"),
			Формат(Число(ПараметрыОтчета.НомерСтроки), "ЧГ="),
			Формат(Число(ПараметрыОтчета.НомерКолонки), "ЧГ="));
		
		Если ИДРедакцииОтчета >= "ФормаОтчета2020Кв1" Тогда
			ПараметрыОтчета.ВидОтчета = "1ТГМС2019";
		Иначе
			ПараметрыОтчета.ВидОтчета = "1ТГМС2015";
		КонецЕсли;
		ПараметрыОтчета.Вставить("ДатаАктуальности", ПараметрыОтчета.ДатаКонцаПериодаОтчета);
		ПараметрыОтчета.Вставить("СНачалаГода", Истина);
		
		Если ПараметрыОтчета.НомерКолонки = "03" Тогда // Показатели из штатного расписания
			СформироватьОтчетРасшифровкуШтатноеРасписание(ПараметрыОтчета, ТабличныйДокумент);
		Иначе
			СформироватьОтчетРасшифровку(ПараметрыОтчета, ТабличныйДокумент);
		КонецЕсли;
		
	ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПЗдрав"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПКультура"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПНаука"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПОбразование"
		Или ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПСоц" Тогда
		
		Если ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПЗдрав" Тогда
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетЗПЗдрав_2013";
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПКультура" Тогда
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетЗПКультура_2013";
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПНаука" Тогда
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетЗПНаука_2013";
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПОбразование" Тогда
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетЗПОбразование_2013";
		ИначеЕсли ИДОтчета = "РегламентированныйОтчетСтатистикаФормаЗПСоц" Тогда
			ПараметрыОтчета.ВидОтчета = "РегламентированныйОтчетЗПСоц_2013";
		КонецЕсли;
		
		Показатель = РазобратьИмяПоказателяРегламентированногоОтчета(ИмяПоказателя);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтчета, Показатель, Истина);
		Если Не ЗначениеЗаполнено(ПараметрыОтчета.НомерСтроки)
			Или Не ЗначениеЗаполнено(ПараметрыОтчета.НомерКолонки)
			Или ПараметрыОтчета.НомерСтроки = "001"
			Или ПараметрыОтчета.НомерКолонки = "03"
			Или ПараметрыОтчета.НомерКолонки = "05" Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Форма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Строка %1, графа %2'"),
			Формат(Число(ПараметрыОтчета.НомерСтроки), "ЧГ="),
			Формат(Число(ПараметрыОтчета.НомерКолонки), "ЧГ="));
		
		Если ИДРедакцииОтчета < "ФормаОтчета2015Кв1" Тогда
			ПараметрыОтчета.Вставить("ВариантРасчетаПоказателей", 2013);
		Иначе
			ПараметрыОтчета.Вставить("ВариантРасчетаПоказателей", 2015);
		КонецЕсли;
		ПараметрыОтчета.Вставить("СНачалаГода", Истина);
		
		СформироватьОтчетРасшифровку(ПараметрыОтчета, ТабличныйДокумент);
		
	Иначе
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БазовыйКонтракт

// П-4

// Получает показатели, которые могут быть заполнены при заполнении отчета.
// 
// Параметры:
//  ПоказателиОтчета - структура
//
Процедура ДобавитьПоказателиФормыП4_2013Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач2", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач3", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач4", Истина, Ложь);
	
	ИндексТекущейСтроки = 2;
	
	Пока ИндексТекущейСтроки < 12 Цикл
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "А1", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "В1", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "02", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "03", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "04", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "05", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "06", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "08", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "09", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "10", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "11", Истина, Истина);
		
		ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
		
	КонецЦикла;

КонецПроцедуры

// Заполняет показатели отчета.
// 
// Параметры:
//  ПараметрыОтчета - структура.
//  Контейнер - структура - содержит все показатели отчета и их значения.
//
Процедура ЗаполнитьПоказателиФормыП4_2013Кв1(ПараметрыОтчета, Контейнер) Экспорт

	// Сведения для отчета берутся за последний месяц.
	ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаНачалаПериодаВыборки", НачалоГода(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
	ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчета);
	
	// Получение зарплатных данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияЗарплата, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполненияЗарплата.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		// Заполнение отчета
		ВыводитьКвартальныеПоказатели = КонецМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета) = КонецКвартала(ПараметрыОтчета.ДатаКонцаПериодаОтчета);
		ИндексТекущейСтроки = 2;
		КоличествоСтрок = 11;
		Если ПараметрыОтчетаДляЗаполненияЗарплата.Свойство("СНачалаГода") И ПараметрыОтчетаДляЗаполненияЗарплата.СНачалаГода Тогда
			Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
				
				ЗаполнитьПоказателиЗарплата_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, СтрокаОКВЭД, СтрокаОКВЭД, ВыводитьКвартальныеПоказатели);
				
				ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
				
				Если ИндексТекущейСтроки = 12 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
				Для Каждого СтрокаОКВЭДПериод Из СтрокаОКВЭД.Строки Цикл
					
					Если СтрокаОКВЭДПериод.МесяцНачисления <> ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета Тогда
						Продолжить;
					КонецЕсли;
					
					ЗаполнитьПоказателиЗарплата_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, СтрокаОКВЭД, СтрокаОКВЭДПериод, ВыводитьКвартальныеПоказатели);
					
					ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
					
					Если ИндексТекущейСтроки > КоличествоСтрок Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		ПустаяСтруктура = Новый Структура();
		ПустаяСтруктура.Вставить("ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава", 0);
		ПустаяСтруктура.Вставить("ОтработаноЧасовСНачалаГодаВнешнимиСовместителями", 0);
		ПустаяСтруктура.Вставить("НаименованиеОКВЭД", "");
		ПустаяСтруктура.Вставить("КодОКВЭД", "");
		ПустаяСтруктура.Вставить("НачисленоРаботникамСписочногоСостава", 0);
		ПустаяСтруктура.Вставить("НачисленоВнешнимСовместителям", 0);
		ПустаяСтруктура.Вставить("НачисленоРаботникамПоДоговорамГПХ", 0);
		ПустаяСтруктура.Вставить("НачисленияСоциальногоХарактера", 0);
		Пока ИндексТекущейСтроки <= КоличествоСтрок Цикл
			ЗаполнитьПоказателиЗарплата_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, ПустаяСтруктура, ПустаяСтруктура, Ложь);
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
	// Получение кадровых данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияКадры, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполненияКадры.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		// Заполнение отчета
		ИндексТекущейСтроки = 2;
		КоличествоСтрок = 11;
		Если ПараметрыОтчетаДляЗаполненияКадры.Свойство("СНачалаГода") И ПараметрыОтчетаДляЗаполненияКадры.СНачалаГода Тогда
			Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
				
				ЗаполнитьПоказателиКадры_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, СтрокаОКВЭД, СтрокаОКВЭД, ВыводитьКвартальныеПоказатели);
				
				ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
				
				Если ИндексТекущейСтроки = 12 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
				Для Каждого СтрокаОКВЭДПериод Из СтрокаОКВЭД.Строки Цикл
					
					Если СтрокаОКВЭДПериод.Месяц <> ПараметрыОтчетаДляЗаполненияКадры.ДатаНачалаПериодаОтчета Тогда
						Продолжить;
					КонецЕсли;
					
					ЗаполнитьПоказателиКадры_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, СтрокаОКВЭД, СтрокаОКВЭДПериод, ВыводитьКвартальныеПоказатели);
					
					ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
					
					Если ИндексТекущейСтроки > КоличествоСтрок Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		ПустаяСтруктура = Новый Структура();
		ПустаяСтруктура.Вставить("СредняяЧисленностьЗаПериодСписочногоСостава", 0);
		ПустаяСтруктура.Вставить("СредняяЧисленностьЗаПериодВнешнихСовместителей", 0);
		ПустаяСтруктура.Вставить("СредняяЧисленностьЗаПериодДоговорников", 0);
		ПустаяСтруктура.Вставить("НаименованиеОКВЭД", "");
		ПустаяСтруктура.Вставить("КодОКВЭД", "");
		Пока ИндексТекущейСтроки <= КоличествоСтрок Цикл
			ЗаполнитьПоказателиКадры_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, ПустаяСтруктура, ПустаяСтруктура, Ложь);
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет показатели отчета.
// 
// Параметры:
//  ПараметрыОтчета - структура.
//  Контейнер - структура - содержит все показатели отчета и их значения.
//
Процедура ЗаполнитьПоказателиФормыП4_2017Кв1(ПараметрыОтчета, Контейнер) Экспорт
	// Подготовка настроек вывода данных отчета.
	ОписаниеПоказателейОтчета = ОписаниеПоказателейОтчета_СтатистикаФормаП4_2017Кв1();
	
	// Получение данных отчета.
	ДанныеОтчета = ДанныеОтчета_СтатистикаФормаП4_2017Кв1(ОписаниеПоказателейОтчета, ПараметрыОтчета);
	
	// Вывод данных отчета.
	Попытка
	
		СтруктураПоказателей = Контейнер.ПолеТабличногоДокументаФормаОтчета;
		Количество = ДанныеОтчета.Количество();
		Для Номер = 1 По 10 Цикл // Выводится 10 строк.
			Если Номер > Количество Тогда
				СтрокаТаблицы = ДанныеОтчета.Добавить(); // Если данных нет, то заполняются пустые значения.
			Иначе
				СтрокаТаблицы = ДанныеОтчета[Номер-1];
			КонецЕсли;
			ПредставлениеНомера = Формат(Номер+1, "ЧЦ=2; ЧВН="); // Нумерация начинается с "02".
			
			Для Каждого ОписаниеПоказателя Из ОписаниеПоказателейОтчета Цикл
				Значение = СтрокаТаблицы[ОписаниеПоказателя.ИмяКолонки];
				ПутьКДанным  = СтрЗаменить(ОписаниеПоказателя.ПутьКДанным,  "##", ПредставлениеНомера);
				ПутьКФормату = СтрЗаменить(ОписаниеПоказателя.ПутьКФормату, "##", ПредставлениеНомера);
				СтруктураПоказателей[ПутьКДанным] = Значение;
				Если ЗначениеЗаполнено(ПутьКФормату) И Значение = 0 И СтруктураПоказателей.Свойство(ПутьКФормату) Тогда
					СтруктураПоказателей[ПутьКФормату] = "ЧН=-";
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Порядок заполнения отчета устарел, рекомендуем обновить конфигурацию.%1'"), Символы.ПС);
		ТекстСообщения = ТекстСообщения + НСтр("ru = 'Отчет не заполнен.'");
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
КонецПроцедуры

// П-4 (НЗ)

// Получает показатели, которые могут быть заполнены при заполнении отчета.
// 
// Параметры:
//  ПоказателиОтчета - структура
//
Процедура ДобавитьПоказателиФормыП4НЗ_2015Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКод1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКод2", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКод3", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКод4", Истина, Ложь);
	
	Для ИндексТекущейСтроки = 1 По 22 Цикл
		Если СтрНайти("4,11,18,19", Строка(ИндексТекущейСтроки)) > 0 Тогда
			Продолжить;
		КонецЕсли;
		Если СтрНайти("1,14,21,22", Строка(ИндексТекущейСтроки)) > 0 Тогда
			ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П000100" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "01", Истина, Ложь);
		Иначе
			ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П000100" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "01", Истина, Истина);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Заполняет показатели отчета.
// 
// Параметры:
//  ПараметрыОтчета - структура.
//  Контейнер - структура - содержит все показатели отчета и их значения.
//
Процедура ЗаполнитьПоказателиФормыП4НЗ_2015Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	// Очистим ячейки
	Попытка
		Для ИндексТекущейСтроки = 1 По 22 Цикл
			Если ИндексТекущейСтроки = 4 Или ИндексТекущейСтроки = 11 Или ИндексТекущейСтроки = 18 Или ИндексТекущейСтроки = 19 Тогда
				Продолжить;
			КонецЕсли;
			Контейнер.ПолеТабличногоДокументаФормаОтчета["П000100" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "01"] = 0;
		КонецЦикла;
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Порядок заполнения отчета устарел, рекомендуем обновить конфигурацию.%1'"), Символы.ПС);
		ТекстСообщения = ТекстСообщения + НСтр("ru = 'Отчет не заполнен.'");
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
	ПараметрыОтчетаДляЗаполнения = СкопироватьКоллекцию(ПараметрыОтчета);
	
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиП4НЗ("РегламентированныйОтчетСтатистикаФормаП4НЗ", ПараметрыОтчетаДляЗаполнения, ДанныеОтчета);
	
	КоличествоСтавокНаКонецПериода = 0;
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполнения.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000201"] = СтрокаОрганизации.ЧисленностьРаботавшихНеполноеРабочееВремя;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000301"] = СтрокаОрганизации.ЧисленностьСотрудниковВПростое;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000501"] = СтрокаОрганизации.ЧисленностьСотрудниковВОтпускеБезОплаты;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000701"] = СтрокаОрганизации.ЧисленностьПринятыхВПериодеНаНовуюПозицию;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000901"] = СтрокаОрганизации.ЧисленностьУволенныхПоСоглашениюСторон;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001001"] = СтрокаОрганизации.ЧисленностьУволенныхВСвязиССокращением;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001201"] = СтрокаОрганизации.ЧисленностьУволенныхПоСобственномуЖеланию;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001601"] = СтрокаОрганизации.ЧисленностьЖенщинВОтпускеДоПолутораЛет;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001701"] = СтрокаОрганизации.ЧисленностьЖенщинВОтпускеДоТрехЛет;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001002001"] = СтрокаОрганизации.ЧисленностьРаботавшихВахтовымМетодом;
		КоличествоСтавокНаКонецПериода = СтрокаОрганизации.КоличествоСтавок;
	КонецЕсли;
	
	// Получение кадровых данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	ПараметрыОтчета.Вставить("ТолькоОсновноеМестоРаботы", Истина);
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчета, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчета.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		// Заполнение отчета
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000601"] = СтрокаОрганизации.ПринятоСписочногоСостава;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001000801"] = СтрокаОрганизации.УволеноСписочногоСостава;
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001301"] = СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаСписочногоСостава;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001401"] = КоличествоСтавокНаКонецПериода - Контейнер.ПолеТабличногоДокументаФормаОтчета["П0001001301"];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Форма14

// Возвращает сведения о должностях и численности.
Функция СведенияОДолжностяхИЧисленностиРегламентированнойОтчетности(Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	СтруктураРезультат = Новый Структура("ДанныеШтатногоРасписания,ДанныеСреднесписочнойЧисленности");
	
	Вариант = "РегламентированныйОтчетСтатистикаФорма14";
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация", Организация);
	ПараметрыОтчета.Вставить("СНачалаГода", Истина);
	ПараметрыОтчета.Вставить("ДатаНачалаПериодаОтчета", ДатаНачала);
	ПараметрыОтчета.Вставить("ДатаКонцаПериодаОтчета", ДатаОкончания);
	
	ДанныеШтатногоРасписания = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиШР1ТГМС(Вариант, ПараметрыОтчета, ДанныеШтатногоРасписания);
	СтруктураРезультат.ДанныеШтатногоРасписания = ДанныеШтатногоРасписания;
	
	ДанныеСреднесписочнойЧисленности = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(Вариант, ПараметрыОтчета, ДанныеСреднесписочнойЧисленности);
	СтруктураРезультат.ДанныеСреднесписочнойЧисленности = ДанныеСреднесписочнойЧисленности;	
	
	Возврат СтруктураРезультат;
		
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкиПодсистемы

// Чтение настроек подсистемы статистики персонала.
// Если формирование отчетных форм статистики отключено, функция возвращает
// структуру с единственным реквизитом ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы,
// установленным в значение ЛОЖЬ.
//
// ВозвращаемоеЗначение:
//  Структура настроек
//
Функция НастройкиСтатистикиПерсонала() Экспорт

	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы", Ложь);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы") Тогда
		Возврат СтруктураНастроек;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиСтатистикиПерсонала.ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы,
		|	НастройкиСтатистикиПерсонала.ФормаОтчетностиМониторингаРаботниковСоциальнойСферы
		|ИЗ
		|	РегистрСведений.НастройкиСтатистикиПерсонала КАК НастройкиСтатистикиПерсонала";

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Если РезультатЗапроса.Количество() > 0 Тогда
		СтруктураНастроек = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(РезультатЗапроса[0]);
	КонецЕсли;
	
	Возврат СтруктураНастроек;

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Процедура = "Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.НачальноеЗаполнение";
		Обработчик.ОбщиеДанные = Истина;
		Обработчик.НачальноеЗаполнение = Истина;
	КонецЕсли;
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Процедура = "Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ОбновлениеПояснений";
		Обработчик.ОбщиеДанные = Истина;
		Обработчик.НачальноеЗаполнение = Истина;
	КонецЕсли;
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.СтрокиОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.1.7.13";
		Обработчик.Процедура = "Справочники.СтрокиОтчетностиРасходовИЧисленностиРаботниковГосударственныхОрганов.НачальноеЗаполнение";
		Обработчик.ОбщиеДанные = Истина;
		Обработчик.НачальноеЗаполнение = Истина;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы.ПодключаемыеХарактеристики") Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.1.8.31";
		Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
		Обработчик.Идентификатор = Новый УникальныйИдентификатор("4a426be8-6557-48db-8bb0-0cc20e8eab29");
		Обработчик.Процедура = "СтатистикаПерсоналаРасширенный.ЗаполнитьПодключаемыеХарактеристики";
		Обработчик.Комментарий = НСтр("ru = 'Обновление подключаемых характеристик'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодключаемыеХарактеристики(ПараметрыОбновления = Неопределено) Экспорт
	
	Настройки = РегистрыСведений.НастройкиСтатистикиПерсонала.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	
	РегистрыСведений.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ОбновитьПодключаемыеХарактеристики(
		Настройки.ИспользоватьОтчетностьМониторингаРаботниковСоциальнойСферы, ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#Область Форма1ТГМС

Процедура ДобавитьПоказателиФормы1ТГМС_2015(ПоказателиОтчета)
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач2", Истина, Ложь);
	
	Для Номер = 1 По 4 Цикл
		НомерСтроки = Формат(Номер, "ЧЦ=2; ЧВН=");
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "03", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "04", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "05", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "06", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "07", Истина, Истина);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьПоказателиФормы1ТГМС_2015(ПараметрыОтчета, Контейнер)
	
	ТаблицаДанныхОтчета = ДанныеОтчета1ТГМС_2015(ПараметрыОтчета);
	
	Попытка
		Документ = Контейнер.ПолеТабличногоДокументаФормаОтчета;
		Для Каждого СтрокаТаблицы Из ТаблицаДанныхОтчета Цикл
			НомерСтроки = Формат(СтрокаТаблицы.НомерСтроки1ТГМС2015, "ЧЦ=2; ЧВН=");
			// Утверждено штатных единиц на конец отчетного периода
			Документ["П00000100" + НомерСтроки + "03"] = СтрокаТаблицы.КоличествоСтавок;
			// Численность работников (без внешних совместителей), человек фактически на конец отчетного периода.
			Документ["П00000100" + НомерСтроки + "04"] = СтрокаТаблицы.ЧисленностьНаКонецПериодаСписочногоСостава;
			// Численность работников (без внешних совместителей), средне-списочная за отчетный период.
			Документ["П00000100" + НомерСтроки + "05"] = Окр(СтрокаТаблицы.СредняяЧисленностьЗаПериодСписочногоСостава, 1);
			// Начислено средств на оплату труда работников в отчетном периоде, тыс руб (нарастающим итогом с начала года).
			Документ["П00000100" + НомерСтроки + "06"] = Окр(СтрокаТаблицы.НачисленоРаботникамСписочногоСостава / 1000, 1);
			// Сумма выплат социального характера, начисленных в отчетном периоде, тыс руб (нарастающим итогом с начала года).
			Документ["П00000100" + НомерСтроки + "07"] = Окр(СтрокаТаблицы.НачисленияСоциальногоХарактера / 1000, 1);
		КонецЦикла;
	Исключение
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Порядок заполнения отчета устарел, рекомендуется обновить конфигурацию.
				|Подробности: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьПоказателиФормы1ТГМС_2019(ПоказателиОтчета)
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач2", Истина, Ложь);
	
	Для Номер = 1 По 3 Цикл
		НомерСтроки = Формат(Номер, "ЧЦ=2; ЧВН=");
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "03", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "04", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "05", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "06", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000100" + НомерСтроки + "07", Истина, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоказателиФормы1ТГМС_2019(ПараметрыОтчета, Контейнер)
	
	ТаблицаДанныхОтчета = ДанныеОтчета1ТГМС_2019(ПараметрыОтчета);
	
	Попытка
		Документ = Контейнер.ПолеТабличногоДокументаФормаОтчета;
		Для Каждого СтрокаТаблицы Из ТаблицаДанныхОтчета Цикл
			НомерСтроки = Формат(СтрокаТаблицы.НомерСтроки1ТГМС2019, "ЧЦ=2; ЧВН=");
			// Утверждено штатных единиц на конец отчетного периода
			Документ["П00000100" + НомерСтроки + "03"] = Окр(СтрокаТаблицы.КоличествоСтавок, 1);
			// Численность работников (без внешних совместителей), человек фактически на конец отчетного периода.
			Документ["П00000100" + НомерСтроки + "04"] = СтрокаТаблицы.ЧисленностьНаКонецПериодаСписочногоСостава;
			// Численность работников (без внешних совместителей), средне-списочная за отчетный период.
			Документ["П00000100" + НомерСтроки + "05"] = Окр(СтрокаТаблицы.СредняяЧисленностьЗаПериодСписочногоСостава, 1);
			// Начислено средств на оплату труда работников в отчетном периоде, тыс руб (нарастающим итогом с начала года).
			Документ["П00000100" + НомерСтроки + "06"] = Окр(СтрокаТаблицы.НачисленоРаботникамСписочногоСостава / 1000, 1);
			// Сумма выплат социального характера, начисленных в отчетном периоде, тыс руб (нарастающим итогом с начала года).
			Документ["П00000100" + НомерСтроки + "07"] = Окр(СтрокаТаблицы.НачисленияСоциальногоХарактера / 1000, 1);
		КонецЦикла;
	Исключение
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Порядок заполнения отчета устарел, рекомендуется обновить конфигурацию.
				|Подробности: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Функция ДанныеОтчета1ТГМС_2015(ПараметрыОтчета)
	ИмяКолонкиНомерСтроки1ТГМС = "НомерСтроки1ТГМС2015";
	ТаблицаДанныхОтчета = ДанныеОтчета1ТГМС(ПараметрыОтчета, ИмяКолонкиНомерСтроки1ТГМС);
	
	// Наполнение таблицы пустыми строками чтобы очистить данные предыдущего заполнения.
	Для НомерСтроки = 1 По 4 Цикл
		СтрокаТаблицы = ТаблицаДанныхОтчета.Найти(НомерСтроки, ИмяКолонкиНомерСтроки1ТГМС);
		Если СтрокаТаблицы = Неопределено Тогда
			СтрокаТаблицы = ТаблицаДанныхОтчета.Добавить();
			СтрокаТаблицы[ИмяКолонкиНомерСтроки1ТГМС] = НомерСтроки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаДанныхОтчета;
КонецФункции

Функция ДанныеОтчета1ТГМС_2019(ПараметрыОтчета)
	ИмяКолонкиНомерСтроки1ТГМС = "НомерСтроки1ТГМС2019";
	ТаблицаДанныхОтчета = ДанныеОтчета1ТГМС(ПараметрыОтчета, ИмяКолонкиНомерСтроки1ТГМС);
	
	// Наполнение таблицы пустыми строками чтобы очистить данные предыдущего заполнения.
	Для НомерСтроки = 1 По 3 Цикл
		СтрокаТаблицы = ТаблицаДанныхОтчета.Найти(НомерСтроки, ИмяКолонкиНомерСтроки1ТГМС);
		Если СтрокаТаблицы = Неопределено Тогда
			СтрокаТаблицы = ТаблицаДанныхОтчета.Добавить();
			СтрокаТаблицы[ИмяКолонкиНомерСтроки1ТГМС] = НомерСтроки;
		КонецЕсли;
		// Получение итоговых данных в строке № 1.
		Если НомерСтроки = 1 Тогда
			СтрокаТаблицы.КоличествоСтавок                            = ТаблицаДанныхОтчета.Итог("КоличествоСтавок");
			СтрокаТаблицы.ЧисленностьНаКонецПериодаСписочногоСостава  = ТаблицаДанныхОтчета.Итог("ЧисленностьНаКонецПериодаСписочногоСостава");
			СтрокаТаблицы.СредняяЧисленностьЗаПериодСписочногоСостава = ТаблицаДанныхОтчета.Итог("СредняяЧисленностьЗаПериодСписочногоСостава");
			СтрокаТаблицы.НачисленияСоциальногоХарактера              = ТаблицаДанныхОтчета.Итог("НачисленияСоциальногоХарактера");
			СтрокаТаблицы.НачисленоРаботникамСписочногоСостава        = ТаблицаДанныхОтчета.Итог("НачисленоРаботникамСписочногоСостава");
		КонецЕсли;
	КонецЦикла;
	
	// Удаление строк, данные которых вошли только в итог.
	СтрокаТаблицы = ТаблицаДанныхОтчета.Найти(0, ИмяКолонкиНомерСтроки1ТГМС);
	Если СтрокаТаблицы <> Неопределено Тогда
		ТаблицаДанныхОтчета.Удалить(СтрокаТаблицы);
	КонецЕсли;
	
	Возврат ТаблицаДанныхОтчета;
КонецФункции

Функция ДанныеОтчета1ТГМС(ПараметрыОтчета, ИмяКолонкиНомерСтроки1ТГМС)
	// Сведения для отчета берутся за последний месяц.
	ПараметрыОтчетаДляЗаполнения = СкопироватьКоллекцию(ПараметрыОтчета);
	
	ТаблицаДанныхОтчета = Новый ТаблицаЗначений;
	ТаблицаДанныхОтчета.Колонки.Добавить("ПользовательскиеПоля" + ИмяКолонкиНомерСтроки1ТГМС, Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("КоличествоСтавок", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ЧисленностьНаКонецПериодаСписочногоСостава", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СредняяЧисленностьЗаПериодСписочногоСостава", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("НачисленияСоциальногоХарактера", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("НачисленоРаботникамСписочногоСостава", Новый ОписаниеТипов("Число"));
	
	Фильтр = Новый Структура("Организация", ПараметрыОтчетаДляЗаполнения.Организация);
	
	// Получение зарплатных данных отчета.
	ДанныеОтчета = Новый ТаблицаЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
		"РегламентированныйОтчетСтатистикаФорма1ТГМС",
		ПараметрыОтчетаДляЗаполнения,
		ДанныеОтчета);
	Найденные = ДанныеОтчета.НайтиСтроки(Фильтр);
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанныхОтчета.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
	// Получение кадровых данных отчета.
	ДанныеОтчета = Новый ТаблицаЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(
		"РегламентированныйОтчетСтатистикаФорма1ТГМС",
		ПараметрыОтчетаДляЗаполнения,
		ДанныеОтчета);
	Найденные = ДанныеОтчета.НайтиСтроки(Фильтр);
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанныхОтчета.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
	// Получение данных штатного расписания.
	ДанныеОтчета = Новый ТаблицаЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиШР1ТГМС(
		"РегламентированныйОтчетСтатистикаФорма1ТГМС",
		ПараметрыОтчетаДляЗаполнения,
		ДанныеОтчета);
	Найденные = ДанныеОтчета.НайтиСтроки(Фильтр);
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанныхОтчета.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
	// Номер строки 1Т (ГМС) вычисляется в пользовательских полях СКД, его требуется привести к проектному.
	ТаблицаДанныхОтчета.Колонки["ПользовательскиеПоля" + ИмяКолонкиНомерСтроки1ТГМС].Имя = ИмяКолонкиНомерСтроки1ТГМС;
	
	// Вычисление итогов.
	СвернутьТаблицу(ТаблицаДанныхОтчета, ИмяКолонкиНомерСтроки1ТГМС, Ложь);
	
	Возврат ТаблицаДанныхОтчета;
КонецФункции

#КонецОбласти

#Область Форма57Т

// Получает показатели, которые могут быть заполнены при заполнении отчета.
// 
// Параметры:
//  ПоказателиОтчета - структура
//
Процедура ДобавитьПоказателиФормы57Т_2017(ПоказателиОтчета)
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач2", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач3", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач4", Истина, Ложь);
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П010203", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П010303", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П010204", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П010304", Истина, Ложь);
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П030503", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П030603", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П030703", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П030803", Истина, Ложь);
	
	Для НомерСтрокиОтчета = 1 По 64 Цикл
		
		Для НомерКолонки = 2 По 13 Цикл
			
			Если НомерКолонки = 8 Тогда
				Продолжить;
			КонецЕсли;
			
			ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П02"
				 + Формат(НомерКолонки, "ЧЦ=2; ЧВН=")
				 + "_" + Формат(НомерСтрокиОтчета, "ЧЦ=2"), Истина, Ложь);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет показатели отчета.
// 
// Параметры:
//  ПараметрыОтчета - структура.
//  Контейнер - структура - содержит все показатели отчета и их значения.
//
Процедура ЗаполнитьПоказателиФормы57Т_2017(ПараметрыОтчета, Контейнер)
	
	ПараметрыОтчета.ДатаНачалаПериодаОтчета = Дата(Год(ПараметрыОтчета.ДатаНачалаПериодаОтчета), 10, 1);
	ПараметрыОтчета.ДатаКонцаПериодаОтчета = КонецМесяца(ПараметрыОтчета.ДатаНачалаПериодаОтчета);
	
	// Получение Кадровых данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	
	ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ТолькоОсновноеМестоРаботы", Истина);
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(
		"РегламентированныйОтчетСтатистикаФорма57Т",
		ПараметрыОтчетаДляЗаполненияКадры,
		ДанныеОтчета);
	
	Если ДанныеОтчета.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОрганизации = ДанныеОтчета.Строки[0];
	
	// Раздел I. Численность и начисленная заработная плата работников списочного состава.
	// |- Таблица 1.
	// |- Среднесписочная численность работников.
	
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П010203"] = СтрокаОрганизации.СредняяЧисленностьМужчинСписочногоСостава;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П010303"] = СтрокаОрганизации.СредняяЧисленностьЖенщинСписочногоСостава;
	
	// Раздел I. Численность и начисленная заработная плата работников списочного состава.
	// |- Таблица 2.
	// |- Численность работников списочного состава, полностью отработавших октябрь (без внешних совместителей).
	
	ВсеСотрудникиПоКатегориям = СотрудникиДляЗаполненияРаздела1Формы57Т(ПараметрыОтчета.Организация, НачалоМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета), СтрокаОрганизации);
	
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П030503"] = ВсеСотрудникиПоКатегориям.НайтиСтроки(Новый Структура("Колонка13", "1")).Количество();
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П030603"] = ВсеСотрудникиПоКатегориям.НайтиСтроки(Новый Структура("Колонка13", "2")).Количество();
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П030703"] = ВсеСотрудникиПоКатегориям.НайтиСтроки(Новый Структура("Колонка13", "3")).Количество();
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П030803"] = ВсеСотрудникиПоКатегориям.НайтиСтроки(Новый Структура("Колонка13", "4")).Количество();
	
	// Раздел I. Численность и начисленная заработная плата работников списочного состава.
	// |- Таблица 1.
	// |- Начислено сумм заработной платы за октябрь, тыс. руб.
	//
	// Получение зарплатных данных по аналогии с П-4.
	
	ДанныеОтчета = Новый ДеревоЗначений;
	
	ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
		"РегламентированныйОтчетСтатистикаФорма57Т",
		ПараметрыОтчетаДляЗаполненияЗарплата,
		ДанныеОтчета);
	
	Если ДанныеОтчета.Строки.Количество() > 0 Тогда
		СтрокаОрганизации = ДанныеОтчета.Строки[0];
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П010204"] = Окр(СтрокаОрганизации.НачисленоМужчинамСписочногоСостава / 1000, 1);
		Контейнер.ПолеТабличногоДокументаФормаОтчета["П010304"] = Окр(СтрокаОрганизации.НачисленоЖенщинамСписочногоСостава / 1000, 1);
	КонецЕсли;
	
	// Раздел II. Данные по отдельным работникам, полностью отработавшим.
	//
	// Получение зарплатных данных с отбором по периоду действия.
	
	Форма57ТРаздел2Сотрудники = СотрудникиДляЗаполненияРаздела2Формы57Т(ПараметрыОтчета.Организация, НачалоМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета), ВсеСотрудникиПоКатегориям);
	
	ДанныеОтчета = Новый ДеревоЗначений;
	
	ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ТолькоОсновноеМестоРаботы", Истина);
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключаяПерерасчеты", Истина);
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
		"РегламентированныйОтчетСтатистикаФорма57Т",
		ПараметрыОтчетаДляЗаполненияЗарплата,
		ДанныеОтчета);
	
	Если ДанныеОтчета.Строки.Количество() > 0 Тогда
		УчтенаКвартальнаяПремия = Ложь;
		ДополнитьФорма57ТРаздел2СотрудникиДаннымиОНачислениях(Форма57ТРаздел2Сотрудники, СтрокаОрганизации, УчтенаКвартальнаяПремия);
		
		Если Не УчтенаКвартальнаяПремия Тогда
		
			ДанныеОтчета = Новый ДеревоЗначений;
			
			ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
			ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета = ДобавитьМесяц(ПараметрыОтчета.ДатаНачалаПериодаОтчета, -3);
			ПараметрыОтчетаДляЗаполненияЗарплата.ДатаКонцаПериодаОтчета = КонецМесяца(ДобавитьМесяц(ПараметрыОтчета.ДатаНачалаПериодаОтчета, -1));
			ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ТолькоОсновноеМестоРаботы", Истина);
			ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключаяПерерасчеты", Истина);
			ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
				"РегламентированныйОтчетСтатистикаФорма57Т",
				ПараметрыОтчетаДляЗаполненияЗарплата,
				ДанныеОтчета);
			
			Если ДанныеОтчета.Строки.Количество() > 0 Тогда
				
				СтрокаОрганизации = ДанныеОтчета.Строки[0];
				ДополнитьФорма57ТРаздел2СотрудникиДаннымиОНачислениях(Форма57ТРаздел2Сотрудники, СтрокаОрганизации, УчтенаКвартальнаяПремия, Истина);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполняются колонки со 2 до 13, при этом 8 заполняется самим отчетом.
	НомераКолонок = СтрРазделить("02,03,04,05,06,07,09,10,11,12,13", ",", Ложь);
	
	// Строки, по которым не удалось получить данные, очищаются.
	ДоступноСтрок = Форма57ТРаздел2Сотрудники.Количество();
	
	// Колонки, по которым не удалось получить данные, очищаются.
	ДоступныеИменаКолонок = Новый Соответствие;
	Для Каждого Колонка Из Форма57ТРаздел2Сотрудники.Колонки Цикл
		ИмяКолонки = Колонка.Имя;
		Если СтрНачинаетсяС(ИмяКолонки, "Колонка") Тогда
			НомерКолонкиСтрокой = Сред(ИмяКолонки, СтрДлина("Колонка") + 1);
			Если СтрДлина(НомерКолонкиСтрокой) = 1 Тогда
				НомерКолонкиСтрокой = "0" + НомерКолонкиСтрокой;
			КонецЕсли;
			ДоступныеИменаКолонок.Вставить(НомерКолонкиСтрокой, ИмяКолонки);
		КонецЕсли;
	КонецЦикла;
	
	Для НомерСтроки = 1 По 64 Цикл
		СтрокаФормы = ?(НомерСтроки > ДоступноСтрок, Неопределено, Форма57ТРаздел2Сотрудники[НомерСтроки-1]);
		Для Каждого НомерКолонкиСтрокой Из НомераКолонок Цикл
			Если СтрокаФормы = Неопределено Тогда
				ЗначениеЯчейки = Неопределено;
			Иначе
				ИмяКолонки = ДоступныеИменаКолонок[НомерКолонкиСтрокой];
				Если ИмяКолонки = Неопределено Тогда
					ЗначениеЯчейки = Неопределено;
				Иначе
					ЗначениеЯчейки = СтрокаФормы[ИмяКолонки];
				КонецЕсли;
			КонецЕсли;
			ИмяЯчейки = "П02" + НомерКолонкиСтрокой + "_" + Формат(НомерСтроки, "ЧЦ=2");
			Контейнер.ПолеТабличногоДокументаФормаОтчета[ИмяЯчейки] = ЗначениеЯчейки;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция СотрудникиДляЗаполненияРаздела1Формы57Т(Организация, ПериодОтчета, ДанныеОрганизации = Неопределено, ТолькоДолжностиСОКПДТРКатегорией = Истина)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеОрганизации = Неопределено Тогда
		
		ДанныеОтчета = Новый ДеревоЗначений;
		
		ПараметрыОтчетаДляЗаполненияКадры = Новый Структура;
		ПараметрыОтчетаДляЗаполненияКадры.Вставить("Организация", Организация);
		ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаНачалаПериодаВыборки", НачалоМесяца(ПериодОтчета));
		ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаКонцаПериодаОтчета", КонецМесяца(ПериодОтчета));
		ПараметрыОтчетаДляЗаполненияКадры.Вставить("НомерСтроки", 0);
		
		ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(
			"РегламентированныйОтчетСтатистикаФорма57Т",
			ПараметрыОтчетаДляЗаполненияКадры,
			ДанныеОтчета);
		
		Если ДанныеОтчета.Строки.Количество() = 0 Тогда
			ДанныеОрганизации = ДанныеОтчета; // Пустое дерево значений.
		Иначе
			ДанныеОрганизации = ДанныеОтчета.Строки[0];
		КонецЕсли;
		
	КонецЕсли;
	
	СписокСотрудников = ДанныеОрганизации.Строки.ВыгрузитьКолонку("Сотрудник");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.УстановитьПараметр("НачалоПериодаОтчета", НачалоМесяца(ПериодОтчета));
	Запрос.УстановитьПараметр("КонецПериодаОтчета", КонецМесяца(ПериодОтчета));
	
	// Отбор полностью отработавших сотрудников
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	&НачалоПериодаОтчета КАК ДатаНачала,
		|	&КонецПериодаОтчета КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&СписокСотрудников)";
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = УчетРабочегоВремениРасширенный.ПараметрыДляЗапросВТДанныеУчетаВремениИСостоянийСотрудников();
	ПараметрыЗаполнения.ИмяВТСотрудники = "ВТСотрудники";
	ПараметрыЗаполнения.ИмяВТРезультат = "ВТДанныеУчетаВремени";
	ПараметрыЗаполнения.РассчитыватьПлановоеВремя = Истина;
	ПараметрыЗаполнения.ДатаАктуальности  = КонецМесяца(ПериодОтчета);
	ПараметрыЗаполнения.ДатаНачала = НачалоМесяца(ПериодОтчета);
	ПараметрыЗаполнения.ДатаОкончания = КонецМесяца(ПериодОтчета);;
	ПараметрыЗаполнения.МесяцДатаНачала = НачалоМесяца(ПериодОтчета);;
	ПараметрыЗаполнения.МесяцДатаОкончания = КонецМесяца(ПериодОтчета);;
	ПараметрыЗаполнения.ВыделятьВыходныеВПериодыОтклонений = Ложь;
	
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеУчетаВремениИСостоянийСотрудников(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыЗаполнения);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеУчетаВремени.Сотрудник КАК Сотрудник,
		|	ДанныеУчетаВремени.ВидУчетаВремени КАК ВидУчетаВремени,
		|	СУММА(ДанныеУчетаВремени.Дни) КАК Дни
		|ПОМЕСТИТЬ ВТСотрудникиСОтклонениями
		|ИЗ
		|	ВТДанныеУчетаВремени КАК ДанныеУчетаВремени
		|ГДЕ
		|	ДанныеУчетаВремени.ВидУчетаВремени <> ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.РабочееВремя)
		|	И ДанныеУчетаВремени.ВидУчетаВремени <> ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ВыходныеДни)
		|	И НЕ ДанныеУчетаВремени.ВидУчетаВремени.РабочееВремя
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеУчетаВремени.Сотрудник,
		|	ДанныеУчетаВремени.ВидУчетаВремени
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(Сотрудники.ДатаОкончания, ДЕНЬ) КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиСОтклонениями КАК СотрудникиСОтклонениями
		|		ПО Сотрудники.Сотрудник = СотрудникиСОтклонениями.Сотрудник
		|ГДЕ
		|	СотрудникиСОтклонениями.Сотрудник ЕСТЬ NULL";
	
	Запрос.Выполнить();
	
	// Получение кадровых данных полностью отработавших сотрудников
	ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
	
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВТ,
		Ложь,
		"Пол,
		|ДатаРождения,
		|Должность,
		|Образование1ВидОбразования,
		|СтажРаботыНаПредприятииЛет,
		|СтажРаботыНаПредприятииМесяцев,
		|ДоляНеполногоРабочегоВремени,
		|ДатаПриема,
		|ДатаУвольнения");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	КадровыеДанныеСотрудников.Сотрудник.Наименование КАК Наименование,
		|	КадровыеДанныеСотрудников.Должность КАК Колонка2,
		|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Должность КАК Справочник.Должности).ОКПДТРКод КАК Колонка3,
		|	ВЫБОР
		|		КОГДА КадровыеДанныеСотрудников.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Мужской)
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Колонка4,
		|	ГОД(КадровыеДанныеСотрудников.ДатаРождения) КАК Колонка5,
		|	ВЫБОР
		|		КОГДА КадровыеДанныеСотрудников.Образование1ВидОбразования В (ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.ВысшееОбразование), ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.ВысшееОбразованиеБакалавриат), ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.ВысшееОбразованиеСпециалитетМагистратура))
		|			ТОГДА 1
		|		КОГДА КадровыеДанныеСотрудников.Образование1ВидОбразования В (ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.СреднееПрофессиональноеОбразование))
		|			ТОГДА 2
		|		КОГДА КадровыеДанныеСотрудников.Образование1ВидОбразования В (ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.НачальноеПрофессиональноеОбразование))
		|			ТОГДА 3
		|		КОГДА КадровыеДанныеСотрудников.Образование1ВидОбразования В (ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.СреднееПолноеОбщееОбразование), ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.СреднееОбщееОбразование))
		|			ТОГДА 4
		|		КОГДА КадровыеДанныеСотрудников.Образование1ВидОбразования В (ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.ОсновноеОбщееОбразование))
		|			ТОГДА 5
		|		ИНАЧЕ 6
		|	КОНЕЦ КАК Колонка6,
		|	КадровыеДанныеСотрудников.СтажРаботыНаПредприятииЛет + КадровыеДанныеСотрудников.СтажРаботыНаПредприятииМесяцев / 12 КАК Колонка7,
		|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Должность КАК Справочник.Должности).ОКПДТРКатегория КАК Колонка13
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|ГДЕ
		|	КадровыеДанныеСотрудников.ДоляНеполногоРабочегоВремени >= 1
		|	И КадровыеДанныеСотрудников.ДатаПриема > ДАТАВРЕМЯ(1, 1, 1)
		|	И КадровыеДанныеСотрудников.ДатаПриема <= &НачалоПериодаОтчета
		|	И (КадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ КОНЕЦПЕРИОДА(КадровыеДанныеСотрудников.ДатаУвольнения, ДЕНЬ) >= &КонецПериодаОтчета)
		|	И ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Должность КАК Справочник.Должности).ОКПДТРКатегория <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	КадровыеДанныеСотрудников.Сотрудник.Наименование";
	
	Если Не  ТолькоДолжностиСОКПДТРКатегорией Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Должность КАК Справочник.Должности).ОКПДТРКатегория <> """"", "");
	КонецЕсли;
	
	ВсеСотрудникиОрганизации = Запрос.Выполнить().Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВсеСотрудникиОрганизации;
	
КонецФункции

Функция СотрудникиДляЗаполненияРаздела2Формы57Т(Организация, ПериодОтчета, ВсеСотрудникиПоКатегориям)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получение ранее отобранных сотрудников
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодОтчета", ПериодОтчета);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Форма57ТРаздел2Сотрудники.Сотрудник КАК Сотрудник,
		|	Форма57ТРаздел2Сотрудники.НомерСтрокиОтчета КАК НомерСтрокиОтчета
		|ИЗ
		|	РегистрСведений.Форма57ТРаздел2Сотрудники КАК Форма57ТРаздел2Сотрудники
		|ГДЕ
		|	Форма57ТРаздел2Сотрудники.Организация = &Организация
		|	И Форма57ТРаздел2Сотрудники.ПериодОтчета = &ПериодОтчета";
	
	ОтобранныеСотрудникиРегистра = Запрос.Выполнить().Выгрузить();
	ОтобранныеСотрудникиРегистра.Индексы.Добавить("Сотрудник");
	
	ОбщееЧислоРаботников = ВсеСотрудникиПоКатегориям.Количество();
	
	Если ОбщееЧислоРаботников < 50 Тогда
		ЧислоОбследуемых = 8;
	ИначеЕсли ОбщееЧислоРаботников < 100 Тогда
		ЧислоОбследуемых = 12;
	ИначеЕсли ОбщееЧислоРаботников < 250 Тогда
		ЧислоОбследуемых = 16;
	ИначеЕсли ОбщееЧислоРаботников < 500 Тогда
		ЧислоОбследуемых = 20;
	ИначеЕсли ОбщееЧислоРаботников < 700 Тогда
		ЧислоОбследуемых = 24;
	ИначеЕсли ОбщееЧислоРаботников < 1000 Тогда
		ЧислоОбследуемых = 32;
	ИначеЕсли ОбщееЧислоРаботников < 1500 Тогда
		ЧислоОбследуемых = 40;
	ИначеЕсли ОбщееЧислоРаботников < 2000 Тогда
		ЧислоОбследуемых = 48;
	ИначеЕсли ОбщееЧислоРаботников < 4000 Тогда
		ЧислоОбследуемых = 56;
	Иначе
		ЧислоОбследуемых = 64;
	КонецЕсли;
	
	ИнтервалОтбора = ОбщееЧислоРаботников / ЧислоОбследуемых;
	
	ВсеСотрудникиПоКатегориям1Категории = ВсеСотрудникиПоКатегориям.Скопировать(Новый Структура("Колонка13", "1"));
	ВсеСотрудникиПоКатегориям2Категории = ВсеСотрудникиПоКатегориям.Скопировать(Новый Структура("Колонка13", "2"));
	ВсеСотрудникиПоКатегориям3Категории = ВсеСотрудникиПоКатегориям.Скопировать(Новый Структура("Колонка13", "3"));
	ВсеСотрудникиПоКатегориям4Категории = ВсеСотрудникиПоКатегориям.Скопировать(Новый Структура("Колонка13", "4"));
	
	Форма57ТРаздел2Сотрудники = ВсеСотрудникиПоКатегориям.СкопироватьКолонки();
	ВсеОтобранныеСотрудникиРегистраНайдены = Не ОтобранныеСотрудникиРегистра.Количество() > 0;
	
	Генератор = Новый ГенераторСлучайныхЧисел();
	НачалоОтбора = Генератор.СлучайноеЧисло(1, ИнтервалОтбора);
	
	Для НомерСтрокиОтчета = 1 По ЧислоОбследуемых Цикл
		
		НомерСтрокиОтбора = НачалоОтбора + ИнтервалОтбора * (НомерСтрокиОтчета - 1);
		
		Если Не ВсеОтобранныеСотрудникиРегистраНайдены Тогда
			
			СотрудникНайден = Ложь;
			Для Каждого СтрокаОтобранныеСотрудникиРегистра Из ОтобранныеСотрудникиРегистра Цикл
				
				НайденнаяСтрока = ВсеСотрудникиПоКатегориям.Найти(СтрокаОтобранныеСотрудникиРегистра.Сотрудник, "Сотрудник");
				Если НайденнаяСтрока <> Неопределено Тогда
					
					СотрудникНайден = Истина;
					ЗаполнитьЗначенияСвойств(Форма57ТРаздел2Сотрудники.Добавить(), НайденнаяСтрока);
					
					ОтобранныеСотрудникиРегистра.Удалить(СтрокаОтобранныеСотрудникиРегистра);
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если СотрудникНайден Тогда
				Продолжить;
			Иначе
				ВсеОтобранныеСотрудникиРегистраНайдены = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		НомерСтрокиДляПоиска = Цел(НомерСтрокиОтбора);
		НайденнаяСтрокаСотрудника = Неопределено;
		ПоискСНачалаКоллекции = Ложь;
		
		Пока НайденнаяСтрокаСотрудника = Неопределено Цикл
			
			СтрокаСотрудника = Неопределено;
			Если НомерСтрокиДляПоиска <= ВсеСотрудникиПоКатегориям1Категории.Количество() Тогда
				СтрокаСотрудника = ВсеСотрудникиПоКатегориям1Категории[НомерСтрокиДляПоиска - 1];
			ИначеЕсли НомерСтрокиДляПоиска <= ВсеСотрудникиПоКатегориям1Категории.Количество() + ВсеСотрудникиПоКатегориям2Категории.Количество() Тогда
				СтрокаСотрудника = ВсеСотрудникиПоКатегориям2Категории[НомерСтрокиДляПоиска - ВсеСотрудникиПоКатегориям1Категории.Количество() - 1];
			ИначеЕсли НомерСтрокиДляПоиска <= ВсеСотрудникиПоКатегориям1Категории.Количество() + ВсеСотрудникиПоКатегориям2Категории.Количество() + ВсеСотрудникиПоКатегориям3Категории.Количество() Тогда
				СтрокаСотрудника = ВсеСотрудникиПоКатегориям3Категории[НомерСтрокиДляПоиска - ВсеСотрудникиПоКатегориям1Категории.Количество() - ВсеСотрудникиПоКатегориям2Категории.Количество() - 1];
			ИначеЕсли НомерСтрокиДляПоиска <= ВсеСотрудникиПоКатегориям1Категории.Количество() + ВсеСотрудникиПоКатегориям2Категории.Количество() + ВсеСотрудникиПоКатегориям3Категории.Количество() +  + ВсеСотрудникиПоКатегориям4Категории.Количество() Тогда
				СтрокаСотрудника = ВсеСотрудникиПоКатегориям4Категории[НомерСтрокиДляПоиска - ВсеСотрудникиПоКатегориям1Категории.Количество() - ВсеСотрудникиПоКатегориям2Категории.Количество() -  + ВсеСотрудникиПоКатегориям3Категории.Количество() - 1];
			КонецЕсли;
			
			Если СтрокаСотрудника <> Неопределено
				И Форма57ТРаздел2Сотрудники.Найти(СтрокаСотрудника.Сотрудник, "Сотрудник") = Неопределено Тогда
				
				НайденнаяСтрокаСотрудника = СтрокаСотрудника;
				
			Иначе
				
				НомерСтрокиДляПоиска = НомерСтрокиДляПоиска + 1;
				Если НомерСтрокиДляПоиска > ВсеСотрудникиПоКатегориям.Количество() Тогда
					
					Если ПоискСНачалаКоллекции Тогда
						Прервать;
					Иначе
						
						НомерСтрокиДляПоиска = 1;
						ПоискСНачалаКоллекции = Истина;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НайденнаяСтрокаСотрудника = Неопределено Тогда
			Прервать;
		Иначе
			ЗаполнитьЗначенияСвойств(Форма57ТРаздел2Сотрудники.Добавить(), НайденнаяСтрокаСотрудника);
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьСотрудниковКатегорииВФорме57Т(Форма57ТРаздел2Сотрудники, ВсеСотрудникиПоКатегориям1Категории);
	ДобавитьСотрудниковКатегорииВФорме57Т(Форма57ТРаздел2Сотрудники, ВсеСотрудникиПоКатегориям2Категории);
	ДобавитьСотрудниковКатегорииВФорме57Т(Форма57ТРаздел2Сотрудники, ВсеСотрудникиПоКатегориям3Категории);
	ДобавитьСотрудниковКатегорииВФорме57Т(Форма57ТРаздел2Сотрудники, ВсеСотрудникиПоКатегориям4Категории);
	
	Если Форма57ТРаздел2Сотрудники.Количество() > 0 Тогда
		
		Форма57ТРаздел2Сотрудники.Сортировать("Колонка13,Наименование");
		Форма57ТРаздел2Сотрудники.Колонки.Добавить("НомерСтрокиОтчета");
		
		НаборЗаписей = РегистрыСведений.Форма57ТРаздел2Сотрудники.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Отбор.ПериодОтчета.Установить(ПериодОтчета);
		
		Для НомерСтрокиОтчета = 1 По Форма57ТРаздел2Сотрудники.Количество() Цикл
			
			СтрокаФормы57ТРаздел2Сотрудники = Форма57ТРаздел2Сотрудники[НомерСтрокиОтчета - 1];
			
			Запись = НаборЗаписей.Добавить();
			Запись.Организация = Организация;
			Запись.ПериодОтчета = ПериодОтчета;
			Запись.Сотрудник = СтрокаФормы57ТРаздел2Сотрудники.Сотрудник;
			Запись.НомерСтрокиОтчета = НомерСтрокиОтчета;
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Форма57ТРаздел2Сотрудники;
	
КонецФункции

Процедура ЗаполнитьСотрудниковРаздела2Формы57Т(Организация, ПериодОтчета) Экспорт
	
	ВсеСотрудникиПоКатегориям = СотрудникиДляЗаполненияРаздела1Формы57Т(Организация, ПериодОтчета);
	СотрудникиДляЗаполненияРаздела2Формы57Т(Организация, ПериодОтчета, ВсеСотрудникиПоКатегориям);
	
КонецПроцедуры

Процедура ДобавитьСотрудниковКатегорииВФорме57Т(Форма57ТРаздел2Сотрудники, СотрудникиКатегории)
	
	Если СотрудникиКатегории.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма57ТРаздел2Сотрудники.НайтиСтроки(Новый Структура("Колонка13", СотрудникиКатегории[0].Колонка13)).Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДобавленных = 0;
	Для Каждого СтрокаСотрудника Из СотрудникиКатегории Цикл
		
		Если Форма57ТРаздел2Сотрудники.Найти(СтрокаСотрудника.Сотрудник, "Сотрудник") <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Форма57ТРаздел2Сотрудники.Добавить(), СтрокаСотрудника);
		
		КоличествоДобавленных = КоличествоДобавленных + 1;
		Если КоличествоДобавленных > 1 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьФорма57ТРаздел2СотрудникиДаннымиОНачислениях(Форма57ТРаздел2Сотрудники, ДанныеОрганизации, УчтенаКвартальнаяПремия, УчестьТолькоКвартальнуюПремию = Ложь)
	
	Если Форма57ТРаздел2Сотрудники.Колонки.Найти("Колонка9") = Неопределено Тогда
		
		Форма57ТРаздел2Сотрудники.Колонки.Добавить("Колонка9", Новый ОписаниеТипов("Число"));
		Форма57ТРаздел2Сотрудники.Колонки.Добавить("Колонка10", Новый ОписаниеТипов("Число"));
		Форма57ТРаздел2Сотрудники.Колонки.Добавить("Колонка11", Новый ОписаниеТипов("Число"));
		Форма57ТРаздел2Сотрудники.Колонки.Добавить("Колонка12", Новый ОписаниеТипов("Число"));
		
	КонецЕсли;
	
	Для Каждого СтрокаФормы Из Форма57ТРаздел2Сотрудники Цикл
		
		СтрокиНачислений = ДанныеОрганизации.Строки.НайтиСтроки(Новый Структура("Сотрудник", СтрокаФормы.Сотрудник));
		Для Каждого СтрокаНачислений Из СтрокиНачислений Цикл
			
			НачисленоРаботникам = СтрокаНачислений.НачисленоРаботникамСписочногоСостава;
			
			Если СтрокаНачислений.ВидРасчетаКатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КвартальнаяПремия Тогда
				
				УчтенаКвартальнаяПремия = Истина;
				НачисленоРаботникам = НачисленоРаботникам / 3;
				
			ИначеЕсли УчестьТолькоКвартальнуюПремию Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаНачислений.ВидРасчетаКатегорияСтатистическогоНаблюдения57Т = Перечисления.КатегорииСтатистическогоНаблюдения57Т.ТарифныйЗаработок Тогда
				
				СтрокаФормы.Колонка9 = СтрокаФормы.Колонка9 + НачисленоРаботникам;
				СтрокаФормы.Колонка12 = СтрокаФормы.Колонка12 + СтрокаНачислений.ОтработаноЧасов;
				
			ИначеЕсли СтрокаНачислений.ВидРасчетаКатегорияСтатистическогоНаблюдения57Т = Перечисления.КатегорииСтатистическогоНаблюдения57Т.ВыплатыПоРайонномуРегулированию Тогда
				
				СтрокаФормы.Колонка10 = СтрокаФормы.Колонка10 + НачисленоРаботникам;
				
			ИначеЕсли СтрокаНачислений.ВидРасчетаКатегорияСтатистическогоНаблюдения57Т = Перечисления.КатегорииСтатистическогоНаблюдения57Т.ДругиеВыплаты Тогда
				
				СтрокаФормы.Колонка11 = СтрокаФормы.Колонка11 + НачисленоРаботникам;
				
			Иначе
				Продолжить;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Форма1

// Получает показатели, которые могут быть заполнены при заполнении отчета.
// 
// Параметры:
//  ПоказателиОтчета - структура
//
Процедура ДобавитьПоказателиФормы1_2016(ПоказателиОтчета)
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач2", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач3", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач4", Истина, Ложь);
	
	Для НомерСтрокиОтчета = 1 По 21 Цикл
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П01"
			 + Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "03", Истина, Ложь);
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П01"
			 + Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "04", Истина, Ложь);
		
	КонецЦикла;
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П012303", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П012403", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П012503", Истина, Ложь);
	
КонецПроцедуры

// Заполняет показатели отчета.
// 
// Параметры:
//  ПараметрыОтчета - структура.
//  Контейнер - структура - содержит все показатели отчета и их значения.
//
Процедура ЗаполнитьПоказателиФормы1_2016(ПараметрыОтчета, Контейнер)
	
	// Получение Кадровых данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	
	ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаНачалаПериодаОтчета", ПараметрыОтчетаДляЗаполненияКадры.мДатаНачалаПериодаОтчета);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаКонцаПериодаОтчета", ПараметрыОтчетаДляЗаполненияКадры.мДатаКонцаПериодаОтчета);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ТолькоОсновноеМестоРаботы", Истина);
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(
		"РегламентированныйОтчетСтатистикаФорма57Т",
		ПараметрыОтчетаДляЗаполненияКадры,
		ДанныеОтчета);
	
	Если ДанныеОтчета.Строки.Количество() > 0 Тогда
		
		СтрокаОрганизации = ДанныеОтчета.Строки[0];
		ВсеСотрудникиПоКатегориям = СотрудникиДляЗаполненияРаздела1Формы57Т(
			ПараметрыОтчета.Организация, НачалоМесяца(ПараметрыОтчетаДляЗаполненияКадры.ДатаКонцаПериодаОтчета), СтрокаОрганизации, Ложь);
		
		// Получение зарплатных данных
		ДанныеОтчета = Новый ДеревоЗначений;
		
		ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаНачалаПериодаОтчета", ПараметрыОтчетаДляЗаполненияЗарплата.мДатаНачалаПериодаОтчета);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаКонцаПериодаОтчета", ПараметрыОтчетаДляЗаполненияЗарплата.мДатаКонцаПериодаОтчета);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ТолькоОсновноеМестоРаботы", Истина);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ОтборСотрудников", ОбщегоНазначения.ВыгрузитьКолонку(ВсеСотрудникиПоКатегориям, "Сотрудник"));
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключатьГодовыеПремии", Истина);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключаяПерерасчеты", Истина);
		ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
			"РегламентированныйОтчетСтатистикаФорма57Т",
			ПараметрыОтчетаДляЗаполненияЗарплата,
			ДанныеОтчета);
		
		Если ДанныеОтчета.Строки.Количество() > 0 Тогда
			
			ТаблицаСотрудников = Новый ТаблицаЗначений;
			ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
			ТаблицаСотрудников.Колонки.Добавить("Начислено", Новый ОписаниеТипов("Число"));
			
			СтрокаОрганизации = ДанныеОтчета.Строки[0];
			
			УчтенаКвартальнаяПремия = Ложь;
			ДополнитьТаблицуСотрудникамиСЗаработкомФорма1(ТаблицаСотрудников, СтрокаОрганизации, УчтенаКвартальнаяПремия);
			
			Если Не УчтенаКвартальнаяПремия Тогда
				
				ДанныеОтчета = Новый ДеревоЗначений;
				
				ПараметрыОтчетаДляЗаполненияЗарплата.ДатаКонцаПериодаОтчета = ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета - 1;
				ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета = ДобавитьМесяц(ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета, -3);
				ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключаяПерерасчеты", Истина);
				ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
					"РегламентированныйОтчетСтатистикаФорма57Т",
					ПараметрыОтчетаДляЗаполненияЗарплата,
					ДанныеОтчета);
				
				Если ДанныеОтчета.Строки.Количество() > 0 Тогда
					
					СтрокаОрганизации = ДанныеОтчета.Строки[0];
					ДополнитьТаблицуСотрудникамиСЗаработкомФорма1(ТаблицаСотрудников, СтрокаОрганизации, УчтенаКвартальнаяПремия, Истина);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТаблицаСотрудников.Количество() > 0 Тогда
				
				ТаблицаСотрудников.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число"));
				ТаблицаСотрудников.Колонки.Добавить("НомерСтрокиОтчета", Новый ОписаниеТипов("Число"));
				
				П012303 = 0;
				
				ВесаСтрок = Новый Массив;
				ВесаСтрок.Добавить(7500);
				ВесаСтрок.Добавить(9000);
				ВесаСтрок.Добавить(10600);
				ВесаСтрок.Добавить(12200);
				ВесаСтрок.Добавить(13800);
				ВесаСтрок.Добавить(15400);
				ВесаСтрок.Добавить(17000);
				ВесаСтрок.Добавить(18600);
				ВесаСтрок.Добавить(21800);
				ВесаСтрок.Добавить(25000);
				ВесаСтрок.Добавить(30000);
				ВесаСтрок.Добавить(35000);
				ВесаСтрок.Добавить(40000);
				ВесаСтрок.Добавить(50000);
				ВесаСтрок.Добавить(60000);
				ВесаСтрок.Добавить(75000);
				ВесаСтрок.Добавить(100000);
				ВесаСтрок.Добавить(250000);
				ВесаСтрок.Добавить(500000);
				ВесаСтрок.Добавить(1000000);
				
				Для Каждого СтрокаСотрудника Из ТаблицаСотрудников Цикл
					
					СтрокаСотрудника.Счетчик = 1;
					Начислено = Цел(СтрокаСотрудника.Начислено);
					
					Для НомерСтроки = 1 По ВесаСтрок.Количество() Цикл
						
						Если Начислено <= ВесаСтрок[НомерСтроки - 1] Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					СтрокаСотрудника.НомерСтрокиОтчета = НомерСтроки;
					
					Если Начислено = 7500 Тогда
						П012303 = П012303 + 1;
					КонецЕсли;
					
				КонецЦикла;
				
				ТаблицаСотрудников.Свернуть("НомерСтрокиОтчета","Счетчик,Начислено");
				Для НомерСтрокиОтчета = 1 По 21 Цикл
					
					СтрокаОтчета = ТаблицаСотрудников.Найти(НомерСтрокиОтчета, "НомерСтрокиОтчета");
					Если СтрокаОтчета = Неопределено Тогда
						Счетчик = 0;
						Начислено = 0;
					Иначе
						Счетчик = СтрокаОтчета.Счетчик;
						Начислено = СтрокаОтчета.Начислено;
					КонецЕсли;
					
					Контейнер.ПолеТабличногоДокументаФормаОтчета["П01"
						+ Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "03"] = Счетчик;
					
					Контейнер.ПолеТабличногоДокументаФормаОтчета["П01"
						+ Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "04"] = Начислено;
					
				КонецЦикла;
				
				Контейнер.ПолеТабличногоДокументаФормаОтчета["П012303"] = П012303;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	П012403 = 0;
	П012503 = 0;
	
	ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаНачалаПериодаОтчета", '20170401000000');
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаКонцаПериодаОтчета", '20170430235959');
	ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчетаДляЗаполненияЗарплата);
	
	// Получение зарплатных данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияЗарплата, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполненияЗарплата.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		
		ИндексТекущейСтроки = 2;
		КоличествоСтрок = 11;
		Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
			Для Каждого СтрокаОКВЭДПериод Из СтрокаОКВЭД.Строки Цикл
				
				Если СтрокаОКВЭДПериод.МесяцНачисления <> ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета Тогда
					Продолжить;
				КонецЕсли;
				
				П012503 = П012503 + СтрокаОКВЭДПериод.НачисленоРаботникамСписочногоСостава;
				
				ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
				Если ИндексТекущейСтроки > КоличествоСтрок Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Получение кадровых данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияКадры, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполненияКадры.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		
		ИндексТекущейСтроки = 2;
		КоличествоСтрок = 11;
		Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
			
			П012403 = П012403 + СтрокаОКВЭД.СредняяЧисленностьЗаПериодСписочногоСостава;
			
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
			Если ИндексТекущейСтроки > КоличествоСтрок Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П012403"] = П012403;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П012503"] = П012503;
	
КонецПроцедуры

// Получает показатели, которые могут быть заполнены при заполнении отчета.
// 
// Параметры:
//  ПоказателиОтчета - структура
//
Процедура ДобавитьПоказателиФормы1_2019(ПоказателиОтчета)
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "ОргКодОКПО", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач1", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач2", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач3", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "КодЗнач4", Истина, Ложь);
	
	Для НомерСтрокиОтчета = 1 По 28 Цикл
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П01"
			 + Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "03", Истина, Ложь);
		
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П01"
			 + Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "04", Истина, Ложь);
		
	КонецЦикла;
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П013003", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П013103", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П013203", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П013303", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П013403", Истина, Ложь);
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П013503", Истина, Ложь);
	
КонецПроцедуры

// Заполняет показатели отчета.
// 
// Параметры:
//  ПараметрыОтчета - структура.
//  Контейнер - структура - содержит все показатели отчета и их значения.
//
Процедура ЗаполнитьПоказателиФормы1_2019(ПараметрыОтчета, Контейнер)
	
	ДатаНачалаПериодаОтчета = '20190401000000';
	ДатаКонцаПериодаОтчета = '20190430235959';
	
	// Получение Кадровых данных отчета
	ДанныеОтчета = Новый ДеревоЗначений;
	
	ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаНачалаПериодаОтчета", ДатаНачалаПериодаОтчета);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаКонцаПериодаОтчета", ДатаКонцаПериодаОтчета);
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(
		"РегламентированныйОтчетСтатистикаФорма57Т",
		ПараметрыОтчетаДляЗаполненияКадры,
		ДанныеОтчета);
	
	СотрудникиСМинимальнойЗарплатой = Новый Массив;
	
	Если ДанныеОтчета.Строки.Количество() > 0 Тогда
		
		СтрокаОрганизации = ДанныеОтчета.Строки[0];
		ВсеСотрудникиПоКатегориям = СотрудникиДляЗаполненияФормы1(ПараметрыОтчета.Организация, СтрокаОрганизации.Строки.ВыгрузитьКолонку("Сотрудник"), ДатаНачалаПериодаОтчета);
		
		// Получение зарплатных данных
		ДанныеОтчета = Новый ДеревоЗначений;
		
		ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаНачалаПериодаОтчета", ДатаНачалаПериодаОтчета);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаКонцаПериодаОтчета", ДатаКонцаПериодаОтчета);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ОтборСотрудников", ОбщегоНазначения.ВыгрузитьКолонку(ВсеСотрудникиПоКатегориям, "Сотрудник"));
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключатьГодовыеПремии", Истина);
		ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ИсключаяПерерасчеты", Истина);
		ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
			"РегламентированныйОтчетСтатистикаФорма57Т",
			ПараметрыОтчетаДляЗаполненияЗарплата,
			ДанныеОтчета);
		
		Если ДанныеОтчета.Строки.Количество() > 0 Тогда
			
			ТаблицаСотрудников = Новый ТаблицаЗначений;
			ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
			ТаблицаСотрудников.Колонки.Добавить("Начислено", Новый ОписаниеТипов("Число"));
			
			СтрокаОрганизации = ДанныеОтчета.Строки[0];
			
			УчтенаКвартальнаяПремия = Ложь;
			ДополнитьТаблицуСотрудникамиСЗаработкомФорма1(ТаблицаСотрудников, СтрокаОрганизации, УчтенаКвартальнаяПремия);
			
			Если Не УчтенаКвартальнаяПремия Тогда
				
				ДанныеОтчета = Новый ДеревоЗначений;
				
				ПараметрыОтчетаДляЗаполненияЗарплата.ДатаКонцаПериодаОтчета = ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета - 1;
				ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета = ДобавитьМесяц(ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета, -3);
				ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(
					"РегламентированныйОтчетСтатистикаФорма57Т",
					ПараметрыОтчетаДляЗаполненияЗарплата,
					ДанныеОтчета);
				
				Если ДанныеОтчета.Строки.Количество() > 0 Тогда
					
					СтрокаОрганизации = ДанныеОтчета.Строки[0];
					ДополнитьТаблицуСотрудникамиСЗаработкомФорма1(ТаблицаСотрудников, СтрокаОрганизации, УчтенаКвартальнаяПремия, Истина);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ТаблицаСотрудников.Количество() > 0 Тогда
				
				// Сворачиваем до Физических лиц
				ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВсеСотрудникиПоКатегориям.ВыгрузитьКолонку("Сотрудник"), "ФизическоеЛицо");
				ТаблицаСотрудников.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
				Для Каждого СтрокаСотрудника Из ТаблицаСотрудников Цикл
					СтрокаСотрудника.ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаСотрудника.Сотрудник];
				КонецЦикла;
				ТаблицаСотрудников.Свернуть("ФизическоеЛицо", "Начислено");
				ТаблицаСотрудников.Колонки.ФизическоеЛицо.Имя = "Сотрудник";
				
				ТаблицаСотрудников.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число"));
				ТаблицаСотрудников.Колонки.Добавить("НомерСтрокиОтчета", Новый ОписаниеТипов("Число"));
				
				П012303 = 0;
				
				ВесаСтрок = Новый Массив;
				ВесаСтрок.Добавить(11280);
				ВесаСтрок.Добавить(12140);
				ВесаСтрок.Добавить(13000);
				ВесаСтрок.Добавить(13850);
				ВесаСтрок.Добавить(14700);
				ВесаСтрок.Добавить(15850);
				ВесаСтрок.Добавить(17000);
				ВесаСтрок.Добавить(18050);
				ВесаСтрок.Добавить(19200);
				ВесаСтрок.Добавить(20350);
				ВесаСтрок.Добавить(21450);
				ВесаСтрок.Добавить(22560);
				ВесаСтрок.Добавить(23690);
				ВесаСтрок.Добавить(24820);
				ВесаСтрок.Добавить(27100);
				ВесаСтрок.Добавить(29300);
				ВесаСтрок.Добавить(31600);
				ВесаСтрок.Добавить(33900);
				ВесаСтрок.Добавить(40000);
				ВесаСтрок.Добавить(45000);
				ВесаСтрок.Добавить(50000);
				ВесаСтрок.Добавить(60000);
				ВесаСтрок.Добавить(75000);
				ВесаСтрок.Добавить(100000);
				ВесаСтрок.Добавить(250000);
				ВесаСтрок.Добавить(500000);
				ВесаСтрок.Добавить(1000000);
				
				П013003 = 0;
				Для Каждого СтрокаСотрудника Из ТаблицаСотрудников Цикл
					
					СтрокаСотрудника.Счетчик = 1;
					Начислено = Цел(СтрокаСотрудника.Начислено);
					
					Для НомерСтроки = 1 По ВесаСтрок.Количество() Цикл
						
						Если Начислено <= ВесаСтрок[НомерСтроки - 1] Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					СтрокаСотрудника.НомерСтрокиОтчета = НомерСтроки;
					
					Если Начислено = 11280 Тогда
						П013003 = П013003 + 1;
					КонецЕсли;
					
					Если Начислено <= 11280 Тогда
						СотрудникиСМинимальнойЗарплатой.Добавить(СтрокаСотрудника.Сотрудник);
					КонецЕсли;
					
				КонецЦикла;
				
				ТаблицаСотрудников.Свернуть("НомерСтрокиОтчета","Счетчик,Начислено");
				Для НомерСтрокиОтчета = 1 По 28 Цикл
					
					СтрокаОтчета = ТаблицаСотрудников.Найти(НомерСтрокиОтчета, "НомерСтрокиОтчета");
					Если СтрокаОтчета = Неопределено Тогда
						Счетчик = 0;
						Начислено = 0;
					Иначе
						Счетчик = СтрокаОтчета.Счетчик;
						Начислено = СтрокаОтчета.Начислено;
					КонецЕсли;
					
					Контейнер.ПолеТабличногоДокументаФормаОтчета["П01"
						+ Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "03"] = Счетчик;
					
					Контейнер.ПолеТабличногоДокументаФормаОтчета["П01"
						+ Формат(НомерСтрокиОтчета, "ЧЦ=2; ЧВН=") + "04"] = Начислено;
					
				КонецЦикла;
				
				Контейнер.ПолеТабличногоДокументаФормаОтчета["П013003"] = П013003;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	П013103 = 0;
	П013203 = 0;
	П013303 = 0;
	П013403 = 0;
	П013503 = 0;
	
	ПараметрыОтчетаДляЗаполненияЗарплата = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаНачалаПериодаОтчета", '20190401000000');
	ПараметрыОтчетаДляЗаполненияЗарплата.Вставить("ДатаКонцаПериодаОтчета", '20190430235959');
	ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчетаДляЗаполненияЗарплата);
	
	// Получение зарплатных данных отчета (строки 34 и 35)
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияЗарплата, ДанныеОтчета);
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполненияЗарплата.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		ИндексТекущейСтроки = 2;
		КоличествоСтрок = 11;
		Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
			Для Каждого СтрокаОКВЭДПериод Из СтрокаОКВЭД.Строки Цикл
				Если СтрокаОКВЭДПериод.МесяцНачисления <> ПараметрыОтчетаДляЗаполненияЗарплата.ДатаНачалаПериодаОтчета Тогда
					Продолжить;
				КонецЕсли;
				
				П013503 = П013503 + СтрокаОКВЭДПериод.НачисленоРаботникамСписочногоСостава;
				П013403 = П013403 + СтрокаОКВЭДПериод.НачисленоРаботникамСписочногоСостава 
					+ СтрокаОКВЭДПериод.НачисленоВнешнимСовместителям
					+ СтрокаОКВЭДПериод.НачисленоРаботникамПоДоговорамГПХ;
					
				ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
				Если ИндексТекущейСтроки > КоличествоСтрок Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	// Получение кадровых данных отчета П-4 (строка 33)
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияКадры, ДанныеОтчета);
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполненияКадры.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		ИндексТекущейСтроки = 2;
		КоличествоСтрок = 11;
		Для Каждого СтрокаОКВЭД Из СтрокаОрганизации.Строки Цикл
			П013303 = П013303 + СтрокаОКВЭД.СредняяЧисленностьЗаПериодСписочногоСостава;
			
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
			Если ИндексТекущейСтроки > КоличествоСтрок Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Получение кадровых данных отчета П-4(НЗ) (строка 32)
	ДанныеОтчета = Новый ДеревоЗначений;
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ТолькоОсновноеМестоРаботы", Истина);
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаНачалаПериодаОтчета", '20190301000000');
	ПараметрыОтчетаДляЗаполненияКадры.Вставить("ДатаКонцаПериодаОтчета", '20190331235959');
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыОтчетаДляЗаполненияКадры, ДанныеОтчета);
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчета.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		П013203 = СтрокаОрганизации.ЧисленностьНаНачалоСледующегоПериодаСписочногоСостава;
	КонецЕсли;
	
	// Получение кадровых данных отчета П-4(НЗ) (строка 31)
	Если СотрудникиСМинимальнойЗарплатой.Количество() Тогда
		ДанныеОтчета = Новый ДеревоЗначений;
		ПараметрыОтчетаДляЗаполненияКадры = СкопироватьКоллекцию(ПараметрыОтчетаДляЗаполненияЗарплата);
		ПараметрыОтчетаДляЗаполненияКадры.Вставить("ОтборСотрудников", СотрудникиСМинимальнойЗарплатой);
		ДанныеДляЗаполненияРегламентированнойОтчетностиП4НЗ("РегламентированныйОтчетСтатистикаФормаП4НЗ", ПараметрыОтчетаДляЗаполненияКадры, ДанныеОтчета);
		СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчета.Организация, "Организация");
		Если СтрокаОрганизации <> Неопределено Тогда
			П013103 = СтрокаОрганизации.ЧисленностьРаботавшихНеполноеРабочееВремя;
		КонецЕсли;
	КонецЕсли;
	
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П013103"] = П013103;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П013203"] = П013203;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П013303"] = П013303;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П013403"] = П013403;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П013503"] = П013503;
	
КонецПроцедуры

Процедура ДополнитьТаблицуСотрудникамиСЗаработкомФорма1(ТаблицаСотрудников, СтрокаОрганизации, УчтенаКвартальнаяПремия, УчестьТолькоКвартальнуюПремию = Ложь)
	
	Для Каждого СтрокаНачислений Из СтрокаОрганизации.Строки Цикл
		
		НачисленоРаботникам = СтрокаНачислений.НачисленоРаботникамСписочногоСостава;
		
		Если СтрокаНачислений.ВидРасчетаКатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КвартальнаяПремия Тогда
			
			УчтенаКвартальнаяПремия = Истина;
			НачисленоРаботникам = НачисленоРаботникам / 3;
			
		ИначеЕсли УчестьТолькоКвартальнуюПремию Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаСотрудника = ТаблицаСотрудников.Найти(СтрокаНачислений.Сотрудник, "Сотрудник");
		Если СтрокаСотрудника = Неопределено Тогда
			СтрокаСотрудника = ТаблицаСотрудников.Добавить();
			СтрокаСотрудника.Сотрудник = СтрокаНачислений.Сотрудник;
		КонецЕсли;
		
		СтрокаСотрудника.Начислено = СтрокаСотрудника.Начислено + НачисленоРаботникам;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СотрудникиДляЗаполненияФормы1(Организация, СписокСотрудников, ПериодОтчета)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	Запрос.УстановитьПараметр("НачалоПериодаОтчета", НачалоМесяца(ПериодОтчета));
	Запрос.УстановитьПараметр("КонецПериодаОтчета", КонецМесяца(ПериодОтчета));
	
	// Отбор полностью отработавших сотрудников
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	&НачалоПериодаОтчета КАК ДатаНачала,
		|	&КонецПериодаОтчета КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&СписокСотрудников)";
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = УчетРабочегоВремениРасширенный.ПараметрыДляЗапросВТДанныеУчетаВремениИСостоянийСотрудников();
	ПараметрыЗаполнения.ИмяВТСотрудники = "ВТСотрудники";
	ПараметрыЗаполнения.ИмяВТРезультат = "ВТДанныеУчетаВремени";
	ПараметрыЗаполнения.РассчитыватьПлановоеВремя = Истина;
	ПараметрыЗаполнения.ДатаАктуальности  = КонецМесяца(ПериодОтчета);
	ПараметрыЗаполнения.ДатаНачала = НачалоМесяца(ПериодОтчета);
	ПараметрыЗаполнения.ДатаОкончания = КонецМесяца(ПериодОтчета);;
	ПараметрыЗаполнения.МесяцДатаНачала = НачалоМесяца(ПериодОтчета);;
	ПараметрыЗаполнения.МесяцДатаОкончания = КонецМесяца(ПериодОтчета);;
	ПараметрыЗаполнения.ВыделятьВыходныеВПериодыОтклонений = Ложь;
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеУчетаВремениИСостоянийСотрудников(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыЗаполнения);
	
	ИменаЭлементов = Новый Массив;
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.ВынужденныйПрогул");
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.Простой");
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.ПростойПоВинеРаботодателя");
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.НеоплачиваемыйОтпускПоРазрешениюРаботодателя");
	ОтсутствияДо5Дней = Новый Массив;
	Для Каждого ИмяВидаВремени Из ИменаЭлементов Цикл
		ВидВремени = ОбщегоНазначения.ПредопределенныйЭлемент(ИмяВидаВремени);
		Если ВидВремени <> Неопределено Тогда
			ОтсутствияДо5Дней.Добавить(ВидВремени);
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("ОтсутствияДо5Дней", ОтсутствияДо5Дней);
	
	ИменаЭлементов.Очистить();
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.Болезнь");
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.БолезньБезОплаты");
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.ОтпускПоБеременностиИРодам");
	ИменаЭлементов.Добавить("Справочник.ВидыИспользованияРабочегоВремени.ОтпускПоУходуЗаРебенком");
	Отсутствия = Новый Массив;
	Для Каждого ИмяВидаВремени Из ИменаЭлементов Цикл
		ВидВремени = ОбщегоНазначения.ПредопределенныйЭлемент(ИмяВидаВремени);
		Если ВидВремени <> Неопределено Тогда
			Отсутствия.Добавить(ВидВремени);
		КонецЕсли;
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Отсутствия, ОтсутствияДо5Дней);
	Запрос.УстановитьПараметр("Отсутствия", Отсутствия);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеУчетаВремени.Сотрудник КАК Сотрудник,
	|	ДанныеУчетаВремени.ВидУчетаВремени КАК ВидУчетаВремени,
	|	СУММА(ДанныеУчетаВремени.Дни) КАК Дни
	|ПОМЕСТИТЬ ВТСотрудникиСОтклонениями
	|ИЗ
	|	ВТДанныеУчетаВремени КАК ДанныеУчетаВремени
	|ГДЕ
	|	ДанныеУчетаВремени.ВидУчетаВремени В (&Отсутствия)
	|	И НЕ ДанныеУчетаВремени.ВидУчетаВремени.РабочееВремя
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчетаВремени.Сотрудник,
	|	ДанныеУчетаВремени.ВидУчетаВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеУчетаВремени.Сотрудник КАК Сотрудник,
	|	СУММА(ДанныеУчетаВремени.Дни) КАК Дни
	|ПОМЕСТИТЬ ВТСотрудникиСОтклонениямиДо5Дней
	|ИЗ
	|	ВТДанныеУчетаВремени КАК ДанныеУчетаВремени
	|ГДЕ
	|	ДанныеУчетаВремени.ВидУчетаВремени В(&ОтсутствияДо5Дней)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчетаВремени.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеУчетаВремени.Дни) < 5
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	НАЧАЛОПЕРИОДА(Сотрудники.ДатаОкончания, ДЕНЬ) КАК Период
	|ПОМЕСТИТЬ ВТСотрудникиПериоды
	|ИЗ
	|	ВТСотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиСОтклонениями КАК СотрудникиСОтклонениями
	|		ПО Сотрудники.Сотрудник = СотрудникиСОтклонениями.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиСОтклонениямиДо5Дней КАК ОтклонениямиДо5Дней
	|		ПО Сотрудники.Сотрудник = ОтклонениямиДо5Дней.Сотрудник
	|ГДЕ
	|	(СотрудникиСОтклонениями.Сотрудник ЕСТЬ NULL
	|			ИЛИ ОтклонениямиДо5Дней.Сотрудник ЕСТЬ НЕ NULL )";
	Запрос.Выполнить();
	
	// Получение кадровых данных полностью отработавших сотрудников
	ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
	
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВТ,
		Ложь,
		"ВидЗанятости,
		|ДатаПриема,
		|ДатаУвольнения");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство)
	|	И КадровыеДанныеСотрудников.ДатаПриема > ДАТАВРЕМЯ(1, 1, 1)
	|	И КадровыеДанныеСотрудников.ДатаПриема <= &НачалоПериодаОтчета
	|	И (КадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ КОНЕЦПЕРИОДА(КадровыеДанныеСотрудников.ДатаУвольнения, ДЕНЬ) >= &КонецПериодаОтчета)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КадровыеДанныеСотрудников.Сотрудник.Наименование";
	
	ВсеСотрудникиОрганизации = Запрос.Выполнить().Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВсеСотрудникиОрганизации;
	
КонецФункции

#КонецОбласти

#Область ФормаП4

Процедура ЗаполнитьПоказателиЗарплата_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, СтрокаОКВЭД, СтрокаОКВЭДПериод, ВыводитьКвартальныеПоказатели)
	
	Если Не ВыводитьКвартальныеПоказатели Тогда
		СтрокаОКВЭД.ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава = 0;
		СтрокаОКВЭД.ОтработаноЧасовСНачалаГодаВнешнимиСовместителями = 0;
		СтрокаОКВЭД.НачисленияСоциальногоХарактера = 0;
	КонецЕсли;
	
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "А1"] = СтрокаОКВЭДПериод.НаименованиеОКВЭД;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "В1"] = СтрокаОКВЭДПериод.КодОКВЭД;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "05"] = СтрокаОКВЭД.ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "06"] = СтрокаОКВЭД.ОтработаноЧасовСНачалаГодаВнешнимиСовместителями;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "08"] = Окр(СтрокаОКВЭДПериод.НачисленоРаботникамСписочногоСостава / 1000, 1);
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "09"] = Окр(СтрокаОКВЭДПериод.НачисленоВнешнимСовместителям / 1000, 1);
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "10"] = Окр(СтрокаОКВЭДПериод.НачисленоРаботникамПоДоговорамГПХ / 1000, 1);
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "11"] = Окр(СтрокаОКВЭД.НачисленияСоциальногоХарактера / 1000, 1);
	
	УстановитьПустоеЗначение(СтрокаОКВЭД.ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "05", Контейнер);
	УстановитьПустоеЗначение(СтрокаОКВЭД.ОтработаноЧасовСНачалаГодаВнешнимиСовместителями, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "06", Контейнер);
	
	УстановитьПустоеЗначение(СтрокаОКВЭДПериод.НачисленоРаботникамСписочногоСостава, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "08", Контейнер);
	УстановитьПустоеЗначение(СтрокаОКВЭДПериод.НачисленоВнешнимСовместителям, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "09", Контейнер);
	УстановитьПустоеЗначение(СтрокаОКВЭДПериод.НачисленоРаботникамПоДоговорамГПХ, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "10", Контейнер);
	УстановитьПустоеЗначение(СтрокаОКВЭД.НачисленияСоциальногоХарактера, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "11", Контейнер);
	
КонецПроцедуры

Процедура ЗаполнитьПоказателиКадры_СтатистикаФормаП4_2013Кв1(Контейнер, ИндексТекущейСтроки, СтрокаОКВЭД, СтрокаОКВЭДПериод, ВыводитьКвартальныеПоказатели)
	
	СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодСписочногоСостава = Окр(СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодСписочногоСостава);
	СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодВнешнихСовместителей = Окр(СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодВнешнихСовместителей, 1);
	СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодДоговорников = Окр(СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодДоговорников);
	
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "А1"] = СтрокаОКВЭДПериод.НаименованиеОКВЭД;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П00010" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "В1"] = СтрокаОКВЭДПериод.КодОКВЭД;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "02"] = СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодСписочногоСостава;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "03"] = СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодВнешнихСовместителей;
	Контейнер.ПолеТабличногоДокументаФормаОтчета["П01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "04"] = СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодДоговорников;
	
	УстановитьПустоеЗначениеЧисленности(СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодСписочногоСостава, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "02", Контейнер);
	УстановитьПустоеЗначениеЧисленности(СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодВнешнихСовместителей, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "03", Контейнер);
	УстановитьПустоеЗначениеЧисленности(СтрокаОКВЭДПериод.СредняяЧисленностьЗаПериодДоговорников, "ЧНП01000" + Формат(ИндексТекущейСтроки, "ЧЦ=2; ЧВН=") + "04", Контейнер);
	
КонецПроцедуры

Функция ОписаниеПоказателейОтчета_СтатистикаФормаП4_2017Кв1()
	ОписаниеПоказателейОтчета = Новый ТаблицаЗначений;
	ОписаниеПоказателейОтчета.Колонки.Добавить("ИмяКолонки", Новый ОписаниеТипов("Строка"));
	ОписаниеПоказателейОтчета.Колонки.Добавить("ТипКолонки", Новый ОписаниеТипов("ОписаниеТипов"));
	ОписаниеПоказателейОтчета.Колонки.Добавить("ПутьКДанным", Новый ОписаниеТипов("Строка"));
	ОписаниеПоказателейОтчета.Колонки.Добавить("ПутьКФормату", Новый ОписаниеТипов("Строка"));
	
	ОписаниеПоказателейОтчета.Индексы.Добавить("ИмяКолонки");
	
	// Колонки для общих данных за отчетный период.
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки = "НаименованиеОКВЭД2";
	ОписаниеПоказателя.ТипКолонки = Новый ОписаниеТипов("Строка");
	ОписаниеПоказателя.ПутьКДанным = "П00010##А1";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки = "КодОКВЭД2";
	ОписаниеПоказателя.ТипКолонки = Новый ОписаниеТипов("Строка");
	ОписаниеПоказателя.ПутьКДанным = "П00010##В1";
	
	// Колонки для кадровых данных за отчетный период.
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "СредняяЧисленностьЗаПериодСписочногоСостава";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##02";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##02";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "СредняяЧисленностьЗаПериодВнешнихСовместителей";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##03";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##03";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "СредняяЧисленностьЗаПериодДоговорников";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##04";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##04";
	
	// Колонки для зарплатных данных за отчетный период.
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##05";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##05";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "ОтработаноЧасовСНачалаГодаВнешнимиСовместителями";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##06";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##06";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "НачисленияСоциальногоХарактера";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##11";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##11";
	
	// Колонки для зарплатных данных за месяц начисления.
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "НачисленоРаботникамСписочногоСостава";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##08";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##08";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "НачисленоВнешнимСовместителям";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##09";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##09";
	
	ОписаниеПоказателя = ОписаниеПоказателейОтчета.Добавить();
	ОписаниеПоказателя.ИмяКолонки  = "НачисленоРаботникамПоДоговорамГПХ";
	ОписаниеПоказателя.ТипКолонки   = Новый ОписаниеТипов("Число");
	ОписаниеПоказателя.ПутьКДанным  = "П01000##10";
	ОписаниеПоказателя.ПутьКФормату = "ЧНП01000##10";
	
	Возврат ОписаниеПоказателейОтчета;
КонецФункции

Функция ДанныеОтчета_СтатистикаФормаП4_2017Кв1(ОписаниеПоказателейОтчета, ПараметрыОтчета)
	ДанныеОтчета = Новый ТаблицаЗначений;
	Для Каждого ОписаниеПоказателя Из ОписаниеПоказателейОтчета Цикл
		ДанныеОтчета.Колонки.Добавить(ОписаниеПоказателя.ИмяКолонки, ОписаниеПоказателя.ТипКолонки);
	КонецЦикла;
	
	// Получение зарплатных данных отчета.
	ДеревоЗначений = Новый ДеревоЗначений;
	ПараметрыПолученияЗарплатныхДанных = СкопироватьКоллекцию(ПараметрыОтчета);
	ПараметрыПолученияЗарплатныхДанных.Вставить("ДатаНачалаПериодаВыборки", НачалоГода(ПараметрыОтчета.ДатаКонцаПериодаОтчета));
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыПолученияЗарплатныхДанных, ДеревоЗначений);
	
	СНачалаГода = ПараметрыПолученияЗарплатныхДанных.Свойство("СНачалаГода") И ПараметрыПолученияЗарплатныхДанных.СНачалаГода;
	ВыводитьКвартальныеПоказатели = КонецМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета) = КонецКвартала(ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	ДелительСумм = 1;
	
	СтрокаОрганизации = ДеревоЗначений.Строки.Найти(ПараметрыОтчета.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		Для Каждого СтрокаЗарплаты_ОКВЭД Из СтрокаОрганизации.Строки Цикл
			Если СНачалаГода Тогда
				СтрокаЗарплаты_МесяцНачисления = СтрокаЗарплаты_ОКВЭД;
			Иначе
				СтрокаЗарплаты_МесяцНачисления = СтрокаЗарплаты_ОКВЭД.Строки.Найти(ПараметрыПолученияЗарплатныхДанных.ДатаНачалаПериодаОтчета, "МесяцНачисления", Ложь);
			КонецЕсли;
			
			СтрокаОтчета = ДанныеОтчета.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОтчета, СтрокаЗарплаты_ОКВЭД, "КодОКВЭД2, НаименованиеОКВЭД2");
			
			Если СтрокаЗарплаты_МесяцНачисления <> Неопределено Тогда
				СтрокаОтчета.НачисленоРаботникамСписочногоСостава = Окр(СтрокаЗарплаты_МесяцНачисления.НачисленоРаботникамСписочногоСостава / ДелительСумм, 1);
				СтрокаОтчета.НачисленоВнешнимСовместителям        = Окр(СтрокаЗарплаты_МесяцНачисления.НачисленоВнешнимСовместителям / ДелительСумм, 1);
				СтрокаОтчета.НачисленоРаботникамПоДоговорамГПХ    = Окр(СтрокаЗарплаты_МесяцНачисления.НачисленоРаботникамПоДоговорамГПХ / ДелительСумм, 1);
			КонецЕсли;
			
			Если ВыводитьКвартальныеПоказатели Тогда
				ЗаполнитьЗначенияСвойств(СтрокаОтчета, СтрокаЗарплаты_ОКВЭД, "ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава, ОтработаноЧасовСНачалаГодаВнешнимиСовместителями");
				СтрокаОтчета.НачисленияСоциальногоХарактера = Окр(СтрокаЗарплаты_ОКВЭД.НачисленияСоциальногоХарактера / ДелительСумм, 1);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Получение кадровых данных отчета и объединение с зарплатными данными по кодам ОКВЭД.
	ДанныеОтчета.Колонки.Добавить("КадровыеДанныеЗаполнены", Новый ОписаниеТипов("Булево"));
	
	ДеревоЗначений = Новый ДеревоЗначений;
	ПараметрыПолученияКадровыхДанных = СкопироватьКоллекцию(ПараметрыОтчета);
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаП4", ПараметрыПолученияКадровыхДанных, ДеревоЗначений);
	
	СтрокаОрганизации = ДеревоЗначений.Строки.Найти(ПараметрыОтчета.Организация, "Организация");
	Если СтрокаОрганизации <> Неопределено Тогда
		Для Каждого СтрокаКадров Из СтрокаОрганизации.Строки Цикл
			Найденные = ДанныеОтчета.НайтиСтроки(Новый Структура("КодОКВЭД2, КадровыеДанныеЗаполнены", СтрокаКадров.КодОКВЭД2, Ложь));
			Если Найденные.Количество() > 0 Тогда
				СтрокаОтчета = Найденные[0];
			Иначе
				СтрокаОтчета = ДанныеОтчета.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаОтчета, СтрокаКадров, "КодОКВЭД2, НаименованиеОКВЭД2");
			КонецЕсли;
			СтрокаОтчета.КадровыеДанныеЗаполнены = Истина;
			СтрокаОтчета.СредняяЧисленностьЗаПериодСписочногоСостава    = Окр(СтрокаКадров.СредняяЧисленностьЗаПериодСписочногоСостава, 1);
			СтрокаОтчета.СредняяЧисленностьЗаПериодВнешнихСовместителей = Окр(СтрокаКадров.СредняяЧисленностьЗаПериодВнешнихСовместителей, 1);
			СтрокаОтчета.СредняяЧисленностьЗаПериодДоговорников         = Окр(СтрокаКадров.СредняяЧисленностьЗаПериодДоговорников, 1);
		КонецЦикла;
	КонецЕсли;
	
	ДанныеОтчета.Колонки.Удалить("КадровыеДанныеЗаполнены");
	
	Возврат ДанныеОтчета;
КонецФункции

Процедура УстановитьПустоеЗначение(Значение, ИмяЯчейки, Контейнер)
	Если Контейнер.ПолеТабличногоДокументаФормаОтчета.Свойство(ИмяЯчейки) И (Значение = 0 Или Значение = Null) Тогда
		Контейнер.ПолеТабличногоДокументаФормаОтчета[ИмяЯчейки] = "ЧН=-";
	КонецЕсли;		
КонецПроцедуры

Процедура УстановитьПустоеЗначениеЧисленности(Значение, ИмяЯчейки, Контейнер)
	Если Контейнер.ПолеТабличногоДокументаФормаОтчета.Свойство(ИмяЯчейки) И (Значение = Null Или Значение < 0.1) Тогда
		Контейнер.ПолеТабличногоДокументаФормаОтчета[ИмяЯчейки] = "ЧН=-";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ФормаФинмониторинга

Процедура ДобавитьПоказателиФормыФинмониторинга(ПоказателиОтчета, ВидОтчета)

	ВсегоСтрок = КоличествоЗаполняемыхСтрокФормыФинмониторинга(ВидОтчета);
	Для Сч = 2 По ВсегоСтрок Цикл
		
		ТекстСтроки = Формат(Сч,"ЧЦ=2; ЧДЦ=0; ЧВН=");
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "01", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "02", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "04", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "06", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "07", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "08", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "09", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "10", Истина, Истина);
		ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "П00000000" + ТекстСтроки + "11", Истина, Истина);
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоказателиФормыФинмониторинга(ПараметрыОтчета, Контейнер, ВидОтчета, ВариантРасчетаПоказателей)
	
	ТаблицаДанныхОтчета = ДанныеДляЗаполнениеОтчетовМониторинга(ПараметрыОтчета, ВариантРасчетаПоказателей);
	
	МассивКатегорий = МассивКатегорийОтчета(ВидОтчета);
	ВсегоСтрок = КоличествоЗаполняемыхСтрокФормыФинмониторинга(ВидОтчета);
	СтруктураДанных = Новый Структура;
	Для Сч = 2 По ВсегоСтрок Цикл
		
		ТекстСтроки = Формат(Сч,"ЧЦ=2; ЧДЦ=0; ЧВН=");
		
		КатегорияПерсонала = МассивКатегорий[Сч - 2];
		СтрокиИсключения = МассивСтрокИсключений(ВидОтчета);
		
		СтрокаДанных = ?(СтрокиИсключения.Найти(Сч) <> Неопределено, Неопределено, ТаблицаДанныхОтчета.Найти(КатегорияПерсонала,"КатегорияПерсонала"));
		Если СтрокаДанных = Неопределено Тогда
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "01", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "02", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "04", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "06", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "07", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "08", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "09", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "10", 0);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "11", 0);
		Иначе	
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "01", СтрокаДанных.СредняяЧисленностьЗаПериодСписочногоСостава);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "02", СтрокаДанных.СредняяЧисленностьЗаПериодВнешнихСовместителей);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "04", СтрокаДанных.ФОТВнутреннихСовместителей);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "06", СтрокаДанных.ФОТСписочногоСоставаБюджет);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "07", СтрокаДанных.ФОТСписочногоСоставаОМС);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "08", СтрокаДанных.ФОТСписочногоСоставаДоход);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "09", СтрокаДанных.ФОТВнешнихСовместителейБюджет);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "10", СтрокаДанных.ФОТВнешнихСовместителейОМС);
			СтруктураДанных.Вставить("П00000000" + ТекстСтроки + "11", СтрокаДанных.ФОТВнешнихСовместителейДоход);
		КонецЕсли;
	
	КонецЦикла;
	
	СоответствиеВключаемыхСтрок = СоответствиеВключаемыхСтрок(ВидОтчета);
	Для каждого СтрокаСоответствия Из СоответствиеВключаемыхСтрок Цикл
		ПодСтрока = СтрокаСоответствия.ПодСтрока;
		РодительскаяСтрока = СтрокаСоответствия.РодительскаяСтрока;
		Для НомерКолонки = 1 По 11 Цикл
			Если НомерКолонки = 3 Или НомерКолонки = 5 Тогда
				Продолжить;
			КонецЕсли;
			ТекстКолонки = Формат(НомерКолонки,"ЧЦ=2; ЧДЦ=0; ЧВН=");
			СтруктураДанных["П00000000" + РодительскаяСтрока + ТекстКолонки] = СтруктураДанных["П00000000" + РодительскаяСтрока + ТекстКолонки] + СтруктураДанных["П00000000" + ПодСтрока + ТекстКолонки];
		КонецЦикла;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Контейнер.ПолеТабличногоДокументаФормаОтчета, СтруктураДанных);

КонецПроцедуры

// Формирование таблицы с данными для заполнения статистически отчетов:
// 		ЗП-Здрав (Сфера здравоохранения).
// 		ЗП-Культура (Сфера культуры).
// 		ЗП-Наука (Научные исследования и разработки).
// 		ЗП-Образование (Сфера образования).
// 		ЗП-Соц (Сфера социального обслуживания).
//
// Параметры:
//  ПараметрыОтчета - структура с полями.
//		Организация
//		ДатаНачалаПериодаОтчета
//		ДатаКонцаПериодаОтчета
//		ИсключитьПодразделения, необязательный, содержит массив подразделений для исключения из отчета
//		ВключитьПодразделения, необязательный, содержит массив подразделений для включения в отчет.
//
//	Возвращаемое значение - таблица значений с данными для заполнения отчетов. Таблица содержит сведения:
//		КатегорияПерсонала
//		СредняяЧисленностьЗаПериодСписочногоСостава (без внешних совместителей)
//		СредняяЧисленностьЗаПериодВнешнихСовместителей
//		ФОТВнутреннихСовместителей
//		ФОТСписочногоСоставаБюджет (за счет средств бюджета без внешних совместителей)
//		ФОТСписочногоСоставаОМС (за счет средств ОМС без внешних совместителей)
//		ФОТСписочногоСоставаДоход (за счет средств от приносящей доход деятельности без внешних совместителей)
//		ФОТВнешнихСовместителейБюджет (за счет средств бюджета)
//		ФОТВнешнихСовместителейОМС (за счет средств ОМС)
//		ФОТВнешнихСовместителейДоход (за счет средств от приносящей доход деятельности).
//	
Функция ДанныеДляЗаполнениеОтчетовМониторинга(ПараметрыОтчета, ВариантРасчетаПоказателей)
	
	ТаблицаДанныхОтчета = Новый ТаблицаЗначений;
	ТаблицаДанныхОтчета.Колонки.Добавить("КатегорияПерсонала", Новый ОписаниеТипов("СправочникСсылка.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СредняяЧисленностьЗаПериодСписочногоСостава", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("СредняяЧисленностьЗаПериодВнешнихСовместителей", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТВнутреннихСовместителей", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТСписочногоСоставаБюджет", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТСписочногоСоставаОМС", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТСписочногоСоставаДоход", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТВнешнихСовместителейБюджет", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТВнешнихСовместителейОМС", Новый ОписаниеТипов("Число"));
	ТаблицаДанныхОтчета.Колонки.Добавить("ФОТВнешнихСовместителейДоход", Новый ОписаниеТипов("Число"));
	// Расширение структуры настроек.
	ПараметрыОтчетаДляЗаполнения = СкопироватьКоллекцию(ПараметрыОтчета);
	
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата("РегламентированныйОтчетСтатистикаФормаЗП", ПараметрыОтчетаДляЗаполнения, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполнения.Организация, "Организация");
	Если СтрокаОрганизации = Неопределено Тогда
		Возврат ТаблицаДанныхОтчета;
	КонецЕсли;
	
	// Заполнение таблицы данных отчета.
	Для Каждого СтрокаОтчета Из СтрокаОрганизации.Строки Цикл
		
		НоваяСтрока = ТаблицаДанныхОтчета.Добавить();
		
		НоваяСтрока.КатегорияПерсонала = СтрокаОтчета.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы;
		
		Если ВариантРасчетаПоказателей = 2015 Тогда
			НоваяСтрока.ФОТВнутреннихСовместителей = Окр((СтрокаОтчета.НачисленоВнутреннимСовместителям + СтрокаОтчета.НачисленоРаботникамСписочногоСоставаПоСовмещению) / 1000, 1);
		Иначе
			НоваяСтрока.ФОТВнутреннихСовместителей = Окр(СтрокаОтчета.НачисленоВнутреннимСовместителям / 1000, 1);
		КонецЕсли;
		
		НоваяСтрока.ФОТСписочногоСоставаБюджет = Окр(СтрокаОтчета.ФОТСписочногоСоставаБюджет / 1000, 1);
		НоваяСтрока.ФОТСписочногоСоставаОМС = Окр(СтрокаОтчета.ФОТСписочногоСоставаОМС / 1000, 1);
		НоваяСтрока.ФОТСписочногоСоставаДоход = Окр(СтрокаОтчета.ФОТСписочногоСоставаДоход / 1000, 1);
		НоваяСтрока.ФОТВнешнихСовместителейБюджет = Окр(СтрокаОтчета.ФОТВнешнихСовместителейБюджет / 1000, 1);
		НоваяСтрока.ФОТВнешнихСовместителейОМС = Окр(СтрокаОтчета.ФОТВнешнихСовместителейОМС / 1000, 1);
		НоваяСтрока.ФОТВнешнихСовместителейДоход = Окр(СтрокаОтчета.ФОТВнешнихСовместителейДоход / 1000, 1);
	
	КонецЦикла;
	
	ДанныеОтчета = Новый ДеревоЗначений;
	ДанныеДляЗаполненияРегламентированнойОтчетностиКадры("РегламентированныйОтчетСтатистикаФормаЗП", ПараметрыОтчетаДляЗаполнения, ДанныеОтчета);
	
	СтрокаОрганизации = ДанныеОтчета.Строки.Найти(ПараметрыОтчетаДляЗаполнения.Организация, "Организация");
	Если СтрокаОрганизации = Неопределено Тогда
		Возврат ТаблицаДанныхОтчета;
	КонецЕсли;
	
	// Заполнение таблицы данных отчета.
	Для Каждого СтрокаОтчета Из СтрокаОрганизации.Строки Цикл
		
		НоваяСтрока = ТаблицаДанныхОтчета.Найти(СтрокаОтчета.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы, "КатегорияПерсонала");
		Если НоваяСтрока = Неопределено Тогда
			НоваяСтрока = ТаблицаДанныхОтчета.Добавить();
		КонецЕсли;
		НоваяСтрока.КатегорияПерсонала = СтрокаОтчета.СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы;
		НоваяСтрока.СредняяЧисленностьЗаПериодСписочногоСостава = Окр(СтрокаОтчета.СредняяЧисленностьЗаПериодСписочногоСостава, 1);
		НоваяСтрока.СредняяЧисленностьЗаПериодВнешнихСовместителей = Окр(СтрокаОтчета.СредняяЧисленностьЗаПериодВнешнихСовместителей, 1);
		
	КонецЦикла;
	
	Возврат ТаблицаДанныхОтчета;
	
КонецФункции

Функция МассивКатегорийОтчета(ВидОтчета)

	ФормаМониторинга = Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы.ПустаяСсылка();
	Если ВидОтчета = "РегламентированныйОтчетЗПЗдрав_2013" Тогда
		ФормаМониторинга = Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы.ЗПЗдрав	
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПКультура_2013" Тогда
		ФормаМониторинга = Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы.ЗПКультура	
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПНаука_2013" Тогда
		ФормаМониторинга = Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы.ЗПНаука	
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПОбразование_2013" Тогда
		ФормаМониторинга = Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы.ЗПОбразование	
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПСоц_2013" Тогда
		ФормаМониторинга = Перечисления.ВидыФормМониторингаРаботниковСоциальнойСферы.ЗПСоц	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Ссылка
	|ИЗ
	|	Справочник.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы КАК СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы
	|ГДЕ
	|	СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ФормаМониторинга = &ФормаМониторинга
	|	И НЕ(СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ФормаМониторинга = ЗНАЧЕНИЕ(Перечисление.ВидыФормМониторингаРаботниковСоциальнойСферы.ЗПНаука)
	|				И СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.КодКатегорииПерсонала = ""311"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.Код";
	Запрос.УстановитьПараметр("ФормаМониторинга", ФормаМониторинга);
	
	КатегорииОтчета = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Если ВидОтчета = "РегламентированныйОтчетЗПНаука_2013" Тогда
		КатегорииОтчета.Вставить(0, Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ПустаяСсылка());
		КатегорииОтчета.Вставить(3, Справочники.СтрокиОтчетностиМониторингаРаботниковСоциальнойСферы.ПустаяСсылка());
	КонецЕсли;
	
	Возврат КатегорииОтчета;
	
КонецФункции 

Функция КоличествоЗаполняемыхСтрокФормыФинмониторинга(ВидОтчета)
	
	Если ВидОтчета = "РегламентированныйОтчетЗПЗдрав_2013" Или ВидОтчета = "РегламентированныйОтчетЗПСоц_2013" Тогда
		Возврат 13;
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПКультура_2013" Тогда
		Возврат 13;
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПНаука_2013" Тогда
		Возврат 14;
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПОбразование_2013" Тогда
		Возврат 28;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции 

Функция СоответствиеВключаемыхСтрок(ВидОтчета)

	СоответствиеВключаемыхСтрок = Новый ТаблицаЗначений;
	СоответствиеВключаемыхСтрок.Колонки.Добавить("ПодСтрока", Новый ОписаниеТипов("Строка"));
	СоответствиеВключаемыхСтрок.Колонки.Добавить("РодительскаяСтрока", Новый ОписаниеТипов("Строка"));

	Если ВидОтчета = "РегламентированныйОтчетЗПЗдрав_2013" Или ВидОтчета = "РегламентированныйОтчетЗПСоц_2013" Тогда
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "05","04");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "09","08");
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПКультура_2013" Тогда
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "08","07");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "07","06");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "09","06");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "10","06");
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПНаука_2013" Тогда
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "06","05");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "07","05");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "03","02");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "04","02");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "05","02");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "08","02");
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПОбразование_2013" Тогда
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "06","05");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "09","08");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "10","08");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "12","11");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "13","11");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "15","14");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "16","14");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "20","19");
		ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, "22","21");
	КонецЕсли;
	
	Возврат СоответствиеВключаемыхСтрок
	
КонецФункции 

Процедура ДобавитьСтрокуСоответствия(СоответствиеВключаемыхСтрок, ПодСтрока,РодительскаяСтрока)

	НоваяСтрока = СоответствиеВключаемыхСтрок.Добавить();
	НоваяСтрока.ПодСтрока = ПодСтрока;
	НоваяСтрока.РодительскаяСтрока = РодительскаяСтрока;

КонецПроцедуры

Функция МассивСтрокИсключений(ВидОтчета)

	МассивСтрок = Новый Массив();
	
	Если ВидОтчета = "РегламентированныйОтчетЗПНаука_2013" Тогда
		МассивСтрок.Добавить(2);
		МассивСтрок.Добавить(5);
	КонецЕсли;
	
	Возврат МассивСтрок
	
КонецФункции 

#КонецОбласти

#Область ПолучениеДанных

Функция СКДОтчетаСтатистикаПерсонала(ВидСКД)
	
	СхемаКомпоновки = Отчеты.СтатистикаПерсонала.ПолучитьМакет(ВидСКД);
	
	Если ВидСКД <> "СхемаКомпоновкиДанныхФормаП4НЗ" Тогда
		
		ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(Новый Структура("СхемаКомпоновкиДанных", СхемаКомпоновки), , Ложь);
		УдалитьКлючевоеСловоРазрешенные(СхемаКомпоновки);
		
	КонецЕсли;
	
	Возврат СхемаКомпоновки;
	
КонецФункции

// Формирование расшифровки отчетов статистики.
//
Процедура СформироватьОтчетРасшифровку(Параметры, ДокументРезультат)
	
	ВидСКД = "";
	ВнешниеНаборыДанных = Неопределено;
	ОпределитьВидСКД(Параметры, , ВидСКД, Ложь, ВнешниеНаборыДанных);
	
	СхемаКомпоновки = СКДОтчетаСтатистикаПерсонала(ВидСКД);
	
	НастройкиОтчета = ЗаполнитьКомпоновщикНастроек(Параметры.ИмяСКД, Параметры, СхемаКомпоновки);
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанных(СхемаКомпоновки, НастройкиОтчета);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	Если ВнешниеНаборыДанных <> Неопределено Тогда
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, , Истина);
	Иначе
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , , Истина);
	КонецЕсли;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
КонецПроцедуры

// Формирование расшифровки отчетов статистики.
//
Процедура СформироватьОтчетРасшифровкуШтатноеРасписание(Параметры, ДокументРезультат)
	
	ВидСКД = "";
	ВнешниеНаборыДанных = Неопределено;
	ОпределитьВидСКД(Параметры, , ВидСКД, Ложь, ВнешниеНаборыДанных);
	
	СхемаКомпоновки = Отчеты.ШтатноеРасписание.ПолучитьМакет(ВидСКД);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(Новый Структура("СхемаКомпоновкиДанных", СхемаКомпоновки));
	УдалитьКлючевоеСловоРазрешенные(СхемаКомпоновки);
	
	НастройкиОтчета = ЗаполнитьКомпоновщикНастроек(Параметры.ИмяСКД, Параметры, СхемаКомпоновки);
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанных(СхемаКомпоновки, НастройкиОтчета);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	Если ВнешниеНаборыДанных <> Неопределено Тогда
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, , Истина);
	Иначе
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , , Истина);
	КонецЕсли;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
КонецПроцедуры

Процедура ДанныеДляЗаполненияРегламентированнойОтчетностиЗарплата(Вариант, ПараметрыОтчета, ДанныеОтчета, СоответствиеПользовательскихПолей = Неопределено)
	
	СхемаКомпоновки = СКДОтчетаСтатистикаПерсонала("СхемаКомпоновкиДанныхЗарплата");
	
	ПараметрыОтчета.Вставить("Вариант", Вариант);
	ПараметрыОтчета.Вставить("ГоловнаяОрганизация", ЗарплатаКадры.ГоловнаяОрганизация(ПараметрыОтчета.Организация));
	Если Вариант = "РегламентированныйОтчетСтатистикаФормаП4" Тогда
		Если НачалоМесяца(ПараметрыОтчета.ДатаКонцаПериодаОтчета) <> ПараметрыОтчета.ДатаНачалаПериодаОтчета Тогда
			ПараметрыОтчета.Вставить("СНачалаГода", Истина);
		КонецЕсли;
		ПараметрыОтчета.Вставить("ТолькоГражданские", Истина);
	ИначеЕсли Вариант = "РегламентированныйОтчетСтатистикаФормаЗП" Тогда
		ПараметрыОтчета.Вставить("СНачалаГода", Истина);
		ПараметрыОтчета.Вставить("ТолькоГражданские", Истина);
	ИначеЕсли Вариант = "СхемаКомпоновкиДанныхЗарплата1ТГМС" Тогда
		ПараметрыОтчета.Вставить("СНачалаГода", Истина);
	КонецЕсли;
	
	НастройкиОтчета = ЗаполнитьКомпоновщикНастроек(Вариант, ПараметрыОтчета, СхемаКомпоновки);
	СоответствиеПользовательскихПолей = ЗарплатаКадрыОтчеты.СоответствиеПользовательскихПолей(НастройкиОтчета);
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновки, НастройкиОтчета);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Процедура ДанныеДляЗаполненияРегламентированнойОтчетностиП4НЗ(Вариант, ПараметрыОтчета, ДанныеОтчета)
	
	СхемаКомпоновки = СКДОтчетаСтатистикаПерсонала("СхемаКомпоновкиДанныхФормаП4НЗ");
	
	НастройкиОтчета = ЗаполнитьКомпоновщикНастроек(Вариант, ПараметрыОтчета, СхемаКомпоновки);
	
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновки, НастройкиОтчета);
	
	ВнешниеНаборыДанных = ВнешнийИсточникСтатистикиФормыП4НЗ(ПараметрыОтчета);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Функция ВнешнийИсточникСтатистикиФормыП4НЗ(ПараметрыОтчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.ДатаНачалаПериодаОтчета);
	Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	
	ИсключитьДанныеОбособленныхПодразделений = Ложь;
	Если Не ПараметрыОтчета.Свойство("ИсключитьДанныеОбособленныхПодразделений", ИсключитьДанныеОбособленныхПодразделений) Тогда
		ИсключитьДанныеОбособленныхПодразделений = Ложь
	КонецЕсли;
	
	Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	Если Не ПараметрыОтчета.Свойство("ОбособленноеПодразделение", Подразделение) Тогда
		Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка()
	КонецЕсли;
	
	МассивПодразделений = ПодразделенияИсключения(ПараметрыОтчета.Организация, Подразделение, ИсключитьДанныеОбособленныхПодразделений);
	
	ИспользоватьШтатноеРасписание = ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание");
	
	// Сотрудники по организации
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.Организация = ПараметрыОтчета.Организация;
	ПараметрыПолученияСотрудников.НачалоПериода = ПараметрыОтчета.ДатаНачалаПериодаОтчета;
	ПараметрыПолученияСотрудников.ОкончаниеПериода = ПараметрыОтчета.ДатаКонцаПериодаОтчета;
	Если МассивПодразделений.Количество() > 0 Тогда
		Отборы = Новый Массив;
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(Отборы, "Подразделение", ?(ИсключитьДанныеОбособленныхПодразделений, "НЕ В", "В"), МассивПодразделений);
		ПараметрыПолученияСотрудников.Отборы = Отборы;
	КонецЕсли;
	ИмяВТСотрудникиОрганизации = "ВТСотрудникиОрганизацииДляСтатистикиФормыП4НЗ";
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияСотрудников, ИмяВТСотрудникиОрганизации);
	
	// Рабочие места сотрудников
	// Уволенные в последний день периода должны включаться в отчет, поэтому увеличим период на 1 день.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиОрганизации.Сотрудник,
	|	&НачалоПериода КАК НачалоПериода,
	|	ДОБАВИТЬКДАТЕ(&КонецПериода, ДЕНЬ, 1) КАК ОкончаниеПериода
	|ПОМЕСТИТЬ ВТСотрудникиРабочиеМеста
	|ИЗ
	|	#СотрудникиОрганизации КАК СотрудникиОрганизации";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#СотрудникиОрганизации", ИмяВТСотрудникиОрганизации);
	Запрос.Выполнить();
	
	ПараметрыПолученияРабочихМест = КадровыйУчет.ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице("ВТСотрудникиРабочиеМеста", "Сотрудник", "НачалоПериода", "ОкончаниеПериода");
	ПараметрыПолученияРабочихМест.Организация = ПараметрыОтчета.Организация;
	Если МассивПодразделений.Количество() > 0 Тогда
		Отборы = Новый Массив;
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(Отборы, "Подразделение", ?(ИсключитьДанныеОбособленныхПодразделений, "НЕ В", "В"), МассивПодразделений);
		ПараметрыПолученияРабочихМест.Отборы = Отборы;
	КонецЕсли;
	КадровыйУчет.СоздатьВТРабочиеМестаСотрудниковПоВременнойТаблице(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияРабочихМест);
	
	// Из увеличенного периода нужно оставить только уволенных, также нужно убрать уволенных на первый день периода,
	// т.к. они должны войти в предыдущий период.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РабочиеМестаСотрудников.Сотрудник,
	|	РабочиеМестаСотрудников.Период
	|ПОМЕСТИТЬ ВТСотрудникиКадровыеДанные
	|ИЗ
	|	ВТРабочиеМестаСотрудников КАК РабочиеМестаСотрудников
	|ГДЕ
	|	(РабочиеМестаСотрудников.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|				И РабочиеМестаСотрудников.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|			ИЛИ РабочиеМестаСотрудников.Период МЕЖДУ ДОБАВИТЬКДАТЕ(&НачалоПериода, ДЕНЬ, 1) И ДОБАВИТЬКДАТЕ(&КонецПериода, ДЕНЬ, 1)
	|				И РабочиеМестаСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))";
	
	Запрос.Выполнить();
	
	// Кадровые данные по рабочим местам сотрудников
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиКадровыеДанные");
	КадровыеДанные = "Организация, ВидСобытия, ВидЗанятости, ПриказОбУвольненииСтатьяТКРФ, ГрафикРаботы, КоличествоСтавок, ДолжностьПоШтатномуРасписанию, Пол, ДатаПриема, ДатаУвольнения";
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, КадровыеДанные);
	
	Если ИспользоватьШтатноеРасписание Тогда
		
		// Сведения по позициям штатного расписания
		ПараметрыПостроения = УправлениеШтатнымРасписанием.ПараметрыПостроенияВТШтатноеРасписаниеПоТаблицеФильтра("ВТКадровыеДанныеСотрудников");
		ПараметрыПостроения.ИмяПоляПозицияШтатногоРасписания = "ДолжностьПоШтатномуРасписанию";
		
		УправлениеШтатнымРасписанием.СоздатьВТПозицииШтатногоРасписанияПоВременнойТаблице(
			Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПостроения, "КоличествоСтавок", "ВТПозицииШтатногоРасписания");
		
		ПараметрыПостроения = УправлениеШтатнымРасписанием.ПараметрыПостроенияВТШтатноеРасписание(
			ПараметрыОтчета.ДатаКонцаПериодаОтчета, ПараметрыОтчета.Организация);
			
		Если МассивПодразделений.Количество() > 0 Тогда
			
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(
				ПараметрыПостроения.Отборы, "Подразделение", ?(ИсключитьДанныеОбособленныхПодразделений, "НЕ В", "В"), МассивПодразделений);
			
			КонецЕсли;
			
		УправлениеШтатнымРасписанием.СоздатьВТШтатноеРасписание(
			Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПостроения, "КоличествоСтавок", "ВТПозицииШтатногоРасписанияНаКонецПериода");
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	NULL КАК ПозицияШтатногоРасписания,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Дата
		|ПОМЕСТИТЬ ВТПозицииШтатногоРасписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	NULL КАК ПозицияШтатногоРасписания,
		|	0 КАК КоличествоСтавок
		|ПОМЕСТИТЬ ВТПозицииШтатногоРасписанияНаКонецПериода";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Состояния сотрудников
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиОрганизации.Сотрудник,
	|	&НачалоПериода КАК ДатаНачала,
	|	&КонецПериода КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТСотрудникиПериодыСостояний
	|ИЗ
	|	#СотрудникиОрганизации КАК СотрудникиОрганизации";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#СотрудникиОрганизации", ИмяВТСотрудникиОрганизации);
	Запрос.Выполнить();
	
	СостоянияСотрудников.СоздатьВТПериодыСостоянийСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериодыСостояний");
	
	// Сотрудники, работавшие не полное рабочее время
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(Запрос.МенеджерВременныхТаблиц, ПараметрыОтчета.ДатаНачалаПериодаОтчета, ПараметрыОтчета.ДатаКонцаПериодаОтчета, "День");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадровыеДанныеСотрудников.Сотрудник,
	|	КадровыеДанныеСотрудников.Период КАК ДатаНачала,
	|	ЕСТЬNULL(МИНИМУМ(КадровыеДанныеСотрудниковВспом.Период), &КонецПериода) КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТСотрудникиРаботавшиеНеполноеРабочееВремяВременно
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудниковВспом
	|		ПО КадровыеДанныеСотрудников.Сотрудник = КадровыеДанныеСотрудниковВспом.Сотрудник
	|			И КадровыеДанныеСотрудников.Период < КадровыеДанныеСотрудниковВспом.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГрафикиРаботыСотрудников КАК ГрафикиРаботыСотрудников
	|		ПО КадровыеДанныеСотрудников.ГрафикРаботы = ГрафикиРаботыСотрудников.Ссылка
	|ГДЕ
	|	(КадровыеДанныеСотрудников.КоличествоСтавок < 1
	|			ИЛИ ГрафикиРаботыСотрудников.НеполноеРабочееВремя)
	|
	|СГРУППИРОВАТЬ ПО
	|	КадровыеДанныеСотрудников.Сотрудник,
	|	КадровыеДанныеСотрудников.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыСостоянийСотрудников.Сотрудник,
	|	ПериодыСостоянийСотрудников.Начало КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА ПериодыСостоянийСотрудников.Окончание = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ПериодыСостоянийСотрудников.ОкончаниеПредположительно
	|		ИНАЧЕ ПериодыСостоянийСотрудников.Окончание
	|	КОНЕЦ КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТСотрудникиВОтпускеПоУходу
	|ИЗ
	|	ВТПериодыСостоянийСотрудников КАК ПериодыСостоянийСотрудников
	|ГДЕ
	|	ПериодыСостоянийСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускПоУходуЗаРебенком)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник КАК Сотрудник,
	|	Периоды.Период КАК Дата
	|ПОМЕСТИТЬ ВТДниНеполногоРабочегоВремени
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиРаботавшиеНеполноеРабочееВремяВременно КАК СотрудникиРаботавшиеНеполноеРабочееВремя
	|		ПО (Периоды.Период МЕЖДУ СотрудникиРаботавшиеНеполноеРабочееВремя.ДатаНачала И СотрудникиРаботавшиеНеполноеРабочееВремя.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыСостоянийСотрудников КАК ПериодыСостоянийСотрудников
	|		ПО (СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник = ПериодыСостоянийСотрудников.Сотрудник)
	|			И (ПериодыСостоянийСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.Работа))
	|			И (Периоды.Период МЕЖДУ ПериодыСостоянийСотрудников.Начало И ВЫБОР
	|				КОГДА ПериодыСостоянийСотрудников.Окончание = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА &КонецПериода
	|				ИНАЧЕ ПериодыСостоянийСотрудников.Окончание
	|			КОНЕЦ)
	|ГДЕ
	|	СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник ЕСТЬ НЕ NULL 
	|	И ПериодыСостоянийСотрудников.Сотрудник ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник,
	|	Периоды.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДниНеполногоРабочегоВремени.Сотрудник,
	|	КОЛИЧЕСТВО(ДниНеполногоРабочегоВремени.Дата) КАК КоличествоДней
	|ПОМЕСТИТЬ ВТСотрудникиРаботавшиеНеполноеРабочееВремяПредварительно
	|ИЗ
	|	ВТДниНеполногоРабочегоВремени КАК ДниНеполногоРабочегоВремени
	|
	|СГРУППИРОВАТЬ ПО
	|	ДниНеполногоРабочегоВремени.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПериоды";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&КонецПериода КАК Период,
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТОтбораСотрудников
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиВОтпускеПоУходу КАК СотрудникиВОтпускеПоУходу
	|		ПО КадровыеДанныеСотрудников.Сотрудник = СотрудникиВОтпускеПоУходу.Сотрудник
	|			И (КадровыеДанныеСотрудников.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Женский))
	|
	|СГРУППИРОВАТЬ ПО
	|	КадровыеДанныеСотрудников.Сотрудник";
	
	Запрос.Выполнить();
	
	// Отпуска по уходу за ребенком
	Отборы = Новый Массив;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоПолутораЛет)";
	
	Отборы.Добавить(Новый Структура("ЛевоеЗначение,ВидСравнения,ПравоеЗначение",
			"Начисление", "В", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Начисление")));
	ОтборыНачислений = Новый Структура("ПлановыеНачисления", ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Отборы));
	
	ЗапросВТПлановыеНачисления = КадровыйУчетРасширенный.ЗапросВТПлановыеНачисленияСотрудников(
		Истина, "ВТПлановыеНачисленияСотрудниковДоПолутораЛетПредварительно", "ВТОтбораСотрудников", "Сотрудник,Период", ОтборыНачислений);
	
	ЗапросВТПлановыеНачисления.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросВТПлановыеНачисления.Выполнить();
	
	Отборы = Новый Массив;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоТрехЛет)";
	
	Отборы.Добавить(Новый Структура("ЛевоеЗначение,ВидСравнения,ПравоеЗначение",
			"Начисление", "В", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Начисление")));
	ОтборыНачислений = Новый Структура("ПлановыеНачисления", ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Отборы));
	
	ЗапросВТПлановыеНачисления = КадровыйУчетРасширенный.ЗапросВТПлановыеНачисленияСотрудников(
		Истина, "ВТПлановыеНачисленияСотрудниковДоТрехЛетПредварительно", "ВТОтбораСотрудников", "Сотрудник,Период", ОтборыНачислений);
	
	ЗапросВТПлановыеНачисления.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	ЗапросВТПлановыеНачисления.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПлановыеНачисленияСотрудниковДоПолутораЛет.Сотрудник
	|ПОМЕСТИТЬ ВТПлановыеНачисленияСотрудниковДоПолутораЛет
	|ИЗ
	|	ВТПлановыеНачисленияСотрудниковДоПолутораЛетПредварительно КАК ПлановыеНачисленияСотрудниковДоПолутораЛет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО ПлановыеНачисленияСотрудниковДоПолутораЛет.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыеНачисленияСотрудниковДоПолутораЛет.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеНачисленияСотрудниковДоПолутораЛет.Сотрудник
	|ИЗ
	|	ВТПлановыеНачисленияСотрудниковДоПолутораЛет КАК ПлановыеНачисленияСотрудниковДоПолутораЛет";
	
	ВТПлановыеНачисленияСотрудниковДоПолутораЛет = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПлановыеНачисленияСотрудниковДоТрехЛет.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТПлановыеНачисленияСотрудниковДоТрехЛетПредварительно КАК ПлановыеНачисленияСотрудниковДоТрехЛет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО ПлановыеНачисленияСотрудниковДоТрехЛет.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПлановыеНачисленияСотрудниковДоПолутораЛет КАК ПлановыеНачисленияСотрудниковДоПолутораЛет
	|		ПО ПлановыеНачисленияСотрудниковДоТрехЛет.Сотрудник = ПлановыеНачисленияСотрудниковДоПолутораЛет.Сотрудник
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|	И ПлановыеНачисленияСотрудниковДоПолутораЛет.Сотрудник ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыеНачисленияСотрудниковДоТрехЛет.Сотрудник";
	
	ВТПлановыеНачисленияСотрудниковДоТрехЛет = Запрос.Выполнить().Выгрузить();
	
	// Сотрудники, находившиеся в простое
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПериодыСостоянийСотрудников.Сотрудник
	|ИЗ
	|	ВТПериодыСостоянийСотрудников КАК ПериодыСостоянийСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО ПериодыСостоянийСотрудников.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|	И (ПериодыСостоянийСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ПростойНеЗависящийОтРаботодателяИРаботника)
	|			ИЛИ ПериодыСостоянийСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ПростойПоВинеРаботодателя))";
	
	ВТСотрудникиВПростое = Запрос.Выполнить().Выгрузить();
	
	// Сотрудники, находившиеся в отпуске без оплаты
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПериодыСостоянийСотрудников.Сотрудник,
	|	СУММА(РАЗНОСТЬДАТ(ПериодыСостоянийСотрудников.Начало, ВЫБОР
	|				КОГДА ПериодыСостоянийСотрудников.Окончание = ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА ПериодыСостоянийСотрудников.ОкончаниеПредположительно
	|				ИНАЧЕ ПериодыСостоянийСотрудников.Окончание
	|			КОНЕЦ, ДЕНЬ) + 1) КАК КоличествоДней
	|ПОМЕСТИТЬ ВТСотрудникиВОтпускеБезОплатыПредварительно
	|ИЗ
	|	ВТПериодыСостоянийСотрудников КАК ПериодыСостоянийСотрудников
	|ГДЕ
	|	(ПериодыСостоянийСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускНеоплачиваемыйПоРазрешениюРаботодателя)
	|			ИЛИ ПериодыСостоянийСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудника.ОтпускНеоплачиваемыйПоЗаконодательству))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыСостоянийСотрудников.Сотрудник";
	
	Запрос.Выполнить();
	
	// Сотрудник показывается один раз по строке 02 или 05, исходя из причины его работы неполное рабочее время
	// или нахождения в отпуске без сохранения заработной платы большую часть времени в квартале.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник
	|ИЗ
	|	ВТСотрудникиРаботавшиеНеполноеРабочееВремяПредварительно КАК СотрудникиРаботавшиеНеполноеРабочееВремя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиВОтпускеБезОплатыПредварительно КАК СотрудникиВОтпускеБезОплаты
	|		ПО СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник = СотрудникиВОтпускеБезОплаты.Сотрудник
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|	И (СотрудникиРаботавшиеНеполноеРабочееВремя.КоличествоДней >= СотрудникиВОтпускеБезОплаты.КоличествоДней
	|			ИЛИ СотрудникиВОтпускеБезОплаты.Сотрудник ЕСТЬ NULL )";
	
	ВТСотрудникиРаботавшиеНеполноеРабочееВремя = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СотрудникиВОтпускеБезОплаты.Сотрудник
	|ИЗ
	|	ВТСотрудникиВОтпускеБезОплатыПредварительно КАК СотрудникиВОтпускеБезОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ПО СотрудникиВОтпускеБезОплаты.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиРаботавшиеНеполноеРабочееВремяПредварительно КАК СотрудникиРаботавшиеНеполноеРабочееВремя
	|		ПО СотрудникиВОтпускеБезОплаты.Сотрудник = СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник
	|ГДЕ
	|	КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|	И (СотрудникиВОтпускеБезОплаты.КоличествоДней > СотрудникиРаботавшиеНеполноеРабочееВремя.КоличествоДней
	|			ИЛИ СотрудникиРаботавшиеНеполноеРабочееВремя.Сотрудник ЕСТЬ NULL )";
	
	ВТСотрудникиВОтпускеБезОплаты = Запрос.Выполнить().Выгрузить();
	
	// Сотрудники, работавшие вахтовым методом
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(Запрос.МенеджерВременныхТаблиц, ПараметрыОтчета.ДатаНачалаПериодаОтчета, ПараметрыОтчета.ДатаКонцаПериодаОтчета, "Месяц");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиПериоды.Сотрудник,
	|	&КонецПериода КАК ДатаАктуальности,
	|	Периоды.Период КАК Месяц,
	|	НАЧАЛОПЕРИОДА(Периоды.Период, МЕСЯЦ) КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(Периоды.Период, МЕСЯЦ) КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТСотрудникиДляРасчетаВремени
	|ИЗ
	|	ВТСотрудникиПериодыСостояний КАК СотрудникиПериоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериоды КАК Периоды
	|		ПО (Периоды.Период МЕЖДУ СотрудникиПериоды.ДатаНачала И СотрудникиПериоды.ДатаОкончания)";
	
	Запрос.Выполнить();
	
	ПараметрыПолученияДанныхОВремени = УчетРабочегоВремениРасширенный.ПараметрыДляСоздатьВТДанныеУчетаРабочегоВремениСотрудников();
	ПараметрыПолученияДанныхОВремени.ИмяВТСотрудники = "ВТСотрудникиДляРасчетаВремени";
	ПараметрыПолученияДанныхОВремени.ДатаНачала = ПараметрыОтчета.ДатаНачалаПериодаОтчета;
	ПараметрыПолученияДанныхОВремени.ДатаОкончания = ПараметрыОтчета.ДатаКонцаПериодаОтчета;
	
	УчетРабочегоВремениРасширенный.СоздатьВТДанныеУчетаРабочегоВремениСотрудников(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияДанныхОВремени);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеУчетаРабочегоВремениСотрудников.Сотрудник
	|ИЗ
	|	ВТДанныеУчетаРабочегоВремениСотрудников КАК ДанныеУчетаРабочегоВремениСотрудников
	|ГДЕ
	|	ДанныеУчетаРабочегоВремениСотрудников.ВидУчетаВремени = ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.Вахта)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчетаРабочегоВремениСотрудников.Сотрудник";
	
	ВТСотрудникиВахта = Запрос.Выполнить().Выгрузить();
	
	// Данные сотрудников
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадровыеДанныеСотрудников.Организация КАК Организация,
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию КАК ПозицияШтатногоРасписания,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|				И КадровыеДанныеСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
	|				И (КадровыеДанныеСотрудников.ДатаПриема МЕЖДУ &НачалоПериода И &КонецПериода)
	|			ТОГДА КадровыеДанныеСотрудников.Сотрудник
	|	КОНЕЦ КАК ЧисленностьПринятыхВПериоде,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|				И КадровыеДанныеСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
	|				И (КадровыеДанныеСотрудников.ДатаПриема МЕЖДУ &НачалоПериода И &КонецПериода)
	|				И (ШтатноеРасписание.ДатаУтверждения МЕЖДУ &НачалоПериода И &КонецПериода)
	|			ТОГДА КадровыеДанныеСотрудников.Сотрудник
	|	КОНЕЦ КАК ЧисленностьПринятыхВПериодеНаНовуюПозицию,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|				И КадровыеДанныеСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|				И (КадровыеДанныеСотрудников.ДатаУвольнения МЕЖДУ &НачалоПериода И &КонецПериода)
	|			ТОГДА КадровыеДанныеСотрудников.Сотрудник
	|	КОНЕЦ КАК ЧисленностьУволенныхВПериоде,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|				И КадровыеДанныеСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|				И КадровыеДанныеСотрудников.ПриказОбУвольненииСтатьяТКРФ = ЗНАЧЕНИЕ(Справочник.ОснованияУвольнения.Статья77п1)
	|				И (КадровыеДанныеСотрудников.ДатаУвольнения МЕЖДУ &НачалоПериода И &КонецПериода)
	|			ТОГДА КадровыеДанныеСотрудников.Сотрудник
	|	КОНЕЦ КАК ЧисленностьУволенныхПоСоглашениюСторон,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|				И КадровыеДанныеСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|				И КадровыеДанныеСотрудников.ПриказОбУвольненииСтатьяТКРФ = ЗНАЧЕНИЕ(Справочник.ОснованияУвольнения.Статья81п2)
	|				И (КадровыеДанныеСотрудников.ДатаУвольнения МЕЖДУ &НачалоПериода И &КонецПериода)
	|			ТОГДА КадровыеДанныеСотрудников.Сотрудник
	|	КОНЕЦ КАК ЧисленностьУволенныхВСвязиССокращением,
	|	ВЫБОР
	|		КОГДА КадровыеДанныеСотрудников.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|				И КадровыеДанныеСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|				И КадровыеДанныеСотрудников.ПриказОбУвольненииСтатьяТКРФ = ЗНАЧЕНИЕ(Справочник.ОснованияУвольнения.Статья77п3)
	|				И (КадровыеДанныеСотрудников.ДатаУвольнения МЕЖДУ &НачалоПериода И &КонецПериода)
	|			ТОГДА КадровыеДанныеСотрудников.Сотрудник
	|	КОНЕЦ КАК ЧисленностьУволенныхПоСобственномуЖеланию,
	|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	КадровыеДанныеСотрудников.ВидСобытия КАК ВидСобытия,
	|	КадровыеДанныеСотрудников.ДатаПриема КАК ДатаПриема,
	|	КадровыеДанныеСотрудников.ДатаУвольнения КАК ДатаУвольнения,
	|	КадровыеДанныеСотрудников.ПриказОбУвольненииСтатьяТКРФ КАК ПриказОбУвольненииСтатьяТКРФ,
	|	&НачалоПериода КАК НачалоПериода,
	|	&КонецПериода КАК КонецПериода
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|		ПО КадровыеДанныеСотрудников.ДолжностьПоШтатномуРасписанию = ШтатноеРасписание.Ссылка";
	
	ДанныеСотрудников = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПозицииШтатногоРасписанияНаКонецПериода.ПозицияШтатногоРасписания,
	|	ПозицииШтатногоРасписанияНаКонецПериода.КоличествоСтавок
	|ИЗ
	|	ВТПозицииШтатногоРасписанияНаКонецПериода КАК ПозицииШтатногоРасписанияНаКонецПериода";
	
	ВТПозицииШтатногоРасписанияНаКонецПериода = Запрос.Выполнить().Выгрузить();
	
	ВнешниеНаборыДанных=Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеСотрудников", ДанныеСотрудников);
	ВнешниеНаборыДанных.Вставить("ПозицииШтатногоРасписанияНаКонецПериода",		ВТПозицииШтатногоРасписанияНаКонецПериода);
	ВнешниеНаборыДанных.Вставить("СотрудникиРаботавшиеНеполноеРабочееВремя",	ВТСотрудникиРаботавшиеНеполноеРабочееВремя);
	ВнешниеНаборыДанных.Вставить("СотрудникиВОтпускеБезОплаты",					ВТСотрудникиВОтпускеБезОплаты);
	ВнешниеНаборыДанных.Вставить("СотрудникиВПростое",							ВТСотрудникиВПростое);
	ВнешниеНаборыДанных.Вставить("СотрудникиВахта",								ВТСотрудникиВахта);
	ВнешниеНаборыДанных.Вставить("СотрудникиВОтпускеДоПолутораЛет",				ВТПлановыеНачисленияСотрудниковДоПолутораЛет);
	ВнешниеНаборыДанных.Вставить("СотрудникиВОтпускеДоТрехЛет",		ВТПлановыеНачисленияСотрудниковДоТрехЛет);
	
	Возврат ВнешниеНаборыДанных;
	
КонецФункции

Функция ЗаполнитьКомпоновщикНастроек(КлючВарианта, ПараметрыОтчета, СхемаКД)
	
	Организация = ПараметрыОтчета.Организация;
	Если ПараметрыОтчета.Свойство("ДатаНачалаПериодаВыборки") Тогда
		ДатаНачалаПериодаОтчета = ПараметрыОтчета.ДатаНачалаПериодаВыборки;
	Иначе
		ДатаНачалаПериодаОтчета = ПараметрыОтчета.ДатаНачалаПериодаОтчета;
	КонецЕсли;
	ДатаКонцаПериодаОтчета = ПараметрыОтчета.ДатаКонцаПериодаОтчета;
	
	// Настройка отборов
	ИсключитьДанныеОбособленныхПодразделений = Ложь;
	Если Не ПараметрыОтчета.Свойство("ИсключитьДанныеОбособленныхПодразделений", ИсключитьДанныеОбособленныхПодразделений) Тогда
		ИсключитьДанныеОбособленныхПодразделений = Ложь
	КонецЕсли;
	
	Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	Если Не ПараметрыОтчета.Свойство("ОбособленноеПодразделение", Подразделение) Тогда
		Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка()
	КонецЕсли;
	
	МассивПодразделений = ПодразделенияИсключения(Организация, Подразделение, ИсключитьДанныеОбособленныхПодразделений);
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКД));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКД.ВариантыНастроек[КлючВарианта].Настройки);
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	
	ПараметрПериод = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "Период");
	Если ПараметрПериод = Неопределено Тогда
		ПараметрПериод = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "ПериодОтчета");
	КонецЕсли;
	Если ПараметрПериод <> Неопределено Тогда
		ПараметрПериод.Значение.ДатаНачала = ДатаНачалаПериодаОтчета;
		ПараметрПериод.Значение.ДатаОкончания = ДатаКонцаПериодаОтчета;
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("СНачалаГода") Тогда
		ПараметрСНачалаГода = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "СНачалаГода");
		Если ПараметрСНачалаГода <> Неопределено Тогда
			ПараметрСНачалаГода.Значение = ПараметрыОтчета.СНачалаГода;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ДатаАктуальности") Тогда
		ПараметрДатаАктуальности = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "ДатаАктуальности");
		Если ПараметрДатаАктуальности <> Неопределено Тогда
			ПараметрДатаАктуальности.Значение = ДатаКонцаПериодаОтчета;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрИспользоватьШтатноеРасписание = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "ИспользоватьШтатноеРасписание");
	Если ПараметрИспользоватьШтатноеРасписание <> Неопределено Тогда
		ПараметрИспользоватьШтатноеРасписание.Значение = ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание");
	КонецЕсли;
	
	ПараметрКД = ЗарплатаКадрыОтчеты.НайтиПараметр(КомпоновщикНастроек, "ВидыВыплатНаПериодТрудоустройства");
	Если ПараметрКД <> Неопределено Тогда
		ПараметрКД.Значение = МассивВСписокЗначений(Справочники.ВидыВыплатБывшимСотрудникам.ВидыВыплатНаПериодТрудоустройства());
	КонецЕсли;
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	Отбор = Настройки.Отбор;
	Отбор.Элементы.Очистить();
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "Организация", ВидСравненияКомпоновкиДанных.Равно, Организация);
	
	Если Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("ГоловнаяОрганизация")) <> Неопределено Тогда
		ГоловнаяОрганизация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ГоловнаяОрганизация");
		Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
			ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "ГоловнаяОрганизация", ВидСравненияКомпоновкиДанных.Равно, ГоловнаяОрганизация);
	КонецЕсли;
	
	ТолькоГражданские = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ТолькоГражданские");
	Если ТолькоГражданские = Истина
		И Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("ВидТрудовогоДоговора")) <> Неопределено Тогда
		ИсключаемыеВидыДоговоров = Новый Массив;
		ИсключаемыеВидыДоговоров.Добавить(Перечисления.ВидыДоговоровССотрудниками.ВоеннослужащийПоПризыву);
		ИсключаемыеВидыДоговоров.Добавить(Перечисления.ВидыДоговоровССотрудниками.КонтрактВоеннослужащего);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Отбор, "ВидТрудовогоДоговора", ИсключаемыеВидыДоговоров, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;
	
	ТолькоОсновноеМестоРаботы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ТолькоОсновноеМестоРаботы");
	Если ТолькоОсновноеМестоРаботы = Истина
		И Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("ВидЗанятости")) <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Отбор, "ВидЗанятости", Перечисления.ВидыЗанятости.ОсновноеМестоРаботы);
	КонецЕсли;
	
	ИсключатьГодовыеПремии = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ИсключатьГодовыеПремии");
	Если ИсключатьГодовыеПремии = Истина
		И Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("ВидРасчета")) <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Отбор, "ВидРасчета", ПланыВидовРасчета.Начисления.ГодовыеПремии(), ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;
	
	ОтборСотрудников = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "ОтборСотрудников");
	Если ОтборСотрудников <> Неопределено
		И Отбор.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных("Сотрудник")) <> Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Отбор, "Сотрудник", ОтборСотрудников, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "Подразделение", ВидСравненияКомпоновкиДанных.ВСписке, МассивПодразделений);
	КонецЕсли;
	Если ИсключитьДанныеОбособленныхПодразделений Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "Подразделение", ВидСравненияКомпоновкиДанных.НеВСписке, МассивПодразделений);
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ИсключаяПерерасчеты") Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, "ПериодДействия", ВидСравненияКомпоновкиДанных.Равно, Новый ПолеКомпоновкиДанных("МесяцНачисления"));
	КонецЕсли;
	
	// Далее следует настройка варианта расшифровки.
	Если КлючВарианта <> "Расшифровка" Тогда
		Возврат КомпоновщикНастроек.ПолучитьНастройки();
	КонецЕсли;
	
	// Включение полей отчета, по которым выводится расшифровка.
	ВключитьВычисляемоеПоле(Настройки.Выбор, "Сотрудник");
	
	ОпределитьВидСКД(ПараметрыОтчета, Настройки);
	
	// Добавление отборов
	ЗаполнитьОтборДляОтчетаРасшифровки(Отбор, ПараметрыОтчета, ПараметрыОтчета.НомерСтроки);
	
	Возврат КомпоновщикНастроек.ПолучитьНастройки();
	
КонецФункции

Процедура ОпределитьВидСКД(ПараметрыОтчета, Настройки = Неопределено, ВидСКД = "", ДополнитьКомпоновщикНастроек = Истина, ВнешниеНаборыДанных = Неопределено)
	
	ВидОтчета = ПараметрыОтчета.ВидОтчета;
	НомерСтроки = ПараметрыОтчета.НомерСтроки;
	НомерКолонки = ПараметрыОтчета.НомерКолонки;
	ВариантРасчетаПоказателей = ?(ПараметрыОтчета.Свойство("ВариантРасчетаПоказателей"), ПараметрыОтчета.ВариантРасчетаПоказателей, 0);
	
	Если Настройки = Неопределено Тогда
		КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
		Настройки = КомпоновщикНастроек.Настройки;
	КонецЕсли;
	
	Отбор = Настройки.Отбор;
	Если ВидОтчета = "РегламентированныйОтчетП4_2013" Или ВидОтчета = "РегламентированныйОтчетП4_2017" Тогда
		
		// Отключение группировки по виду расчета для расшифровки численности.
		Если НомерКолонки = "02"
			Или НомерКолонки = "03"
			Или НомерКолонки = "04"
			Или НомерКолонки = "05"
			Или НомерКолонки = "06" Тогда
			Если ДополнитьКомпоновщикНастроек Тогда
				Настройки.Структура[0].Колонки[0].Использование = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Включение полей отчета, по которым выводится расшифровка.
		ПолеРесурса = "";
		Если НомерКолонки = "02" Тогда
			ПолеРесурса = "СредняяЧисленностьЗаПериодСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерКолонки = "03" Тогда
			ПолеРесурса = "СредняяЧисленностьЗаПериодВнешнихСовместителей";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерКолонки = "04" Тогда
			ПолеРесурса = "СредняяЧисленностьЗаПериодДоговорников";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерКолонки = "05" Тогда
			ПолеРесурса = "ОтработаноЧасовСНачалаГодаРаботникамиСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		ИначеЕсли НомерКолонки = "06" Тогда
			ПолеРесурса = "ОтработаноЧасовСНачалаГодаВнешнимиСовместителями";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		ИначеЕсли НомерКолонки = "08" Тогда
			ПолеРесурса = "НачисленоРаботникамСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		ИначеЕсли НомерКолонки = "09" Тогда
			ПолеРесурса = "НачисленоВнешнимСовместителям";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		ИначеЕсли НомерКолонки = "10" Тогда
			ПолеРесурса = "НачисленоРаботникамПоДоговорамГПХ";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		ИначеЕсли НомерКолонки = "11" Тогда
			ПолеРесурса = "НачисленияСоциальногоХарактера";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		КонецЕсли;
		
		Если ДополнитьКомпоновщикНастроек Тогда
			ВключитьВычисляемоеПоле(Настройки.Выбор, ПолеРесурса);
			
			// Отбор на пустые строки
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, ПолеРесурса, ВидСравненияКомпоновкиДанных.НеРавно, 0);
		КонецЕсли;
	ИначеЕсли ВидОтчета = "1ТГМС2015" Или ВидОтчета = "1ТГМС2019" Тогда
		
		// Включение полей отчета, по которым выводится расшифровка.
		ПолеРесурса = "";
		Если НомерКолонки = "03" Тогда
			ПолеРесурса = "КоличествоСтавок";
			ВидСКД = "ОсновнаяСхемаКомпоновкиДанных";
		ИначеЕсли НомерКолонки = "04" Тогда
			ПолеРесурса = "ЧисленностьНаКонецПериодаСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерКолонки = "05" Тогда
			ПолеРесурса = "СредняяЧисленностьЗаПериодСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерКолонки = "06" Тогда
			ПолеРесурса = "НачисленоРаботникамСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		ИначеЕсли НомерКолонки = "07" Тогда
			ПолеРесурса = "НачисленияСоциальногоХарактера";
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
		КонецЕсли;
		
		Если ДополнитьКомпоновщикНастроек Тогда
			Если ВидОтчета = "1ТГМС2015" Тогда
				ПутьКДаннымНомераСтроки = "ПользовательскиеПоля.НомерСтроки1ТГМС2015";
			ИначеЕсли ВидОтчета = "1ТГМС2019" Тогда
				ПутьКДаннымНомераСтроки = "ПользовательскиеПоля.НомерСтроки1ТГМС2019";
			КонецЕсли;
			
			// Отключение группировки по виду расчета для расшифровки численности.
			Если НомерКолонки = "04"
				Или НомерКолонки = "05" Тогда
				Настройки.Структура[0].Колонки[0].Использование = Ложь;
			КонецЕсли;
			
			ВключитьВычисляемоеПоле(Настройки.Выбор, ПолеРесурса);
			
			// Отбор на пустые строки
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, ПолеРесурса, ВидСравненияКомпоновкиДанных.НеРавно, 0);
			
			// Отбор по номеру строки 1-Т(ГМС)
			НомерСтрокиЧислом = Число(НомерСтроки);
			Если ВидОтчета = "1ТГМС2015"
				Или (ВидОтчета = "1ТГМС2019" И НомерСтрокиЧислом <> 1) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, ПутьКДаннымНомераСтроки, ВидСравненияКомпоновкиДанных.Равно, НомерСтрокиЧислом);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетП4НЗ_2015" Тогда
		
		// Включение полей отчета, по которым выводится расшифровка.
		ПолеРесурса = "";
		Если НомерСтроки = "02" Тогда
			ПолеРесурса = "ЧисленностьРаботавшихНеполноеРабочееВремя";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "03" Тогда
			ПолеРесурса = "ЧисленностьСотрудниковВПростое";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "05" Тогда
			ПолеРесурса = "ЧисленностьСотрудниковВОтпускеБезОплаты";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "06" Тогда
			ПолеРесурса = "ПринятоСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерСтроки = "07" Тогда
			ПолеРесурса = "ЧисленностьПринятыхВПериодеНаНовуюПозицию";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "08" Тогда
			ПолеРесурса = "УволеноСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		ИначеЕсли НомерСтроки = "09" Тогда
			ПолеРесурса = "ЧисленностьУволенныхПоСоглашениюСторон";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "10" Тогда
			ПолеРесурса = "ЧисленностьУволенныхВСвязиССокращением";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "12" Тогда
			ПолеРесурса = "ЧисленностьУволенныхПоСобственномуЖеланию";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "16" Тогда
			ПолеРесурса = "ЧисленностьЖенщинВОтпускеДоПолутораЛет";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "17" Тогда
			ПолеРесурса = "ЧисленностьЖенщинВОтпускеДоТрехЛет";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "20" Тогда
			ПолеРесурса = "ЧисленностьРаботавшихВахтовымМетодом";
			ВидСКД = "СхемаКомпоновкиДанныхФормаП4НЗ";
		ИначеЕсли НомерСтроки = "13" Тогда
			ПолеРесурса = "ЧисленностьНаНачалоСледующегоПериодаСписочногоСостава";
			ВидСКД = "СхемаКомпоновкиДанныхКадры";
		КонецЕсли;
		ВнешниеНаборыДанных = ВнешнийИсточникСтатистикиФормыП4НЗ(ПараметрыОтчета);
		
		Если ДополнитьКомпоновщикНастроек Тогда
			ВключитьВычисляемоеПоле(Настройки.Выбор, ПолеРесурса);
			
			// Отбор на пустые строки
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, ПолеРесурса, ВидСравненияКомпоновкиДанных.НеРавно, 0);
		КонецЕсли;
		
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетЗПЗдрав_2013"
		Или ВидОтчета = "РегламентированныйОтчетЗПКультура_2013" 
		Или ВидОтчета = "РегламентированныйОтчетЗПНаука_2013" 
		Или ВидОтчета = "РегламентированныйОтчетЗПОбразование_2013" 
		Или ВидОтчета = "РегламентированныйОтчетЗПСоц_2013" Тогда
		
		// Отключение группировки по виду расчета для расшифровки численности.
		Если НомерКолонки = "01" Или НомерКолонки = "02" Тогда
			Если ДополнитьКомпоновщикНастроек Тогда
				Настройки.Структура[0].Колонки[0].Использование = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Включение полей отчета, по которым выводится расшифровка.
		Если НомерКолонки = "04" И ВариантРасчетаПоказателей = 2015 Тогда
			
			ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			Если ДополнитьКомпоновщикНастроек Тогда
				ВключитьВычисляемоеПоле(Настройки.Выбор, "НачисленоВнутреннимСовместителям");
				ВключитьВычисляемоеПоле(Настройки.Выбор, "НачисленоРаботникамСписочногоСоставаПоСовмещению");
				
				ГруппаИЛИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Отбор.Элементы, "ГруппаИЛИ", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаИЛИ, "НачисленоВнутреннимСовместителям", 0, ВидСравненияКомпоновкиДанных.НеРавно);
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ГруппаИЛИ, "НачисленоРаботникамСписочногоСоставаПоСовмещению", 0, ВидСравненияКомпоновкиДанных.НеРавно);
			КонецЕсли;
			
		Иначе
			
			ПолеРесурса = "";
			Если НомерКолонки = "01" Тогда
				ПолеРесурса = "СредняяЧисленностьЗаПериодСписочногоСостава";
				ВидСКД = "СхемаКомпоновкиДанныхКадры";
			ИначеЕсли НомерКолонки = "02" Тогда
				ПолеРесурса = "СредняяЧисленностьЗаПериодВнешнихСовместителей";
				ВидСКД = "СхемаКомпоновкиДанныхКадры";
			ИначеЕсли НомерКолонки = "04" Тогда
				ПолеРесурса = "НачисленоВнутреннимСовместителям";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			ИначеЕсли НомерКолонки = "06" Тогда
				ПолеРесурса = "ФОТСписочногоСоставаБюджет";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			ИначеЕсли НомерКолонки = "07" Тогда
				ПолеРесурса = "ФОТСписочногоСоставаОМС";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			ИначеЕсли НомерКолонки = "08" Тогда
				ПолеРесурса = "ФОТСписочногоСоставаДоход";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			ИначеЕсли НомерКолонки = "09" Тогда
				ПолеРесурса = "ФОТВнешнихСовместителейБюджет";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			ИначеЕсли НомерКолонки = "10" Тогда
				ПолеРесурса = "ФОТВнешнихСовместителейОМС";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			ИначеЕсли НомерКолонки = "11" Тогда
				ПолеРесурса = "ФОТВнешнихСовместителейДоход";
				ВидСКД = "СхемаКомпоновкиДанныхЗарплата";
			КонецЕсли;
			
			Если ДополнитьКомпоновщикНастроек Тогда
				ВключитьВычисляемоеПоле(Настройки.Выбор, ПолеРесурса);
				
				// Отбор на пустые строки
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор, ПолеРесурса, ВидСравненияКомпоновкиДанных.НеРавно, 0);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполнение отбора компоновщика настроек СКД в зависимости от формы отчета.
//
// Параметры:
//  Отбор - ОтборКомпоновкиДанных
//  ПараметрыОтчета - Структура
//  ИмяСтроки - Строка.
//
Процедура ЗаполнитьОтборДляОтчетаРасшифровки(Отбор, ПараметрыОтчета, ИмяСтроки)
	
	ВидОтчета = ПараметрыОтчета.ВидОтчета;
	
	Если ВидОтчета = "РегламентированныйОтчетЗПЗдрав_2013"
		Или ВидОтчета = "РегламентированныйОтчетЗПКультура_2013"
		Или ВидОтчета = "РегламентированныйОтчетЗПНаука_2013"
		Или ВидОтчета = "РегламентированныйОтчетЗПОбразование_2013"
		Или ВидОтчета = "РегламентированныйОтчетЗПСоц_2013" Тогда
		
		// Отбор по категориям
		КатегорииОтчета = МассивКатегорийОтчета(ВидОтчета);
		СоответствиеВключаемыхСтрок = СоответствиеВключаемыхСтрок(ВидОтчета);
		
		ЗначениеОтбора = Новый Массив;
		Категория = КатегорииОтчета[Число(ИмяСтроки) - 2];
		Если ЗначениеЗаполнено(Категория) Тогда
			ЗначениеОтбора.Добавить(Категория);
		КонецЕсли;
		
		ВключенныеКатегории = СоответствиеВключаемыхСтрок.НайтиСтроки(Новый Структура("РодительскаяСтрока", Прав(ИмяСтроки, 2)));
		Для Каждого СтрокаКатегории Из ВключенныеКатегории Цикл
			Категория = КатегорииОтчета[Число(СтрокаКатегории.ПодСтрока) - 2];
			Если ЗначениеЗаполнено(Категория) Тогда
				ЗначениеОтбора.Добавить(Категория);
			КонецЕсли;
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор,
			"СтрокаОтчетностиМониторингаРаботниковСоциальнойСферы", ВидСравненияКомпоновкиДанных.ВСписке, ЗначениеОтбора);
		
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетП4_2013" Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор,
			"КодОКВЭД", ВидСравненияКомпоновкиДанных.Равно, ПараметрыОтчета.КодПоОКВЭД);
		
	ИначеЕсли ВидОтчета = "РегламентированныйОтчетП4_2017" Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Отбор,
			"КодОКВЭД2", ВидСравненияКомпоновкиДанных.Равно, ПараметрыОтчета.КодПоОКВЭД);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПодразделенияИсключения(Организация, Подразделение, ИсключитьДанныеОбособленныхПодразделений)
	
	// Пока подразделения с точки зрения статистики не выделяются.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Если ИсключитьДанныеОбособленныхПодразделений  Тогда
		
		// Отбираем подразделения с непустым кодом для статистики, эти подразделения не войдут в отчет.
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка
		|ПОМЕСТИТЬ ВТПодразделенияВерхнегоУровня
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	НЕ ПодразделенияОрганизаций.НомерТерриториальногоОрганаРосстата В (&ПустыеКоды)
		|	И ПодразделенияОрганизаций.Владелец = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				ПодразделенияВерхнегоУровня.Ссылка
		|			ИЗ
		|				ВТПодразделенияВерхнегоУровня КАК ПодразделенияВерхнегоУровня)
		|	И ПодразделенияОрганизаций.Владелец = &Организация";
		ПустыеКоды = Новый Массив;
		ПустыеКоды.Добавить("");
		ПустыеКоды.Добавить("              ");
		Запрос.УстановитьПараметр("ПустыеКоды", ПустыеКоды);
		
	ИначеЕсли ЗначениеЗаполнено(Подразделение) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ(&Подразделение)
		|	И ПодразделенияОрганизаций.Владелец = &Организация";
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		
	Иначе
		
		Возврат Новый Массив
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции 

Процедура ДанныеДляЗаполненияРегламентированнойОтчетностиКадры(Вариант, ПараметрыОтчета, ДанныеОтчета, СоответствиеПользовательскихПолей = Неопределено)
	
	СхемаКомпоновки = СКДОтчетаСтатистикаПерсонала("СхемаКомпоновкиДанныхКадры");
	
	ПараметрыОтчета.Вставить("Вариант", Вариант);
	Если Вариант = "РегламентированныйОтчетСтатистикаФормаП4" Тогда
		ПараметрыОтчета.Вставить("ТолькоГражданские", Истина);
	КонецЕсли;
	
	НастройкиОтчета = ЗаполнитьКомпоновщикНастроек(Вариант, ПараметрыОтчета, СхемаКомпоновки);
	СоответствиеПользовательскихПолей = ЗарплатаКадрыОтчеты.СоответствиеПользовательскихПолей(НастройкиОтчета);
	
	// Запрос данных
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновки, НастройкиОтчета);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Процедура ДанныеДляЗаполненияРегламентированнойОтчетностиШР1ТГМС(Вариант, ПараметрыОтчета, ДанныеОтчета, СоответствиеПользовательскихПолей = Неопределено) 
	
	СхемаКомпоновки = Отчеты.ШтатноеРасписание.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	ПараметрыОтчета.Вставить("Вариант", Вариант);
	ПараметрыОтчета.Вставить("ДатаАктуальности", ПараметрыОтчета.ДатаКонцаПериодаОтчета);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(Новый Структура("СхемаКомпоновкиДанных", СхемаКомпоновки));
	УдалитьКлючевоеСловоРазрешенные(СхемаКомпоновки);
	
	НастройкиОтчета = ЗаполнитьКомпоновщикНастроек(Вариант, ПараметрыОтчета, СхемаКомпоновки);
	
	СоответствиеПользовательскихПолей = ЗарплатаКадрыОтчеты.СоответствиеПользовательскихПолей(НастройкиОтчета);
	
	// Запрос данных
	МакетКомпоновки = ЗарплатаКадрыОтчеты.МакетКомпоновкиДанныхДляКоллекцииЗначений(СхемаКомпоновки, НастройкиОтчета);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
КонецПроцедуры

#КонецОбласти

#Область РегламентированнаяОтчетность

Функция РазобратьИмяПоказателяРегламентированногоОтчета(ИмяПоказателя, Шаблон = "П---РР--СССКК") Экспорт
	Результат = Новый Структура("НомерРаздела, НомерСтроки, НомерКолонки", "", "", "");
	
	КоличествоСимволов = СтрДлина(ИмяПоказателя);
	Если КоличествоСимволов <> СтрДлина(Шаблон) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для НомерСимвола = 1 По КоличествоСимволов Цикл
		
		СимволПоказателя = Сред(ИмяПоказателя, НомерСимвола, 1);
		СимволШаблона = Сред(Шаблон, НомерСимвола, 1);
		
		Если СимволШаблона = "Р" Тогда
			Результат.НомерРаздела = Результат.НомерРаздела + СимволПоказателя;
		ИначеЕсли СимволШаблона = "С" Тогда
			Результат.НомерСтроки = Результат.НомерСтроки + СимволПоказателя;
		ИначеЕсли СимволШаблона = "К" Тогда
			Результат.НомерКолонки = Результат.НомерКолонки + СимволПоказателя;
		ИначеЕсли СимволШаблона = " " Или СимволШаблона = "_" Или СимволШаблона = "-" Тогда
			// Пропуск.
		ИначеЕсли СимволШаблона <> СимволПоказателя Тогда
			Возврат Результат;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область ОбщегоНазначения

Функция МассивВСписокЗначений(Массив) Экспорт
	Результат = Новый СписокЗначений;
	Результат.ЗагрузитьЗначения(Массив);
	Возврат Результат;
КонецФункции

Функция СкопироватьКоллекцию(Коллекция) Экспорт
	Возврат ОбщегоНазначения.ЗначениеИзСтрокиXML(ОбщегоНазначения.ЗначениеВСтрокуXML(Коллекция));
КонецФункции

Функция СвернутьТаблицу(Знач ТаблицаЗначений, ИменаКолонокГруппировки, Копировать = Истина) Экспорт
	МассивКолонокГруппировки = СтрРазделить(ИменаКолонокГруппировки, ", ", Ложь);
	МассивКолонокСуммирования = Новый Массив;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если МассивКолонокГруппировки.Найти(Колонка.Имя) = Неопределено Тогда
			МассивКолонокСуммирования.Добавить(Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	Если Копировать Тогда
		ТаблицаЗначений = ТаблицаЗначений.Скопировать();
	КонецЕсли;
	ТаблицаЗначений.Свернуть(ИменаКолонокГруппировки, СтрСоединить(МассивКолонокСуммирования, ", "));
	Возврат ТаблицаЗначений;
КонецФункции

#КонецОбласти

#Область СистемаКомпоновкиДанных

Процедура ВключитьВычисляемоеПоле(ВыбранныеПоля, ИмяПоля) Экспорт
	
	Если Лев(ИмяПоля, 20) = "ПользовательскиеПоля" Тогда
		ИмяПоля = СтрЗаменить(ИмяПоля, "ПользовательскиеПоля", "ПользовательскиеПоля.");
	КонецЕсли;
	
	ИскомоеПоле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	Для Каждого НастройкаПоля Из ВыбранныеПоля.Элементы Цикл
		Если НастройкаПоля.Поле = ИскомоеПоле Тогда
			НастройкаПоля.Использование = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьКлючевоеСловоРазрешенные(СхемаКомпоновки) Экспорт
	
	// Заполнение регламентированного отчета бессмысленно по разрешенным данным
	Для Каждого НаборДанных Из СхемаКомпоновки.НаборыДанных Цикл
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "РАЗРЕШЕННЫЕ", "");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
