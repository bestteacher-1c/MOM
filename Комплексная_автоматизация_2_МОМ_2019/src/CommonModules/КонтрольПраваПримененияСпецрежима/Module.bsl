#Область ПрограммныйИнтерфейс

#Область НормативноСправочнаяИнформацияПоПравуПримененияСпецрежима

// Возвращает лимит доходов налогоплательщика, ограничивающий его право на применение УСН
// с учетом коэффициента дефлятора для указанной даты.
// 
// Параметры:
//  Дата - Дата - Дата, на которую получаем границу.
//
// Возвращаемое значение:
//  Число - Число - Граница доходов, ограничивающая право применения УСН.
//
Функция ГраницаДоходовОграничивающаяПравоПримененияУСН(Дата) Экспорт

	БазовыйЛимитДоходов = БазовыйЛимитДоходовДляПримененияУСН(Дата);
	
	// Коэффициент ежегодной индексации базовой величины предельного дохода.

	КоэффициентДефлятор = 1;
	
	Если Год(Дата) = 2014 Тогда
		КоэффициентДефлятор = 1.067; // Приказ Минэкономразвития России от 07.11.2013 № 652.
	ИначеЕсли Год(Дата) = 2015 Тогда
		КоэффициентДефлятор = 1.147; // Приказ Минэкономразвития России от 29.10.2014 № 685.
	ИначеЕсли Год(Дата) = 2016 Тогда
		КоэффициентДефлятор = 1.329; // Приказ Минэкономразвития России от 20.10.2015 № 772.
	ИначеЕсли Год(Дата) >= 2017 Тогда
		КоэффициентДефлятор = 1;     // Федеральный закон от 03.07.2016 N 243-ФЗ.
	КонецЕсли;
	
	Возврат БазовыйЛимитДоходов * КоэффициентДефлятор;
	
КонецФункции

// Возвращает базовый лимит доходов налогоплательщика для применения УСН,
// действующий на указанную дату, без учета коэффициента-дефлятора.
//
// Параметры:
//  Дата - Дата - Дата, на которую получаем базовый лимит.
//
// Возвращаемое значение:
//  Число - Число - Лимит доходов, законодательно установленный для применения УСН.
//
Функция БазовыйЛимитДоходовДляПримененияУСН(Дата) Экспорт

	// Лимит доходов налогоплательщика, ограничивающий его право на применение УСН.
	// п. 4 ст. 346.13 НК РФ.
	БазовыйЛимит = 60000000; // в редакции Федерального закона от 25.06.2012 N 94-ФЗ.
	
	Если Год(Дата) >= 2017 Тогда
		БазовыйЛимит = 150000000; // в редакции Федерального закона от 30.11.2016 № 401-ФЗ.
	КонецЕсли;
	
	Возврат БазовыйЛимит;

КонецФункции

// Возвращает сведения о применении на указанную дату коэффициента-дефлятора
// для индексации базового лимита доходов, ограничивающих право применения УСН.
//
// Параметры:
//  Дата - Дата - Дата, на которую получаем сведения об использовании дефлятора.
//
// Возвращаемое значение:
//  Булево - Булево - Если Истина, то коэффициент-дефлятор применяется.
//
Функция ИспользуетсяКоэффициентДефляторУСН(Дата) Экспорт

	Возврат Год(Дата) < 2017;

КонецФункции

// Возвращает лимит доходов налогоплательщика, ограничивающий его право на применение УСН
// с учетом коэффициента дефлятора для указанной даты.
//
// Параметры:
//	Дата - Дата - Дата, на которую требуется вернуть лимит.
// 
// Возвращаемое значение:
//  Число - Число - Граница стоимости основных средств, ограничивающая право применения УСН.
//
Функция ГраницаСтоимостиОсновныхСредствОграничивающаяПравоПримененияУСН(Дата) Экспорт

	// Лимит остаточной стоимости основных средств, определяемой по правилам бухгалтерского учета.
	// См. пп. 16 п. 3 ст. 346.12 НК РФ.
	ПредельнаяСтоимостьОС = 100000000;
	
	Если Год(Дата) >= 2017 Тогда
		ПредельнаяСтоимостьОС = 150000000; // Федеральный закон от 03.07.2016 N 243-ФЗ.
	КонецЕсли;
	
	Возврат ПредельнаяСтоимостьОС;
	
КонецФункции

// Возвращает лимит средней численности работников.
//
// Возвращаемое значение:
//  Число - Число - Граница среднесписочной численности работников, ограничивающая право применения УСН.
//
Функция ГраницаСреднесписочнойЧисленностиРаботниковОграничивающаяПравоПримененияУСН() Экспорт
	
	// Лимит средней численности работников за налоговый период,
	// пп. 15 п. 3 ст. 346.12 НК РФ. 
	Возврат 100;
	
КонецФункции

#КонецОбласти

#КонецОбласти
