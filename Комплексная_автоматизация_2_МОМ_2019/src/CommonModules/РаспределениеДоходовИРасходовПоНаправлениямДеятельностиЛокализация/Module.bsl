
#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	Движения = Объект.Движения;
	ДополнительныеСвойства = Объект.ДополнительныеСвойства;
	//++ Локализация
	//++ НЕ УТ
	РеглУчетПроведениеСервер.ЗарегистрироватьКОтражению(Объект, ДополнительныеСвойства, Движения, Отказ);
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.<Имя документа> - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	#Область СписаниеКосвенныхРасходовОСНО
	ТекстСписаниеКосвенныхРасходовОСНО = "
	|ВЫБРАТЬ 
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(КосвенныеРасходы.СуммаРеглОСНО) КАК Сумма,
	|	СУММА(КосвенныеРасходы.СуммаУпрОСНО) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовОСНО) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьУчетЗатратПоНаправлениямДеятельности ТОГДА
	|			КосвенныеРасходы.КорНаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаНУДт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаОСНО) КАК СуммаПРДт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СУММА(КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаНУКт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаОСНО) КАК СуммаПРКт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаОСНО) КАК СуммаВРКт,
	|	""Списание косвенных расходов (ОСНО)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (КосвенныеРасходы.СуммаРеглОСНО <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаОСНО <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаОСНО <> 0
	|		ИЛИ КосвенныеРасходы.СуммаУпрОСНО <> 0)
	|
	|СГРУППИРОВАТЬ ПО 
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	КосвенныеРасходы.Организация,
	|	КосвенныеРасходы.СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов,
	|	КосвенныеРасходы.Подразделение,
	|	КосвенныеРасходы.НаправлениеДеятельности,
	|	КосвенныеРасходы.КорНаправлениеДеятельности
	|";
	#КонецОбласти
	
	#Область СписаниеКосвенныхРасходовЕНВД
	ТекстСписаниеКосвенныхРасходовЕНВД = "
	|ВЫБРАТЬ 
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СУММА(КосвенныеРасходы.СуммаРеглЕНВД) КАК Сумма,
	|	СУММА(КосвенныеРасходы.СуммаУпрЕНВД) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьУчетЗатратПоНаправлениямДеятельности ТОГДА
	|			КосвенныеРасходы.КорНаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаНУДт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаЕНВД) КАК СуммаПРДт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	КосвенныеРасходы.АналитикаРасходов КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СУММА(КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаНУКт,
	|	СУММА(КосвенныеРасходы.ПостояннаяРазницаЕНВД) КАК СуммаПРКт,
	|	СУММА(КосвенныеРасходы.ВременнаяРазницаЕНВД) КАК СуммаВРКт,
	|	""Списание косвенных расходов (ЕНВД)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (КосвенныеРасходы.СуммаРеглЕНВД <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаЕНВД <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаЕНВД <> 0
	|		ИЛИ КосвенныеРасходы.СуммаУпрЕНВД <> 0)
	|
	|СГРУППИРОВАТЬ ПО 
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	КосвенныеРасходы.Организация,
	|	КосвенныеРасходы.СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов,
	|	КосвенныеРасходы.Подразделение,
	|	КосвенныеРасходы.НаправлениеДеятельности,
	|	КосвенныеРасходы.КорНаправлениеДеятельности
	|";
	#КонецОбласти

	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстСписаниеКосвенныхРасходовОСНО);
	ТекстыОтражения.Добавить(ТекстСписаниеКосвенныхРасходовЕНВД);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Регистратор,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	КосвенныеРасходы.Подразделение КАК Подразделение,
	|	КосвенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	КосвенныеРасходы.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаРегл
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.СуммаРегл - ВЫРАЗИТЬ(КосвенныеРасходы.СуммаРегл * ЕСТЬNULL(Доли.ДоляЕНВД, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРеглОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаРегл
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.СуммаРегл * ЕСТЬNULL(Доли.ДоляЕНВД, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРеглЕНВД,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаУпр
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.СуммаУпр - ВЫРАЗИТЬ(КосвенныеРасходы.СуммаУпр * ЕСТЬNULL(Доли.ДоляЕНВД, 0) / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпрОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаУпр
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.СуммаУпр * ЕСТЬNULL(Доли.ДоляЕНВД, 0) / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпрЕНВД,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ)
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница - ВЫРАЗИТЬ(КосвенныеРасходы.ПостояннаяРазница * ЕСТЬNULL(Доли.ДоляЕНВД, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ПостояннаяРазницаОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.ПостояннаяРазница * ЕСТЬNULL(Доли.ДоляЕНВД, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ПостояннаяРазницаЕНВД,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ)
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница - ВЫРАЗИТЬ(КосвенныеРасходы.ВременнаяРазница * ЕСТЬNULL(Доли.ДоляЕНВД, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ВременнаяРазницаОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.ВременнаяРазница * ЕСТЬNULL(Доли.ДоляЕНВД, 0) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ВременнаяРазницаЕНВД
	|
	|ПОМЕСТИТЬ КосвенныеРасходы 
	|ИЗ
	|	Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		КосвенныеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|	ПО 
	|		КосвенныеРасходы.Организация = ПорядокОтраженияРасходов.Организация
	|		И КосвенныеРасходы.СтатьяРасходов = ПорядокОтраженияРасходов.СтатьяРасходов
	|		И КосвенныеРасходы.Подразделение = ПорядокОтраженияРасходов.Подразделение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(Операция.Дата, ДЕНЬ) = УчетнаяПолитикаОрганизаций.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДолиСписанияКосвенныхРасходов КАК Доли
	|	ПО
	|		Доли.Период МЕЖДУ НАЧАЛОПЕРИОДА(Операция.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(Операция.Дата, МЕСЯЦ)
	|		И Доли.Организация = КосвенныеРасходы.Организация 
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = Доли.ПериодРасчета
	|		
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И КосвенныеРасходы.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И НЕ ВЫБОР
	|			КОГДА ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|				ТОГДА ПорядокОтраженияРасходов.СчетУчета
	|			ИНАЧЕ СтатьиРасходов.СчетУчета
	|		КОНЕЦ В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходыИРасходы), 
	|						  ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи))
	|;
	|";
	
	Возврат ТекстЗапроса;
	
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры
//++ Локализация
//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.РаспределениеДоходовПоНаправлениямДеятельности.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеДоходы", ТекстыЗапроса) Тогда
		Документы.РаспределениеДоходовПоНаправлениямДеятельности.ТекстЗапросаТаблицаВтПрочиеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период                      КАК Период,
	|	ПрочиеДоходы.Организация     КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтПрочиеДоходы КАК ПрочиеДоходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период                      КАК Период,
	|	ПрочиеРасходы.Организация    КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтПрочиеРасходы КАК ПрочиеРасходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ
//-- Локализация

#КонецОбласти

#КонецОбласти
