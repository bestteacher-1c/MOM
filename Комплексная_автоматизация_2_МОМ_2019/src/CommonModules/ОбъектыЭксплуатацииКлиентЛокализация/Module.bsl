////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации справочника "Объекты эксплуатации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры = Неопределено) Экспорт

	ТребуетсяВызовСервера = Ложь;
	ПродолжитьИзменениеРеквизита = Истина;
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	//++ Локализация
	
	Если ИмяЭлемента = Элементы.ГруппаОС.Имя Тогда	
		
		Если ЗначениеЗаполнено(Объект.ГруппаОС) Тогда
			Объект.НедвижимоеИмущество = ВнеоборотныеАктивыКлиентСерверЛокализация.ЭтоНедвижимоеИмущество(Объект.ГруппаОС);
		КонецЕсли; 
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", ИмяЭлемента);
		
	ИначеЕсли ИмяЭлемента = Элементы.ДрагоценныеМатериалыДрагоценныйМатериал.Имя Тогда	
		
		ТребуетсяВызовСервера = Истина;
		
	ИначеЕсли ИмяЭлемента = Элементы.КодПоОКОФ.Имя Тогда	
		
		КодПоОКОФПриИзменении(Объект, ТребуетсяВызовСервера);
		
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьИзменениеРеквизита Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
			Форма, 
			ИмяЭлемента, 
			ДополнительныеПараметры,
			ТребуетсяВызовСервера);
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура НачалоВыбораРеквизита(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент.Имя = Элементы.КодПоОКОФ.Имя Тогда	
		
		КодПоОКОФНачалоВыбора(Форма, СтандартнаяОбработка);
		
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт

	//++ Локализация
	
	Если ИмяСобытия = "ВыборАмортизационнойГруппыОС" 
		И Источник = Форма.УникальныйИдентификатор Тогда
		
		ОбработатьВыборКодаПоОКОФ(Форма, Параметр);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

Процедура КодПоОКОФПриИзменении(Объект, ТребуетсяВызовСервера)
	
	Если Не ЗначениеЗаполнено(Объект.АмортизационнаяГруппа) И ЗначениеЗаполнено(Объект.КодПоОКОФ) Тогда
		ТребуетсяВызовСервера = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура КодПоОКОФНачалоВыбора(Форма, СтандартнаяОбработка)
	
	Объект = Форма.Объект;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("КодПоОКОФ", Объект.КодПоОКОФ);
	ПараметрыОткрытия.Вставить("АмортизационнаяГруппа", Объект.АмортизационнаяГруппа);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораАмортизационнойГруппы", ПараметрыОткрытия, Форма);
	
КонецПроцедуры

Процедура ОбработатьВыборКодаПоОКОФ(Форма, ВыбранноеЗначение)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиент.ПродолжитьИзменениеРеквизита(
		Форма, 
		"ОбработатьВыборКодаПоОКОФ", 
		ВыбранноеЗначение,
		Истина);
	
КонецПроцедуры

//-- Локализация

#КонецОбласти
