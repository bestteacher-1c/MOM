////////////////////////////////////////////////////////////////////////////////
// НСИ производства: Процедуры подсистемы управления данными об изделиях
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Спецификации

// Возвращает спецификацию изделия по приоритету и параметрам выбора
//
// Параметры:
//  Номенклатура				 - СправочникСсылка.Номенклатура - номенклатура
//  Характеристика				 - СправочникСсылка.ХарактеристикиНоменклатуры	 - характеристика номенклатуры
//  НачатьНеРанее				 - Дата											 - дата начала производства (на эту дату спецификация должна быть действующей)
//  Подразделение				 - СправочникСсылка.СтруктураПредприятия		 - подразделение, за которым закреплено производство
//  ПараметрыВыбораСпецификаций	 - Структура									 - см. УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификаций()
//  ТекущаяСпецификация			 - СправочникСсылка.РесурсныеСпецификации		 - спецификация, которая уже указана для изделия (если необходимо не менять спецификацию).
// 
// Возвращаемое значение:
//  Структура, Неопределено - содержит данные спецификации изделия.
//
Функция СпецификацияИзделия(Номенклатура, Характеристика, НачатьНеРанее, Подразделение, ПараметрыВыбораСпецификаций, ТекущаяСпецификация = Неопределено) Экспорт
	
	ДанныеСпецификации = Неопределено;
	СтрокаТекущаяСпецификация = Неопределено;
	
	СписокСпецификаций = УправлениеДаннымиОбИзделиях.СпецификацииИзделия(
		Номенклатура,
		Характеристика,
		НачатьНеРанее,
		Подразделение,
		ПараметрыВыбораСпецификаций);
	
	Если СписокСпецификаций.ВГраница() <> -1 Тогда
		
		СпецификацияПоУмолчанию = СписокСпецификаций[0];
		
		Если ЗначениеЗаполнено(ТекущаяСпецификация) Тогда
			Для каждого СтрокаСпецификация Из СписокСпецификаций Цикл
				Если СтрокаСпецификация.Спецификация = ТекущаяСпецификация Тогда
					СтрокаТекущаяСпецификация = СтрокаСпецификация;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Если выбрана текущая спецификация, то вернем ее, в случае если она есть в списке доступных.
		Если СтрокаТекущаяСпецификация <> Неопределено Тогда
			
			ДанныеСпецификации = СтрокаТекущаяСпецификация;
			ДанныеСпецификации.Вставить("ВыбранаОсновнаяСпецификация", (СпецификацияПоУмолчанию.ОсновнаяСпецификация И СпецификацияПоУмолчанию.Спецификация = ТекущаяСпецификация));
			
		ИначеЕсли СпецификацияПоУмолчанию.ОсновнаяСпецификация ИЛИ СписокСпецификаций.Количество() = 1 Тогда
			
			ДанныеСпецификации = СпецификацияПоУмолчанию;
			ДанныеСпецификации.Вставить("ВыбранаОсновнаяСпецификация", СпецификацияПоУмолчанию.ОсновнаяСпецификация);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеСпецификации;
	
КонецФункции

// Копирует спецификацию и этапы
//
// Параметры:
//  Источник	- СправочникСсылка.РесурсныеСпецификации - спецификация, которую нужно скопировать.
//
// Возвращаемое значение:
//   СправочникСсылка.РесурсныеСпецификации   - копия спецификации.
//
Функция КопироватьРесурснуюСпецификацию(Источник) Экспорт

	НачатьТранзакцию();
	Попытка
		
		СсылкаНового = Справочники.РесурсныеСпецификации.ПолучитьСсылку();
		
		Объект = Источник.Скопировать();
		Объект.Наименование = Объект.Наименование + " " + НСтр("ru = '(копия)'");
		
		Если НЕ ЗаполнитьЭтапыПоРесурснойСпецификации(Объект, Источник, СсылкаНового) Тогда
			
			ОтменитьТранзакцию();
			Возврат Неопределено;
			
		КонецЕсли;
		
		Объект.УстановитьСсылкуНового(СсылкаНового);
		Объект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		
		ОтменитьТранзакцию();
		Возврат Неопределено;
		
	КонецПопытки;
	
	Возврат Объект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область ОсновныеСпецификации

// Получает основную спецификацию для указанных цеха, номенклатуры на дату начала производства.
//
// Параметры:
//  Подразделение      - СправочникСсылка.СтруктураПредприятия - подразделение, за которым закреплено обеспечение изделием
//  Номенклатура       - СправочникСсылка.Номенклатура - производимое изделие
//  Характеристика     - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика производимого изделия
//  НачалоПроизводства - Дата - дата начала производства, на эту дату спецификация должна быть действующей.
//
// Возвращаемое значение:
//   СправочникСсылка.РесурсныеСпецификации - действующая спецификация.
//
Функция ПолучитьОсновнуюСпецификацию(Подразделение, Номенклатура, Характеристика, НачалоПроизводства) Экспорт

	ТекстЗапроса = УправлениеДаннымиОбИзделиях.ПолучитьТекстЗапросаОсновнойСпецификации();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Подразделение",      Подразделение);
	Запрос.УстановитьПараметр("Номенклатура",       Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",     Характеристика);
	Запрос.УстановитьПараметр("НачалоПроизводства", НачалоПроизводства);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.Спецификация;
	
КонецФункции

// Проверяет, является ли спецификация основной
//
// Параметры:
//  Спецификация	 - СправочникСсылка.РесурсныеСпецификации	 - спецификация
//  Подразделение	 - СправочникСсылка.СтруктураПредприятия	 - подразделение, за которым закреплено обеспечение изделием
//  Номенклатура	 - СправочникСсылка.Номенклатура			 - изделие
//  Характеристика	 - СправочникСсылка.ХарактеристикиНоменклатуры	 - характеристика изделия.
// 
// Возвращаемое значение:
//  Булево - признак, истина если спецификация основная.
//
Функция СпецификацияОсновная(Спецификация, Подразделение = Неопределено, Номенклатура = Неопределено, Характеристика = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ОсновныеСпецификации КАК Т
	|ГДЕ
	|	Т.Спецификация = &Спецификация
	|	И Т.Подразделение = &Подразделение
	|	И &ОтборНоменклатура
	|	И &ОтборХарактеристика");
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	
	Если Подразделение <> Неопределено Тогда
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Иначе
		Запрос.УстановитьПараметр("Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	КонецЕсли;
	
	Если Номенклатура <> Неопределено Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборНоменклатура", "Т.Номенклатура = &ОтборНоменклатура");
		Запрос.УстановитьПараметр("ОтборНоменклатура", Номенклатура);
		
		Если Характеристика <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборХарактеристика", "Т.Характеристика = &ОтборХарактеристика");
			Запрос.УстановитьПараметр("ОтборХарактеристика", Характеристика);
		Иначе
			Запрос.УстановитьПараметр("ОтборХарактеристика", Истина);
		КонецЕсли;
		
	Иначе
		Запрос.УстановитьПараметр("ОтборНоменклатура", Истина);
		Запрос.УстановитьПараметр("ОтборХарактеристика", Истина);
	КонецЕсли;
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Назначает спецификацию основной для указанного изделия и подразделения
//
// Параметры:
//  СвойстваЗаписи - Структура - содержит значения свойств записи
//  НавигационнаяСсылка - Строка - навигационная ссылка на измененную запись
//  СтруктураОшибки	- Структура - содержит результат проверки.
//
// Возвращаемое значение:
//   Булево - Истина, если спецификация назначена основной.
//
Функция НазначитьОсновнойСпецификацией(СвойстваЗаписи, НавигационнаяСсылка = Неопределено, СтруктураОшибки = Неопределено) Экспорт
	
	СтруктураОшибки = Новый Структура;
	
	СвойстваСпецификации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СвойстваЗаписи.Спецификация, "Статус,ТипПроизводственногоПроцесса");
	
	Если Не УправлениеДаннымиОбИзделияхКлиентСервер.СпецификациюМожноНазначитьОсновной(СвойстваСпецификации, СтруктураОшибки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьОшибки = НЕ РегистрыСведений.ОсновныеСпецификации.НазначитьДляИзделия(СвойстваЗаписи);
	Если ЕстьОшибки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтруктураТекущейЗаписи = РегистрыСведений.ОсновныеСпецификации.СтруктураЗаписи();
	ЗаполнитьЗначенияСвойств(СтруктураТекущейЗаписи, СвойстваЗаписи);
	
	КлючЗаписи = РегистрыСведений.ОсновныеСпецификации.СоздатьКлючЗаписи(СтруктураТекущейЗаписи);
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(КлючЗаписи);
	
	Возврат Истина;
	
КонецФункции

// Назначает спецификации основными, для указанных изделий и подразделений
//
// Параметры:
//  НаборДанных	 - Массив	 - содержит набор структур см. РегистрыСведений.ОсновныеСпецификации.СтруктураЗаписи()
//  МассивОшибок - Массив	 - содержит результат проверки.
// 
// Возвращаемое значение:
//  Булево - Истина, если все спецификации назначены основными.
//
Функция НазначитьОсновныеСпецификацииДляИзделий(НаборДанных, МассивОшибок = Неопределено) Экспорт
	
	МассивОшибок = Новый Массив;
	
	МассивСпецификаций = Новый Массив;
	Для каждого Строка Из НаборДанных Цикл
		Если МассивСпецификаций.Найти(Строка.Спецификация) = Неопределено Тогда
			МассивСпецификаций.Добавить(Строка.Спецификация);
		КонецЕсли;
	КонецЦикла;
	СвойстваСпецификаций = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСпецификаций, "Статус,ТипПроизводственногоПроцесса");
	
	Для каждого Строка Из НаборДанных Цикл
		
		СтруктураОшибки = Новый Структура;
		
		СвойстваСпецификации = СвойстваСпецификаций[Строка.Спецификация];
		
		Если НЕ УправлениеДаннымиОбИзделияхКлиентСервер.СпецификациюМожноНазначитьОсновной(СвойстваСпецификации, СтруктураОшибки) Тогда
			ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Строка);
			ОписаниеОшибки.Вставить("СтруктураОшибки", СтруктураОшибки);
			МассивОшибок.Добавить(ОписаниеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивОшибок.ВГраница() <> -1 ИЛИ НЕ РегистрыСведений.ОсновныеСпецификации.НазначитьДляИзделий(НаборДанных) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Снимает признак "Основная спецификация" для указанной спецификации
//
// Параметры:
//  Спецификация	 - СправочникСсылка.РесурсныеСпецификации	 - спецификация.
//
// Возвращаемое значение:
//   Булево - Истина, если признак снят успешно.
//
Функция УбратьПризнакОсновнойСпецификации(Спецификация) Экспорт
	
	Возврат РегистрыСведений.ОсновныеСпецификации.УбратьПризнакДляСпецификации(Спецификация);
	
КонецФункции

// Снимает отметку "Основная спецификация" для указанного изделия и подразделения
//
// Параметры:
//  СтруктураЗаписи - Структура - структура записи, см. СтруктураЗаписи().
//
// Возвращаемое значение:
//   Булево - Истина, если признак снят успешно.
//
Функция УбратьПризнакОсновнаяСпецификацияДляИзделия(СтруктураЗаписи) Экспорт

	Возврат РегистрыСведений.ОсновныеСпецификации.УбратьПризнакДляИзделия(СтруктураЗаписи);
	
КонецФункции

#КонецОбласти


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РесурсныеСпецификацииОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Параметры.Свойство("ПолучитьСпецификацииПоНоменклатуре") И Параметры.Свойство("Номенклатура") Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		
		Номенклатура = Параметры.Номенклатура;
		
		Если Параметры.Свойство("Характеристика") Тогда
			Характеристика = Параметры.Характеристика;
		Иначе
			Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
		Если Параметры.Свойство("НачалоПроизводства") Тогда
			НачалоПроизводства = Параметры.НачалоПроизводства;
		Иначе
			НачалоПроизводства = '00010101';
		КонецЕсли;
		
		Если Параметры.Свойство("Подразделение") Тогда
			Подразделение = Параметры.Подразделение;
		Иначе
			Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
		КонецЕсли;
		
		Список = УправлениеДаннымиОбИзделиях.СписокСпецификацийПоНоменклатуре(Номенклатура, Характеристика, НачалоПроизводства, Подразделение, Параметры);
		ДанныеВыбора.ЗагрузитьЗначения(Список);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура РесурсныеСпецификацииОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, СтандартнаяОбработка) Экспорт

	Если ВидФормы = "ФормаВыбора" И Параметры.Свойство("ПолучитьСпецификацииПоНоменклатуре") И Параметры.Свойство("Номенклатура") Тогда
		ВыбраннаяФорма = "ФормаВыбораПоНоменклатуре";
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

Функция УстановитьСтатусСпецификаций(ВыделенныеСсылки, ЗначениеСтатуса) Экспорт

	НовыйСтатус = Перечисления.СтатусыСпецификаций[ЗначениеСтатуса];
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РесурсныеСпецификации.Ссылка,
	|	РесурсныеСпецификации.Наименование,
	|	РесурсныеСпецификации.ПометкаУдаления
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|ГДЕ
	|	РесурсныеСпецификации.Статус <> &НовыйСтатус
	|	И РесурсныеСпецификации.Ссылка В(&ВыделенныеСсылки)";
	
	Запрос.УстановитьПараметр("ВыделенныеСсылки", ВыделенныеСсылки);
	Запрос.УстановитьПараметр("НовыйСтатус",      НовыйСтатус);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	КоличествоОбработанных = 0;
	ОчередьКРасчету = Новый Массив;
	
	Если НовыйСтатус = Перечисления.СтатусыСпецификаций.ВРазработке Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПометкаУдаления Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Нельзя изменить статус помеченной на удаление спецификации ""%1"".'"),
									Выборка.Наименование);
									
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Выборка.Ссылка); 
			Продолжить;
		КонецЕсли;
		
		СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		НачатьТранзакцию();
		
		Попытка
			
			Если НовыйСтатус = Перечисления.СтатусыСпецификаций.ВРазработке Тогда
				ПараметрыПроверки = Новый Структура("Объект", Выборка.Ссылка);
				Справочники.РесурсныеСпецификации.ПроверитьИспользованиеОбъекта(ПараметрыПроверки, АдресХранилища);
				ЕстьСсылки = ПолучитьИзВременногоХранилища(АдресХранилища);
				Если ЕстьСсылки Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Спецификация ""%1"" используется. Установка статуса ""В разработке"" допускается только в форме спецификации.'"),
											СпрОбъект.Наименование);
											
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Выборка.Ссылка); 
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;
			КонецЕсли; 
			
			СпрОбъект.Статус = НовыйСтатус;
			СпрОбъект.ДополнительныеСвойства.Вставить("ПроверитьЭтапы");
			СпрОбъект.ДополнительныеСвойства.Вставить("ЗапретитьРасчетДлительностиПроизводства");
			Если СпрОбъект.ПроверитьЗаполнение() Тогда
				СпрОбъект.Записать();
				КоличествоОбработанных = КоличествоОбработанных + 1;
				Если НовыйСтатус = Перечисления.СтатусыСпецификаций.Действует Тогда
					ОчередьКРасчету.Добавить(Выборка.Ссылка);
				КонецЕсли;
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
	
	РегистрыСведений.ЗаданияКРасчетуДлительностиПроизводства.ЗапуститьЗадание(ОчередьКРасчету);
	
	Возврат КоличествоОбработанных;

КонецФункции

Функция ЗаполнитьЭтапыПоРесурснойСпецификации(Приемник, Источник, СсылкаПриемника)

	Запрос = Новый Запрос(
	// 0
	"ВЫБРАТЬ
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	РесурсныеСпецификацииВыходныеИзделия.Характеристика КАК Характеристика,
	|	РесурсныеСпецификацииВыходныеИзделия.Упаковка КАК Упаковка,
	|	РесурсныеСпецификацииВыходныеИзделия.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РесурсныеСпецификацииВыходныеИзделия.Количество КАК Количество,
	|
	|	РесурсныеСпецификацииВыходныеИзделия.ДоляСтоимости КАК ДоляСтоимости,
	|
	|	РесурсныеСпецификацииВыходныеИзделия.ОбработатьПоСпецификации КАК ОбработатьПоСпецификации,
	|	РесурсныеСпецификацииВыходныеИзделия.Спецификация КАК Спецификация,
	|
	|	РесурсныеСпецификацииВыходныеИзделия.ПроцентБрака КАК ПроцентБрака,
	|
	|	РесурсныеСпецификацииВыходныеИзделия.ЭтапРедактирование КАК ЭтапРедактирование,
	|
	|	РесурсныеСпецификацииВыходныеИзделия.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
	|	РесурсныеСпецификацииВыходныеИзделия.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
	|	РесурсныеСпецификацииВыходныеИзделия.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
	|	РесурсныеСпецификацииВыходныеИзделия.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
	|	РесурсныеСпецификацииВыходныеИзделия.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|
	|	РесурсныеСпецификацииВыходныеИзделия.КлючСвязи КАК КлючСвязи
	|
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РесурсныеСпецификацииВыходныеИзделия
	|ГДЕ
	|	РесурсныеСпецификацииВыходныеИзделия.Ссылка = &Источник
	|
	|УПОРЯДОЧИТЬ ПО
	|	РесурсныеСпецификацииВыходныеИзделия.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1
	|ВЫБРАТЬ
	|	РесурсныеСпецификацииВозвратныеОтходы.Номенклатура КАК Номенклатура,
	|	РесурсныеСпецификацииВозвратныеОтходы.Характеристика КАК Характеристика,
	|	РесурсныеСпецификацииВозвратныеОтходы.Упаковка КАК Упаковка,
	|	РесурсныеСпецификацииВозвратныеОтходы.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РесурсныеСпецификацииВозвратныеОтходы.Количество КАК Количество,
	|
	|	РесурсныеСпецификацииВозвратныеОтходы.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|
	|	РесурсныеСпецификацииВозвратныеОтходы.ОбработатьПоСпецификации КАК ОбработатьПоСпецификации,
	|	РесурсныеСпецификацииВозвратныеОтходы.Спецификация КАК Спецификация,
	|
	|	РесурсныеСпецификацииВозвратныеОтходы.ЭтапРедактирование КАК ЭтапРедактирование,
	|
	|	РесурсныеСпецификацииВозвратныеОтходы.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
	|	РесурсныеСпецификацииВозвратныеОтходы.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
	|	РесурсныеСпецификацииВозвратныеОтходы.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
	|	РесурсныеСпецификацииВозвратныеОтходы.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
	|	РесурсныеСпецификацииВозвратныеОтходы.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|	РесурсныеСпецификацииВозвратныеОтходы.ОписаниеИзделия КАК ОписаниеИзделия,
	|
	|	РесурсныеСпецификацииВозвратныеОтходы.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВозвратныеОтходы КАК РесурсныеСпецификацииВозвратныеОтходы
	|ГДЕ
	|	РесурсныеСпецификацииВозвратныеОтходы.Ссылка = &Источник
	|
	|УПОРЯДОЧИТЬ ПО
	|	РесурсныеСпецификацииВозвратныеОтходы.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2
	|ВЫБРАТЬ
	|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	РесурсныеСпецификацииМатериалыИУслуги.Характеристика КАК Характеристика,
	|	РесурсныеСпецификацииМатериалыИУслуги.Упаковка КАК Упаковка,
	|	РесурсныеСпецификацииМатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РесурсныеСпецификацииМатериалыИУслуги.Количество КАК Количество,
	|	РесурсныеСпецификацииМатериалыИУслуги.ЭтапРедактирование КАК ЭтапРедактирование,
	|	РесурсныеСпецификацииМатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	РесурсныеСпецификацииМатериалыИУслуги.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
	|	РесурсныеСпецификацииМатериалыИУслуги.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
	|	РесурсныеСпецификацииМатериалыИУслуги.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
	|	РесурсныеСпецификацииМатериалыИУслуги.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
	|	РесурсныеСпецификацииМатериалыИУслуги.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|	РесурсныеСпецификацииМатериалыИУслуги.КлючСвязи КАК КлючСвязи,
	|
	|	РесурсныеСпецификацииМатериалыИУслуги.СпособПолученияМатериала КАК СпособПолученияМатериала,
	|	РесурсныеСпецификацииМатериалыИУслуги.ИсточникПолученияПолуфабриката КАК ИсточникПолученияПолуфабриката,
	|	РесурсныеСпецификацииМатериалыИУслуги.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|
	|	РесурсныеСпецификацииМатериалыИУслуги.ПланироватьНеРанее КАК ПланироватьНеРанее,
	|	РесурсныеСпецификацииМатериалыИУслуги.СпецификацияРемонта КАК СпецификацияРемонта,
	|
	|	РесурсныеСпецификацииМатериалыИУслуги.ПрименениеМатериала КАК ПрименениеМатериала,
	|	РесурсныеСпецификацииМатериалыИУслуги.Альтернативный КАК Альтернативный,
	|	РесурсныеСпецификацииМатериалыИУслуги.Вероятность КАК Вероятность
	|
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
	|ГДЕ
	|	РесурсныеСпецификацииМатериалыИУслуги.Ссылка = &Источник
	|
	|УПОРЯДОЧИТЬ ПО
	|	РесурсныеСпецификацииМатериалыИУслуги.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3
	|ВЫБРАТЬ
	|	РесурсныеСпецификацииТрудозатраты.ВидРабот КАК ВидРабот,
	|	РесурсныеСпецификацииТрудозатраты.Количество КАК Количество,
	|	РесурсныеСпецификацииТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	РесурсныеСпецификацииТрудозатраты.ЭтапРедактирование КАК ЭтапРедактирование,
	|	РесурсныеСпецификацииТрудозатраты.НазначениеРабот КАК НазначениеРабот,
	|	РесурсныеСпецификацииТрудозатраты.АлгоритмРасчетаКоличества КАК АлгоритмРасчетаКоличества,
	|	РесурсныеСпецификацииТрудозатраты.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Справочник.РесурсныеСпецификации.Трудозатраты КАК РесурсныеСпецификацииТрудозатраты
	|ГДЕ
	|	РесурсныеСпецификацииТрудозатраты.Ссылка = &Источник
	|
	|УПОРЯДОЧИТЬ ПО
	|	РесурсныеСпецификацииТрудозатраты.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 4
	|ВЫБРАТЬ
	|	СправочникЭтапыПроизводства.Ссылка КАК Ссылка,
	|	СправочникЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа,
	|	СправочникЭтапыПроизводства.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК СправочникЭтапыПроизводства
	|ГДЕ
	|	СправочникЭтапыПроизводства.Владелец = &Источник
	|	И НЕ СправочникЭтапыПроизводства.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Источник", Источник);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Приемник.ВыходныеИзделия.Загрузить(Результат[0].Выгрузить());
	Приемник.ВозвратныеОтходы.Загрузить(Результат[1].Выгрузить());
	Приемник.МатериалыИУслуги.Загрузить(Результат[2].Выгрузить());
	Приемник.Трудозатраты.Загрузить(Результат[3].Выгрузить());
	
	Выборка = Результат[4].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЭтапОбъект = Выборка.Ссылка.Скопировать();
		ЭтапОбъект.Владелец = СсылкаПриемника;
		ЭтапОбъект.НомерЭтапа = Выборка.НомерЭтапа;
		ЭтапОбъект.НомерСледующегоЭтапа = Выборка.НомерСледующегоЭтапа;
		
		ЭтапОбъект.ОбменДанными.Загрузка = Истина;
		
		Попытка
			
			ЭтапОбъект.Записать();
			
		Исключение
			
			Возврат Ложь;
			
		КонецПопытки;
		
		ВыполнитьЗаменуСсылокВКоллекции(Приемник.ВыходныеИзделия,  "ЭтапРедактирование", Выборка.Ссылка, ЭтапОбъект.Ссылка);
		ВыполнитьЗаменуСсылокВКоллекции(Приемник.ВозвратныеОтходы, "ЭтапРедактирование", Выборка.Ссылка, ЭтапОбъект.Ссылка);
		
		ВыполнитьЗаменуСсылокВКоллекции(Приемник.МатериалыИУслуги, "ЭтапРедактирование",             Выборка.Ссылка, ЭтапОбъект.Ссылка);
		ВыполнитьЗаменуСсылокВКоллекции(Приемник.МатериалыИУслуги, "ИсточникПолученияПолуфабриката", Выборка.Ссылка, ЭтапОбъект.Ссылка);
		ВыполнитьЗаменуСсылокВКоллекции(Приемник.МатериалыИУслуги, "ПланироватьНеРанее",             Выборка.Ссылка, ЭтапОбъект.Ссылка);
		
		ВыполнитьЗаменуСсылокВКоллекции(Приемник.Трудозатраты,     "ЭтапРедактирование", Выборка.Ссылка, ЭтапОбъект.Ссылка);
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ВыполнитьЗаменуСсылокВКоллекции(Коллекция, ИмяПоля, Ссылка, НоваяСсылка)
	
	Отбор = Новый Структура(ИмяПоля, Ссылка);
	
	НайденныеСтроки = Коллекция.НайтиСтроки(Отбор);
	
	Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		
		НайденнаяСтрока[ИмяПоля] = НоваяСсылка;
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти
