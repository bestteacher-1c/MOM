////////////////////////////////////////////////////////////////////////////////
// Подсистема "Сервис доставки".
// ОбщийМодуль.СервисДоставкиВызовСервера.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Имя формы выбора метаданных.
//
// Параметры:
//  ИмяОпределяемогоТипа - Строка - наименование определяемого типа.
// 
// Возвращаемое значение:
//  Строка - имя формы выбора.
//
Функция ИмяФормыВыбораПоОпределяемомуТипу(ИмяОпределяемогоТипа) Экспорт
	
	ОпределяемыйТип = Метаданные.ОпределяемыеТипы.Найти(ИмяОпределяемогоТипа);
	Если ОпределяемыйТип <> Неопределено Тогда
		
		ИмяФормы = "";
		Тип = ОпределяемыйТип.Тип.Типы()[0];
		Менеджер = Метаданные.НайтиПоТипу(Тип);
		Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Справочник." + Менеджер.Имя + ".ФормаВыбора";
		ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Документ." + Менеджер.Имя + ".ФормаВыбора";
		КонецЕсли;
		
		Возврат ИмяФормы;
	КонецЕсли;
	
КонецФункции

// Имя формы выбора метаданных.
//
// Параметры:
//  ИмяОпределяемогоТипа - Строка - наименование определяемого типа.
// 
// Возвращаемое значение:
//  Строка - имя формы выбора.
//
Функция ИмяФормыОбъектаПоОпределяемомуТипу(ИмяОпределяемогоТипа) Экспорт
	
	ОпределяемыйТип = Метаданные.ОпределяемыеТипы.Найти(ИмяОпределяемогоТипа);
	Если ОпределяемыйТип <> Неопределено Тогда
		
		ИмяФормы = "";
		Тип = ОпределяемыйТип.Тип.Типы()[0];
		Менеджер = Метаданные.НайтиПоТипу(Тип);
		Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Справочник." + Менеджер.Имя + ".ФормаОбъекта";
		ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Документ." + Менеджер.Имя + ".ФормаОбъекта";
		КонецЕсли;
		
		Возврат ИмяФормы;
	КонецЕсли;
	
КонецФункции

Функция ПараметрыЗаказаНаДоставкуПоДокументуОснованию(Основание) Экспорт
	
	ПараметрыЗаказа = СервисДоставки.НовыйПараметрыЗаказаНаДоставку();
	ПараметрыЗаказа.ДокументОснование = Основание;
	
	СервисДоставкиПереопределяемый.ЗаполнитьПараметрыЗаказаНаДоставку(ПараметрыЗаказа);
	
	ПараметрыЗаказа.Вставить("РежимМастера", 0);
	ПараметрыЗаказа.Удалить("Услуги");
		
	Возврат ПараметрыЗаказа;
	
КонецФункции

Функция ОрганизацияПоУмолчанию() Экспорт
	
	НастройкиФормы = НастройкиФормыСпискаЗаказов();
	
	Если НастройкиФормы = Неопределено Тогда
		НастройкиФормы = Новый Соответствие;
	КонецЕсли;
	
	ОрганизацияБизнесСетиСсылка = НастройкиФормы.Получить("ОрганизацияБизнесСетиСсылка");
	
	Если Не ЗначениеЗаполнено(ОрганизацияБизнесСетиСсылка) 
		ИЛИ Не ОрганизацияПодключена(ОрганизацияБизнесСетиСсылка) Тогда
		
		ОрганизацияБизнесСетиСсылка = Неопределено;
		ДанныеОрганизаций = БизнесСеть.ПодключенныеОрганизации();
		
		Если ДанныеОрганизаций.Количество() Тогда
			
			Если ДанныеОрганизаций.Количество() = 1 Тогда
				ОрганизацияБизнесСетиСсылка = ДанныеОрганизаций[0].Организация;
				НастройкиФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
				СохранитьНастройкиФормыСпискаЗаказов(НастройкиФормы);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОрганизацияБизнесСетиСсылка;
	
КонецФункции

Функция ОрганизацияПодключена(Организация) Экспорт
	
	Возврат СервисДоставкиСлужебный.ОрганизацияПодключена(Организация)
	
КонецФункции

Функция СохранитьНастройкиФормыСпискаЗаказов(Настройки) Экспорт
	
	КлючНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов/ТекущиеДанные";
	ХранилищеСистемныхНастроек.Сохранить(КлючНастроекФормы,,Настройки);
	
	Возврат Настройки;
	
КонецФункции

Функция КонтактнаяИнформацияПоПредставлению(Представление, ТипКонтактнойИнформацииИмя) Экспорт
	
	Если ТипКонтактнойИнформацииИмя = "Телефон" Тогда
		ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
	Иначе
		ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес;
	КонецЕсли;
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ТипКонтактнойИнформации);
	
КонецФункции

Процедура ПроверитьПодключениеИнтернетПоддержки(ЕстьПодключениеКСервису) Экспорт
	
	СервисДоставки.ПроверитьПодключениеИнтернетПоддержки(ЕстьПодключениеКСервису);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкиФормыСпискаЗаказов()
	
	КлючНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов/ТекущиеДанные";
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючНастроекФормы);
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти
