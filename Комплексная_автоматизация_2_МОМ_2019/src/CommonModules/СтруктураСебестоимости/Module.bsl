
#Область ПрограммныйИнтерфейс

// Строит дерево себестоимости для изделия.
//
// Параметры:
//  ПараметрыДерева	 - Структура		 - см. СтруктураСебестоимости.ПараметрыДереваСебестоимости()
//  ПараметрыУзла	 - Структура		 - см. СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости().
//
Процедура ПостроитьДеревоСебестоимости(ПараметрыДерева, ПараметрыУзла = Неопределено) Экспорт
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ОбщийМодуль.СтруктураСебестоимости.ПостроитьДеревоСебестоимости");
	ПараметрыДерева.Вставить("КоличествоСтрок", 0);
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
	Если ПараметрыУзла = Неопределено Тогда
		ПараметрыУзла = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
	КонецЕсли;
	
	Если ПараметрыДерева.Результат = Неопределено Тогда
		ПустоеДеревоСебестоимости(ПараметрыДерева, ПараметрыУзла);
	КонецЕсли;
	
	ЗаполнитьДеревоСебестоимости(ПараметрыДерева, ПараметрыУзла);
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ПараметрыДерева.КоличествоСтрок / 1000);
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
КонецПроцедуры

// Строит дерево себестоимости для изделия в фоне.
//
// Параметры:
//  ПараметрыДерева	 - Структура		 - см. СтруктураСебестоимости.ПараметрыДереваСебестоимости()
//  ПараметрыУзла	 - Структура		 - см. СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости().
//
Процедура ПостроитьДеревоСебестоимостиВФоне(Параметры, АдресХранилища) Экспорт
	
	ПостроитьДеревоСебестоимости(Параметры.ПараметрыДерева, Параметры.ПараметрыУзла);
	
	Результат = Новый Структура("ДеревоСебестоимости, Успех");
	Результат.ДеревоСебестоимости	= Параметры.ПараметрыДерева.Результат;
	Результат.Успех					= Истина;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Вызывается перед раскрытием узла дерева себестоимости.
//
// Параметры:
//  ПараметрыДерева		- Структура	- см. СтруктураСебестоимости.ПараметрыДереваСебестоимости()
//  ПараметрыУзла		- Структура	- см. СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости()
//  ОписаниеПродукции	- Структура	- Аналитика учета, партия и уровень продукции.
//
Функция ДеревоСебестоимостиПередРазворачиванием(ПараметрыДерева, ПараметрыУзла, ОписаниеПродукции) Экспорт
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ОбщийМодуль.СтруктураСебестоимости.ДеревоСебестоимостиПередРазворачиванием");
	ПараметрыДерева.Вставить("КоличествоСтрок", 0);
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
	РазузловатьУзелДереваСебестоимости(ПараметрыДерева, ПараметрыУзла, ПараметрыДерева.Результат, ОписаниеПродукции);
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ПараметрыДерева.КоличествоСтрок / 100);
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДеревоСебестоимости(ПараметрыДерева, ПараметрыУзла)
	
	Запрос = ЗапросВыпущеннаяПродукция(ПараметрыУзла, Ложь);
	ВыпущеннаяПродукция = Запрос.Выполнить().Выгрузить();
	
	Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений" Тогда
		Результат = ПараметрыДерева.Результат.Строки;
	ИначеЕсли ПараметрыДерева.ТипРезультата = "ТаблицаЗначений" Тогда
		Результат = ПараметрыДерева.Результат;
	Иначе
		Результат = Новый ТаблицаЗначений;
		ИнициализироватьПоляДереваСебестоимости(Результат);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	ПараметрыДерева.КоличествоСтрок = ПараметрыДерева.КоличествоСтрок + ВыпущеннаяПродукция.Количество();
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
	Для Каждого ТекПартия Из ВыпущеннаяПродукция Цикл
		
		Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений"
			ИЛИ ПараметрыДерева.ДобавлятьПолуфабрикатыВРезультат
			ИЛИ Не ТекПартия.ТребуетсяРазузлование Тогда
			СтрокаПартия = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПартия, ТекПартия);
			
			СтрокаПартия.Идентификатор	= Новый УникальныйИдентификатор();
			СтрокаПартия.ВидСтроки		= Перечисления.ВидыСтрокДереваСебестоимости.ПартияПродукции;
			
			СтрокаПартия.АналитикаУчетаВыходноеИзделие			= ТекПартия.АналитикаУчетаПродукции;
			СтрокаПартия.АналитикаУчетаПартийВыходноеИзделие	= ТекПартия.АналитикаУчетаПартийПродукции;
			СтрокаПартия.ПартияВыходноеИзделие					= ТекПартия.ПартияПродукции;
			
			СтрокаПартия.Номенклатура			= ТекПартия.Продукция;
			СтрокаПартия.Характеристика			= ТекПартия.ХарактеристикаПродукции;
			СтрокаПартия.Серия					= ТекПартия.СерияПродукции;
			СтрокаПартия.Назначение				= ТекПартия.НазначениеПродукции;
			СтрокаПартия.Партия					= ТекПартия.ПартияПродукции;
			СтрокаПартия.АналитикаУчетаПартий	= ТекПартия.АналитикаУчетаПартийПродукции;
			СтрокаПартия.ЕдиницаИзмерения		= ТекПартия.ЕдиницаИзмеренияПродукция;
			
			СтрокаПартия.Количество	= ТекПартия.КоличествоПродукция;
			СтрокаПартия.Сумма		= ТекПартия.СуммаПродукция;
			СтрокаПартия.СуммаЗабалансовая = ТекПартия.СуммаПродукцияЗабалансовая;
		КонецЕсли;
		
		Если ТекПартия.ТребуетсяРазузлование
			И ПараметрыДерева.ТипРезультата = "ДеревоЗначений"
			И ПараметрыДерева.ДинамическоеСчитывание Тогда
			
			СтрокаПартия.Строки.Добавить();
			
		ИначеЕсли ТекПартия.ТребуетсяРазузлование Тогда
			
			ПараметрыУзлаПартии = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
			
			Отборы = ПараметрыУзлаПартии.Отборы;
			Отборы.ДанныеПоСебестоимости = ПараметрыУзла.Отборы.ДанныеПоСебестоимости;
			Отборы.Продукция.Добавить(ТекПартия.Продукция);
			Отборы.ХарактеристикиПродукции.Добавить(ТекПартия.ХарактеристикаПродукции);
			Отборы.СерииПродукции.Добавить(ТекПартия.СерияПродукции);
			Отборы.ПартииПродукции.Добавить(ТекПартия.ПартияПродукции);
			Отборы.АналитикиУчетаПартийПродукции.Добавить(ТекПартия.АналитикаУчетаПартийПродукции);
			Отборы.Числитель = ТекПартия.Числитель;
			
			ОписаниеПродукции = Новый Структура("АналитикаУчетаПродукции, ПартияПродукции, АналитикаУчетаПартийПродукции");
			ЗаполнитьЗначенияСвойств(ОписаниеПродукции, ТекПартия);
			ОписаниеПродукции.Вставить("Уровень", 0);
			
			Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений" Тогда
				ТаблицаРезультата = СтрокаПартия;
			Иначе
				ТаблицаРезультата = Результат;
			КонецЕсли;
			
			РазузловатьУзелДереваСебестоимости(ПараметрыДерева, ПараметрыУзлаПартии, ТаблицаРезультата, ОписаниеПродукции);
			
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыДерева.ТипРезультата = "МенеджерВременныхТаблиц" Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ПараметрыДерева.Результат;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ Результат
		|ИЗ
		|	&Результат КАК Результат";
		
		Результат.Колонки.Удалить("Идентификатор");
		Запрос.УстановитьПараметр("Результат", Результат);
		// Структуру ВТ Результат см. в СтруктураСебестоимости.СтруктураПолейДереваСебестоимости.
		Запрос.Выполнить();
	КонецЕсли;
	
КонецПроцедуры

Процедура РазузловатьУзелДереваСебестоимости(ПараметрыДерева, ПараметрыУзла, ТаблицаРезультата, ОписаниеПродукции)
	
	Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений" Тогда
		Результат = ТаблицаРезультата.Строки;
		Если ПараметрыДерева.ДинамическоеСчитывание Тогда
			Результат.Очистить();
		КонецЕсли;
	Иначе
		Результат = ТаблицаРезультата
	КонецЕсли;
	
	АналитикаУчетаПродукции			= ОписаниеПродукции.АналитикаУчетаПродукции;
	ПартияПродукции					= ОписаниеПродукции.ПартияПродукции;
	АналитикаУчетаПартийПродукции	= ОписаниеПродукции.АналитикаУчетаПартийПродукции;
	УровеньУзла						= ОписаниеПродукции.Уровень + 1;
	
	СебестоимостьУзла = СебестоимостьУзла(ПараметрыДерева, ПараметрыУзла);
	
	// СтандартныеПодсистемы.ЗамерПроизводительности
	ПараметрыДерева.КоличествоСтрок = ПараметрыДерева.КоличествоСтрок + СебестоимостьУзла.Количество();
	// Конец СтандартныеПодсистемы.ЗамерПроизводительности
	
	Для Каждого ТекПартия Из СебестоимостьУзла Цикл
		
		Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений"
			ИЛИ ПараметрыДерева.ДобавлятьПолуфабрикатыВРезультат
			ИЛИ Не ТекПартия.ТребуетсяРазузлование Тогда
			СтрокаПартия = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПартия, ТекПартия);
			
			СтрокаПартия.Идентификатор	= Новый УникальныйИдентификатор();
			Если ТекПартия.ТребуетсяРазузлование Тогда
				
				СтрокаПартия.ВидСтроки		= Перечисления.ВидыСтрокДереваСебестоимости.ПартияПолуфабриката;
				СтрокаПартия.Числитель 		= ТекПартия.КоличествоЗатрата;
				
			Иначе
				СтрокаПартия.ВидСтроки		= Перечисления.ВидыСтрокДереваСебестоимости.ПартияЗатраты;
			КонецЕсли;
			
			СтрокаПартия.АналитикаУчетаПродукции		= АналитикаУчетаПродукции;
			СтрокаПартия.ПартияПродукции				= ПартияПродукции;
			СтрокаПартия.АналитикаУчетаПартийПродукции	= АналитикаУчетаПартийПродукции;
			
			СтрокаПартия.Номенклатура			= ТекПартия.Затрата;
			СтрокаПартия.Характеристика			= ТекПартия.ХарактеристикаЗатрата;
			СтрокаПартия.Серия					= ТекПартия.СерияЗатрата;
			СтрокаПартия.Назначение				= ТекПартия.НазначениеЗатрата;
			СтрокаПартия.Партия					= ТекПартия.ПартияЗатрата;
			СтрокаПартия.АналитикаУчетаПартий	= ТекПартия.АналитикаУчетаПартийЗатрата;
			СтрокаПартия.ЕдиницаИзмерения		= ТекПартия.ЕдиницаИзмеренияЗатрата;
			СтрокаПартия.Уровень				= УровеньУзла;
			
			СтрокаПартия.Количество	= ТекПартия.КоличествоЗатрата;
			СтрокаПартия.Сумма		= ТекПартия.СуммаЗатрата;
			СтрокаПартия.СуммаЗабалансовая = ТекПартия.СуммаЗатратаЗабалансовая;
		КонецЕсли;
		
		Если ТекПартия.ТребуетсяРазузлование
			И ПараметрыДерева.ТипРезультата = "ДеревоЗначений"
			И ПараметрыДерева.ДинамическоеСчитывание Тогда
			СтрокаПартия.Строки.Добавить();
		ИначеЕсли ТекПартия.ТребуетсяРазузлование Тогда
			ПараметрыУзлаПартии = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
			
			Отборы = ПараметрыУзлаПартии.Отборы;
			Отборы.ДанныеПоСебестоимости = ПараметрыУзла.Отборы.ДанныеПоСебестоимости;
			Отборы.Продукция.Добавить(ТекПартия.Затрата);
			Отборы.ХарактеристикиПродукции.Добавить(ТекПартия.ХарактеристикаЗатрата);
			Отборы.СерииПродукции.Добавить(ТекПартия.СерияЗатрата);
			Отборы.ПартииПродукции.Добавить(ТекПартия.ПартияЗатрата);
			Отборы.АналитикиУчетаПартийПродукции.Добавить(ТекПартия.АналитикаУчетаПартийЗатрата);
			Отборы.Числитель = ТекПартия.КоличествоЗатрата;
			
			Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений" Тогда
				ТаблицаРезультатаПартии = СтрокаПартия;
			Иначе
				ТаблицаРезультатаПартии = Результат;
			КонецЕсли;
			
			ОписаниеПродукции.Вставить("Уровень", УровеньУзла);
			РазузловатьУзелДереваСебестоимости(ПараметрыДерева, ПараметрыУзлаПартии, ТаблицаРезультатаПартии, ОписаниеПродукции);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СебестоимостьУзла(ПараметрыДерева, ПараметрыУзла)
	
	Запрос = ЗапросВыпущеннаяПродукция(ПараметрыУзла, Истина);
	Запрос.Текст = Запрос.Текст
					+ ТекстЗапросаЗатратыНаПродукцию();
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

Функция ТекстЗапросаЗатратыНаПродукцию()
	
	ТекстыЗапроса = Новый Массив;
	
	#Область ТекстМатериальныеЗатраты
	ТекстМатериальныеЗатраты =
		"ВЫБРАТЬ
		|	ВыпущеннаяПродукция.Организация						КАК Организация,
		|	ВыпущеннаяПродукция.ПартияПродукции					КАК ПартияПродукции,
		|	ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции	КАК АналитикаУчетаПартийПродукции,
		|	ВыпущеннаяПродукция.АналитикаУчетаПродукции			КАК АналитикаУчетаПродукции,
		|	СебестоимостьТоваров.Партия							КАК ПартияПродукцииНЗП,
		|	СебестоимостьТоваров.АналитикаУчетаПартий			КАК АналитикаУчетаПартийЗатратаНЗП,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры		КАК АналитикаЗатратаНЗП,
		|	МАКСИМУМ(ВыпущеннаяПродукция.Числитель)				КАК Числитель,
		|	МАКСИМУМ(ВыпущеннаяПродукция.Знаменатель)			КАК Знаменатель,
		|	СУММА(СебестоимостьТоваров.Количество)				КАК КоличествоЗатрата
		|ПОМЕСТИТЬ ПартииМатериаловНЗП
		|ИЗ
		|	ВыпущеннаяПродукция КАК ВыпущеннаяПродукция
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО ВыпущеннаяПродукция.АналитикаУчетаПродукции = СебестоимостьТоваров.КорАналитикаУчетаНоменклатуры
		|		И ВыпущеннаяПродукция.ПартияПродукции = СебестоимостьТоваров.КорПартия
		|		И ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции = СебестоимостьТоваров.КорАналитикаУчетаПартий
		|
		|ГДЕ
		|	НЕ СебестоимостьТоваров.Партия = НЕОПРЕДЕЛЕНО
		|	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыпущеннаяПродукция.Организация,
		|	ВыпущеннаяПродукция.ПартияПродукции,
		|	ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции,
		|	ВыпущеннаяПродукция.АналитикаУчетаПродукции,
		|	СебестоимостьТоваров.Партия,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СебестоимостьТоваров.Партия,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП КАК ПартияПродукцииНЗП
		|ПОМЕСТИТЬ ПартииПродукцииНЗП
		|ИЗ
		|	ПартииМатериаловНЗП КАК ПартииМатериаловНЗП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СебестоимостьТоваров.Партия								КАК ПартияПродукции,
		|	СебестоимостьТоваров.АналитикаУчетаПартий				КАК АналитикаУчетаПартийЗатратаНЗП,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры			КАК АналитикаЗатратаНЗП,
		|	СУММА(СебестоимостьТоваров.Количество)					КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|		КОНЕЦ)												КАК Материальные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ТрудозатратыРегл
		|		КОНЕЦ)												КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК ДопРасходы,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансоваяРегл
		|		КОНЕЦ)												КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|				+ СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|				+ СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|				+ СебестоимостьТоваров.ТрудозатратыРегл
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|				+ СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	СУММА(СебестоимостьТоваров.Стоимость)					КАК МатериальныеСНДС,
		|	СУММА(СебестоимостьТоваров.Трудозатраты)				КАК ТрудозатратыСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеСНДС)	КАК ПостатейныеПостоянныеСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеСНДС)	КАК ПостатейныеПеременныеСНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходы)					КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	СУММА(СебестоимостьТоваров.СтоимостьБезНДС)				КАК МатериальныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС)	КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеБезНДС)	КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходыБезНДС)			КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	СУММА(СебестоимостьТоваров.СтоимостьРегл)				КАК МатериальныеРегл,
		|	СУММА(СебестоимостьТоваров.ТрудозатратыРегл)			КАК ТрудозатратыРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеРегл)	КАК ПостатейныеПостоянныеРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеРегл)	КАК ПостатейныеПеременныеРегл,
		|	СУММА(СебестоимостьТоваров.ДопРасходыРегл)				КАК ДопРасходыРегл,
		|	СУММА(СебестоимостьТоваров.СтоимостьЗабалансоваяРегл)	КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ИтогоПартииМатериаловНЗП
		|ИЗ
		|	ПартииПродукцииНЗП КАК ПартииПродукцииНЗП
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО ПартииПродукцииНЗП.ПартияПродукцииНЗП = СебестоимостьТоваров.Партия
		|ГДЕ
		|	СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	СебестоимостьТоваров.Партия,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СебестоимостьТоваров.Партия,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СебестоимостьТоваров.Партия								КАК ПартияЗатрата,
		|	СебестоимостьТоваров.АналитикаУчетаПартий				КАК АналитикаУчетаПартийЗатрата,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры			КАК АналитикаЗатрата,
		|	ИтогоПартииМатериаловНЗП.ПартияПродукции				КАК ПартияПродукции,
		|	ИтогоПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП	КАК АналитикаУчетаПартийЗатратаНЗП,
		|	ИтогоПартииМатериаловНЗП.АналитикаЗатратаНЗП			КАК АналитикаЗатратаНЗП,
		|	СУММА(СебестоимостьТоваров.Количество)					КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|		КОНЕЦ)												КАК Материальные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ТрудозатратыРегл
		|		КОНЕЦ)												КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК ДопРасходы,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансоваяРегл
		|		КОНЕЦ)												КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|				+ СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|				+ СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|				+ СебестоимостьТоваров.ТрудозатратыРегл
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|				+ СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	СУММА(СебестоимостьТоваров.Стоимость)					КАК МатериальныеСНДС,
		|	СУММА(СебестоимостьТоваров.Трудозатраты)				КАК ТрудозатратыСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеСНДС)	КАК ПостатейныеПостоянныеСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеСНДС)	КАК ПостатейныеПеременныеСНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходы)					КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	СУММА(СебестоимостьТоваров.СтоимостьБезНДС)				КАК МатериальныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС)	КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеБезНДС)	КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходыБезНДС)			КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	СУММА(СебестоимостьТоваров.СтоимостьРегл)				КАК МатериальныеРегл,
		|	СУММА(СебестоимостьТоваров.ТрудозатратыРегл)			КАК ТрудозатратыРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеРегл)	КАК ПостатейныеПостоянныеРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеРегл)	КАК ПостатейныеПеременныеРегл,
		|	СУММА(СебестоимостьТоваров.ДопРасходыРегл)				КАК ДопРасходыРегл,
		|	СУММА(СебестоимостьТоваров.СтоимостьЗабалансоваяРегл)	КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ПартииМатериалов
		|ИЗ
		|	ИтогоПартииМатериаловНЗП КАК ИтогоПартииМатериаловНЗП
		|		
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.КорАналитикаУчетаНоменклатуры = ИтогоПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|		И СебестоимостьТоваров.КорПартия = ИтогоПартииМатериаловНЗП.ПартияПродукции
		|		И СебестоимостьТоваров.КорАналитикаУчетаПартий = ИтогоПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП
		|ГДЕ
		|	СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		|СГРУППИРОВАТЬ ПО
		|	СебестоимостьТоваров.Партия,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры,
		|	ИтогоПартииМатериаловНЗП.ПартияПродукции,
		|	ИтогоПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП,
		|	ИтогоПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Фикс. стоимость.
		|ВЫБРАТЬ
		|	СебестоимостьТоваров.Регистратор					КАК ПартияЗатрата,
		|	СебестоимостьТоваров.АналитикаУчетаПартий			КАК АналитикаУчетаПартийЗатрата,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры		КАК АналитикаЗатрата,
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП				КАК ПартияПродукции,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП	КАК АналитикаУчетаПартийЗатратаНЗП,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП				КАК АналитикаЗатратаНЗП,
		|	СУММА(СебестоимостьТоваров.Количество)				КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|		КОНЕЦ)												КАК Материальные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ТрудозатратыРегл
		|		КОНЕЦ)												КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК ДопРасходы,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансоваяРегл
		|		КОНЕЦ)												КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|				+ СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|				+ СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|				+ СебестоимостьТоваров.ТрудозатратыРегл
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|				+ СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	СУММА(СебестоимостьТоваров.Стоимость)					КАК МатериальныеСНДС,
		|	СУММА(СебестоимостьТоваров.Трудозатраты)				КАК ТрудозатратыСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеСНДС)	КАК ПостатейныеПостоянныеСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеСНДС)	КАК ПостатейныеПеременныеСНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходы)					КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	СУММА(СебестоимостьТоваров.СтоимостьБезНДС)				КАК МатериальныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС)	КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеБезНДС)	КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходыБезНДС)			КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	СУММА(СебестоимостьТоваров.СтоимостьРегл)				КАК МатериальныеРегл,
		|	СУММА(СебестоимостьТоваров.ТрудозатратыРегл)			КАК ТрудозатратыРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеРегл)	КАК ПостатейныеПостоянныеРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеРегл)	КАК ПостатейныеПеременныеРегл,
		|	СУММА(СебестоимостьТоваров.ДопРасходыРегл)				КАК ДопРасходыРегл,
		|	СУММА(СебестоимостьТоваров.СтоимостьЗабалансоваяРегл)	КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ПартииМатериаловНЗП КАК ПартииМатериаловНЗП
		|		
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.АналитикаУчетаНоменклатуры = ПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|		И СебестоимостьТоваров.Партия = ПартииМатериаловНЗП.ПартияПродукцииНЗП
		|		И СебестоимостьТоваров.АналитикаУчетаПартий = ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП
		|ГДЕ
		|	СебестоимостьТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
		|	И СебестоимостьТоваров.Количество < 0
		|
		|СГРУППИРОВАТЬ ПО
		|	СебестоимостьТоваров.Регистратор,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры,
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Услуги переработчика.
		|ВЫБРАТЬ
		|	СебестоимостьТоваров.Регистратор					КАК ПартияЗатрата,
		|	СебестоимостьТоваров.АналитикаУчетаПартий			КАК АналитикаУчетаПартийЗатрата,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры		КАК АналитикаЗатрата,
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП				КАК ПартияПродукции,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП	КАК АналитикаУчетаПартийЗатратаНЗП,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП				КАК АналитикаЗатратаНЗП,
		|	СУММА(СебестоимостьТоваров.Количество)				КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|		КОНЕЦ)												КАК Материальные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.Трудозатраты
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ТрудозатратыРегл
		|		КОНЕЦ)												КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|		КОНЕЦ)												КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК ДопРасходы,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансовая
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьЗабалансоваяРегл
		|		КОНЕЦ)												КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА &ДанныеПоСебестоимости = 1
		|				ТОГДА СебестоимостьТоваров.Стоимость
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеСНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеСНДС
		|				+ СебестоимостьТоваров.ДопРасходы
		|			КОГДА &ДанныеПоСебестоимости = 2
		|				ТОГДА СебестоимостьТоваров.СтоимостьБезНДС
		|				+ СебестоимостьТоваров.Трудозатраты
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеБезНДС
		|				+ СебестоимостьТоваров.ДопРасходыБезНДС
		|			КОГДА &ДанныеПоСебестоимости = 3
		|				ТОГДА СебестоимостьТоваров.СтоимостьРегл
		|				+ СебестоимостьТоваров.ТрудозатратыРегл
		|				+ СебестоимостьТоваров.ПостатейныеПостоянныеРегл
		|				+ СебестоимостьТоваров.ПостатейныеПеременныеРегл
		|				+ СебестоимостьТоваров.ДопРасходыРегл
		|		КОНЕЦ)												КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	СУММА(СебестоимостьТоваров.Стоимость)					КАК МатериальныеСНДС,
		|	СУММА(СебестоимостьТоваров.Трудозатраты)				КАК ТрудозатратыСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеСНДС)	КАК ПостатейныеПостоянныеСНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеСНДС)	КАК ПостатейныеПеременныеСНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходы)					КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	СУММА(СебестоимостьТоваров.СтоимостьБезНДС)				КАК МатериальныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеБезНДС)	КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеБезНДС)	КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(СебестоимостьТоваров.ДопРасходыБезНДС)			КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	СУММА(СебестоимостьТоваров.СтоимостьРегл)				КАК МатериальныеРегл,
		|	СУММА(СебестоимостьТоваров.ТрудозатратыРегл)			КАК ТрудозатратыРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПостоянныеРегл)	КАК ПостатейныеПостоянныеРегл,
		|	СУММА(СебестоимостьТоваров.ПостатейныеПеременныеРегл)	КАК ПостатейныеПеременныеРегл,
		|	СУММА(СебестоимостьТоваров.ДопРасходыРегл)				КАК ДопРасходыРегл,
		|	СУММА(СебестоимостьТоваров.СтоимостьЗабалансоваяРегл)	КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ПартииМатериаловНЗП КАК ПартииМатериаловНЗП
		|		
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.АналитикаУчетаНоменклатуры = ПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|		И СебестоимостьТоваров.Партия = ПартииМатериаловНЗП.ПартияПродукцииНЗП
		|		И СебестоимостьТоваров.АналитикаУчетаПартий = ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП
		|ГДЕ
		|	СебестоимостьТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|	И СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
		|
		|СГРУППИРОВАТЬ ПО
		|	СебестоимостьТоваров.Регистратор,
		|	СебестоимостьТоваров.АналитикаУчетаПартий,
		|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры,
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартияПродукции,
		|	АналитикаУчетаПартийЗатратаНЗП,
		|	АналитикаЗатратаНЗП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииМатериалов.ПартияЗатрата					КАК ПартияЗатрата,
		|	ПартииМатериалов.АналитикаУчетаПартийЗатрата	КАК АналитикаУчетаПартийЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата				КАК АналитикаЗатрата,
		|	СУММА(ПартииМатериалов.КоличествоЗатрата)		КАК ОбщееКоличество
		|ПОМЕСТИТЬ ИтогиПартийМатериалов
		|ИЗ
		|	ПартииМатериалов КАК ПартииМатериалов
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииМатериалов.ПартияЗатрата,
		|	ПартииМатериалов.АналитикаУчетаПартийЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартииМатериалов.ПартияЗатрата,
		|	ПартииМатериалов.АналитикаУчетаПартийЗатрата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИтогиПартийМатериалов.ПартияЗатрата					КАК ПартияЗатрата,
		|	ИтогиПартийМатериалов.АналитикаУчетаПартийЗатрата	КАК АналитикаУчетаПартийЗатрата,
		|	ИтогиПартийМатериалов.АналитикаЗатрата				КАК АналитикаЗатрата,
		|	СУММА(СебестоимостьТоваров.Количество)				КАК ОбщееКоличество
		|ПОМЕСТИТЬ ПартииПолуфабрикатов
		|ИЗ
		|	ИтогиПартийМатериалов КАК ИтогиПартийМатериалов
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ИтогиПартийМатериалов.ПартияЗатрата
		|		И СебестоимостьТоваров.АналитикаУчетаПартий = ИтогиПартийМатериалов.АналитикаУчетаПартийЗатрата
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартий
		|		ПО СебестоимостьТоваров.АналитикаУчетаПартий = АналитикиУчетаПартий.КлючАналитики
		|
		|ГДЕ
		|	СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И СебестоимостьТоваров.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|													ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
		|													ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
		|	И НЕ АналитикиУчетаПартий.КодСтроки = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ИтогиПартийМатериалов.ПартияЗатрата,
		|	ИтогиПартийМатериалов.АналитикаУчетаПартийЗатрата,
		|	ИтогиПартийМатериалов.АналитикаЗатрата
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИтогиПартийМатериалов.ПартияЗатрата,
		|	ИтогиПартийМатериалов.АналитикаУчетаПартийЗатрата,
		|	ИтогиПартийМатериалов.АналитикаЗатрата
		|;
		|";
	#КонецОбласти
	
	#Область ТекстМатериалыОбъединение
	ТекстМатериалыОбъединение =
		"ВЫБРАТЬ
		|	ПартииМатериаловНЗП.Организация							КАК Организация,
		|	ПартииМатериаловНЗП.ПартияПродукции						КАК ПартияВыходноеИзделие,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийПродукции		КАК АналитикаУчетаПартийВыходноеИзделие,
		|	ПартииМатериаловНЗП.АналитикаУчетаПродукции				КАК АналитикаУчетаВыходноеИзделие,
		|	ПартииМатериалов.ПартияЗатрата							КАК ПартияЗатрата,
		|	ПартииМатериалов.АналитикаУчетаПартийЗатрата			КАК АналитикаУчетаПартийЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата						КАК АналитикаУчетаЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата.Номенклатура			КАК Затрата,
		|	ПартииМатериалов.АналитикаЗатрата.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата.Характеристика		КАК ХарактеристикаЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата.Серия					КАК СерияЗатрата,
		|	ПартииМатериалов.АналитикаЗатрата.Назначение			КАК НазначениеЗатрата,
		|	ПартииМатериалов.АналитикаЗатратаНЗП.СтатьяКалькуляции	КАК СтатьяКалькуляции,
		|	ВЫБОР
		|		КОГДА ПартииМатериалов.АналитикаЗатратаНЗП.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
		|			ТОГДА ПартииМатериалов.АналитикаЗатратаНЗП.МестоХранения
		|		ИНАЧЕ ПартииМатериалов.АналитикаЗатратаНЗП.СкладскаяТерритория.Подразделение
		|	КОНЕЦ													КАК Подразделение,
		|	ПартииПолуфабрикатов.ПартияЗатрата ЕСТЬ НЕ NULL			КАК ТребуетсяРазузлование,
		|	""Материальные""										КАК ТипЗатрат,
		|	ПартииМатериалов.КоличествоЗатрата
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	ПартииМатериалов.Материальные
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК Материальные,
		|	ПартииМатериалов.Трудозатраты
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК Трудозатраты,
		|	ПартииМатериалов.ПостатейныеПостоянные
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПостоянные,
		|	ПартииМатериалов.ПостатейныеПеременные
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПеременные,
		|	ПартииМатериалов.ДопРасходы
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ДопРасходы,
		|	ПартииМатериалов.СуммаЗатратаЗабалансовая
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК СуммаЗатратаЗабалансовая,
		|	ПартииМатериалов.СуммаЗатрата
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	ПартииМатериалов.МатериальныеСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК МатериальныеСНДС,
		|	ПартииМатериалов.ТрудозатратыСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ТрудозатратыСНДС,
		|	ПартииМатериалов.ПостатейныеПостоянныеСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПостоянныеСНДС,
		|	ПартииМатериалов.ПостатейныеПеременныеСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПеременныеСНДС,
		|	ПартииМатериалов.ДопРасходыСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	ПартииМатериалов.МатериальныеБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК МатериальныеБезНДС,
		|	ПартииМатериалов.ПостатейныеПостоянныеБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПостоянныеБезНДС,
		|	ПартииМатериалов.ПостатейныеПеременныеБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПеременныеБезНДС,
		|	ПартииМатериалов.ДопРасходыБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	ПартииМатериалов.МатериальныеРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК МатериальныеРегл,
		|	ПартииМатериалов.ТрудозатратыРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ТрудозатратыРегл,
		|	ПартииМатериалов.ПостатейныеПостоянныеРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПостоянныеРегл,
		|	ПартииМатериалов.ПостатейныеПеременныеРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ПостатейныеПеременныеРегл,
		|	ПартииМатериалов.ДопРасходыРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК ДопРасходыРегл,
		|	ПартииМатериалов.СтоимостьЗабалансоваяРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)			КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ПартииМатериаловНЗП КАК ПартииМатериаловНЗП
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииМатериалов КАК ПартииМатериалов
		|		ПО ПартииМатериаловНЗП.ПартияПродукцииНЗП = ПартииМатериалов.ПартияПродукции
		|		И ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП = ПартииМатериалов.АналитикаУчетаПартийЗатратаНЗП
		|		И ПартииМатериаловНЗП.АналитикаЗатратаНЗП = ПартииМатериалов.АналитикаЗатратаНЗП
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИтогоПартииМатериаловНЗП КАК ИтогоПартииМатериаловНЗП
		|		ПО ИтогоПартииМатериаловНЗП.ПартияПродукции = ПартииМатериалов.ПартияПродукции
		|		И ИтогоПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП = ПартииМатериалов.АналитикаУчетаПартийЗатратаНЗП
		|		И ИтогоПартииМатериаловНЗП.АналитикаЗатратаНЗП = ПартииМатериалов.АналитикаЗатратаНЗП
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииПолуфабрикатов КАК ПартииПолуфабрикатов
		|		ПО ПартииМатериалов.ПартияЗатрата = ПартииПолуфабрикатов.ПартияЗатрата
		|		И ПартииМатериалов.АналитикаУчетаПартийЗатрата = ПартииПолуфабрикатов.АналитикаУчетаПартийЗатрата
		|		И ПартииМатериалов.АналитикаЗатрата = ПартииПолуфабрикатов.АналитикаЗатрата
		|		И (НЕ ПартииМатериаловНЗП.ПартияПродукции = ПартииПолуфабрикатов.ПартияЗатрата
		|			ИЛИ НЕ ПартииМатериаловНЗП.АналитикаУчетаПартийПродукции = ПартииПолуфабрикатов.АналитикаУчетаПартийЗатрата
		|			ИЛИ НЕ ПартииМатериаловНЗП.АналитикаУчетаПродукции = ПартииПолуфабрикатов.АналитикаЗатрата)
		|";
	
	ТекстыЗапроса.Добавить(ТекстМатериалыОбъединение);
	
	#КонецОбласти
	
	#Область ТекстМатериалыНачальныеОстаткиНЗПОбъединение
	ТекстМатериалыНачальныеОстаткиНЗПОбъединение =
		"ВЫБРАТЬ
		|	ПартииМатериаловНЗП.Организация								КАК Организация,
		|	ПартииМатериаловНЗП.ПартияПродукции							КАК ПартияВыходноеИзделие,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийПродукции			КАК АналитикаУчетаПартийВыходноеИзделие,
		|	ПартииМатериаловНЗП.АналитикаУчетаПродукции					КАК АналитикаУчетаВыходноеИзделие,
		|	ПартииМатериаловНЗП.ПартияПродукцииНЗП						КАК ПартияЗатрата,
		|	ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП			КАК АналитикаУчетаПартийЗатрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП						КАК АналитикаУчетаЗатрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП.Номенклатура		КАК Затрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияЗатрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП.Характеристика		КАК ХарактеристикаЗатрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП.Серия				КАК СерияЗатрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП.Назначение			КАК НазначениеЗатрата,
		|	ПартииМатериаловНЗП.АналитикаЗатратаНЗП.СтатьяКалькуляции	КАК СтатьяКалькуляции,
		|	ВЫБОР
		|		КОГДА ПартииМатериаловНЗП.АналитикаЗатратаНЗП.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
		|			ТОГДА ПартииМатериаловНЗП.АналитикаЗатратаНЗП.МестоХранения
		|		ИНАЧЕ ПартииМатериаловНЗП.АналитикаЗатратаНЗП.СкладскаяТерритория.Подразделение
		|	КОНЕЦ														КАК Подразделение,
		|	ЛОЖЬ														КАК ТребуетсяРазузлование,
		|	""Материальные""											КАК ТипЗатрат,
		|	ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	ИтогоПартииМатериаловНЗП.Материальные
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК Материальные,
		|	ИтогоПартииМатериаловНЗП.Трудозатраты
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК Трудозатраты,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПостоянные
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПостоянные,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПеременные
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПеременные,
		|	ИтогоПартииМатериаловНЗП.ДопРасходы
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ДопРасходы,
		|	ИтогоПартииМатериаловНЗП.СуммаЗатратаЗабалансовая
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК СуммаЗатратаЗабалансовая,
		|	ИтогоПартииМатериаловНЗП.СуммаЗатрата
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	ИтогоПартииМатериаловНЗП.МатериальныеСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК МатериальныеСНДС,
		|	ИтогоПартииМатериаловНЗП.ТрудозатратыСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ТрудозатратыСНДС,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПостоянныеСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПостоянныеСНДС,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПеременныеСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПеременныеСНДС,
		|	ИтогоПартииМатериаловНЗП.ДопРасходыСНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	ИтогоПартииМатериаловНЗП.МатериальныеБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК МатериальныеБезНДС,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПостоянныеБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПостоянныеБезНДС,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПеременныеБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПеременныеБезНДС,
		|	ИтогоПартииМатериаловНЗП.ДопРасходыБезНДС
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	ИтогоПартииМатериаловНЗП.МатериальныеРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК МатериальныеРегл,
		|	ИтогоПартииМатериаловНЗП.ТрудозатратыРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ТрудозатратыРегл,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПостоянныеРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПостоянныеРегл,
		|	ИтогоПартииМатериаловНЗП.ПостатейныеПеременныеРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ПостатейныеПеременныеРегл,
		|	ИтогоПартииМатериаловНЗП.ДопРасходыРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК ДопРасходыРегл,
		|	ИтогоПартииМатериаловНЗП.СтоимостьЗабалансоваяРегл
		|			* (ПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Числитель)
		|			/ (ИтогоПартииМатериаловНЗП.КоличествоЗатрата
		|				* ПартииМатериаловНЗП.Знаменатель)				КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ПартииМатериаловНЗП КАК ПартииМатериаловНЗП
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИтогоПартииМатериаловНЗП КАК ИтогоПартииМатериаловНЗП
		|		ПО ИтогоПартииМатериаловНЗП.ПартияПродукции = ПартииМатериаловНЗП.ПартияПродукцииНЗП
		|		И ИтогоПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП = ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП
		|		И ИтогоПартииМатериаловНЗП.АналитикаЗатратаНЗП = ПартииМатериаловНЗП.АналитикаЗатратаНЗП
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПартииМатериалов КАК ПартииМатериалов
		|		ПО ПартииМатериаловНЗП.ПартияПродукцииНЗП = ПартииМатериалов.ПартияПродукции
		|		И ПартииМатериаловНЗП.АналитикаУчетаПартийЗатратаНЗП = ПартииМатериалов.АналитикаУчетаПартийЗатратаНЗП
		|		И ПартииМатериаловНЗП.АналитикаЗатратаНЗП = ПартииМатериалов.АналитикаЗатратаНЗП
		|
		|ГДЕ
		|	ПартииМатериалов.ПартияПродукции ЕСТЬ NULL
		|";
	
	ТекстыЗапроса.Добавить(ТекстМатериалыНачальныеОстаткиНЗПОбъединение);
	
	#КонецОбласти
	
	#Область ТекстТрудозатратыОбъединение
	ТекстТрудозатратыОбъединение =
		"ВЫБРАТЬ
		|	ТрудозатратыНЗП.Организация						КАК Организация,
		|	ТрудозатратыНЗП.Регистратор						КАК ПартияВыходноеИзделие,
		|	ТрудозатратыНЗП.КорАналитикаУчетаПартий			КАК АналитикаУчетаПартийВыходноеИзделие,
		|	ТрудозатратыНЗП.КорАналитикаУчетаПродукции		КАК АналитикаУчетаВыходноеИзделие,
		|	НЕОПРЕДЕЛЕНО									КАК ПартияЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК АналитикаУчетаПартийЗатрата,
		|	ТрудозатратыНЗП.ВидРабот						КАК АналитикаЗатрата,
		|	ТрудозатратыНЗП.ВидРабот						КАК Затрата,
		|	СпрВидыРабот.ЕдиницаИзмерения					КАК ЕдиницаИзмеренияЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК ХарактеристикаЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК СерияЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК НазначениеЗатрата,
		|	ТрудозатратыНЗП.СтатьяКалькуляции				КАК СтатьяКалькуляции,
		|	ТрудозатратыНЗП.Подразделение					КАК Подразделение,
		|	ЛОЖЬ											КАК ТребуетсяРазузлование,
		|	""Трудозатраты""								КАК ТипЗатрат,
		|	СУММА(ТрудозатратыНЗП.Количество
		|		* ВыпущеннаяПродукция.Числитель
		|		/ ВыпущеннаяПродукция.Знаменатель)			КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	0												КАК Материальные,
		|	СУММА(ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ТрудозатратыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК Трудозатраты,
		|	0												КАК ПостатейныеПостоянные,
		|	0												КАК ПостатейныеПеременные,
		|	0												КАК ДопРасходы,
		|	0												КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ТрудозатратыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	0												КАК МатериальныеСНДС,
		|	СУММА(ТрудозатратыНЗП.Стоимость
		|			* ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК ТрудозатратыСНДС,
		|	0												КАК ПостатейныеПостоянныеСНДС,
		|	0												КАК ПостатейныеПеременныеСНДС,
		|	0												КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	0												КАК МатериальныеБезНДС,
		|	0												КАК ПостатейныеПостоянныеБезНДС,
		|	0												КАК ПостатейныеПеременныеБезНДС,
		|	0												КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	0												КАК МатериальныеРегл,
		|	СУММА(ТрудозатратыНЗП.СтоимостьРегл
		|			* ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК ТрудозатратыРегл,
		|	0												КАК ПостатейныеПостоянныеРегл,
		|	0												КАК ПостатейныеПеременныеРегл,
		|	0												КАК ДопРасходыРегл,
		|	0												КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ВыпущеннаяПродукция КАК ВыпущеннаяПродукция
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
		|		ПО ВыпущеннаяПродукция.ПартияПродукции = ТрудозатратыНЗП.Регистратор
		|		И ВыпущеннаяПродукция.АналитикаУчетаПродукции = ТрудозатратыНЗП.КорАналитикаУчетаПродукции
		|		И ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции = ТрудозатратыНЗП.КорАналитикаУчетаПартий
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК СпрВидыРабот
		|		ПО СпрВидыРабот.Ссылка = ТрудозатратыНЗП.ВидРабот
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РаботыДавальца КАК РаботыДавальца
		|		ПО РаботыДавальца.ПартияПродукции = ТрудозатратыНЗП.Регистратор
		|		И РаботыДавальца.АналитикаУчетаПродукции = ТрудозатратыНЗП.КорАналитикаУчетаПродукции
		|		И РаботыДавальца.АналитикаУчетаПартийПродукции = ТрудозатратыНЗП.КорАналитикаУчетаПартий
		|
		|ГДЕ
		|	РаботыДавальца.ПартияПродукции ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ТрудозатратыНЗП.Организация,
		|	ТрудозатратыНЗП.ВидРабот,
		|	ТрудозатратыНЗП.СтатьяКалькуляции,
		|	СпрВидыРабот.ЕдиницаИзмерения,
		|	ТрудозатратыНЗП.КорАналитикаУчетаПродукции,
		|	ТрудозатратыНЗП.Регистратор,
		|	ТрудозатратыНЗП.КорАналитикаУчетаПартий,
		|	ТрудозатратыНЗП.Подразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТрудозатратыНЗП.Организация						КАК Организация,
		|	ТрудозатратыНЗП.Регистратор						КАК ПартияВыходноеИзделие,
		|	ТрудозатратыНЗП.КорАналитикаУчетаПартий			КАК АналитикаУчетаПартийВыходноеИзделие,
		|	РаботыДавальца.АналитикаУчетаПродукции			КАК АналитикаУчетаВыходноеИзделие,
		|	НЕОПРЕДЕЛЕНО									КАК ПартияЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК АналитикаУчетаПартийЗатрата,
		|	ТрудозатратыНЗП.ВидРабот						КАК АналитикаЗатрата,
		|	ТрудозатратыНЗП.ВидРабот						КАК Затрата,
		|	СпрВидыРабот.ЕдиницаИзмерения					КАК ЕдиницаИзмеренияЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК ХарактеристикаЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК СерияЗатрата,
		|	НЕОПРЕДЕЛЕНО									КАК НазначениеЗатрата,
		|	ТрудозатратыНЗП.СтатьяКалькуляции				КАК СтатьяКалькуляции,
		|	ТрудозатратыНЗП.Подразделение					КАК Подразделение,
		|	ЛОЖЬ											КАК ТребуетсяРазузлование,
		|	""Трудозатраты""								КАК ТипЗатрат,
		|	СУММА(ТрудозатратыНЗП.Количество
		|		* ВыпущеннаяПродукция.Числитель
		|		/ ВыпущеннаяПродукция.Знаменатель)			КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	0												КАК Материальные,
		|	СУММА(ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ТрудозатратыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК Трудозатраты,
		|	0												КАК ПостатейныеПостоянные,
		|	0												КАК ПостатейныеПеременные,
		|	0												КАК ДопРасходы,
		|	0												КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ТрудозатратыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ТрудозатратыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	0												КАК МатериальныеСНДС,
		|	СУММА(ТрудозатратыНЗП.Стоимость
		|			* ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК ТрудозатратыСНДС,
		|	0												КАК ПостатейныеПостоянныеСНДС,
		|	0												КАК ПостатейныеПеременныеСНДС,
		|	0												КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	0												КАК МатериальныеБезНДС,
		|	0												КАК ПостатейныеПостоянныеБезНДС,
		|	0												КАК ПостатейныеПеременныеБезНДС,
		|	0												КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	0												КАК МатериальныеРегл,
		|	СУММА(ТрудозатратыНЗП.СтоимостьРегл
		|			* ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)		КАК ТрудозатратыРегл,
		|	0												КАК ПостатейныеПостоянныеРегл,
		|	0												КАК ПостатейныеПеременныеРегл,
		|	0												КАК ДопРасходыРегл,
		|	0												КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	РаботыДавальца КАК РаботыДавальца
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпущеннаяПродукция КАК ВыпущеннаяПродукция
		|		ПО РаботыДавальца.ПартияПродукции = ВыпущеннаяПродукция.ПартияПродукции
		|		И РаботыДавальца.АналитикаУчетаПродукции = ВыпущеннаяПродукция.АналитикаУчетаПродукции
		|		И РаботыДавальца.АналитикаУчетаПартийПродукции = ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНЗП
		|		ПО РаботыДавальца.ПартияПродукции = ТрудозатратыНЗП.Регистратор
		|		И РаботыДавальца.АналитикаРаботыДавальца = ТрудозатратыНЗП.КорАналитикаУчетаПродукции
		|		И РаботыДавальца.АналитикаУчетаПартийПродукции = ТрудозатратыНЗП.КорАналитикаУчетаПартий
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК СпрВидыРабот
		|		ПО СпрВидыРабот.Ссылка = ТрудозатратыНЗП.ВидРабот
		|
		|СГРУППИРОВАТЬ ПО
		|	ТрудозатратыНЗП.Организация,
		|	ТрудозатратыНЗП.ВидРабот,
		|	ТрудозатратыНЗП.СтатьяКалькуляции,
		|	СпрВидыРабот.ЕдиницаИзмерения,
		|	РаботыДавальца.АналитикаУчетаПродукции,
		|	ТрудозатратыНЗП.Регистратор,
		|	ТрудозатратыНЗП.КорАналитикаУчетаПартий,
		|	ТрудозатратыНЗП.Подразделение
		|";
	
	ТекстыЗапроса.Добавить(ТекстТрудозатратыОбъединение);
	
	#КонецОбласти
	
	#Область ТекстПрочиеРасходыОбъединение
	ТекстПрочиеРасходыОбъединение =
		"ВЫБРАТЬ
		|	ПрочиеРасходыНЗП.Организация				КАК Организация,
		|	ПрочиеРасходыНЗП.Регистратор				КАК ПартияВыходноеИзделие,
		|	ПрочиеРасходыНЗП.КорАналитикаУчетаПартий	КАК АналитикаУчетаПартийВыходноеИзделие,
		|	ПрочиеРасходыНЗП.АналитикаУчетаПродукции	КАК АналитикаУчетаВыходноеИзделие,
		|	НЕОПРЕДЕЛЕНО								КАК ПартияЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК АналитикаУчетаПартийЗатрата,
		|	ПрочиеРасходыНЗП.СтатьяРасходов				КАК АналитикаЗатрата,
		|	ПрочиеРасходыНЗП.СтатьяРасходов				КАК Затрата,
		|	НЕОПРЕДЕЛЕНО								КАК ЕдиницаИзмеренияЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК ХарактеристикаЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК СерияЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК НазначениеЗатрата,
		|	ПрочиеРасходыНЗП.СтатьяКалькуляции			КАК СтатьяКалькуляции,
		|	ПрочиеРасходыНЗП.Подразделение				КАК Подразделение,
		|	ЛОЖЬ										КАК ТребуетсяРазузлование,
		|	""Постатейные""								КАК ТипЗатрат,
		|	0											КАК КоличествоЗатрата,
		|
		|// Стоимости согласно параметру ""ДанныеПоСебестоимости""
		|	0											КАК Материальные,
		|	0											КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременные,
		|	0											КАК ДопРасходы,
		|	0											КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	0											КАК МатериальныеСНДС,
		|	0											КАК ТрудозатратыСНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.Стоимость
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянныеСНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.Стоимость
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременныеСНДС,
		|	0											КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	0											КАК МатериальныеБезНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьБезНДС
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьБезНДС
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременныеБезНДС,
		|	0											КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	0											КАК МатериальныеРегл,
		|	0											КАК ТрудозатратыРегл,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянныеРегл,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременныеРегл,
		|	0											КАК ДопРасходыРегл,
		|	0											КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ВыпущеннаяПродукция КАК ВыпущеннаяПродукция
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ПрочиеРасходыНЗП
		|		ПО ВыпущеннаяПродукция.ПартияПродукции = ПрочиеРасходыНЗП.Регистратор
		|		И ВыпущеннаяПродукция.АналитикаУчетаПродукции = ПрочиеРасходыНЗП.АналитикаУчетаПродукции
		|		И ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции = ПрочиеРасходыНЗП.КорАналитикаУчетаПартий
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО ПрочиеРасходыНЗП.СтатьяРасходов = СтатьиРасходов.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РаботыДавальца КАК РаботыДавальца
		|		ПО РаботыДавальца.ПартияПродукции = ПрочиеРасходыНЗП.Регистратор
		|		И РаботыДавальца.АналитикаУчетаПродукции = ПрочиеРасходыНЗП.АналитикаУчетаПродукции
		|		И РаботыДавальца.АналитикаУчетаПартийПродукции = ПрочиеРасходыНЗП.КорАналитикаУчетаПартий
		|
		|ГДЕ
		|	НЕ ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ = 0
		|	И РаботыДавальца.ПартияПродукции ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ПрочиеРасходыНЗП.Организация,
		|	ПрочиеРасходыНЗП.СтатьяРасходов,
		|	ПрочиеРасходыНЗП.СтатьяКалькуляции,
		|	ПрочиеРасходыНЗП.АналитикаУчетаПродукции,
		|	ПрочиеРасходыНЗП.Регистратор,
		|	ПрочиеРасходыНЗП.КорАналитикаУчетаПартий,
		|	ПрочиеРасходыНЗП.Подразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПрочиеРасходыНЗП.Организация				КАК Организация,
		|	ПрочиеРасходыНЗП.Регистратор				КАК ПартияВыходноеИзделие,
		|	ПрочиеРасходыНЗП.КорАналитикаУчетаПартий	КАК АналитикаУчетаПартийВыходноеИзделие,
		|	РаботыДавальца.АналитикаУчетаПродукции		КАК АналитикаУчетаВыходноеИзделие,
		|	НЕОПРЕДЕЛЕНО								КАК ПартияЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК АналитикаУчетаПартийЗатрата,
		|	ПрочиеРасходыНЗП.СтатьяРасходов				КАК АналитикаЗатрата,
		|	ПрочиеРасходыНЗП.СтатьяРасходов				КАК Затрата,
		|	НЕОПРЕДЕЛЕНО								КАК ЕдиницаИзмеренияЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК ХарактеристикаЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК СерияЗатрата,
		|	НЕОПРЕДЕЛЕНО								КАК НазначениеЗатрата,
		|	ПрочиеРасходыНЗП.СтатьяКалькуляции			КАК СтатьяКалькуляции,
		|	ПрочиеРасходыНЗП.Подразделение				КАК Подразделение,
		|	ЛОЖЬ										КАК ТребуетсяРазузлование,
		|	""Постатейные""								КАК ТипЗатрат,
		|	0											КАК КоличествоЗатрата,
		|	0											КАК Материальные,
		|	0											КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременные,
		|	0											КАК ДопРасходы,
		|	0											КАК СуммаЗатратаЗабалансовая,
		|	СУММА(ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК СуммаЗатрата,
		|
		|// Стоимости с НДС
		|	0											КАК МатериальныеСНДС,
		|	0											КАК ТрудозатратыСНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.Стоимость
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянныеСНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.Стоимость
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременныеСНДС,
		|	0											КАК ДопРасходыСНДС,
		|
		|// Стоимости без НДС
		|	0											КАК МатериальныеБезНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьБезНДС
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьБезНДС
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременныеБезНДС,
		|	0											КАК ДопРасходыБезНДС,
		|
		|// Стоимости Регл
		|	0											КАК МатериальныеРегл,
		|	0											КАК ТрудозатратыРегл,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПостоянныеРегл,
		|	СУММА(ВЫБОР
		|		КОГДА СтатьиРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ * ВыпущеннаяПродукция.Числитель
		|			/ ВыпущеннаяПродукция.Знаменатель)	КАК ПостатейныеПеременныеРегл,
		|	0											КАК ДопРасходыРегл,
		|	0											КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	РаботыДавальца КАК РаботыДавальца
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпущеннаяПродукция КАК ВыпущеннаяПродукция
		|		ПО РаботыДавальца.ПартияПродукции = ВыпущеннаяПродукция.ПартияПродукции
		|		И РаботыДавальца.АналитикаУчетаПродукции = ВыпущеннаяПродукция.АналитикаУчетаПродукции
		|		И РаботыДавальца.АналитикаУчетаПартийПродукции = ВыпущеннаяПродукция.АналитикаУчетаПартийПродукции
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ПрочиеРасходыНЗП
		|		ПО РаботыДавальца.ПартияПродукции = ПрочиеРасходыНЗП.Регистратор
		|		И РаботыДавальца.АналитикаРаботыДавальца = ПрочиеРасходыНЗП.АналитикаУчетаПродукции
		|		И РаботыДавальца.АналитикаУчетаПартийПродукции = ПрочиеРасходыНЗП.КорАналитикаУчетаПартий
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО ПрочиеРасходыНЗП.СтатьяРасходов = СтатьиРасходов.Ссылка
		|
		|ГДЕ
		|	НЕ ВЫБОР
		|		КОГДА &ДанныеПоСебестоимости = 1
		|			ТОГДА ПрочиеРасходыНЗП.Стоимость
		|		КОГДА &ДанныеПоСебестоимости = 2
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьБезНДС
		|		КОГДА &ДанныеПоСебестоимости = 3
		|			ТОГДА ПрочиеРасходыНЗП.СтоимостьРегл
		|	КОНЕЦ = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ПрочиеРасходыНЗП.Организация,
		|	ПрочиеРасходыНЗП.СтатьяРасходов,
		|	ПрочиеРасходыНЗП.СтатьяКалькуляции,
		|	РаботыДавальца.АналитикаУчетаПродукции,
		|	ПрочиеРасходыНЗП.Регистратор,
		|	ПрочиеРасходыНЗП.КорАналитикаУчетаПартий,
		|	ПрочиеРасходыНЗП.Подразделение
		|;
		|";
	
	ТекстыЗапроса.Добавить(ТекстПрочиеРасходыОбъединение);
	
	#КонецОбласти
	
	Возврат ТекстМатериальныеЗатраты
			+ СтрСоединить(ТекстыЗапроса, " ОБЪЕДИНИТЬ ВСЕ ");
	
КонецФункции

#Область ЗапросПоВыпущеннойПродукции

Функция ЗапросВыпущеннаяПродукция(ПараметрыУзла, ПомещатьВВТ = Ложь) Экспорт
	
	Если ПараметрыУзла.ИнтерактивнаяНастройка Тогда
		АдаптироватьЗапросСКД(ПараметрыУзла.СхемаКД);
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки = КомпоновщикМакета.Выполнить(ПараметрыУзла.СхемаКД, ПараметрыУзла.НастройкиКД,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		НаборДанных = МакетКомпоновки.НаборыДанных.Найти("ВыпущеннаяПродукция");
		
		ПараметрыЗапроса = МакетКомпоновки.ЗначенияПараметров;
		КлючПараметра = "Имя";
	Иначе
		НаборДанных = ПараметрыУзла.СхемаКД.НаборыДанных.Найти("ВыпущеннаяПродукция");
		
		Отборы = ПараметрыУзла.Отборы;
		
		Отборы.Вставить("ЗаВесьПериод",				Не ЗначениеЗаполнено(Отборы.ДатаНачала) И Не ЗначениеЗаполнено(Отборы.ДатаОкончания));
		Отборы.Вставить("ПоВсемОрганизациям",		Не ЗначениеЗаполнено(Отборы.Организации));
		Отборы.Вставить("ПоВсемПодразделениям",		Не ЗначениеЗаполнено(Отборы.Подразделения));
		Отборы.Вставить("ПоВсейПродукции",			Не ЗначениеЗаполнено(Отборы.Продукция));
		Отборы.Вставить("ПоВсемХарактеристикам",	Не ЗначениеЗаполнено(Отборы.ХарактеристикиПродукции));
		Отборы.Вставить("ПоВсемСериям",				Не ЗначениеЗаполнено(Отборы.СерииПродукции));
		Отборы.Вставить("ПоВсемНазначениям",		Не ЗначениеЗаполнено(Отборы.НазначенияПродукции));
		Отборы.Вставить("ПоВсемПартиям",			Не ЗначениеЗаполнено(Отборы.ПартииПродукции));
		Отборы.Вставить("ПоВсемАналитикамПартий",	Не ЗначениеЗаполнено(Отборы.АналитикиУчетаПартийПродукции));
		Отборы.Вставить("РасшифровкаДвиженийДокумента", ЗначениеЗаполнено(Отборы.Регистраторы));
		
		ПараметрыЗапроса = ПараметрыУзла.Отборы;
		КлючПараметра = "Ключ";
	КонецЕсли;
	
	ТекстЗапроса = НаборДанных.Запрос;
	ТекстЗапроса = Лев(ТекстЗапроса, СтрНайти(ТекстЗапроса, ";", НаправлениеПоиска.СКонца) - 1);
	
	Если Не ПараметрыУзла.ИнтерактивнаяНастройка Тогда
		ТекстУсловия = 
		"И (&ЗаВесьПериод
		|		ИЛИ СебестоимостьТоваров.Период МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|	И (&ПоВсемОрганизациям
		|		ИЛИ СебестоимостьТоваров.Организация В (&Организации))
		|	И (&ПоВсемПодразделениям
		|		ИЛИ СпрПодразделения.Ссылка В (&Подразделения))
		|	И (&ПоВсейПродукции
		|		ИЛИ КлючиАналитики.Номенклатура В (&Продукция))
		|	И (&ПоВсемПартиям
		|		ИЛИ СебестоимостьТоваров.Партия В (&ПартииПродукции))
		|	И (&ПоВсемХарактеристикам
		|		ИЛИ КлючиАналитики.Характеристика В (&ХарактеристикиПродукции))
		|	И (&ПоВсемСериям
		|		ИЛИ КлючиАналитики.Серия В (&СерииПродукции))
		|	И (&ПоВсемНазначениям
		|		ИЛИ КлючиАналитики.Назначение В (&НазначенияПродукции))
		|	И (&ПоВсемАналитикамПартий
		|		ИЛИ СебестоимостьТоваров.АналитикаУчетаПартий В (&АналитикиУчетаПартийПродукции))";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстУсловия", ТекстУсловия);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если Не ПомещатьВВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВыпущеннаяПродукция", "");
		ТекстСортировка = 
		"УПОРЯДОЧИТЬ ПО
		|	ВтПродукция.ПродукцияПредставление,
		|	ВтПродукция.ХарактеристикаПродукцииПредставление,
		|	ВтПродукция.СерияПродукцииПредставление,
		|	ВтПродукция.НазначениеПродукцииПредставление,
		|	ВтПродукция.ПартияПродукции
		|;
		|";
		ТекстЗапроса = ТекстЗапроса + ТекстСортировка;
	Иначе
		ТекстЗапроса = ТекстЗапроса + ";";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	УстановитьПараметрыДереваСебестоимости(Запрос, ПараметрыЗапроса, КлючПараметра);
	Возврат Запрос;
	
КонецФункции

Процедура УстановитьПараметрыДереваСебестоимости(Запрос, ПараметрыЗапроса, КлючПараметра)
	
	Для Каждого ТекПараметр Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ТекПараметр[КлючПараметра], ТекПараметр.Значение);
	КонецЦикла;
	
КонецПроцедуры

Процедура АдаптироватьЗапросСКД(СхемаКомпоновки) Экспорт
	
	НаборДанных = СхемаКомпоновки.НаборыДанных.Найти("ВыпущеннаяПродукция");
	
	Если НаборДанных = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Изменен перечень наборов данных схемы компоновки дерева себестоимости.'");
	Иначе
		НаборДанных.Запрос = СтрЗаменить(НаборДанных.Запрос, "И &ТекстУсловия", "");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтруктураДереваСебестоимости

Процедура ПустоеДеревоСебестоимости(ПараметрыДерева, ПараметрыУзла)
	
	Если ПараметрыДерева.ТипРезультата = "ДеревоЗначений" Тогда
		ПараметрыДерева.Результат = Новый ДеревоЗначений;
		ИнициализироватьПоляДереваСебестоимости(ПараметрыДерева.Результат);
	ИначеЕсли ПараметрыДерева.ТипРезультата = "ТаблицаЗначений" Тогда
		ПараметрыДерева.Результат = Новый ТаблицаЗначений;
		ИнициализироватьПоляДереваСебестоимости(ПараметрыДерева.Результат);
	Иначе
		ПараметрыДерева.Результат = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	
КонецПроцедуры

Функция ИнициализироватьПоляДереваСебестоимости(Результат)
	
	СтруктураПолей = СтруктураПолейДереваСебестоимости();
	
	Для Каждого ТекПоле Из СтруктураПолей Цикл
		Если ТекПоле.Значение = Неопределено Тогда
			Результат.Колонки.Добавить(ТекПоле.Ключ);
		Иначе
			Результат.Колонки.Добавить(ТекПоле.Ключ, ТекПоле.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Функция СтруктураПолейДереваСебестоимости()
	
	СтруктураПолей = Новый Структура;
	
	СтруктураПолей.Вставить("ВидСтроки",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыСтрокДереваСебестоимости"));
		
	СтруктураПолей.Вставить("Идентификатор",
		Новый ОписаниеТипов("УникальныйИдентификатор"));
	
	СтруктураПолей.Вставить("АналитикаУчетаПродукции",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	
	СтруктураПолей.Вставить("АналитикаУчетаВыходноеИзделие",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	
	ТипыАналитикиЗатрат = Новый Массив;
	ТипыАналитикиЗатрат.Добавить(Тип("СправочникСсылка.КлючиАналитикиУчетаНоменклатуры"));
	ТипыАналитикиЗатрат.Добавить(Тип("СправочникСсылка.ВидыРаботСотрудников"));
	ТипыАналитикиЗатрат.Добавить(Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТипыАналитикиЗатрат = Новый ОписаниеТипов(ТипыАналитикиЗатрат);
	СтруктураПолей.Вставить("АналитикаУчетаЗатрата", ТипыАналитикиЗатрат);
	
	ТипыАналитикиУчетаПартий = Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПартий");
	СтруктураПолей.Вставить("АналитикаУчетаПартий",					ТипыАналитикиУчетаПартий);
	СтруктураПолей.Вставить("АналитикаУчетаПартийПродукции",		ТипыАналитикиУчетаПартий);
	СтруктураПолей.Вставить("АналитикаУчетаПартийВыходноеИзделие",	ТипыАналитикиУчетаПартий);
	СтруктураПолей.Вставить("АналитикаУчетаПартийЗатрата",			ТипыАналитикиУчетаПартий);
	
	СтруктураПолей.Вставить("Организация",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
		
	СтруктураПолей.Вставить("Подразделение",
		Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	
	СтруктураПолей.Вставить("ТипЗатрат",
		Новый ОписаниеТипов("Строка"));
	
	СтруктураПолей.Вставить("ТребуетсяРазузлование",
		Новый ОписаниеТипов("Булево"));
	
	СтруктураПолей.Вставить("ЕдиницаИзмерения",
		Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	
	ТипыЗатраты = Новый Массив;
	ТипыЗатраты.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ТипыЗатраты.Добавить(Тип("СправочникСсылка.ВидыРаботСотрудников"));
	ТипыЗатраты.Добавить(Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТипыЗатраты = Новый ОписаниеТипов(ТипыЗатраты);
	СтруктураПолей.Вставить("Номенклатура", ТипыЗатраты);
	
	СтруктураПолей.Вставить("Характеристика",
		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	СтруктураПолей.Вставить("Серия",
		Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	СтруктураПолей.Вставить("Назначение",
		Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	
	СтруктураПолей.Вставить("СтатьяКалькуляции",
		Новый ОписаниеТипов("СправочникСсылка.СтатьиКалькуляции"));
	
	СтруктураПолей.Вставить("Уровень",
		Новый ОписаниеТипов("Число"));
	
	ТипыПартий = Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ДокументПартии.Тип.Типы());
	СтруктураПолей.Вставить("Партия",					ТипыПартий);
	СтруктураПолей.Вставить("ПартияПродукции",			ТипыПартий);
	СтруктураПолей.Вставить("ПартияВыходноеИзделие",	ТипыПартий);
	СтруктураПолей.Вставить("ПартияЗатрата",			ТипыПартий);
	
	ТипЧисла = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(19, 6));
	СтруктураПолей.Вставить("Количество",			ТипЧисла);
	СтруктураПолей.Вставить("Числитель",			ТипЧисла);
	СтруктураПолей.Вставить("Знаменатель",			ТипЧисла);
	
	// Стоимости согласно параметру "ДанныеПоСебестоимости"
	СтруктураПолей.Вставить("Сумма",					ТипЧисла);
	СтруктураПолей.Вставить("СуммаЗабалансовая",		ТипЧисла);
	СтруктураПолей.Вставить("Материальные",				ТипЧисла);
	СтруктураПолей.Вставить("Трудозатраты",				ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПостоянные",	ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПеременные",	ТипЧисла);
	СтруктураПолей.Вставить("ДопРасходы",				ТипЧисла);
	
	// Стоимости с НДС
	СтруктураПолей.Вставить("МатериальныеСНДС",				ТипЧисла);
	СтруктураПолей.Вставить("ТрудозатратыСНДС",				ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПостоянныеСНДС",	ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПеременныеСНДС", 	ТипЧисла);
	СтруктураПолей.Вставить("ДопРасходыСНДС",				ТипЧисла);
	
	// Стоимости без НДС
	СтруктураПолей.Вставить("МатериальныеБезНДС",			ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПостоянныеБезНДС",	ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПеременныеБезНДС",	ТипЧисла);
	СтруктураПолей.Вставить("ДопРасходыБезНДС",				ТипЧисла);
	
	// Стоимости Регл
	СтруктураПолей.Вставить("МатериальныеРегл",				ТипЧисла);
	СтруктураПолей.Вставить("ТрудозатратыРегл",				ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПостоянныеРегл",	ТипЧисла);
	СтруктураПолей.Вставить("ПостатейныеПеременныеРегл",	ТипЧисла);
	СтруктураПолей.Вставить("ДопРасходыРегл",				ТипЧисла);
	СтруктураПолей.Вставить("СтоимостьЗабалансоваяРегл",	ТипЧисла);
	
	Возврат СтруктураПолей;
	
КонецФункции

#КонецОбласти

#Область ПараметрыДереваСебестоимости

// Параметры построения дерева себестоимости
// 
// Возвращаемое значение:
// Структура - параметры дерева себестоимости
//    ДинамическоеСчитывание - Булево - Истина, если уровни дерева себестоимости получается последовательно.
//    ТипРезультата - Строка - Тип объекта, в который будет помещен результат - ДеревоЗначений, ТаблицаЗначений, МенеджерВременныхТаблиц.
//    Результат - ДеревоЗначений, ТаблицаЗначений, МенеджерВременныхТаблиц - Объект, в который будет помещен результат разузлования.
//    ДобавлятьПолуфабрикатыВРезультат - Булево - Будут ли добавлены промежуточные полуфабрикаты в результат.
//                                                Если результат ДеревоЗначений, то полуфабрикаты добавляются всегда.
//
Функция ПараметрыДереваСебестоимости() Экспорт
	
	ПараметрыДерева = Новый Структура;
	
	ПараметрыДерева.Вставить("ДинамическоеСчитывание",	Истина);
	ПараметрыДерева.Вставить("ТипРезультата",			"ДеревоЗначений");
	ПараметрыДерева.Вставить("Результат",				Неопределено);
	ПараметрыДерева.Вставить("ДобавлятьПолуфабрикатыВРезультат", Истина);
	
	Возврат ПараметрыДерева;
	
КонецФункции

// Параметры узла дерева себестоимости
// 
// Возвращаемое значение:
// Структура - параметры узла себестоимости
//    ИнтерактивнаяНастройка - Булево - Истина, если для получения таблицы ВыпущеннаяПродукция используется схема КД
//    СхемаКД - СхемаКомпоновкиДанных - Схема КД для получения таблицы ВыпущеннаяПродукция
//    НастройкиКД - НастройкиКомпоновкиДанных - Настройки схемы КД для получения таблицы ВыпущеннаяПродукция
//    Отборы - Структура - Структура отборов, накладываемых на получение таблицы ВыпущеннаяПродукция.
//                         Используется для программного получения данных себестоимости.
//
Функция ПараметрыУзлаДереваСебестоимости() Экспорт
	
	ПараметрыУзла = Новый Структура;
	
	ПараметрыУзла.Вставить("ИнтерактивнаяНастройка",	Ложь);
	ПараметрыУзла.Вставить("СхемаКД",					Отчеты.ДеревоСебестоимостиПродукции.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"));
	ПараметрыУзла.Вставить("НастройкиКД",				ПараметрыУзла.СхемаКД.НастройкиПоУмолчанию);
	
	Отборы = Новый Структура;
	
	Отборы.Вставить("ДанныеПоСебестоимости",			1);
	Отборы.Вставить("ДатаНачала",						'00010101');
	Отборы.Вставить("ДатаОкончания",					'00010101');
	Отборы.Вставить("Регистраторы",						Новый Массив);
	Отборы.Вставить("Организации",						Новый Массив);
	Отборы.Вставить("Подразделения",					Новый Массив);
	Отборы.Вставить("Продукция",						Новый Массив);
	Отборы.Вставить("ХарактеристикиПродукции",			Новый Массив);
	Отборы.Вставить("СерииПродукции",					Новый Массив);
	Отборы.Вставить("НазначенияПродукции",				Новый Массив);
	Отборы.Вставить("ПартииПродукции",					Новый Массив);
	Отборы.Вставить("АналитикиУчетаПартийПродукции",	Новый Массив);
	Отборы.Вставить("Числитель",						0);
	Отборы.Вставить("Знаменатель",						0);
	
	ПараметрыУзла.Вставить("Отборы", Отборы);
	
	Возврат ПараметрыУзла;
	
КонецФункции

#КонецОбласти

#КонецОбласти
