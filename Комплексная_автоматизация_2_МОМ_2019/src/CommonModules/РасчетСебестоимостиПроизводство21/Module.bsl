
// Вызываются процедуры и функции общих модулей:
//	- РасчетСебестоимостиПрикладныеАлгоритмы
//	- РасчетСебестоимостиПроизводство

#Область ПрограммныйИнтерфейс

// Этап 6 (Контекст: "РаспределениеМатериаловИРаботПоБазе")
// ПУ 2.1: РассчитатьРаспределениеМатериаловИРабот(), область РаспределениеМатериаловИРабот.
// 
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеМатериаловИРаботПоБазе(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеМатериаловИРаботПоБазе");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ОписаниеЦепочекРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенийМатериаловИРаботПоБазе(ПараметрыРасчета));

	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- формирует движения по регистру МатериалыИРаботыВПроизводстве.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

// Этап 7 (Контекст: "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями")
// ПУ 2.1: РассчитатьПартииНЗП(), область РасчетПартийНезавершенногоПроизводства.
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями");
	
	// 1. Инициализация структуры данных для расчета
	//	- формирует структуру ПараметрыРасчета.РаспределениеПартий.
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьРаспределениеПартий(
		ПараметрыРасчета,
		ТаблицаДляРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ОписаниеЦепочекРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ОписаниеДвиженийРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ОписаниеНезаписываемыхРаспределенийМатериаловНЗП(ПараметрыРасчета));

	// 2. Получение исходных данных для расчета
	// 	- формирует временную таблицу Данные.
	ПолучитьДанныеДляРаспределенияМатериаловНЗП(ПараметрыРасчета);
	
	// 3. Получение цепочек из исходных данных
	// 	- формирует временные таблицы Источники и Приемники.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПостроитьЦепочкиДвижений(ПараметрыРасчета);
	
	// 4. Заполнение партий в цепочках
	// 	- заполняет таблицу ПараметрыРасчета.РаспределениеПартий.РасчетныеПартии.
	РасчетСебестоимостиПрикладныеАлгоритмы.РассчитатьПартииПоЦепочкам(ПараметрыРасчета);
	
	// 5. Помещение данных в таблицу-приемник и очистка вспомогательных временных таблиц.
	// 	- формирует движения по регистру ПартииНезавершенногоПроизводства.
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗавершитьРаспределениеПартий(ПараметрыРасчета);
	
КонецПроцедуры

#Область Инициализация

// Инициализирует общие параметры расчета, описывающие обслуживаемые механизмом расчета регистры.
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ИнициализироватьОбслуживаемыеРегистры(ПараметрыРасчета) Экспорт
	
	ПараметрыРасчета.РегистрыСРасчетнымиОборотами.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства.Имя, Истина);	
	
КонецПроцедуры

// Дополняет перечень документов, которые могут иметь движения в разных месяцах или по нескольким организациям.
//
// Параметры:
//	РазныеПериоды - Булево - добавлять в результат документы с движениями в разных периодах
//	РазныеОрганизации - Булево - добавлять в результат документы с движениями по нескольким организациям
//	ИмяРегистра - Строка - имя регистра накопления, для которого нужно получить перечень документов;
//		пустое значение - перечень документов для всех регистров.
//	ОписаниеДокументов - Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ДополнитьДокументыСРазнымиПериодамиИлиОрганизациямиВДвижениях(РазныеПериоды, РазныеОрганизации, ИмяРегистра, ОписаниеДокументов) Экспорт
	
	// Для движений в других периодах Значение = Истина означает
	// - наличие первичных+расчетных движений,
	// - расчетные движения формируются при расчете их периода.
	// Если указано значение Ложь, то это означает, что
	// - расчетные движения могут быть без первичных движений,
	// - расчетные движения любых периодов формируются при расчете периода документа.
	// Для движений других организаций Значение должно быть только Истина.
	
	Значение = Истина;
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцедурыЭтапа1а_РаспределениеТрудозатрат

Функция ТекстЗапросаДляРаспределенияТрудозатрат(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// выборка данных
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПервичныеТрудозатратыБезЗаказов()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыБезЗаказовПотребление()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыБезЗаказовПотреблениеАвто()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыБезЗаказовПеремещение()
		
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстПервичныеТрудозатратыБезЗаказов() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПервичныеТрудозатратыБезЗаказов"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПартияБезЗаказа) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&НачалоПериода КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ДД.ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	Приходы.Количество КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(ДД.НормативнаяСтоимость) КАК НормативнаяСтоимость,
		|	СУММА(ДД.Стоимость) КАК Стоимость,
		|	СУММА(ДД.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(ДД.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ДД.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	ИСТИНА КАК Первичное,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	ДД.СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриходыБезЗаказов КАК Приходы
		|		ПО Приходы.Организация = ДД.Организация
		|		И Приходы.Подразделение = ДД.Подразделение
		|		И Приходы.ВидРабот = ДД.ВидРабот
		|		И Приходы.ГруппаПродукции = ДД.ГруппаПродукции
		|ГДЕ
		|	ДД.СлужебноеВидДвиженияПриход
		|	И ДД.КодСтрокиПродукция = 0
		|	И ДД.ПартияПроизводства = ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.ВидФондаВзносов,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ВидРабот,
		|	ДД.ГруппаПродукции,
		|	Приходы.Количество,
		|	ДД.СтатьяКалькуляцииБезЗаказа
		|";
КонецФункции

Функция ТекстТрудозатратыБезЗаказовПотребление() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыБезЗаказовПотребление"" КАК ЗапросИсточник,
		|	40 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ВтВидыФондов.ВидФондаВзносов КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	ВидыРабот.ВидРабот,
		|	ДД.Номенклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	СУММА(ВидыРабот.Количество) КАК Знаменатель,
		|	МАКСИМУМ(ВЫБОР Выпуски.ДоляСтоимости
		|				КОГДА 0
		|					ТОГДА Выпуски.Количество
		|				ИНАЧЕ Выпуски.ДоляСтоимости
		|	КОНЕЦ) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	ВидыРабот.СтатьяКалькуляции КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение
		|ИЗ
		|	Документ.СписаниеЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.Трудозатраты КАК ВидыРабот
		|		ПО ВидыРабот.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпускиБезЗаказов КАК Выпуски
		|		ПО Выпуски.Ссылка = ДД.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыФондов КАК ВтВидыФондов
		|		ПО ВтВидыФондов.Организация = ДД.Организация
		|		И ВтВидыФондов.Подразделение = ВидыРабот.Подразделение
		|		И ВтВидыФондов.ВидРабот = ВидыРабот.ВидРабот
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.СтатьяКалькуляции,
		|	ВтВидыФондов.ВидФондаВзносов,
		|	ДД.Номенклатура
		|";
КонецФункции

Функция ТекстТрудозатратыБезЗаказовПотреблениеАвто() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыБезЗаказовПотреблениеАвто"" КАК ЗапросИсточник,
		|	45 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПотреблениеАвто) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	ВтВидыФондов.ВидФондаВзносов КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	ВидыРабот.ВидРабот,
		|	ДД.Номенклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	СУММА(ВидыРабот.Количество) КАК Знаменатель,
		|	МАКСИМУМ(ВЫБОР Выпуски.ДоляСтоимости
		|				КОГДА 0
		|					ТОГДА Выпуски.Количество
		|				ИНАЧЕ Выпуски.ДоляСтоимости
		|	КОНЕЦ) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	ВидыРабот.СтатьяКалькуляции КАК СтатьяКалькуляцииБезЗаказа,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение
		|ИЗ
		|	Документ.СписаниеЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.Трудозатраты КАК ВидыРабот
		|		ПО ВидыРабот.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыпускиБезЗаказов КАК Выпуски
		|		ПО Выпуски.Ссылка = ДД.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВидыФондов КАК ВтВидыФондов
		|		ПО ВтВидыФондов.Организация = ДД.Организация
		|		И ВтВидыФондов.Подразделение = ВидыРабот.Подразделение
		|		И ВтВидыФондов.ВидРабот = ВидыРабот.ВидРабот
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	ДД.Организация,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.СтатьяКалькуляции,
		|	ВтВидыФондов.ВидФондаВзносов,
		|	ДД.Номенклатура
		|";
КонецФункции

Функция ТекстТрудозатратыБезЗаказовПеремещение() // Производство 2.1
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыБезЗаказовПеремещение"" КАК ЗапросИсточник,
		|	60 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДД.Дата КАК Период,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	СУММА(ВЫБОР Выходы.ДоляСтоимости
		|			КОГДА 0
		|				ТОГДА Выходы.Количество
		|			ИНАЧЕ Выходы.ДоляСтоимости
		|	КОНЕЦ) КАК Знаменатель,
		|	СУММА(Выходы.Количество) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПродукции,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасовПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	Выходы.Распоряжение КАК ДокументВыпуска,
		|	Выходы.КодСтроки КАК КодСтроки,
		|	Выходы.Распоряжение КАК ДокументИсточник,
		|	Выходы.Номенклатура КАК Продукция,
		|	Выходы.Характеристика КАК ХарактеристикаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Назначение
		|ИЗ
		|	Документ.СписаниеЗатратНаВыпуск КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК Выходы
		|		ПО Выходы.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ДД.Дата,
		|	ДД.Ссылка,
		|	ДД.Организация,
		|	Выходы.Номенклатура,
		|	Выходы.Характеристика,
		|	Выходы.Распоряжение,
		|	Выходы.КодСтроки,
		|	Выходы.Распоряжение,
		|	Выходы.КодСтроки
		|";
КонецФункции

#КонецОбласти

#Область ПроцедурыЭтапа1б_РаспределениеТрудозатратНаВыпуск


Функция ТекстЗапросаДляРаспределенияТрудозатратНаВыпуск() Экспорт
	
	ТекстЗапроса = ""
		// выборка данных
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТрудозатратыВыпускиБезЗаказов()
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстТрудозатратыВыпускиБезЗаказов()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстТрудозатратыВыпускиБезЗаказов"" КАК ЗапросИсточник,
		|	90 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВыпускБезЗаказа) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ВидФондаВзносов,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ВидРабот,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	СУММА(ДД.Количество) КАК Знаменатель,
		|	СУММА(ДД.Количество) КАК КоличествоПродукции,
		|	0 КАК Количество,
		|	0 КАК НормативнаяСтоимость,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьРегл,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ПостояннаяРазница,
		|	ЛОЖЬ КАК Первичное,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК Сотрудник,
		|	ЕСТЬNULL(РаботыДляДавальца.АналитикаУчетаНоменклатуры, ДД.АналитикаУчетаПродукции) КАК КорАналитикаУчетаПродукции,
		|	ВЫБОР
		|		КОГДА ДД.КорВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ КАК КорРазделУчета,
		|	ЕСТЬNULL(РаботыДляДавальца.ВидЗапасов, ДД.КорВидЗапасов) КАК КорВидЗапасовПродукции,
		|	ВЫБОР
		|		КОГДА СпрНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|												ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА АналитикиУчетаПартийТоваров.КлючАналитики
		|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			И НЕ Продукция.СписатьНаРасходы
		|			ТОГДА АналитикиУчетаПартийРабот.КлючАналитики
		|		ИНАЧЕ АналитикиУчетаПартийРаботНаРасходы.КлючАналитики
		|	КОНЕЦ КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляцииБезЗаказа,
		|	ДД.Регистратор КАК ДокументВыпуска,
		|	ДД.КодСтроки КАК КодСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	Аналитика.Номенклатура КАК Продукция,
		|	Аналитика.Характеристика КАК ХарактеристикаПродукции,
		|	ДД.Назначение КАК Назначение
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаСписаниеПоНормативам КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаПродукции
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = Аналитика.Номенклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
		|		ПО Назначения.Ссылка = ДД.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ РаботыДляДавальца КАК РаботыДляДавальца
		|		ПО РаботыДляДавальца.Регистратор = ДД.Регистратор
		|			И РаботыДляДавальца.КорАналитикаУчетаНоменклатуры = ДД.АналитикаУчетаПродукции
		|			И РаботыДляДавальца.КорВидЗапасов = ДД.КорВидЗапасов
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Продукция
		|		ПО Продукция.Ссылка = ДД.Регистратор
		|		И Продукция.КодСтроки = ДД.КодСтроки
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО СтатьиРасходов.Ссылка = Продукция.СтатьяРасходов
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийТоваров
		|		ПО АналитикиУчетаПартийТоваров.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
		|			И АналитикиУчетаПартийТоваров.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|			И АналитикиУчетаПартийТоваров.Контрагент			= НЕОПРЕДЕЛЕНО
		|			И АналитикиУчетаПартийТоваров.СтавкаНДС				= ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
		|			И АналитикиУчетаПартийТоваров.НалогообложениеНДС 	= ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			И АналитикиУчетаПартийТоваров.ВидЦенности			= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|			И АналитикиУчетаПартийТоваров.КодСтроки				= 0
		|			И ДД.Организация									В (&ОрганизацииСФИФОСкользящая)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийРабот
		|		ПО АналитикиУчетаПартийРабот.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
		|			И АналитикиУчетаПартийРабот.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|			И АналитикиУчетаПартийРабот.Контрагент				= НЕОПРЕДЕЛЕНО
		|			И АналитикиУчетаПартийРабот.СтавкаНДС				= ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
		|			И АналитикиУчетаПартийРабот.НалогообложениеНДС		= ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			И АналитикиУчетаПартийРабот.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
		|			И АналитикиУчетаПартийРабот.КодСтроки				= 0
		|			И ДД.Организация									В (&ОрганизацииСФИФОСкользящая)
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийРаботНаРасходы
		|		ПО АналитикиУчетаПартийРабот.ГруппаФинансовогоУчета	= АналитикиУчетаПартийРаботНаРасходы.ГруппаФинансовогоУчета
		|			И АналитикиУчетаПартийРабот.Поставщик				= АналитикиУчетаПартийРаботНаРасходы.Поставщик
		|			И АналитикиУчетаПартийРабот.Контрагент				= АналитикиУчетаПартийРаботНаРасходы.Контрагент
		|			И АналитикиУчетаПартийРабот.СтавкаНДС				= АналитикиУчетаПартийРаботНаРасходы.СтавкаНДС
		|			И АналитикиУчетаПартийРабот.НалогообложениеНДС		= АналитикиУчетаПартийРаботНаРасходы.НалогообложениеНДС
		|			И АналитикиУчетаПартийРабот.ВидЦенности				= АналитикиУчетаПартийРаботНаРасходы.ВидЦенности
		|			И АналитикиУчетаПартийРабот.КодСтроки				= АналитикиУчетаПартийРаботНаРасходы.КодСтроки
		|			И ДД.Организация									В (&ОрганизацииСФИФОСкользящая)
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Активность
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.АналитикаУчетаПродукции,
		|	ДД.КорВидЗапасов,
		|	РаботыДляДавальца.ВидЗапасов,
		|	РаботыДляДавальца.АналитикаУчетаНоменклатуры,
		|	ДД.Назначение,
		|	ДД.КодСтроки,
		|	ВЫБОР
		|		КОГДА ДД.КорВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		КОГДА Аналитика.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СпрНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|												ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|			ТОГДА АналитикиУчетаПартийТоваров.КлючАналитики
		|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			И НЕ Продукция.СписатьНаРасходы
		|			ТОГДА АналитикиУчетаПартийРабот.КлючАналитики
		|		ИНАЧЕ АналитикиУчетаПартийРаботНаРасходы.КлючАналитики
		|	КОНЕЦ,
		|	Аналитика.Номенклатура,
		|	Аналитика.Характеристика
		|";
КонецФункции


#КонецОбласти

#Область ПроцедурыЭтапов_1_2_3_4_5

Процедура ДополнитьЗапросДаннымиПоТрудозатратам(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// собственное производство 2.1 без заказов, фактические трудозатраты
	|ВЫБРАТЬ
	|	ДД.Организация						КАК Организация,
	|	ДД.Подразделение					КАК Подразделение,
	|	ДД.ВидРабот							КАК ВидРабот,
	|	ДД.ГруппаПродукции					КАК ГруппаПродукции,
	|	Товары.Назначение					КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
	|	ДД.ДокументВыпуска					КАК ЗаказНаПроизводство,
	|	ДД.КодСтроки						КАК КодСтрокиПродукция,
	|	ЕСТЬNULL(Товары.Назначение.НаправлениеДеятельности, 
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СУММА(ДД.Количество)				КАК Количество,
	|	СУММА(ДД.НормативнаяСтоимость)		КАК НормативнаяСтоимость,
	|	СУММА(ДД.Стоимость)					КАК Стоимость,
	|	СУММА(ДД.СтоимостьРегл)				КАК СтоимостьРегл
	|ИЗ
	|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Товары
	|			ПО ДД.ДокументВыпуска = Товары.Ссылка
	|			И ДД.КодСтроки = Товары.КодСтроки
	|
	|ГДЕ
	|	(&РасчетСебестоимостиВыполнен ИЛИ НЕ &РасшифровкаРаспределения)
	|	И &ВключатьДанныеПроизводства21
	|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.СлужебноеВидДвиженияПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение,
	|	ДД.ВидРабот,
	|	Товары.Назначение,
	|	ДД.Организация,
	|	ДД.ДокументВыпуска,
	|	ДД.КодСтроки,
	|	Товары.Назначение.НаправлениеДеятельности,
	|	ДД.ГруппаПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// собственное производство 2.1 без заказов, плановые трудозатраты для отчета
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Организация		КАК Организация,
	|	ТаблицаТрудозатраты.Подразделение	КАК Подразделение,
	|	ТаблицаТрудозатраты.ВидРабот		КАК ВидРабот,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ								КАК ГруппаПродукции,
	|	ТаблицаТовары.Назначение				КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК ВидДеятельностиНДС,
	|	ДД.Распоряжение						КАК ЗаказНаПроизводство,
	|	ДД.КодСтроки						КАК КодСтрокиПродукция,
	|	ТаблицаТовары.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СУММА(ТаблицаТрудозатраты.Количество * (ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) / Итоги.Всего) КАК Количество,
	|	СУММА(ЕСТЬNULL(Расценки.Расценка, 0) * ТаблицаТрудозатраты.Количество * (ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) / Итоги.Всего) КАК НормативнаяСтоимость,
	|	СУММА(ТаблицаТрудозатраты.Количество
	|		* ВЫБОР
	|			КОГДА ЕСТЬNULL(ТрудозатратыНЗП.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(ТрудозатратыНЗП.Стоимость / ТрудозатратыНЗП.Количество КАК ЧИСЛО(28, 10))
	|		КОНЕЦ 
	|		* (ВЫБОР 
	|			КОГДА ДД.ДоляСтоимости = 0 
	|				ТОГДА ДД.Количество 
	|			ИНАЧЕ 
	|				ДД.ДоляСтоимости
	|		КОНЕЦ / Итоги.Всего)) КАК Стоимость,
	|	СУММА(ТаблицаТрудозатраты.Количество
	|		* ВЫБОР
	|			КОГДА ЕСТЬNULL(ТрудозатратыНЗП.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(ТрудозатратыНЗП.СтоимостьРегл / ТрудозатратыНЗП.Количество КАК ЧИСЛО(28, 10))
	|		КОНЕЦ 
	|		* (ВЫБОР 
	|			КОГДА ДД.ДоляСтоимости = 0 
	|				ТОГДА ДД.Количество 
	|			ИНАЧЕ 
	|				ДД.ДоляСтоимости
	|		КОНЕЦ / Итоги.Всего)) КАК СтоимостьРегл
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ДД.Ссылка КАК Ссылка,
	|		СУММА(ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) КАК Всего
	|	ИЗ
	|		Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск КАК Реквизиты
	|			ПО ДД.Ссылка = Реквизиты.Ссылка
	|	ГДЕ
	|		&РасшифровкаРаспределения И НЕ &РасчетСебестоимостиВыполнен
	|		И &ВключатьДанныеПроизводства21
	|		И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Реквизиты.Организация В (&МассивОрганизаций)
	|		И Реквизиты.Проведен
	|		
	|	СГРУППИРОВАТЬ ПО
	|		ДД.Ссылка
	|		
	|	ИМЕЮЩИЕ
	|		СУММА(ВЫБОР КОГДА ДД.ДоляСтоимости = 0 ТОГДА ДД.Количество ИНАЧЕ ДД.ДоляСтоимости КОНЕЦ) > 0
	|		
	|	) КАК ИТОГИ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск КАК РеквизитыДокумента
	|			ПО Итоги.Ссылка = РеквизитыДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ДД
	|			ПО Итоги.Ссылка = ДД.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.Трудозатраты КАК ТаблицаТрудозатраты
	|			ПО Итоги.Ссылка = ТаблицаТрудозатраты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаТовары
	|			ПО ДД.Распоряжение = ТаблицаТовары.Ссылка
	|			И ДД.КодСтроки = ТаблицаТовары.КодСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&КонецПериода) КАК Расценки
	|			ПО Расценки.ВидРабот = ТаблицаТрудозатраты.ВидРабот
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеТрудозатратыНЗП КАК ТрудозатратыНЗП
	|			ПО РеквизитыДокумента.Организация = ТрудозатратыНЗП.Организация
	|				И ТаблицаТрудозатраты.Подразделение = ТрудозатратыНЗП.Подразделение
	|				И ТаблицаТрудозатраты.ВидРабот = ТрудозатратыНЗП.ВидРабот
	|	ГДЕ
	|		&РасшифровкаРаспределения И НЕ &РасчетСебестоимостиВыполнен
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТрудозатраты.Подразделение,
	|	ТаблицаТрудозатраты.ВидРабот,
	|	ТаблицаТовары.Назначение,
	|	РеквизитыДокумента.Организация,
	|	ДД.Распоряжение,
	|	ДД.КодСтроки,
	|	ТаблицаТовары.Номенклатура
	|";
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа6_РаспределениеМатериаловИРаботПоБазе

// Инициализация данных

Функция ПоляПотребленийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	Возврат "Регистратор, Серия";
	
КонецФункции

Функция ОписаниеЦепочекРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений	= ПоляПотребленийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета);
	
	// Потребление <- (Партия)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Потребление, ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Потребление,
		Перечисления.ТипыЗаписейПартий.Партия,		ПоляПотреблений);
	
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеМатериаловИРаботПоБазе");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели", "Партия");
	ОписаниеДвижений.Вставить("БазисПрихода", "Количество");
	ОписаниеДвижений.Вставить("БазисРасхода", "Количество");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период"); // ПолеПорядка
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенийМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Партия, Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей);
	
КонецФункции

Функция ТаблицаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляРаспределенияМатериаловИРаботПоБазе());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета)
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтборПоМатериаламИерархия", РасчетСебестоимостиПроизводство.ОтборПоМатериаламИерархия(ПараметрыРасчета));
	
	// Нумерация строк ВТ Данные
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"ЗаказНаПроизводство", // разделитель
		"Количество, КРаспределению, Объем, Вес, Стоимость", // ресурсы
		"Период, Регистратор, ТипЗаписи"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета),
		ПараметрыНумерации,
		ДопПараметры);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ ТекстРаспределениеМатериаловИРаботИнициализация() // вт ОтборПоМатериалам, Распределить 
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстДанныеПоУказаннымМатериалам() // вт Данные_ПоУказаннымМатериалам
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстДанныеПоПродукции() // вт ФактЭтапов_ПоПродукции, ПланЭтаповПоЗаказам_ПоПродукции, Данные_ПоПродукции
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстДанныеПоПлановойСтоимости() // вт Данные_ПоПлановойСтоимости
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстДанныеПоУказаннымТрудозатратам() // вт Данные_ПоУказаннымТрудозатратам
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстДанныеПоНормативамРасходаМатериала() // вт ФактЭтапов_ПоНормативам, ПланЭтаповПоЗаказам_ПоНормативам, Данные_ПоНормативамРасходаМатериала
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + РасчетСебестоимостиПроизводство.ТекстРаспаковкаНаборов("Данные_ПоНормативамРасходаМатериала") // вт МатериалыИУслугиСпецификаций
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстБазаРаспределения() // вт БазаРаспределения
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете() + ТекстСуммыПоказателей() // вт ОбщиеСуммы
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете()
		// выборка данных
		+ ТекстОписаниеДанныхДляРаспределенияМатериаловИРаботПоБазе()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстКРаспределению()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстРаспределениеПоБазе()
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляРаспределенияМатериаловИРаботПоБазе()
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) 		 								 КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка) 		  		 КАК ТипЗаписи,
		|	ЛОЖЬ 														  		 КАК РасчетЗавершен,
		|
		|	ДД.ВидДвижения 								 						 КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) 										 КАК Период,
		|	ДД.Регистратор 														 КАК Регистратор,
		|	ДД.Организация 						 								 КАК Организация,
		|	ДД.Номенклатура 						 							 КАК Номенклатура,
		|	ДД.Характеристика 						 							 КАК Характеристика,
		|	ДД.Подразделение 						 							 КАК Подразделение,
		|	ДД.Серия 						 							 		 КАК Серия,
		|	ДД.Назначение 						 							 	 КАК Назначение,
		|	0 																	 КАК Количество,
		|	ДД.СтатьяКалькуляции 						 						 КАК СтатьяКалькуляции,
		|	ДД.ЗаказНаПроизводство 												 КАК ЗаказНаПроизводство,
		|	0 																	 КАК КодСтрокиПродукция,
		|	ДД.Этап 						 							 		 КАК Этап,
		|	ДД.АналитикаУчетаПродукции 						 					 КАК АналитикаУчетаПродукции,
		|	ДД.Спецификация 						 							 КАК Спецификация,
		|	ДД.СтатьяРасходов 						 							 КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов 												 КАК АналитикаРасходов,
		|	ДД.АналитикаУчетаНоменклатуры 						 				 КАК АналитикаУчетаНоменклатуры,
		|	ДД.ПодразделениеПолучатель 						 					 КАК ПодразделениеПолучатель,
		|	ДД.БазаРаспределения 						 						 КАК БазаРаспределения,
		|	0 																	 КАК КРаспределению,
		|	0 																	 КАК Объем,
		|	0 																	 КАК Вес,
		|	0 																	 КАК Стоимость
		|//ВоВременнуюТаблицу
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|";
КонецФункции

// ... формирования временные таблиц

Функция ТекстРаспределениеМатериаловИРаботИнициализация() // вт ОтборПоМатериалам, Распределить
	Возврат
		"ВЫБРАТЬ
		|	ДД.Регистратор КАК Регистратор,
		|	ДД.Материал КАК Материал
		|ПОМЕСТИТЬ ОтборПоМатериалам
		|ИЗ
		|	&ОтборПоМатериаламИерархия КАК ДД
		|;
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Дата,
		|	ДД.Организация,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.Серия,
		|	ДД.Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Назначение,
		|	ДД.БазаРаспределения,
		|	ДД.КРаспределению,
		|	ЕСТЬNULL(ОтборПоМатериалам.Материал, НЕОПРЕДЕЛЕНО) КАК Материал,
		|	ЕСТЬNULL(ОтборПоВидамРабот.ВидРабот, НЕОПРЕДЕЛЕНО) КАК ВидРабот,
		|	ЕСТЬNULL(ОтборПоГруппамПродукции.ГруппаПродукции, НЕОПРЕДЕЛЕНО) КАК ГруппаПродукции,
		|	(ВЫБОР
		|		КОГДА ОтборПоГруппамПродукции.ГруппаПродукции ЕСТЬ NULL ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ) КАК ПоВсемГруппамПродукции
		|ПОМЕСТИТЬ Распределить
		|ИЗ (
		|	ВЫБРАТЬ
		|		ДД.Ссылка КАК Регистратор,
		|		ДД.Дата,
		|		ДД.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ДД.Серия,
		|		ДД.Подразделение,
		|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
		|		ДД.БазаРаспределения КАК БазаРаспределения,
		|		ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ДД.Назначение КАК Назначение,
		|		ДД.КоличествоКРаспределениюПоПравилу КАК КРаспределению
		|	ИЗ
		|		Документ.РаспределениеПроизводственныхЗатрат КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|			ПО Аналитика.Номенклатура = ДД.Номенклатура
		|			И Аналитика.Характеристика = ДД.Характеристика
		|			И Аналитика.Серия = ДД.Серия
		|			И Аналитика.МестоХранения = ДД.Подразделение
		|			И Аналитика.Назначение = ДД.Назначение
		|			И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	ГДЕ
		|		ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ДД.Организация В (&МассивОрганизаций)
		|		И ДД.Проведен
		|		И ДД.СтатусУказанияСерий НЕ В(9,10)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ДД.Ссылка КАК Регистратор,
		|		Т.Дата,
		|		Т.Организация,
		|		ДД.Номенклатура,
		|		ДД.Характеристика,
		|		ДД.Серия,
		|		Т.Подразделение,
		|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
		|		Т.БазаРаспределения КАК БазаРаспределения,
		|		Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		Т.Назначение КАК Назначение,
		|		ДД.КоличествоКРаспределениюПоПравилу КАК КРаспределению
		|	ИЗ
		|		Документ.РаспределениеПроизводственныхЗатрат.Серии КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат КАК Т
		|			ПО Т.Ссылка = ДД.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
		|			ПО Аналитика.Номенклатура = ДД.Номенклатура
		|			И Аналитика.Характеристика = ДД.Характеристика
		|			И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|			И Аналитика.МестоХранения = Т.Подразделение
		|			И Аналитика.Назначение = Т.Назначение
		|			И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	ГДЕ
		|		Т.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Т.Организация В (&МассивОрганизаций)
		|		И Т.Проведен
		|		И Т.СтатусУказанияСерий В (9,10)
		|	) КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ОтборПоМатериалам КАК ОтборПоМатериалам
		|		ПО ОтборПоМатериалам.Регистратор = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоГруппамПродукции КАК ОтборПоГруппамПродукции
		|		ПО ОтборПоГруппамПродукции.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоВидамРабот КАК ОтборПоВидамРабот
		|		ПО ОтборПоВидамРабот.Ссылка = ДД.Регистратор
		|";
КонецФункции

Функция ТекстДанныеПоНормативамРасходаМатериала() // вт ФактЭтапов_ПоНормативам, ПланЭтаповПоЗаказам_ПоНормативам, Данные_ПоНормативамРасходаМатериала
	Возврат
		"
		// Данные по выпуску без распоряжений.
		|ВЫБРАТЬ
		|	Выпуск.Организация,
		|	Выпуск.Подразделение,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.Ссылка КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.Номенклатура КАК НоменклатураВыпуска,
		|	ДД.Характеристика КАК ХарактеристикаВыпуска,
		|	ДД.Спецификация КАК Спецификация,
		|	ДД.Количество КАК ЗапланированоПродукции,
		|	1 КАК ЗапланированоЭтапов,
		|	1 КАК ВыполненоЭтапов,
		|	ДД.Количество
		|ПОМЕСТИТЬ Данные_ПоНормативамРасходаМатериала
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Выпуск
		|		ПО (Выпуск.Ссылка = ДД.Ссылка)
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = ДД.Номенклатура)
		|ГДЕ
		|	Выпуск.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Выпуск.Организация В (&МассивОрганизаций)
		|	И НЕ Выпуск.ВыпускПоРаспоряжениям
		|	И Выпуск.Проведен
		|
		|";
КонецФункции

Функция ТекстДанныеПоУказаннымМатериалам() // вт Данные_ПоУказаннымМатериалам
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДД.Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА НЕ Выпуск.Назначение.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Выпуск.Назначение.НаправлениеДеятельности
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ КАК НаправлениеДеятельности,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура КАК Материал,
		|	(ВЫБОР
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК Продукция,
		|	(ВЫБОР
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	СУММА(ДД.Количество) КАК Количество,
		|	СУММА(&Объем * ДД.Количество) КАК Объем,
		|	СУММА(&Вес * ДД.Количество) КАК Вес
		|ПОМЕСТИТЬ Данные_ПоУказаннымМатериалам
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Выпуск
		|		ПО Выпуск.Ссылка = ДД.ЗаказНаПроизводство
		|		И Выпуск.КодСтроки = ДД.КодСтрокиПродукция
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И (ДД.СтатьяРасходов = НЕОПРЕДЕЛЕНО ИЛИ ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
		|	И ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|	И НЕ ДД.Регистратор Ссылка Документ.ВозвратМатериаловИзПроизводства
		|	И НЕ ДД.Регистратор Ссылка Документ.ОтчетПереработчика
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры.Номенклатура,
		|	(ВЫБОР
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА НЕ Выпуск.Ссылка ЕСТЬ NULL ТОГДА Выпуск.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
		|	ДД.Назначение,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	ВЫБОР
		|		КОГДА НЕ Выпуск.Назначение.НаправлениеДеятельности ЕСТЬ NULL ТОГДА Выпуск.Назначение.НаправлениеДеятельности
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ
		|";
		
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ПодставитьШаблоныВесаИОбъемаВТекстЗапроса(ТекстЗапроса);
	
КонецФункции

Функция ТекстДанныеПоПродукции() // вт ФактЭтапов_ПоПродукции, ПланЭтаповПоЗаказам_ПоПродукции, Данные_ПоПродукции
	
	ТекстЗапроса =
		"
		// Данные по выпуску без распоряжений
		|ВЫБРАТЬ
		|	Выпуск.Организация,
		|	Выпуск.Подразделение,
		|	ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))КАК НаправлениеДеятельности,
		|	ДД.Номенклатура КАК Продукция,
		|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.Ссылка КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.Количество КАК ЗапланированоПродукции,
		|	1 КАК ЗапланированоЭтапов,
		|	1 КАК ВыполненоЭтапов,
		|	ДД.Количество,
		|	&Объем * ДД.Количество КАК Объем,
		|	&Вес * ДД.Количество КАК Вес
		|ПОМЕСТИТЬ Данные_ПоПродукции
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Выпуск
		|		ПО (Выпуск.Ссылка = ДД.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Ссылка = ДД.Номенклатура)
		|ГДЕ
		|	Выпуск.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Выпуск.Организация В (&МассивОрганизаций)
		|	И НЕ Выпуск.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Выпуск.Проведен
		|
		|";
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ПодставитьШаблоныВесаИОбъемаВТекстЗапроса(ТекстЗапроса);
	
КонецФункции

Функция ТекстДанныеПоПлановойСтоимости() // вт ФактЭтапов_ПоПлановойСтоимости, ПланЭтаповПоЗаказам_ПоПлановойСтоимости, Данные_ПоПлановойСтоимости
	Возврат
		"
		// Данные по выпуску без распоряжений.
		|ВЫБРАТЬ
		|	Выпуск.Организация,
		|	Выпуск.Подразделение,
		|	ЕСТЬNULL(ДД.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	ДД.Номенклатура КАК Номенклатура,
		|	ДД.Характеристика КАК Характеристика,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	ДД.Назначение,
		|	ДД.Ссылка КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.Количество КАК ЗапланированоПродукции,
		|	1 КАК ЗапланированоЭтапов,
		|	1 КАК ВыполненоЭтапов,
		|	ДД.Количество
		|ПОМЕСТИТЬ Данные_ПоПлановойСтоимости
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Выпуск
		|		ПО (Выпуск.Ссылка = ДД.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = ДД.Номенклатура)
		|ГДЕ
		|	Выпуск.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Выпуск.Организация В (&МассивОрганизаций)
		|	И НЕ Выпуск.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Выпуск.Проведен
		|";
КонецФункции

Функция ТекстДанныеПоУказаннымТрудозатратам() // вт Данные_ПоУказаннымТрудозатратам
	Возврат
		"ВЫБРАТЬ
		|	ДД.Организация КАК Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(Выпуски.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.КорАналитикаУчетаПродукции.Номенклатура КАК Продукция,
		|	СН.ГруппаАналитическогоУчета КАК ГруппаПродукции,
		|	Выпуски.Назначение КАК Назначение,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	ДД.ДокументВыпуска КАК ДокументВыпуска,
		|	ДД.КодСтроки КАК КодСтроки,
		|	ДД.Количество КАК Количество
		|ПОМЕСТИТЬ Данные_ПоУказаннымТрудозатратам
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|	ПО ДД.КорАналитикаУчетаПродукции.Номенклатура = СН.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Выпуски
		|	ПО ДД.ДокументВыпуска = Выпуски.Ссылка
		|	И ДД.КодСтроки = Выпуски.КодСтроки
		|
		|ГДЕ
		|	ДД.КодСтрокиПродукция = 0
		|	И ДД.Количество <> 0
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|";
КонецФункции

Функция ТекстБазаРаспределения() // вт БазаРаспределения
	Возврат
		// по указанным материалам
		"ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	Данные.Объем,
		|	Данные.Вес,
		|	0 КАК Стоимость
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоУказаннымМатериалам КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И Данные.Подразделение = ДД.Подразделение
		|		И Данные.Материал = ДД.Материал
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов)
		|
		// по продукции
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	Данные.Объем,
		|	Данные.Вес,
		|	0 КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоПродукции КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|		И Данные.Подразделение = ДД.Подразделение
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции)
		|	ИЛИ ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции)
		|
		// по плановой стоимости
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	0 КАК Объем,
		|	0 КАК Вес,
		|	Данные.Количество * Цены.Цена КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоПлановойСтоимости КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|		И Данные.Подразделение = ДД.Подразделение
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&КонецПериода, ВидЦены = &ВидЦеныПлановойСтоимостиМатериаловРабот) КАК Цены
		|		ПО Цены.Номенклатура = Данные.Номенклатура
		|		И Цены.Характеристика = Данные.Характеристика
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции)
		|
		// по указанным трудозатратам
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	ВЫБОР
		|		КОГДА Данные.ДокументВыпуска <> ЗНАЧЕНИЕ(Документ.ВыпускПродукции.ПустаяСсылка)
		|			И Данные.ДокументВыпуска <> НЕОПРЕДЕЛЕНО
		|			ТОГДА Данные.ДокументВыпуска
		|		ИНАЧЕ Данные.ЗаказНаПроизводство
		|	КОНЕЦ КАК ЗаказНаПроизводство,
		|	ВЫБОР
		|		КОГДА Данные.ДокументВыпуска <> ЗНАЧЕНИЕ(Документ.ВыпускПродукции.ПустаяСсылка)
		|			И Данные.ДокументВыпуска <> НЕОПРЕДЕЛЕНО
		|			ТОГДА Данные.КодСтроки
		|		ИНАЧЕ Данные.КодСтрокиПродукция
		|	КОНЕЦ КАК КодСтрокиПродукция,
		|	Данные.Этап,
		|	Данные.Количество,
		|	0 КАК Объем,
		|	0 КАК Вес,
		|	0 КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоУказаннымТрудозатратам КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И Данные.Подразделение = ДД.Подразделение
		|		И Данные.ВидРабот = ДД.ВидРабот
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов)
		|
		// по нормативам
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап,
		|	(ВЫБОР
		|		КОГДА СУММА(Продукция.Количество) <> 0 ТОГДА СУММА(Данные.Количество) * СУММА(Материалы.Количество) / СУММА(Продукция.Количество)
		|		ИНАЧЕ 0 КОНЕЦ) КАК Количество,
		|	0 КАК Объем,
		|	0 КАК Вес,
		|	0 КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Данные_ПоНормативамРасходаМатериала КАК Данные
		|		ПО Данные.Организация = ДД.Организация
		|		И Данные.Подразделение = ДД.Подразделение
		|		И (Данные.ГруппаПродукции = ДД.ГруппаПродукции ИЛИ ДД.ПоВсемГруппамПродукции)
		|		И (Данные.Назначение = ДД.Назначение ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериалыИУслугиСпецификаций КАК Материалы
		|		ПО Материалы.Ссылка = Данные.Спецификация
		|		И Материалы.Номенклатура = Аналитика.Номенклатура
		|		И Материалы.Характеристика = Аналитика.Характеристика
		|		И (Материалы.Характеристика = Аналитика.Характеристика ИЛИ Материалы.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Продукция
		|		ПО Продукция.Ссылка = Данные.Спецификация
		|		И Продукция.Номенклатура = Данные.НоменклатураВыпуска
		|		И Продукция.Характеристика = Данные.ХарактеристикаВыпуска
		|		И (Продукция.Характеристика = Данные.ХарактеристикаВыпуска ИЛИ Продукция.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|ГДЕ
		|	ДД.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Регистратор,
		|	ДД.Подразделение,
		|	ДД.Дата,
		|	ДД.Серия,
		|	Данные.ЗаказНаПроизводство,
		|	Данные.КодСтрокиПродукция,
		|	Данные.Этап
		|";
КонецФункции

Функция ТекстСуммыПоказателей() // вт ОбщиеСуммы
	Возврат
		"ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Серия,
		|	СУММА(ДД.Количество) КАК ОбщееКоличество,
		|	СУММА(ДД.Объем) КАК ОбщийОбъем,
		|	СУММА(ДД.Вес) КАК ОбщийВес,
		|	СУММА(ДД.Стоимость) КАК ОбщаяСтоимость
		|ПОМЕСТИТЬ ОбщиеСуммы
		|ИЗ
		|	БазаРаспределения КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Регистратор,
		|	ДД.Серия
		|";
КонецФункции

// ... выборки данных

Функция ТекстКРаспределению()
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ТекстКРаспределению"" КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Дата КАК Период,
		|	ДД.Регистратор,
		|	ДД.Организация,
		|	ДД.Номенклатура,
		|	ДД.Характеристика,
		|	ДД.Подразделение,
		|	ДД.Серия,
		|	ДД.Назначение КАК Назначение,
		|	Итог.ОбщееКоличество КАК Количество,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Этап,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель,
		|	ДД.БазаРаспределения,
		|	ДД.КРаспределению,
		|	Итог.ОбщийОбъем КАК Объем,
		|	Итог.ОбщийВес КАК Вес,
		|	Итог.ОбщаяСтоимость КАК Стоимость
		|ИЗ
		|	Распределить КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщиеСуммы КАК Итог
		|		ПО Итог.Регистратор = ДД.Регистратор
		|		И Итог.Серия = ДД.Серия
		|ГДЕ
		|	ДД.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|";
КонецФункции

Функция ТекстРаспределениеПоБазе()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстРаспределениеПоБазе"" КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи,
		|	ЛОЖЬ КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Дата КАК Период,
		|	ДД.Регистратор,
		|	НЕОПРЕДЕЛЕНО КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Характеристика,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.Серия КАК Серия,
		|	НЕОПРЕДЕЛЕНО КАК Назначение,
		|	ДД.Количество,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ДД.Этап,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель,
		|	НЕОПРЕДЕЛЕНО КАК БазаРаспределения,
		|	0 КАК КРаспределению,
		|	ДД.Объем,
		|	ДД.Вес,
		|	ДД.Стоимость
		|ИЗ
		|	БазаРаспределения КАК ДД
		|";
КонецФункции

// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловИРаботПоБазе(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Приход = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Потребление;
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Приход,
		"Организация, Номенклатура, Характеристика, Подразделение, Серия,
		|АналитикаУчетаНоменклатуры, БазаРаспределения, Назначение, СтатьяКалькуляции");
	
	Если Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Количество = 0, 0, Расход.Количество / Приход.Количество), 3);
		
	ИначеЕсли Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Объем = 0, 0, Расход.Объем / Приход.Объем), 3);
		
	ИначеЕсли Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов
	 ИЛИ Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Вес = 0, 0, Расход.Вес / Приход.Вес), 3);
		
	ИначеЕсли Приход.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции Тогда
		
		РасчетнаяПартия.Количество = Окр(Приход.КРаспределению * ?(Приход.Стоимость = 0, 0, Расход.Стоимость / Приход.Стоимость), 3);
		
	КонецЕсли;
	
	РасчетнаяПартия.КРаспределению = Приход.КРаспределению;
	
	// корректируем базу расчета в приходе
	Приход.КРаспределению = Приход.КРаспределению - РасчетнаяПартия.Количество;
	Приход.Количество 	  = Приход.Количество - Расход.Количество;
	Приход.Объем 		  = Приход.Объем - Расход.Объем;
	Приход.Вес 			  = Приход.Вес - Расход.Вес;
	Приход.Стоимость 	  = Приход.Стоимость - Расход.Стоимость;
	
	// корректируем базу расчета в расходе
	Расход.Количество = 0;
	Расход.Объем 	  = 0;
	Расход.Вес 		  = 0;
	Расход.Стоимость  = 0;
	
	РасчетнаяПартия.РасчетЗавершен = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа7_РаспределениеМатериаловНЗП

// Инициализация данных

Функция ПоляПотребленийРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	Возврат "Организация, ЗаказНаПроизводство, КодСтрокиПродукция, Спецификация, ДокументВыпуска, НомерГруппыЗатрат";
	
КонецФункции

Функция ОписаниеЦепочекРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	ОписаниеЦепочек = Новый Соответствие;
	ПоляПотреблений	= ПоляПотребленийРаспределенияМатериаловНЗП(ПараметрыРасчета);
	
	// Распределение <- (Партия, Остаток, ПолуфабрикатДавальца)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Распределение, 		 ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Распределение,
		Перечисления.ТипыЗаписейПартий.Партия,				 ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Распределение,
		Перечисления.ТипыЗаписейПартий.Остаток,				 ПоляПотреблений);
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Распределение,
		Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца, ПоляПотреблений);
	
	// Выпуск <- (Распределение)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеПриемника(ОписаниеЦепочек,
		Перечисления.ТипыЗаписейПартий.Выпуск, 		  		  "Организация, ЗаказНаПроизводство, КодСтрокиПродукция, Спецификация, Продукция, ХарактеристикаПродукции");
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеИсточника(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Выпуск,
		Перечисления.ТипыЗаписейПартий.Распределение, 		  "Организация, ЗаказНаПроизводство, КодСтрокиПродукция, Спецификация, Продукция, ХарактеристикаПродукции");
	
	// Дополнение <- (<без источников - для формирования записей по данным запроса>)
	РасчетСебестоимостиПрикладныеАлгоритмы.ДобавитьОписаниеДополнения(ОписаниеЦепочек, Перечисления.ТипыЗаписейПартий.Дополнение);
		
	Возврат ОписаниеЦепочек;
	
КонецФункции

Функция ОписаниеДвиженийРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	КолонкиТаблицыРаспределения = ТаблицаДляРаспределенияМатериаловНЗП(ПараметрыРасчета).Колонки;
	
	ОписаниеДвижений = РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеДвижений();
	ОписаниеДвижений.Вставить("Контекст", "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями");
	ОписаниеДвижений.Вставить("ИмяРегистра", Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства.Имя);
	ОписаниеДвижений.Вставить("ПоляРасчета", РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения));
	ОписаниеДвижений.Вставить("КлючиСравнения",	ПоляПотребленийРаспределенияМатериаловНЗП(ПараметрыРасчета));
	ОписаниеДвижений.Вставить("Показатели", "Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");
	ОписаниеДвижений.Вставить("БазисПрихода", "Знаменатель");
	ОписаниеДвижений.Вставить("БазисРасхода", "Знаменатель");
	ОписаниеДвижений.Вставить("КлючРасхода", "ДокументИсточник");
	ОписаниеДвижений.Вставить("ПолеПорядка", "Период"); // ПолеПорядка
	
	ОписаниеДвижений.Вставить("ПоляСуммирования", "Количество, Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");
	ОписаниеДвижений.Вставить("ПоляГруппировки",  
		РасчетСебестоимостиПрикладныеАлгоритмы.ПереченьПолей(КолонкиТаблицыРаспределения, ОписаниеДвижений.ПоляСуммирования));
	
	Возврат ОписаниеДвижений;
	
КонецФункции

Функция ОписаниеНезаписываемыхРаспределенийМатериаловНЗП(ПараметрыРасчета)
	
	НезаписываемыеТипыЗаписей = Новый Соответствие;
	НезаписываемыеТипыЗаписей.Вставить(Перечисления.ТипыЗаписейПартий.Остаток, Истина);
	
	НезаписываемыеРазделы = Новый Соответствие;
	НезаписываемыеРазделы.Вставить(Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка(), Истина);
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ОписаниеНезаписываемыхДанных(Ложь, НезаписываемыеТипыЗаписей, НезаписываемыеРазделы);
	
КонецФункции

Функция ТаблицаДляРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	Возврат РасчетСебестоимостиПрикладныеАлгоритмы.ТаблицаДляРаспределенияПартий(ПараметрыРасчета,
		ТекстОписаниеДанныхДляРаспределенияМатериаловНЗП());
	
КонецФункции

// Выборка данных

Процедура ПолучитьДанныеДляРаспределенияМатериаловНЗП(ПараметрыРасчета)
	
	ПараметрыНумерации = РасчетСебестоимостиПрикладныеАлгоритмы.СформироватьПараметрыНумерацииСтрокВременнойТаблицы(
		"Организация", // разделитель
		"Знаменатель, КоличествоПродукции, Количество, Стоимость,
			|СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница", // ресурсы
		"Организация, ЗаказНаПроизводство, Приоритет, Период, Регистратор"); // порядок
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьДанныеЭтапаРасчета(
		ПараметрыРасчета,
		ТекстЗапросаДляРаспределенияМатериаловНЗП(ПараметрыРасчета),
		ПараметрыНумерации);
	
КонецПроцедуры

// Тексты запросов

// ... общий

Функция ТекстЗапросаДляРаспределенияМатериаловНЗП(ПараметрыРасчета = Неопределено) Экспорт
	
	ТекстЗапроса = ""
		// подготовка временных таблиц
		+ ТекстПартииНЗПИнициализация() // вт ПартииНЗПНачальныйОстаток, ПрошлыеПоступленияНЗП, ЗаказыКРаспределению, ДолиСтоимости, ВыпускиБезЗаказов
		+ ОбщегоНазначенияУТ.РазделительЗапросовВПакете()
		// выборка данных
		+ ТекстОписаниеДанныхДляРаспределенияМатериаловНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстОстаткиПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПервичныеПартииНЗП()
		+ "";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// ... описания данных

Функция ТекстОписаниеДанныхДляРаспределенияМатериаловНЗП()
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ВЫРАЗИТЬ("""" КАК СТРОКА(80)) 											КАК ЗапросИсточник,
		|	0 																		КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка) 					КАК ТипЗаписи,
		|	ЛОЖЬ 																	КАК РасчетЗавершен,
		|
		|	ДД.ВидДвижения 															КАК ВидДвижения,
		|	ДД.Период 																КАК Период,
		|	ДД.Регистратор 															КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) 	КАК РазделУчета,
		|	ДД.Организация 															КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 					КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры 											КАК АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления 													КАК ДокументПоступления,
		|	ДД.ВидЗапасов 															КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство 													КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция 													КАК КодСтрокиПродукция,
		|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка) 				КАК Спецификация,
		|	ДД.Этап																	КАК Этап,
		|	ДД.СтатьяКалькуляции 													КАК СтатьяКалькуляции,
		|	ДД.АналитикаУчетаПартий 												КАК АналитикаУчетаПартий,
		|	ДД.АналитикаУчетаПродукции 												КАК АналитикаУчетаПродукции,
		|	0 																		КАК Знаменатель,
		|	0 																		КАК КоличествоПродукции,
		|	0 																		КАК Количество,
		|	0 																		КАК Стоимость,
		|	0 																		КАК СтоимостьБезНДС,
		|	0 																		КАК СтоимостьРегл,
		|	0 																		КАК НДСРегл,
		|	0 																		КАК ПостояннаяРазница,
		|	0 																		КАК ВременнаяРазница,
		|	ЛОЖЬ 																	КАК Первичное,
		|	ЛОЖЬ 																	КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) 				КАК ХозяйственнаяОперация,
		|	ДД.КорРазделУчета 														КАК КорРазделУчета,
		|	ДД.АналитикаУчетаНоменклатуры 											КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов 														КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) 											КАК ДатаРегистратора,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) 				КАК НалогообложениеНДС,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) 							КАК Назначение,
		|	ДД.ДокументИсточник 													КАК ДокументИсточник,
		|	ДД.ДокументВыпуска 														КАК ДокументВыпуска,
		|	0 																		КАК НомерГруппыЗатрат,
		|	ДД.Продукция 															КАК Продукция,
		|	ДД.ХарактеристикаПродукции 												КАК ХарактеристикаПродукции
		|//ВоВременнуюТаблицу
		|ИЗ
		|	РегистрНакопления.ПартииНезавершенногоПроизводства КАК ДД
		|";
КонецФункции

// ... формирования временные таблиц

Функция ТекстПартииНЗПИнициализация() // вт ПартииНЗПНачальныйОстаток, ПрошлыеПоступленияНЗП, ЗаказыКРаспределению, ДолиСтоимости
	Возврат
		"ВЫБРАТЬ
		|	Т.Организация,
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.ВидЗапасов,
		|	Т.ЗаказНаПроизводство,
		|	Т.КодСтрокиПродукция,
		|	Т.ДокументПоступления,
		|	Т.Этап,
		|	Т.СтатьяКалькуляции,
		|	Т.АналитикаУчетаПартий,
		|	Т.КоличествоОстаток,
		|	Т.СтоимостьОстаток,
		|	Т.СтоимостьБезНДСОстаток,
		|	Т.СтоимостьРеглОстаток,
		|	Т.НДСРеглОстаток,
		|	Т.ПостояннаяРазницаОстаток,
		|	Т.ВременнаяРазницаОстаток
		|ПОМЕСТИТЬ ПартииНЗПНачальныйОстаток
		|ИЗ
		|	РегистрНакопления.ПартииНезавершенногоПроизводства.Остатки(&НачалоПериода, Организация В (&МассивОрганизаций)) КАК Т
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ДокументПоступления КАК Ссылка,
		|	МАКСИМУМ(Периодика.Период) КАК Период
		|ПОМЕСТИТЬ ПрошлыеПоступленияНЗП
		|ИЗ
		|	ПартииНЗПНачальныйОстаток КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииНезавершенногоПроизводства КАК Периодика
		|		ПО Периодика.Период < &НачалоПериода
		|		И Периодика.ДокументПоступления = ДД.ДокументПоступления
		|ГДЕ
		|	ДД.ДокументПоступления <> НЕОПРЕДЕЛЕНО
		|СГРУППИРОВАТЬ ПО
		|	ДД.ДокументПоступления
		|;
		|////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Строки.ЗаказПереработчику КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	Строки.НомерГруппыЗатрат,
		|	СУММА(ВЫБОР Строки.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ Строки.ДоляСтоимости КОНЕЦ) КАК ДоляСтоимости
		|ПОМЕСТИТЬ ДолиСтоимости
		|ИЗ
		|	Документ.ОтчетПереработчика КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Строки.ЗаказПереработчику,
		|	Строки.НомерГруппыЗатрат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Строки.ДокументПоступления КАК ЗаказНаПроизводство,
		|	0 КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК Спецификация,
		|	Строки.НомерГруппыЗатрат,
		|	СУММА(ВЫБОР Строки.ДоляСтоимости КОГДА 0 ТОГДА 1
		|		ИНАЧЕ Строки.ДоляСтоимости КОНЕЦ) КАК ДоляСтоимости
		|ИЗ
		|	Документ.ОтчетПереработчика КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|СГРУППИРОВАТЬ ПО
		|	Строки.ДокументПоступления,
		|	Строки.НомерГруппыЗатрат
		|";
КонецФункции

// ... выборки данных

Функция ТекстОстаткиПартийНЗП()
	// Передачи материалов в производство и в переработку, возвраты материалов из производства и от переработчика.
	Возврат
		"ВЫБРАТЬ
		|	""ТекстОстаткиПартийНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Остаток) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ЕСТЬNULL(Периодика.Период, &НачалоПериода) КАК Период,
		|	ДД.ДокументПоступления КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
		|	ДД.Организация,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ДокументПоступления,
		|	ДД.ВидЗапасов,
		|	ДД.ВидЗапасов.ГруппаПродукции КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	ЕСТЬNULL(Этапы.Владелец, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	ЕСТЬNULL(Доли.ДоляСтоимости, 1) КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	ДД.КоличествоОстаток КАК Количество,
		|	ДД.СтоимостьОстаток КАК Стоимость,
		|	ДД.СтоимостьБезНДСОстаток КАК СтоимостьБезНДС,
		|	ДД.СтоимостьРеглОстаток КАК СтоимостьРегл,
		|	ДД.НДСРеглОстаток КАК НДСРегл,
		|	ДД.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	ДД.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	ДД.АналитикаУчетаПартий.НалогообложениеНДС КАК НалогообложениеПартии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
		|	Аналитика.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции
		|ИЗ
		|	ПартииНЗПНачальныйОстаток КАК ДД
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПрошлыеПоступленияНЗП КАК Периодика
		|		ПО Периодика.Ссылка = ДД.ДокументПоступления
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимости КАК Доли
		|		ПО Доли.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Доли.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Доли.Спецификация = Этапы.Владелец
		|";
КонецФункции

Функция ТекстПервичныеПартииНЗП()
	Возврат
		"ВЫБРАТЬ
		|	""ТекстПервичныеПартииНЗП"" КАК ЗапросИсточник,
		|	10 КАК Приоритет,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия) КАК ТипЗаписи,
		|	ИСТИНА КАК РасчетЗавершен,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК Период,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	ДД.Организация,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК ДокументПоступления,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	Этапы.Владелец КАК Спецификация,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПродукции,
		|	МАКСИМУМ(ЕСТЬNULL(Доли.ДоляСтоимости, 1)) КАК Знаменатель,
		|	0 КАК КоличествоПродукции,
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК НДСРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	ИСТИНА КАК Первичное,
		|	ЛОЖЬ КАК СохранятьРегл,
		|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаРегистратора,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеПартии,
		|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
		|	ДД.Назначение,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВыпуска,
		|	0 КАК НомерГруппыЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Продукция,
		|	НЕОПРЕДЕЛЕНО КАК ХарактеристикаПродукции
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|		ПО Этапы.Ссылка = ДД.Этап
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимости КАК Доли
		|		ПО Доли.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Доли.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|		И Доли.Спецификация = Этапы.Владелец
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И ДД.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	И НЕ (ЕСТЬNULL(Аналитика.Назначение.ТипНазначения, НЕОПРЕДЕЛЕНО) В (ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое21),
		|														ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22))
		|		И ДД.Количество < 0
		|		И (ДД.Регистратор Ссылка Документ.СписаниеЗатратНаВыпуск
		|			)
		|		)
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция,
		|	Этапы.Владелец,
		|	ДД.Этап,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Назначение
		|";
КонецФункции


// Заполнение расчетной партии

Процедура ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловНЗП(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход)
	
	ЗаполнитьЗначенияСвойств(РасчетнаяПартия, Расход);
	
	Если Приход.Знаменатель = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СуммовыеРесурсы = Новый Структура("Стоимость, СтоимостьБезНДС, СтоимостьРегл, НДСРегл, ПостояннаяРазница, ВременнаяРазница");

	// расчетная партия заполняется на наибольшее общее вычитаемое
	ЗнаменательСписания = Мин(Расход.Знаменатель, Приход.Знаменатель);
	
	// заполняем показатели расчетной партии
	РасчетнаяПартия.Количество = Окр(ЗнаменательСписания * Приход.Количество / Приход.Знаменатель, 3);
	
	Для Каждого ИмяРесурса Из СуммовыеРесурсы Цикл
		Если РасчетнаяПартия.Количество = Приход.Количество Тогда
			РасчетнаяПартия[ИмяРесурса.Ключ] = Приход[ИмяРесурса.Ключ];
		Иначе
			РасчетнаяПартия[ИмяРесурса.Ключ] = Окр(ЗнаменательСписания * Приход[ИмяРесурса.Ключ] / Приход.Знаменатель, 2);
		КонецЕсли;
	КонецЦикла;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ЗатратыБезЗаказа
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.Знаменатель = Расход.КоличествоПродукции;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 И (Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Остаток
	  ИЛИ Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца) Тогда
		РасчетнаяПартия.ТипЗаписи = Приход.ТипЗаписи;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Выпуск
	 И Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ТипЗаписи = Приход.ТипЗаписи;
	КонецЕсли;
	
	// корректируем базу расчета в приходе
	Для Каждого ИмяРесурса Из СуммовыеРесурсы Цикл
		Приход[ИмяРесурса.Ключ] = Приход[ИмяРесурса.Ключ] - РасчетнаяПартия[ИмяРесурса.Ключ];
	КонецЦикла;
	
	Приход.Количество = Приход.Количество - РасчетнаяПартия.Количество;
	Приход.Знаменатель = Приход.Знаменатель - ЗнаменательСписания;

	// Заполняем партионную идентификацию в расчетной партии
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 И Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.РазделУчета = Перечисления.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка();	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ЗаказНаПроизводство)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ЗаказНаПроизводство = Приход.ЗаказНаПроизводство;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.КодСтрокиПродукция)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.КодСтрокиПродукция = Приход.КодСтрокиПродукция;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Этап)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.Этап = Приход.Этап;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Спецификация)
		ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.Спецификация = Приход.Спецификация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ДокументПоступления)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ДокументПоступления = Приход.ДокументПоступления;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Распределение
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Выпуск
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ЗатратыБезЗаказа
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ВыпускБезЗаказа
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Переработка
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ОтчетДавальцу
	 ИЛИ Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПродукцияДавальца Тогда
		РасчетнаяПартия.ДокументИсточник = Приход.ДокументИсточник;
	КонецЕсли;
	
	Если Расход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.Партия
	 И Приход.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ЗатратыБезЗаказа Тогда
		РасчетнаяПартия.ДокументВыпуска = Приход.ДокументВыпуска;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ДокументИсточник) Тогда
		РасчетнаяПартия.ДокументИсточник = Приход.Регистратор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.ВидЗапасов)
	 ИЛИ РасчетнаяПартия.ТипЗаписи = Перечисления.ТипыЗаписейПартий.ПолуфабрикатДавальца Тогда
		РасчетнаяПартия.ВидЗапасов = Приход.ВидЗапасов;
	КонецЕсли;
	
	РасчетнаяПартия.ГруппаПродукции = Приход.ГруппаПродукции;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Период)
	 И РасчетнаяПартия.ТипЗаписи <> Перечисления.ТипыЗаписейПартий.ПродукцияДавальца Тогда
		РасчетнаяПартия.Период = Приход.Период;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Регистратор)
	 И РасчетнаяПартия.ТипЗаписи <> Перечисления.ТипыЗаписейПартий.ПродукцияДавальца Тогда
		РасчетнаяПартия.Регистратор = Приход.Регистратор;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяПартия.Назначение) Тогда
		РасчетнаяПартия.Назначение = Приход.Назначение;
	КонецЕсли;
	
	Если РасчетнаяПартия.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеНДС;
	Иначе
		РасчетнаяПартия.НалогообложениеПартии = Приход.НалогообложениеПартии;
	КонецЕсли;
	
	РасчетнаяПартия.АналитикаУчетаПартий  	   = Приход.АналитикаУчетаПартий;
	РасчетнаяПартия.АналитикаУчетаНоменклатуры = Приход.АналитикаУчетаНоменклатуры;
	РасчетнаяПартия.Подразделение 			   = Приход.Подразделение;
	РасчетнаяПартия.СтатьяКалькуляции 		   = Приход.СтатьяКалькуляции;
	
	РасчетнаяПартия.РасчетЗавершен 			   = Приход.РасчетЗавершен;
	
	Если РасчетнаяПартия.Количество = 0
	 И РасчетнаяПартия.Стоимость = 0
	 И РасчетнаяПартия.СтоимостьБезНДС = 0
	 И РасчетнаяПартия.СтоимостьРегл = 0
	 И РасчетнаяПартия.НДСРегл = 0
	 И РасчетнаяПартия.ПостояннаяРазница = 0
	 И РасчетнаяПартия.ВременнаяРазница = 0 Тогда
		
		РасчетнаяПартия.Знаменатель = 0;
		РасчетнаяПартия.РасчетЗавершен = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа15_РаспределениеДолейПроизводственныхРасходов

// Дополняет массив запросов локализованными фактическими материальными затратами.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросФактическимиМатериальнымЗатратам(ТекстыЗапросов) Экспорт
	
	// материалы версии 2.1 собственное производство без заказа
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                      КАК Организация,
		|	Аналитика.МестоХранения             КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ОтборМатериаловСводно.Материал ЕСТЬ NULL И НЕ &РасшифровкаРаспределения
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ
		|			Аналитика.Номенклатура
		|	КОНЕЦ                               КАК Материал,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ                               КАК ГруппаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.Регистратор                      КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                        КАК КодСтрокиПродукция,
		|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	СУММА(
		|	(ВЫБОР
		|		КОГДА ДД.Стоимость + ДД.ДопРасходы <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.Стоимость + ДД.ДопРасходы
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0))
		|	КОНЕЦ))                             КАК Стоимость,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0))
		|		КОНЕЦ))                                              КАК СтоимостьБезНДС,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0))
		|		КОНЕЦ))                                              КАК СтоимостьРегл,
		|	СУММА((ВЫБОР
		|		КОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр <> 0 ИЛИ &РасчетСебестоимостиВыполнен
		|			ТОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр
		|		ИНАЧЕ ДД.Количество *
		|			(ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0))
		|		КОНЕЦ))                                              КАК СтоимостьУпр,
		|	СУММА(ДД.Количество)                КАК Количество,
		|	СУММА(&Объем * ДД.Количество)       КАК Объем,
		|	СУММА(&Вес * ДД.Количество)         КАК Вес
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО (Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры)
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = Аналитика.Номенклатура)
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.Партия = ДД.Партия
		|			И Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|			И Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета
		|			И Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС
		|			И Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|			И Цены.ВидЗапасов = ДД.ВидЗапасов
		|			И Цены.РазделУчета = ДД.РазделУчета
		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Продукция
		|		ПО ДД.Регистратор = Продукция.Ссылка
		|			И ДД.КодСтроки = Продукция.КодСтроки
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтборМатериаловСводно КАК ОтборМатериаловСводно
		|		ПО Аналитика.Номенклатура = ОтборМатериаловСводно.Материал
		|ГДЕ
		|	(&РасчетСебестоимостиВыполнен ИЛИ НЕ &РасшифровкаРаспределения)
		|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Продукция.Ссылка.ВыпускПоРаспоряжениям
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	Аналитика.МестоХранения,
		|	ВЫБОР
		|		КОГДА ОтборМатериаловСводно.Материал ЕСТЬ NULL И НЕ &РасшифровкаРаспределения
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ
		|			Аналитика.Номенклатура
		|	КОНЕЦ,
		|	ДД.КорАналитикаФинансовогоУчета,
		|	ДД.Регистратор,
		|	ДД.КодСтроки,
		|	Продукция.Назначение.НаправлениеДеятельности,
		|	Продукция.Номенклатура.ГруппаАналитическогоУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованными предварительными материальными затратами.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросПредварительнымиМатериальнымЗатратам(ТекстыЗапросов) Экспорт
	
	// Производство 2.1, по заказам, без заказов, распределения.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Организация                      КАК Организация,
		|	ДД.Подразделение                    КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ОтборМатериаловСводно.Материал ЕСТЬ NULL И НЕ &РасшифровкаРаспределения
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ
		|			ДД.Номенклатура
		|	КОНЕЦ                              КАК Материал,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции ТОГДА
		|			ВЫБОР
		|				КОГДА НЕ ПродукцияВыпуска.Номенклатура.ГруппаАналитическогоУчета ЕСТЬ NULL
		|					ТОГДА ПродукцияВыпуска.Номенклатура.ГруппаАналитическогоУчета
		|				ИНАЧЕ
		|					ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|			КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ                               КАК ГруппаПродукции,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка) КАК ПартияПроизводства,
		|	ДД.ЗаказНаПроизводство              КАК ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция               КАК КодСтрокиПродукция,
		|	ВЫБОР
		|		КОГДА НЕ ПродукцияВыпуска.Назначение.НаправлениеДеятельности ЕСТЬ NULL
		|			ТОГДА ПродукцияВыпуска.Назначение.НаправлениеДеятельности
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|	КОНЕЦ                               КАК НаправлениеДеятельности,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.Стоимость, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0))) КАК Стоимость,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0) + ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0))) КАК СтоимостьБезНДС,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0) + ЕСТЬNULL(Цены.ДопРасходыРегл, 0))) КАК СтоимостьРегл,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьУпр, 0) + ЕСТЬNULL(Цены.ДопРасходыУпр, 0))) КАК СтоимостьУпр,
		|	СУММА(ДД.Количество)                КАК Количество,
		|	СУММА(&Объем * ДД.Количество)       КАК Объем,
		|	СУММА(&Вес * ДД.Количество)         КАК Вес
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО (СН.Ссылка = ДД.Номенклатура)
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСредняяСтоимость КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|			И Цены.Номенклатура = ДД.Номенклатура
		|			И Цены.Характеристика = ДД.Характеристика
		|			И Цены.Склад = ДД.Подразделение
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ПродукцияВыпуска
		|		ПО ДД.ЗаказНаПроизводство = ПродукцияВыпуска.Ссылка
		|			И ДД.КодСтрокиПродукция = ПродукцияВыпуска.КодСтроки
		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтборМатериаловСводно КАК ОтборМатериаловСводно
		|		ПО ДД.Номенклатура = ОтборМатериаловСводно.Материал
		|ГДЕ
		|	(&РасшифровкаРаспределения И НЕ &РасчетСебестоимостиВыполнен)
		|	И ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В(&МассивОрганизаций)
		|	И (ДД.Регистратор Ссылка Документ.РаспределениеПроизводственныхЗатрат
		|		ИЛИ ДД.Регистратор Ссылка Документ.СписаниеЗатратНаВыпуск
		|		)
		|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И ДД.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ВЫБОР
		|		КОГДА ОтборМатериаловСводно.Материал ЕСТЬ NULL И НЕ &РасшифровкаРаспределения
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ
		|			ДД.Номенклатура
		|	КОНЕЦ,
		|	ПродукцияВыпуска.Номенклатура.ГруппаАналитическогоУчета,
		|	ПродукцияВыпуска.Назначение.НаправлениеДеятельности,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.КодСтрокиПродукция";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованными предварительными материальными затратами.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросБазамиРаспределенияДолейПоЭтапам(ТекстыЗапросов) Экспорт
	
	// Производство 2.1, по заказам, без заказов, распределения.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Регистратор,
		|	ДД.Дата,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка),
		|	Данные.СтатьяКалькуляции,
		|	Продукция.Ссылка.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка),
		|	Данные.ДокументВыпуска,
		|	Данные.КодСтроки,
		|	ЕСТЬNULL(Продукция.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|			И Продукция.Номенклатура.ГруппаАналитическогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|				ТОГДА Продукция.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПоПродукции),
		|	НЕОПРЕДЕЛЕНО,
		|	Данные.ДоляСтоимости,
		|	Данные.ДоляСтоимости,
		|	Данные.ДоляСтоимости,
		|	Данные.ДоляСтоимости
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ВыпускиБезРаспоряжения КАК Данные
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК ДД
		|		ПО ДД.Регистратор = Данные.Ссылка
		|		И ДД.ПартииУказаныВручную
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК Продукция
		|		ПО Данные.ДокументВыпуска = Продукция.Ссылка
		|		И Данные.КодСтроки = Продукция.КодСтроки";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа16_РаспределениеДолейПроизводственныхРасходов

// Дополняет массив запросов локализованными материальными затратами в незавершенном производстве.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросМатериальнымиЗатратамиНЗП(ТекстыЗапросов) Экспорт
	
	// материалы версии 2.1 собственное производство без заказа
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения              КАК Подразделение,
		|	ДД.Регистратор                                           КАК РегистраторРасхода,
		|	ДД.Период                                                КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)     КАК ПартияПроизводства,
		|	ДД.Регистратор                                           КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                             КАК КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаНоменклатуры                         КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов										 КАК КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий                               КАК КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета                                        КАК КорРазделУчета,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.Стоимость, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьДопРасходы, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК Стоимость,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьБезНДС, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьДопРасходыБезНДС, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК СтоимостьБезНДС,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьРегл, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыРегл, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансоваяРегл, 0)))      КАК СтоимостьРегл,
		|	СУММА(ДД.Количество *
		|		(ЕСТЬNULL(Цены.СтоимостьУпр, 0)
		|		+ ЕСТЬNULL(Цены.ДопРасходыУпр, 0)
		|		+ ЕСТЬNULL(Цены.СтоимостьЗабалансовая, 0)))          КАК СтоимостьУпр
		|ИЗ
		|	ВТКэшРасчетныеОборотыСебестоимостьТоваров КАК ДД
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтоимостьПартийТоваров КАК Цены
		|		ПО Цены.Организация = ДД.Организация
		|			И Цены.Партия = ДД.Партия
		|			И Цены.АналитикаУчетаПартий = ДД.АналитикаУчетаПартий
		|			И Цены.АналитикаФинансовогоУчета = ДД.АналитикаФинансовогоУчета
		|			И Цены.ВидДеятельностиНДС = ДД.ВидДеятельностиНДС
		|			И Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры
		|			И Цены.ВидЗапасов = ДД.ВидЗапасов
		|			И Цены.РазделУчета = ДД.РазделУчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК ВыпускПродукции
		|		ПО ДД.Регистратор = ВыпускПродукции.Ссылка
		|
		|ГДЕ
		|	НЕ ВыпускПродукции.ВыпускПоРаспоряжениям
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.АналитикаУчетаНоменклатуры.МестоХранения,
		|	ДД.Регистратор,
		|	ДД.Период,
		|	ДД.КодСтроки,
		|	ДД.КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованными трудозатратами в незавершенном производстве.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросТрудозатратамиНЗП(ТекстыЗапросов) Экспорт
	
	// Трудозатраты версии 2.1 собственное производство без заказа.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДД.Подразделение                                        КАК Подразделение,
		|	ДД.Регистратор                                          КАК РегистраторРасхода,
		|	ДД.Период                                               КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)    КАК ПартияПроизводства,
		|	ДД.ДокументВыпуска                                      КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                            КАК КодСтрокиПродукция,
		|	ДД.КорАналитикаУчетаПродукции                           КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорВидЗапасовПродукции                               КАК КорВидЗапасов,
		|	ДД.КорАналитикаУчетаПартий                              КАК КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета                                       КАК КорРазделУчета,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК Стоимость,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьБезНДС,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА СУММА(ДД.СтоимостьРегл) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.СтоимостьРегл)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьРегл,
		|	ВЫРАЗИТЬ(ВЫБОР
		|		КОГДА НЕ &УправленческийУчетОрганизаций
		|			ТОГДА 0
		|		КОГДА СУММА(ДД.Стоимость) = 0
		|			ТОГДА СУММА(ДД.НормативнаяСтоимость)
		|		ИНАЧЕ СУММА(ДД.Стоимость)
		|	КОНЕЦ КАК ЧИСЛО(23,10))                                 КАК СтоимостьУпр
		|ИЗ
		|	ВТКэшРасчетныеОборотыТрудозатратыНезавершенногоПроизводства КАК ДД
		|ГДЕ
		|	ДД.ДокументВыпуска <> НЕОПРЕДЕЛЕНО
		|	И НЕ ДД.СлужебноеВидДвиженияПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Подразделение,
		|	ДД.Регистратор,
		|	ДД.Период,
		|	ДД.ДокументВыпуска,
		|	ДД.КодСтроки,
		|	ДД.КорАналитикаУчетаПродукции,
		|	ДД.КорВидЗапасовПродукции,
		|	ДД.КорАналитикаУчетаПартий,
		|	ДД.КорРазделУчета";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Дополняет массив запросов локализованной выпущенной продукцией.
// Параметры:
//	ТекстыЗапросов - Массив - содержит тексты запросов.
//
Процедура ДополнитьЗапросВыпущеннойПродукцией(ТекстыЗапросов) Экспорт
	
	// Производство 2.1 без заказов.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка                                        КАК РегистраторРасхода,
		|	Реквизиты.Дата                                          КАК ДатаПроизводства,
		|	ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)    КАК ПартияПроизводства,
		|	Реквизиты.Подразделение                                 КАК Подразделение,
		|	Реквизиты.Ссылка                                        КАК ЗаказНаПроизводство,
		|	ДД.КодСтроки                                            КАК КодСтрокиПродукция,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ЕСТЬNULL(АналитикаВПодразделении.КлючАналитики, ДД.АналитикаУчетаНоменклатуры)
		|		ИНАЧЕ ЕСТЬNULL(АналитикаВПодразделенииБезНазначения.КлючАналитики, АналитикаДокументаБезНазначения.КлючАналитики)
		|	КОНЕЦ                                                   КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.ВидЗапасов                                           КАК КорВидЗапасов,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Организация В (&ОрганизацииСФИФОСкользящая)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|		КОГДА ДД.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ТОГДА АналитикиУчетаПартийРабот.КлючАналитики
		|		ИНАЧЕ АналитикиУчетаПартийТоваров.КлючАналитики
		|	КОНЕЦ                                                   КАК КорАналитикаУчетаПартий,
		|	ВЫБОР
		|		КОГДА ДД.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветХранение)
		|		КОГДА Реквизиты.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ                                                   КАК КорРазделУчета,
		|	ВЫРАЗИТЬ(СУММА(
		|		ВЫБОР
		|			КОГДА СписаниеЗатрат.Ссылка ЕСТЬ NULL
		|				ТОГДА ДД.Количество
		|			КОГДА СписаниеЗатрат.ДоляСтоимости = 0
		|				ТОГДА СписаниеЗатрат.Количество
		|			ИНАЧЕ СписаниеЗатрат.ДоляСтоимости
		|		КОНЕЦ) КАК ЧИСЛО(23,10))                            КАК Стоимость,
		|	ВЫРАЗИТЬ(СУММА(
		|		ВЫБОР
		|			КОГДА СписаниеЗатрат.Ссылка ЕСТЬ NULL
		|				ТОГДА ДД.Количество
		|			КОГДА СписаниеЗатрат.ДоляСтоимости = 0
		|				ТОГДА СписаниеЗатрат.Количество
		|			ИНАЧЕ СписаниеЗатрат.ДоляСтоимости
		|		КОНЕЦ) КАК ЧИСЛО(23,10))                            КАК ПолнаяСтоимость,
		|	0                                                       КАК ПлановоеКоличествоПродукции
		|ИЗ
		|	Документ.ВыпускПродукции.Товары КАК ДД
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК СписаниеЗатрат
		|		ПО ДД.Ссылка = СписаниеЗатрат.Распоряжение
		|		И ДД.КодСтроки = СписаниеЗатрат.КодСтроки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК Реквизиты
		|		ПО ДД.Ссылка = Реквизиты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокументаБезНазначения
		|	ПО
		|		АналитикаДокументаБезНазначения.Номенклатура = ДД.АналитикаУчетаНоменклатуры.Номенклатура
		|		И АналитикаДокументаБезНазначения.Характеристика = ДД.АналитикаУчетаНоменклатуры.Характеристика
		|		И АналитикаДокументаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаДокументаБезНазначения.СтатьяКалькуляции = ДД.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
		|		И АналитикаДокументаБезНазначения.МестоХранения = ДД.АналитикаУчетаНоменклатуры.МестоХранения
		|		И АналитикаДокументаБезНазначения.Серия = ДД.АналитикаУчетаНоменклатуры.Серия
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаВПодразделении
		|	ПО
		|		ДД.Номенклатура = АналитикаВПодразделении.Номенклатура
		|		И ДД.Характеристика = АналитикаВПодразделении.Характеристика
		|		И ДД.Назначение = АналитикаВПодразделении.Назначение
		|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаВПодразделении.СтатьяКалькуляции
		|		И (Реквизиты.Подразделение = АналитикаВПодразделении.МестоХранения)
		|		И ВЫБОР
		|			КОГДА ДД.СтатусУказанияСерий = 14
		|			ТОГДА ДД.Серия = АналитикаВПодразделении.Серия
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаВПодразделении.Серия
		|		КОНЕЦ
		|		И ДД.СписатьНаРасходы
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаВПодразделенииБезНазначения
		|	ПО
		|		АналитикаВПодразделенииБезНазначения.Номенклатура = АналитикаВПодразделении.Номенклатура
		|		И АналитикаВПодразделенииБезНазначения.Характеристика = АналитикаВПодразделении.Характеристика
		|		И АналитикаВПодразделенииБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И АналитикаВПодразделенииБезНазначения.СтатьяКалькуляции = АналитикаВПодразделении.СтатьяКалькуляции
		|		И АналитикаВПодразделенииБезНазначения.МестоХранения = АналитикаВПодразделении.МестоХранения
		|		И АналитикаВПодразделенииБезНазначения.Серия = АналитикаВПодразделении.Серия
		|		И ДД.СписатьНаРасходы
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийТоваров
		|		ПО АналитикиУчетаПартийТоваров.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
		|		И АналитикиУчетаПартийТоваров.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		И АналитикиУчетаПартийТоваров.Контрагент			= НЕОПРЕДЕЛЕНО
		|		И АналитикиУчетаПартийТоваров.СтавкаНДС				= ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
		|		И АналитикиУчетаПартийТоваров.НалогообложениеНДС 	= ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|		И АналитикиУчетаПартийТоваров.ВидЦенности			= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
		|		И АналитикиУчетаПартийТоваров.КодСтроки				= 0
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикиУчетаПартийРабот
		|		ПО АналитикиУчетаПартийРабот.ГруппаФинансовогоУчета	= ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
		|		И АналитикиУчетаПартийРабот.Поставщик				= ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		И АналитикиУчетаПартийРабот.Контрагент				= НЕОПРЕДЕЛЕНО
		|		И АналитикиУчетаПартийРабот.СтавкаНДС				= ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
		|		И АналитикиУчетаПартийРабот.НалогообложениеНДС		= ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|		И АналитикиУчетаПартийРабот.ВидЦенности				= ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
		|		И АналитикиУчетаПартийРабот.КодСтроки				= 0
		|
		|ГДЕ
		|	Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Реквизиты.ВыпускПоРаспоряжениям
		|	И ДД.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
		|	И Реквизиты.Проведен
		|	И Реквизиты.Организация В(&МассивОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	Реквизиты.Ссылка,
		|	Реквизиты.Подразделение,
		|	ДД.КодСтроки,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ЕСТЬNULL(АналитикаВПодразделении.КлючАналитики, ДД.АналитикаУчетаНоменклатуры)
		|		ИНАЧЕ ЕСТЬNULL(АналитикаВПодразделенииБезНазначения.КлючАналитики, АналитикаДокументаБезНазначения.КлючАналитики)
		|	КОНЕЦ,
		|	ДД.ВидЗапасов,
		|	ДД.ПередатьДавальцу,
		|	ДД.ВидЗапасов.ТипЗапасов,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.Организация В (&ОрганизацииСФИФОСкользящая)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
		|		КОГДА ДД.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ТОГДА АналитикиУчетаПартийРабот.КлючАналитики
		|		ИНАЧЕ АналитикиУчетаПартийТоваров.КлючАналитики
		|	КОНЕЦ";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ДополнитьЗапросСлужебнымиТаблицами() Экспорт
	

КонецФункции

Процедура ДополнитьЗапросОстаткамиБазРаспределения(ТекстыЗапросов) Экспорт
	
	
КонецПроцедуры

Процедура ДополнитьЗапросРасходовНЗПЛокализованнымиДанными(ТекстыЗапросов) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЭтапа_ЗаполнениеПартийВРегистреСебестоимостьТоваров

Функция ТекстЗапросаДляПартийТоваров() Экспорт
	
	ТекстЗапроса = ""
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеПриходыПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеРасходыПартийНЗП()
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеСписанияЗатратНаВыпуск() // использует Выпуски
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстТекущиеРасходыМатериаловВПроизводстве()
		+ "";
	Возврат ТекстЗапроса;	
	
КонецФункции


Функция ТекстТекущиеПриходыПартийНЗП()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеПриходыПартийНЗП"" 	      				  КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПеремещениеАвто)  КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК Сторно,
		|	95 										  				  КАК Приоритет,
		|	ДД.Количество							  				  КАК Знаменатель,
		|	ЛОЖЬ 													  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ КлючиАналитики.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	ДД.ЗаказНаПроизводство КАК Партия,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	ДД.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
		|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	НЕОПРЕДЕЛЕНО КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки
		|ИЗ
		|	ВТКэшРасчетныеОборотыПартииНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО КлючиАналитики.Номенклатура = Аналитика.Номенклатура
		|			И КлючиАналитики.Характеристика = Аналитика.Характеристика
		|			И КлючиАналитики.Серия = Аналитика.Серия
		|			И КлючиАналитики.МестоХранения = Аналитика.МестоХранения
		|			И КлючиАналитики.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И КлючиАналитики.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
		|ГДЕ
		|	ДД.СлужебноеВидДвиженияПриход
		|	И ДД.РасчетПартий
		|	// До перехода на ПУ 2.2 в регистре ПартииНезавершенногоПроизводства был заполнен вид запасов,
		|	// а затраты в регистре СебестоимостьТоваров были отражены на разделе учета ПроизводственныеЗатраты.
		|	// Такие затраты нужно перенести в раздел учета НезавершенноеПроизводство.
		|	ИЛИ НЕ ДД.СлужебноеВидДвиженияПриход
		|		И ДД.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|";
КонецФункции

Функция ТекстТекущиеРасходыПартийНЗП()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеРасходыПартийНЗП"" 	      				  КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск) КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК Сторно,
		|	96 										  				  КАК Приоритет,
		|	МАКСИМУМ(ДД.КоличествоПродукции)		  				  КАК Знаменатель,
		|	ЛОЖЬ 													  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	ДД.Период КАК ПериодПоступления,
		|	ДД.Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВЫБОР
		|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|			ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ КлючиАналитики.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	ДД.ЗаказНаПроизводство КАК Партия,
		|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	СУММА(ДД.Количество) КАК Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	(ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА ДД.АналитикаУчетаПродукции
		|		ИНАЧЕ АналитикаПродукцииБезНазначения.КлючАналитики КОНЕЦ) КАК КорАналитикаУчетаНоменклатуры,
		|	ДД.КорРазделУчета,
		|	(ВЫБОР КОГДА ДД.КорВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
		|			ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КОНЕЦ) КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	(ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПоступлениеОтПереработчика ТОГДА Поступление.Подразделение
		|		ИНАЧЕ Аналитика.МестоХранения КОНЕЦ) КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Регистратор КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки
		|ИЗ
		|	ВТКэшРасчетныеОборотыПартииНезавершенногоПроизводства КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаПродукции
		|		ПО АналитикаПродукции.Ссылка = ДД.АналитикаУчетаПродукции
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПродукцииБезНазначения
		|		ПО АналитикаПродукции.Номенклатура = АналитикаПродукцииБезНазначения.Номенклатура
		|		И АналитикаПродукции.Характеристика = АналитикаПродукцииБезНазначения.Характеристика
		|		И АналитикаПродукции.Серия = АналитикаПродукцииБезНазначения.Серия
		|		И АналитикаПродукции.МестоХранения = АналитикаПродукцииБезНазначения.МестоХранения
		|		И АналитикаПродукции.СтатьяКалькуляции = АналитикаПродукцииБезНазначения.СтатьяКалькуляции
		|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПродукцииБезНазначения.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО КлючиАналитики.Номенклатура = Аналитика.Номенклатура
		|			И КлючиАналитики.Характеристика = Аналитика.Характеристика
		|			И КлючиАналитики.Серия = Аналитика.Серия
		|			И КлючиАналитики.МестоХранения = Аналитика.МестоХранения
		|			И КлючиАналитики.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И КлючиАналитики.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеОтПереработчика КАК Поступление
		|		ПО Поступление.Ссылка = ДД.Регистратор
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТАналитикиУчетаПоПартнерамИзОтчетовДавальцам КАК АналитикаДавальца
		|		ПО АналитикаДавальца.Регистратор = ДД.Регистратор
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|	И ДД.РасчетПартий
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ,
		|	Аналитика.Номенклатура,
		|	ДД.Период,
		|	ДД.Регистратор,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	КлючиАналитики.КлючАналитики,
		|	ДД.Организация,
		|	ДД.ЗаказНаПроизводство,
		|	ДД.АналитикаУчетаПродукции,
		|	АналитикаПродукцииБезНазначения.КлючАналитики,
		|	ДД.КорРазделУчета,
		|	ВЫБОР КОГДА ДД.КорВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
		|			ТОГДА ДД.КорВидЗапасов
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДД.Регистратор ССЫЛКА Документ.ПоступлениеОтПереработчика ТОГДА Поступление.Подразделение
		|		ИНАЧЕ Аналитика.МестоХранения КОНЕЦ,
		|	ДД.КодСтрокиПродукция,
		|	ДД.СтатьяКалькуляции,
		|	ДД.Регистратор
		|";
КонецФункции

Функция ТекстТекущиеСписанияЗатратНаВыпуск() // использует Выпуски
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеСписанияЗатратНаВыпуск"" 	      			  КАК ЗапросИсточник,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноСписанияНаВыпуск)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск) КОНЕЦ) КАК ТипЗаписи,
		|	ЛОЖЬ													  КАК ЭтоТочноРасчетноеДвижение,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК Сторно,
		|	95 										  				  КАК Приоритет,
		|	МАКСИМУМ(Выпуски.Знаменатель)			  				  КАК Знаменатель,
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ)									  КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									  КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО											  КАК НалогообложениеПартии,
		|
		|	ДД.Период,
		|	(ВЫБОР
		// Для записей СторноСписанияНаВыпуск партии должны быть подобраны в первую очередь.
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА &НачалоПериода
		|		ИНАЧЕ ДД.Период КОНЕЦ) КАК ПериодПоступления,
		|	ДД.ЗаказНаПроизводство									  КАК Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|		ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
		|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА Выпуски.ПродукцияДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	Выпуски.ГруппаАналитическогоУчета КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	СУММА(ДД.Количество),
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация,
		|	Выпуски.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА Выпуски.ВидЗапасов.ТипЗапасов = Значение(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ КАК КорРазделУчета,
		|	Выпуски.ВидЗапасов КАК КорВидЗапасов,
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	Аналитика.МестоХранения КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	ДД.СтатьяКалькуляции,
		|	ДД.ЗаказНаПроизводство КАК ДокументИсточник,
		|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
		|	ДД.КодСтрокиПродукция КАК КодСтроки
		|ИЗ
		|	ВТКэшРасчетныеОборотыМатериалыИРаботыВПроизводстве КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
		|		ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
		|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
		|		И Аналитика.Серия = АналитикаБезНазначения.Серия
		|		И Аналитика.МестоХранения = АналитикаБезНазначения.МестоХранения
		|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
		|		И Аналитика.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Выпуски КАК Выпуски
		|		ПО Выпуски.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
		|		И Выпуски.КодСтрокиПродукция = ДД.КодСтрокиПродукция
		|ГДЕ
		|	НЕ ДД.СлужебноеВидДвиженияПриход
		|СГРУППИРОВАТЬ ПО
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноСписанияНаВыпуск)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписанияНаВыпуск) КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0
		|		 И ДД.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|			ТОГДА &НачалоПериода
		|		ИНАЧЕ ДД.Период КОНЕЦ),
		|
		|	(ВЫБОР
		|		КОГДА ДД.Количество < 0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ КОНЕЦ),
		|	Аналитика.Номенклатура,
		|	ДД.Период,
		|	ДД.ЗаказНаПроизводство,
		|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
		|		ТОГДА ДД.АналитикаУчетаНоменклатуры
		|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА Выпуски.ПродукцияДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ,
		|	ДД.Организация,
		|	Выпуски.ГруппаАналитическогоУчета,
		|	Выпуски.АналитикаУчетаНоменклатуры,
		|	ВЫБОР
		|		КОГДА Выпуски.ВидЗапасов.ТипЗапасов = Значение(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|	КОНЕЦ,
		|	Выпуски.ВидЗапасов,
		|	Аналитика.МестоХранения,
		|	ДД.СтатьяКалькуляции,
		|	ДД.КодСтрокиПродукция
		|";
КонецФункции

Функция ТекстТекущиеРасходыМатериаловВПроизводстве()
	Возврат 
		"ВЫБРАТЬ
		|	""ТекстТекущиеРасходыМатериаловВПроизводстве"" 	      	КАК ЗапросИсточник,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Распределение) 	КАК ТипЗаписи,
		|	ЛОЖЬ													КАК ЭтоТочноРасчетноеДвижение,
		|	ЛОЖЬ													КАК Сторно,
		|	95 										  				КАК Приоритет,
		|	0 										  				КАК Знаменатель,
		|	ЛОЖЬ 													КАК РасчетЗавершен,
		|	Аналитика.Номенклатура 									КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО                              				КАК НалогообложениеПартии,
		|
		|	ДД.Дата КАК Период,
		|	ДД.Дата КАК ПериодПоступления,
		|	ДД.Ссылка КАК Регистратор,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ДД.АналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК РазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ДД.Организация,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
		|
		|	Строки.Количество,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьЗабалансовая,
		|	0 КАК СтоимостьУпр,
		|	0 КАК ДопРасходыУпр,
		|	0 КАК ПостатейныеПеременныеСНДС,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ТрудозатратыУпр,
		|	0 КАК ПостатейныеПостоянныеУпр,
		|	0 КАК ПостатейныеПеременныеУпр,
		|	0 КАК ДопРасходы,
		|	0 КАК ДопРасходыБезНДС,
		|	0 КАК Трудозатраты,
		|	0 КАК ПостатейныеПостоянныеСНДС,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК СтоимостьРегл,
		|	0 КАК СтоимостьЗабалансоваяРегл,
		|	0 КАК ПостояннаяРазница,
		|	0 КАК ВременнаяРазница,
		|	0 КАК ДопРасходыРегл,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременныеРегл,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию) КАК ХозяйственнаяОперация,
		|	ДД.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КАК КорРазделУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов, 
		|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
		|	0 КАК КорСтоимость,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
		|	ДД.Подразделение,
		|	Строки.АналитикаРасходов КАК АналитикаРасходов,
		|	Строки.СтатьяРасходов КАК СтатьяРасходовСписания,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
		|	Строки.СтатьяРасходов КАК СтатьяАктивовПассивов,
		|	Строки.АналитикаАктивовПассивов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументДвижения,
		|	Строки.ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
		|	НЕОПРЕДЕЛЕНО КАК КорПартия,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаПартий,
		|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
		|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДСДокумента,
		|	0 КАК НДСУпр,
		|	0 КАК НДСРегл,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
		|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
		|	ЕСТЬNULL(ДД.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО) КАК КорНаправлениеДеятельности,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	Документ.РаспределениеПроизводственныхЗатрат КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПрочиеРасходы КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
		|		ПО Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И НЕ ДД.НовоеПроизводство
		|	И ДД.Проведен
		|";
КонецФункции

#КонецОбласти

#Область ПроцедурыЭтапов_Контекстные

// Используется для всех вызовов заполнения расчетной партии.
//
Процедура ЗаполнитьРасчетнуюПартию(ПараметрыРасчета, Контекст, РасчетнаяПартия, Расход, Приход, ПартияЗаполнена) Экспорт
	
	Если Контекст = "РаспределениеМатериаловИРаботПоБазе" Тогда
		// Этап 6
		ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловИРаботПоБазе(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	ИначеЕсли Контекст = "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями" Тогда
		// Этап 7
		ЗаполнитьРасчетнуюПартиюРаспределениеМатериаловНЗП(ПараметрыРасчета, РасчетнаяПартия, Расход, Приход);
		ПартияЗаполнена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УниверсальныеПроцедурыОписанияДанныхМеханизма

// Возвращает перечень объектов метаданных, на основании данных которых выполняется расчет партий.
// В перечень не включаются объекты, которые являются одновременно и исходящими данными механизмов расчета партий и себестоимости.
//
// Параметры:
//	ВходящиеДанные - Соответствие - уже инициализированное хранилище для описания входящих данных
//	ТолькоТребующиеПерерасчета - Булево - если установлен, то будет возвращен перечень только тех данных,
//		изменение которых влечет за собой необходимость перерасчета партий и себестоимости
//		При изменении этих данных должна создаваться запись в регистре сведений ЗаданияКРасчетуСебестоимости.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ВходящиеДанныеМеханизма(ВходящиеДанные, ТолькоТребующиеПерерасчета) Экспорт
	
	Если ТолькоТребующиеПерерасчета Тогда
		Значение = Истина; // чтобы можно было проверить вхождение объекта метаданных в это соответствие
	Иначе
		Значение = Неопределено;
	КонецЕсли;
	
	Если НЕ ТолькоТребующиеПерерасчета Тогда 
		ВходящиеДанные.Вставить(Метаданные.Документы.ПередачаМатериаловВПроизводство, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ВозвратМатериаловИзПроизводства, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ВыпускПродукции, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.ИзделияИЗатратыНЗП, Значение);
		ВходящиеДанные.Вставить(Метаданные.Документы.СписаниеЗатратНаВыпуск, Значение);
	КонецЕсли;
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства, Значение);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.РаспоряженияНаСписаниеПоНормативам, Значение);
	
КонецПроцедуры

// Возвращает перечень регистров, обслуживаемых механизмом расчета партий.
//
// Возвращаемое значение:
//	Соответствие - Ключ - ОбъектМетаданных.
//
Процедура ИсходящиеДанныеМеханизма(ИсходящиеДанные) Экспорт
	
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства, Истина);
	ИсходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ВыпускПродукции, Истина);
	
КонецПроцедуры

// Возвращает перечень регистров, рассчитываемых механизмом партионного учета версии 2.2, и используемых при расчете себестоимости.
//
Процедура ИспользуемыеКэшиРегистровПартионногоУчета(ВходящиеДанные) Экспорт
	
	// Здесь перечислены регистры, которые (по И)
	// - рассчитываются механизмом партионного учета версии 2.2
	// - не рассчитываются механизмом расчета себестоимости
	// - являются входящими данными для механизма расчета себестоимости (используются кэши данных регистров)
	//
	// В случае, если выполняется отдельный запуск расчета себестоимости (не из партионного учета версии 2.2),
	// эти регистры не будут инициализированы - к их кэшам обращаться нельзя.
	//
	// Для этого делается следующее:
	// - перед началом расчета себестоимости "принудительно" инициализируются эти регистры (для того, чтобы сформировались их расчетные кэши)
	// - перед записью движений эти регистры удаляются из параметров расчета (записывать их не надо - движения по ним не формировались)
	
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Тестирование

Процедура ДополнитьЭтапыСРаспределениемПартий(СписокЭтапов) Экспорт
	
	СписокЭтапов.Добавить("РаспределениеМатериаловИРаботПоБазе");
	СписокЭтапов.Добавить("РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями");
	
КонецПроцедуры

Процедура ТекстЗапросаДляРасчетаЭтапа(ИмяЭтапа, ПараметрыРасчета, ТекстЗапроса) Экспорт
	
	Если ИмяЭтапа = "РаспределениеМатериаловИРаботПоБазе" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияМатериаловИРаботПоБазе(ПараметрыРасчета);
	ИначеЕсли ИмяЭтапа = "РаспределениеМатериаловМеждуОстаткомНЗПиВыходнымиИзделиями" Тогда
		ТекстЗапроса = ТекстЗапросаДляРаспределенияМатериаловНЗП(ПараметрыРасчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаданияКРасчетуСебестоимости

// Добавляет описания регистров для их подключения к механизму дат запрета изменения.
//
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "РегистрНакопления.ПартииНезавершенногоПроизводства", "Период", "РегламентныеОперации", "Организация");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
