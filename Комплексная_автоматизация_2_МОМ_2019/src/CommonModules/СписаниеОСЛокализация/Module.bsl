////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Списание ОС".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	Движения = Объект.Движения;
	ДополнительныеСвойства = Объект.ДополнительныеСвойства;
	
	//++ Локализация
	
	РеглУчетПроведениеСервер.ЗарегистрироватьКОтражению(Объект, ДополнительныеСвойства, Движения, Отказ);
	
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	//++ Локализация
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		ЗаполнитьНаОснованииИнвентаризацииОСПриВводеНаОсновании(Объект, ДанныеЗаполнения);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.<Имя документа> - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// ОС-4 (Акт о списании ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС4";
	КомандаПечати.Представление = НСтр("ru = 'ОС-4 (Акт о списании ОС)'");
	КомандаПечати.ФункциональныеОпции = "ИспользоватьРеглУчет";

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	//++ Локализация
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС4") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, "ОС4",
			НСтр("ru = 'ОС-4 (Акт о списании ОС)'"),
			ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(СписаниеПервоначальнойСтоимостиОС());
	ТекстыЗапроса.Добавить(СписаниеПервоначальнойСтоимостиОСЦФ());
	ТекстыЗапроса.Добавить(СписаниеНачисленнойАмортизации());
	ТекстыЗапроса.Добавить(СписаниеНачисленнойАмортизацииЦФ());
	ТекстыЗапроса.Добавить(СписаниеНачисленногоИзноса());
	ТекстыЗапроса.Добавить(СписаниеОстаточнойСтоимостиОСНаРасходы());
	ТекстыЗапроса.Добавить(СписаниеОстаточнойСтоимостиОСНаСтатьиАктивовПассивов());
	ТекстыЗапроса.Добавить(СписаниеДоходыОтЦелевогоФинансирования());
	ТекстыЗапроса.Добавить(ВыбытиеАрендованногоОС());
	ТекстыЗапроса.Добавить(СписаниеРезерваДооценки());
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	ТекстыВременныхТаблиц = Новый Массив;
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТАмортизацияОС());
	ТекстыВременныхТаблиц.Добавить(ТекстЗапросаВТСтоимостьОС());
	ТекстыВременныхТаблиц.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#Область Проведение

Процедура ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса) Экспорт

	//++ Локализация
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса);
	ТекстЗапросаПрочиеДоходы(ТекстыЗапроса);
	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
	
	
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьБлокировкуДанныхПриПроведении(Объект, Блокировка) Экспорт

	//++ Локализация
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПараметрыЦелевогоФинансированияОС");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Объект.ОС;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ОсновноеСредство", "ОсновноеСредство");
	
	Если Объект.ОтражатьВРеглУчете Тогда
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПорядокУчетаОСБУ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Объект.ОС;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ОсновноеСредство", "ОсновноеСредство");
		ЭлементБлокировки.УстановитьЗначение("Организация", Объект.Организация);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаМестонахождениеОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВтТаблицаПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки                  КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство             КАК ОсновноеСредство,
	|
	|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
	|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	ПорядокУчета.ПоказательНаработки       КАК ПоказательНаработки,
	|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
	|
	|	МестонахождениеОС.Организация          КАК Организация,
	|	МестонахождениеОС.МОЛ                  КАК МОЛ,
	|	МестонахождениеОС.Местонахождение      КАК Местонахождение,
	|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
	|
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) КАК НачислятьИзнос,
	|
	|	ПараметрыЦелевогоФинансированияОС.СтатьяДоходов    КАК СтатьяДоходов,
	|	ПараметрыЦелевогоФинансированияОС.АналитикаДоходов КАК АналитикаДоходов,
	|
	|	ТаблицаОС.СуммаСписанияУУ              КАК СуммаСписанияУУ,
	|	ТаблицаОС.СуммаСписанияБУ              КАК СуммаСписанияБУ,
	|	ТаблицаОС.СуммаСписанияНУ              КАК СуммаСписанияНУ,
	|	ТаблицаОС.СуммаСписанияПР              КАК СуммаСписанияПР,
	|	ТаблицаОС.СуммаСписанияВР              КАК СуммаСписанияВР
	|	
	|ПОМЕСТИТЬ втТаблицаОС
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО ТаблицаОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыЦелевогоФинансированияОС КАК ПараметрыЦелевогоФинансированияОС
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыЦелевогоФинансированияОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	//-- Локализация

	Возврат ТекстЗапроса;
	
КонецФункции
 
#КонецОбласти

#Область Заполнение

Процедура ЗаполнитьДокументПоОтбору(Объект, Основание) Экспорт

	//++ Локализация
	
	Если Основание.Свойство("ДокументОснование")
		И ТипЗнч(Основание.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияОС") Тогда
		ЗаполнитьНаОснованииИнвентаризацииОСПриВводеНаОсновании(Объект, Основание);
	КонецЕсли; 
	
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаДокумента

Процедура ПриИзмененииРеквизита(ИмяЭлемента, Форма, ДополнительныеПараметры) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ИмяЭлемента = Элементы.ДокументНаОсновании.Имя Тогда
		
		Объект.ОС.Очистить();
		ЗаполнитьНаОснованииИнвентаризацииОС(Объект, Истина);
		
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПриЧтенииСозданииНаСервере(Форма) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ВалютаРегл = Строка(Константы.ВалютаРегламентированногоУчета.Получить());
	Элементы.ОСГруппаЧастичнойЛиквидации.Заголовок = СтрШаблон(НСтр("ru = 'Регламентированный учет (%1)'"), ВалютаРегл);
	
	Элементы.НастроитьОтражениеВРеглУчете.Заголовок = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(Объект);
	
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

Процедура НастроитьЗависимыеЭлементыФормы(Форма, СтруктураИзмененныхРеквизитов) Экспорт

	//++ Локализация
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Объект = Форма.Объект;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ ОбновитьВсе Тогда
		
		Форма.ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

//++ Локализация

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&НомерНаПечать КАК НомерДокумента,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ТаблицаОС.СуммаСписанияБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаЗатратБУ,
	|	ВЫБОР
	|		КОГДА &ЧастичнаяЛиквидация
	|			ТОГДА ТаблицаОС.СуммаСписанияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаЗатратНУ,
	|	0 КАК СуммаЗатратУСН
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, "Документ.СписаниеОС2_4");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ                       КАК НачислятьАмортизациюБУ,
	|	ЛОЖЬ                       КАК НачислятьАмортизациюНУ
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|		&КонецДня,
	|		Регистратор <> &Ссылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)
	|	) КАК ПорядокУчета
	|	ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВРеглУчете 
	|	И НЕ &ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса)

	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, "Документ.СписаниеОС2_4");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, "Документ.СписаниеОС2_4", "втПараметрыОС");
	Документы.СписаниеОС2_4.ТекстЗапросаТаблицаВтНаработкиОбъектовЭксплуатации(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                      КАК Регистратор,
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	&Период                      КАК ДатаПоследнегоИзменения,
	|	ТаблицаОС.ОсновноеСредство   КАК ОсновноеСредство,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) = 0 
	|			ТОГДА 0
	|		ИНАЧЕ (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0))
	|				- (ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) + ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0))
	|				- (ТаблицаОС.СуммаСписанияБУ 
	|					- (ВЫРАЗИТЬ((ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) + ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)) 
	|						* ТаблицаОС.СуммаСписанияБУ 
	|						/ (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)) КАК ЧИСЛО(31,2))))
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииЦФ
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизацииЦФ = 0
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) = 0 
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|				- ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)
	|				- (ТаблицаОС.СуммаСписанияБУ 
	|					- (ВЫРАЗИТЬ(ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) 
	|						* ТаблицаОС.СуммаСписанияБУ 
	|						/ (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)) КАК ЧИСЛО(31,2))))
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ         КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ         КАК СрокПолезногоИспользованияНУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ             КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизацииОСБУ.КоэффициентУскорения                 КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации           КАК ПРДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|		- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|		- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации
	|ИЗ
	|	Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.Ссылка = ТаблицаОС.Ссылка
	|			И СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.Ссылка = ТаблицаОС.Ссылка
	|			И АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И &Организация = Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОС КАК СписокОС)) КАК ПараметрыАмортизацииОСБУ
	|		ПО (ПараметрыАмортизацииОСБУ.Организация = &Организация)
	|			И (ПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНаработкиОбъектовЭксплуатации КАК НаработкиОбъектовЭксплуатации
	|		ПО ТаблицаОС.ОсновноеСредство = НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВРеглУчете
	|	И &ЧастичнаяЛиквидация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаПрочиеДоходы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Документы.СписаниеОС2_4.ТекстЗапросаТаблицаВтОстаточнаяСтоимостьОС(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&ХозяйственнаяОперацияСтоимость        КАК ХозяйственнаяОперация,
	|	&Организация                           КАК Организация,
	|	&Подразделение                         КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	ТаблицаОС.СтатьяДоходов                КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов             КАК АналитикаДоходов,
	|	
	|	ТаблицаОС.Стоимость * ТаблицаОС.СтоимостьРегл / ТаблицаОС.СтоимостьЦФ КАК Сумма,
	|
	|	ТаблицаОС.СтоимостьЦФ                  КАК СуммаРегл
	|
	|ИЗ
	|	втОстаточнаяСтоимостьОС КАК ТаблицаОС
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И &ЧастичнаяЛиквидация
	|	И ТаблицаОС.СтоимостьЦФ <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры


//-- Локализация

#КонецОбласти

#Область Печать

//++ Локализация

Функция ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Перем ПодразделениеОтветственныхЛиц;
	
	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	ИспользоватьУчетДМ = ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов");
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_ОС4";
	
	МакетОС4  = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ОС4");
	МакетОС4А = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ОС4а");
	МакетОС4Б = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_ОС4б");
	
	ДанныеПечатнойФормы = ДанныеПечатнойФормыОС4(МассивОбъектов);
	
	Результат = ДанныеПечатнойФормы.ПоДокументам;
	Если Результат.Пустой() Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	ВыборкаПрочихОприходований = ДанныеПечатнойФормы.ПоПрочимОприходованиям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаДокументов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблонСообщенияОбОшибке = НСтр("ru = 'Для документа %1 не требуется акт о списании (ОС-4)'");
	ПервыйДокумент = Истина;
	
	СведенияАктуальны = Истина;
	
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если ВыборкаДокументов.ЧастичнаяЛиквидация Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(ШаблонСообщенияОбОшибке, ВыборкаДокументов.Ссылка),
				ВыборкаДокументов.Ссылка);
			
			Продолжить;
		КонецЕсли;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		ЭтоАвто = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		СведенияОбОрганизации= БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
		
		ПодразделениеОтветственныхЛиц = ВыборкаДокументов.ПодразделениеОрганизации;
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.Дата, ПодразделениеОтветственныхЛиц);
		
		ВыборкаСтрок = ВыборкаДокументов.Выбрать(ОбходРезультатаЗапроса.Прямой);
		
		Если ВыборкаСтрок.Количество() = 1 Тогда
			ВыборкаСтрок.Следующий();
			
			ЭтоАвто = (ВыборкаСтрок.Автотранспорт);
			Макет = ?(ЭтоАвто, МакетОС4А, МакетОС4);
			
			ДанныеФизЛица = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.МОЛ, ВыборкаДокументов.Дата, Истина);
			
			Страница1 = Макет.ПолучитьОбласть("Страница1");
			Страница1.Параметры.Заполнить(ВыборкаСтрок);
			Страница1.Параметры.Организация = ПредставлениеОрганизации;
			Страница1.Параметры.ТабНомерМОЛ = ДанныеФизЛица.ТабельныйНомер;
			
			ДокументПринятияКУчету     = "";
			ДокументВводаВЭксплуатацию = "";
			ПринятоКУчету              = "";
			ВведеноВЭксплуатацию       = "";
			
			УчетОСВызовСервера.ПолучитьДокументБухСостоянияОС(
				ВыборкаСтрок.ОС,
				ВыборкаСтрок.Организация,
				Перечисления.СостоянияОС.ПринятоКУчету,
				ДокументВводаВЭксплуатацию,
				ВведеноВЭксплуатацию);
			
			УчетОСВызовСервера.ПолучитьДокументБухСостоянияОС(
				ВыборкаСтрок.ОС,
				ВыборкаСтрок.Организация,
				Перечисления.СостоянияОС.ПринятоКУчету,
				ДокументПринятияКУчету,
				ПринятоКУчету);
			
			Если ЭтоАвто Тогда
				Страница1.Параметры.ВведеноВЭксплуатацию = ВведеноВЭксплуатацию;
				Если ДокументВводаВЭксплуатацию = Неопределено Тогда
					Страница1.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаСтрок.ОС, ВыборкаСтрок.Дата, ВыборкаСтрок.Дата, ВыборкаСтрок.Организация);
				Иначе
					Страница1.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаСтрок.ОС, ДокументВводаВЭксплуатацию.Дата, ВыборкаСтрок.Дата, ВыборкаСтрок.Организация);
				КонецЕсли;
			Иначе
				Страница1.Параметры.СрокЭкспл = ?(
					ЗначениеЗаполнено(ВведеноВЭксплуатацию),
					ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
						УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, ВыборкаСтрок.Дата), Ложь),
					0);
			КонецЕсли;
			
			СтоимостьОС = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				ВыборкаСтрок.НачСтоимость,
				ВыборкаСтрок.Стоимость);
			
			АмортизацияОС = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				0,
				ВыборкаСтрок.НачАмортизация);
			
			Страница1.Параметры.ГодВыпуска     = ?(ЗначениеЗаполнено(ВыборкаСтрок.ГодВыпуска), Год(ВыборкаСтрок.ГодВыпуска), 0);
			Страница1.Параметры.ПринятоКУчету  = ПринятоКУчету;
			Страница1.Параметры.НачСтоимость   = СтоимостьОС;
			Страница1.Параметры.НачАмортизация = АмортизацияОС;
			
			Страница1.Параметры.ОстСтоимость = ?(
				ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				0,
				СтоимостьОС - АмортизацияОС);
			
			Страница1.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
			Страница1.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;
			
			Страница1.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаСтрок.Номер);
			
			ТабличныйДокумент.Вывести(Страница1);
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Страница2_Шапка = Макет.ПолучитьОбласть("Страница2_Шапка");
			ТабличныйДокумент.Вывести(Страница2_Шапка);
			
			Если ЭтоАвто Тогда
				
				Страница2_Таблица3_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица3_Шапка");
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Шапка);
				
				Страница2_Таблица3_Строка = Макет.ПолучитьОбласть("Страница2_Таблица3_Строка");
				ТаблицаДМ = ВыборкаСтрок.ДрагоценныеМатериалы.Выгрузить();
				Если ИспользоватьУчетДМ И ТаблицаДМ.Количество()<>0 Тогда
					Для Каждого СтрокаДМ Из ТаблицаДМ Цикл
						Страница2_Таблица3_Строка.Параметры.Заполнить(СтрокаДМ);
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница2_Таблица3_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица3_Подвал");
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Подвал);
				
				Страница2_Подвал = Макет.ПолучитьОбласть("Страница2_Подвал");
				ТабличныйДокумент.Вывести(Страница2_Подвал);
				
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				Страница3_Шапка = Макет.ПолучитьОбласть("Страница3_Шапка");
				ТабличныйДокумент.Вывести(Страница3_Шапка);
				Страница3_Таблица4_Шапка = Макет.ПолучитьОбласть("Страница3_Таблица4_Шапка");
				ТабличныйДокумент.Вывести(Страница3_Таблица4_Шапка);
				Страница3_Таблица4_Строка = Макет.ПолучитьОбласть("Страница3_Таблица4_Строка");
				Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
					ТабличныйДокумент.Вывести(Страница3_Таблица4_Строка);
				КонецЦикла;
				Страница3_Таблица4_Подвал = Макет.ПолучитьОбласть("Страница3_Таблица4_Подвал");
				ТабличныйДокумент.Вывести(Страница3_Таблица4_Подвал);
				Страница3_Таблица5_Шапка = Макет.ПолучитьОбласть("Страница3_Таблица5_Шапка");
				ТабличныйДокумент.Вывести(Страница3_Таблица5_Шапка);
				Страница3_Таблица5_Строка = Макет.ПолучитьОбласть("Страница3_Таблица5_Строка");
				ВыборкаПрочихОприходований.Сбросить();
				ИтогСумма = 0;
				Если ВыборкаПрочихОприходований.НайтиСледующий(Новый Структура("Ссылка", ВыборкаСтрок.Ссылка)) Тогда
					ВыборкаТоваров = ВыборкаПрочихОприходований.Выбрать();
					Пока ВыборкаТоваров.Следующий() Цикл
						ИтогСумма = ИтогСумма + ВыборкаТоваров.УзелСумма;
						Страница3_Таблица5_Строка.Параметры.Заполнить(ВыборкаТоваров);
						ДанныеДокумента = Новый Структура(
							"Номер,Дата",
							ВыборкаТоваров.УзелДокументНомер, ВыборкаТоваров.УзелДокументДата);
						Страница3_Таблица5_Строка.Параметры.УзелДокумент = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
							ДанныеДокумента,
							НСтр("ru='Прочее оприходование'"));
						Страница3_Таблица5_Строка.Параметры.УзелНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
							ВыборкаТоваров.УзелНоменклатураНаименованиеПолное,
							ВыборкаТоваров.УзелХарактеристикаНаименованиеПолное);
						ТабличныйДокумент.Вывести(Страница3_Таблица5_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница3_Таблица5_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница3_Таблица5_Подвал = Макет.ПолучитьОбласть("Страница3_Таблица5_Подвал");
				Страница3_Таблица5_Подвал.Параметры.ИтогСумма = ИтогСумма;
				ТабличныйДокумент.Вывести(Страница3_Таблица5_Подвал);
				
			Иначе
				Страница2_Таблица2_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица2_Шапка");
				ТабличныйДокумент.Вывести(Страница2_Таблица2_Шапка);
				
				Страница2_Таблица2_Строка = Макет.ПолучитьОбласть("Страница2_Таблица2_Строка");
				ТаблицаДМ = ВыборкаСтрок.ДрагоценныеМатериалы.Выгрузить();
				Если ИспользоватьУчетДМ И ТаблицаДМ.Количество()<>0 Тогда
					Для Каждого СтрокаДМ Из ТаблицаДМ Цикл
						Страница2_Таблица2_Строка.Параметры.Заполнить(СтрокаДМ);
						ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница2_Таблица2_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица2_Подвал");
				ТабличныйДокумент.Вывести(Страница2_Таблица2_Подвал);
				
				Страница2_Таблица3_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица3_Шапка");
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Шапка);
				Страница2_Таблица3_Строка = Макет.ПолучитьОбласть("Страница2_Таблица3_Строка");
				ВыборкаПрочихОприходований.Сбросить();
				ИтогСумма = 0;
				Если ВыборкаПрочихОприходований.НайтиСледующий(Новый Структура("Ссылка", ВыборкаСтрок.Ссылка)) Тогда
					ВыборкаТоваров = ВыборкаПрочихОприходований.Выбрать();
					Пока ВыборкаТоваров.Следующий() Цикл
						ИтогСумма = ИтогСумма + ВыборкаТоваров.УзелСумма;
						Страница2_Таблица3_Строка.Параметры.Заполнить(ВыборкаТоваров);
						ДанныеДокумента = Новый Структура(
							"Номер,Дата",
							ВыборкаТоваров.УзелДокументНомер, ВыборкаТоваров.УзелДокументДата);
						Страница2_Таблица3_Строка.Параметры.УзелДокумент = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
							ДанныеДокумента,
							НСтр("ru='Прочее оприходование'"));
						Страница2_Таблица3_Строка.Параметры.УзелНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
							ВыборкаТоваров.УзелНоменклатураНаименованиеПолное,
							ВыборкаТоваров.УзелХарактеристикаНаименованиеПолное);
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				Иначе
					Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
						ТабличныйДокумент.Вывести(Страница2_Таблица3_Строка);
					КонецЦикла;
				КонецЕсли;
				Страница2_Таблица3_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица3_Подвал");
				Страница2_Таблица3_Подвал.Параметры.ИтогСумма = ИтогСумма;
				ТабличныйДокумент.Вывести(Страница2_Таблица3_Подвал);
				
			КонецЕсли;
			
		Иначе // Табличная часть состоит из нескольких строк
			
			Макет = МакетОС4Б;
			
			ДанныеФизЛица = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.МОЛ, ВыборкаДокументов.Дата, Истина);
			
			ПерваяИтерация = Истина;
			НомерСтроки = 1;
			СсылкаДокумента = Неопределено;
			Пока ВыборкаСтрок.Следующий() Цикл
				
				Если ПерваяИтерация Тогда
					
					СсылкаДокумента = ВыборкаСтрок.Ссылка;
					
					Страница1_Шапка = Макет.ПолучитьОбласть("Страница1_Шапка");
					
					Страница1_Шапка.Параметры.Заполнить(ВыборкаСтрок);
					Страница1_Шапка.Параметры.Организация           = ПредставлениеОрганизации;
					Страница1_Шапка.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
					Страница1_Шапка.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;
					Страница1_Шапка.Параметры.ТабНомерМОЛ           = ДанныеФизЛица.ТабельныйНомер;
					
					Страница1_Шапка.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаСтрок.Номер);
					
					ТабличныйДокумент.Вывести(Страница1_Шапка);
					
					Страница1_Таблица1_Шапка = Макет.ПолучитьОбласть("Страница1_Таблица1_Шапка");
					Страница1_Таблица1_Шапка.Параметры.Заполнить(ВыборкаСтрок);
					ТабличныйДокумент.Вывести(Страница1_Таблица1_Шапка);
					
					ПерваяИтерация = Ложь;
					
				КонецЕсли;
				
				Страница1_Таблица1_Строка = Макет.ПолучитьОбласть("Страница1_Таблица1_Строка");
				Страница1_Таблица1_Строка.Параметры.Заполнить(ВыборкаСтрок);
				СтоимостьОС = ?(
					ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					ВыборкаСтрок.НачСтоимость,
					ВыборкаСтрок.Стоимость);
				АмортизацияОС = ?(
					ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					0,
					ВыборкаСтрок.НачАмортизация);
				
				Страница1_Таблица1_Строка.Параметры.НачСтоимость = СтоимостьОС;
				Страница1_Таблица1_Строка.Параметры.НачАмортизация = АмортизацияОС;
				Страница1_Таблица1_Строка.Параметры.ОстСтоимость = ?(
					ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					0,
					СтоимостьОС - АмортизацияОС);
				
				Страница1_Таблица1_Строка.Параметры.Причина = ВыборкаСтрок.ПричинаСписания;
				
				ДокументПринятияКУчету     = "";
				ДокументВводаВЭксплуатацию = "";
				ПринятоКУчету              = "";
				ВведеноВЭксплуатацию       = "";
				
				УчетОСВызовСервера.ПолучитьДокументБухСостоянияОС(
					ВыборкаСтрок.ОС,
					ВыборкаСтрок.Организация,
					Перечисления.СостоянияОС.ПринятоКУчету,
					ДокументВводаВЭксплуатацию,
					ВведеноВЭксплуатацию);
				
				Страница1_Таблица1_Строка.Параметры.СрокЭкспл = ?(
					ЗначениеЗаполнено(ВведеноВЭксплуатацию),
					ВнеоборотныеАктивыКлиентСервер.РасшифровкаСрокаПолезногоИспользования(
						УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, ВыборкаСтрок.Дата), Ложь),
					0);
				
				ПерваяИтерацияДМ = Истина;
				ТаблицаДМ = ВыборкаСтрок.ДрагоценныеМатериалы.Выгрузить();
				Если ИспользоватьУчетДМ И ТаблицаДМ.Количество()<>0 Тогда
					Для Каждого СтрокаДМ Из ТаблицаДМ Цикл
						Если Не ПерваяИтерацияДМ Тогда
							Страница1_Таблица1_Строка = Макет.ПолучитьОбласть("Страница1_Таблица1_Строка");
						КонецЕсли;
						ПерваяИтерацияДМ = Ложь;
						Страница1_Таблица1_Строка.Параметры.Заполнить(СтрокаДМ);
						ТабличныйДокумент.Вывести(Страница1_Таблица1_Строка);
					КонецЦикла;
				Иначе
					ТабличныйДокумент.Вывести(Страница1_Таблица1_Строка);
				КонецЕсли;
				
			КонецЦикла;
			Страница1_Подвал = Макет.ПолучитьОбласть("Страница1_Подвал");
			ТабличныйДокумент.Вывести(Страница1_Подвал);
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Страница2_Шапка = Макет.ПолучитьОбласть("Страница2_Шапка");
			ТабличныйДокумент.Вывести(Страница2_Шапка);
			Страница2_Таблица2_Шапка = Макет.ПолучитьОбласть("Страница2_Таблица2_Шапка");
			ТабличныйДокумент.Вывести(Страница2_Таблица2_Шапка);
			Страница2_Таблица2_Строка = Макет.ПолучитьОбласть("Страница2_Таблица2_Строка");
			ВыборкаПрочихОприходований.Сбросить();
			ИтогСумма = 0;
			Если ВыборкаПрочихОприходований.НайтиСледующий(Новый Структура("Ссылка", СсылкаДокумента)) Тогда
				ВыборкаТоваров = ВыборкаПрочихОприходований.Выбрать();
				Пока ВыборкаТоваров.Следующий() Цикл
					ИтогСумма = ИтогСумма + ВыборкаТоваров.УзелСумма;
					Страница2_Таблица2_Строка.Параметры.Заполнить(ВыборкаТоваров);
					Страница2_Таблица2_Строка.Параметры.УзелДокументНаименование = НСтр("ru='Прочее оприходование'");
					Страница2_Таблица2_Строка.Параметры.УзелДокументНомер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
						ВыборкаТоваров.УзелДокументНомер);
					Страница2_Таблица2_Строка.Параметры.УзелНаименование = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
						ВыборкаТоваров.УзелНоменклатураНаименованиеПолное,
						ВыборкаТоваров.УзелХарактеристикаНаименованиеПолное);
					ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
				КонецЦикла;
			Иначе
				Для к=0 По 6 Цикл // Пустые строки для заполнения вручную
					ТабличныйДокумент.Вывести(Страница2_Таблица2_Строка);
				КонецЦикла;
			КонецЕсли;
			Страница2_Таблица2_Подвал = Макет.ПолучитьОбласть("Страница2_Таблица2_Подвал");
			Страница2_Таблица2_Подвал.Параметры.ИтогСумма = ИтогСумма;
			ТабличныйДокумент.Вывести(Страница2_Таблица2_Подвал);
			
		КонецЕсли;
		
		Подвал = Макет.ПолучитьОбласть(?(ЭтоАвто, "Страница3_Подвал", "Страница2_Подвал"));
		Подвал.Параметры.ГлавБух = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		ТабличныйДокумент.Вывести(Подвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
		Если НЕ ВыборкаДокументов.СведенияАктуальны Тогда
			СведенияАктуальны = Ложь;
		КонецЕсли; 
		
	КонецЦикла;
	
	ВнеоборотныеАктивы.ДобавитьПредупреждениеЕслиСведенияНеАктуальны(СведенияАктуальны, ТабличныйДокумент);
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ДанныеПечатнойФормыОС4(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр(
		"СуммаЗаданаВДокументе",
		Константы.ВалютаРегламентированногоУчета.Получить() = Константы.ВалютаУправленческогоУчета.Получить());
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Организация КАК Организация,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втОбъектыДокументов
	|ИЗ
	|	Документ.СписаниеОС2_4 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Дата";
	
	Запрос.Выполнить();
	
	ВнеоборотныеАктивыЛокализация.ПолучитьСведенияОбОсновныхСредствахВДокументах(Запрос.МенеджерВременныхТаблиц, МассивОбъектов);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК Состояние,
	|	Документ.ПричинаСписания КАК ПричинаСписания,
	|	Документ.Организация КАК Организация,
	|	Документ.Организация.КодПоОКПО КАК КодОКПО,
	|	Документ.Подразделение КАК ПодразделениеОрганизации,
	|	Документ.МоментВремени КАК МоментВремени,
	|	втОбъектыДокументов.НомерСтроки КАК НомерСтроки,
	|	втОбъектыДокументов.ОсновноеСредство КАК ОС,
	|	ОбъектыЭксплуатации.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ВЫБОР
	|		КОГДА втСведенияОбъектовДокументов.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Автотранспорт,
	|	ОбъектыЭксплуатации.ДатаВыпуска КАК ГодВыпуска,
	|	ВЫБОР
	|		КОГДА ОбъектыЭксплуатации.НаименованиеПолное = """"
	|			ТОГДА ОбъектыЭксплуатации.Наименование
	|		ИНАЧЕ ОбъектыЭксплуатации.НаименованиеПолное
	|	КОНЕЦ КАК НаимОС,
	|	втСведенияОбъектовДокументов.ГруппаОС КАК ГруппаОС,
	|	втСведенияОбъектовДокументов.АмортизационнаяГруппа.Порядок КАК АмортизационнаяГруппа,
	|	втСведенияОбъектовДокументов.ПервоначальнаяСтоимостьБУ КАК НачСтоимость,
	|	втСведенияОбъектовДокументов.ИнвентарныйНомер КАК ИнвНомер,
	|	втСведенияОбъектовДокументов.ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
	|	втСведенияОбъектовДокументов.Местонахождение КАК Подразделение,
	|	втСведенияОбъектовДокументов.МОЛ КАК МОЛ,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СтоимостьРасход, 0) КАК Стоимость,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.АмортизацияРасход, 0) КАК НачАмортизация,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СведенияАктуальны, ЛОЖЬ) КАК СведенияАктуальны,
	|	ОбъектыЭксплуатации.ДрагоценныеМатериалы.(
	|		ДрагоценныйМатериал КАК ДМНаименование,
	|		Количество КАК ДММасса,
	|		ЕдиницаИзмерения КАК ДМЕдиницаИзмерения
	|	) КАК ДрагоценныеМатериалы,
	|	Документ.ЧастичнаяЛиквидация КАК ЧастичнаяЛиквидация
	|ИЗ
	|	Документ.СписаниеОС2_4 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъектыДокументов КАК втОбъектыДокументов
	|		ПО (втОбъектыДокументов.Ссылка = Документ.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияОбъектовДокументов КАК втСведенияОбъектовДокументов
	|		ПО (втОбъектыДокументов.Ссылка = втСведенияОбъектовДокументов.Ссылка)
	|			И (втОбъектыДокументов.ОсновноеСредство = втСведенияОбъектовДокументов.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО втОбъектыДокументов.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ 
	|	МИНИМУМ(СведенияАктуальны)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДемонтируемыеУзлы.Ссылка.ДокументОснование КАК Ссылка,
	|	ДемонтируемыеУзлы.Ссылка КАК УзелДокумент,
	|	ДемонтируемыеУзлы.Ссылка.Номер КАК УзелДокументНомер,
	|	ДемонтируемыеУзлы.Ссылка.Дата КАК УзелДокументДата,
	|	ТИПЗНАЧЕНИЯ(ДемонтируемыеУзлы.Ссылка) КАК УзелДокументНаименование,
	|	ДемонтируемыеУзлы.Номенклатура КАК УзелНоменклатура,
	|	ДемонтируемыеУзлы.Номенклатура.НаименованиеПолное КАК УзелНоменклатураНаименованиеПолное,
	|	ДемонтируемыеУзлы.Характеристика КАК УзелХарактеристика,
	|	ДемонтируемыеУзлы.Характеристика.НаименованиеПолное КАК УзелХарактеристикаНаименованиеПолное,
	|	ДемонтируемыеУзлы.Количество КАК УзелКоличество,
	|	ДемонтируемыеУзлы.Номенклатура.ЕдиницаИзмерения КАК УзелЕдиницаИзмерения,
	|	ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл / ДемонтируемыеУзлы.Количество, ДемонтируемыеУзлы.Цена) КАК УзелЦена,
	|	ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаБезНДСРегл, ДемонтируемыеУзлы.Сумма) КАК УзелСумма
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров.Товары КАК ДемонтируемыеУзлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ДемонтируемыеУзлы.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|			И (НЕ &СуммаЗаданаВДокументе)
	|ГДЕ
	|	ДемонтируемыеУзлы.Ссылка.Проведен
	|	И НЕ ДемонтируемыеУзлы.Ссылка.ПометкаУдаления
	|	И ДемонтируемыеУзлы.Ссылка.ДокументОснование В(&МассивОбъектов)
	|	И ДемонтируемыеУзлы.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОприходованиеЗаСчетДоходов)
	|	И (&СуммаЗаданаВДокументе
	|			ИЛИ НЕ СуммыДокументовВВалютеРегл.Регистратор ЕСТЬ NULL )
	|ИТОГИ ПО
	|	Ссылка";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"ПоДокументам, ПоПрочимОприходованиям",
		Пакет[0],
		Пакет[1]);
	
КонецФункции

Функция ПолучитьПробегАвто(ОбъектОС, НачДата, КонДата, Организация)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЕСТЬNULL(НаработкиПериодОкончание.Значение, 0) - ЕСТЬNULL(НаработкиПериодНачало.Значение, 0) КАК Пробег
		|ИЗ
		|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(&НачГраница, ОбъектЭксплуатации = &ОС) КАК НаработкиПериодНачало
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(&КонГраница, ОбъектЭксплуатации = &ОС) КАК НаработкиПериодОкончание
		|		ПО НаработкиПериодНачало.ОбъектЭксплуатации = НаработкиПериодОкончание.ОбъектЭксплуатации
		|			И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки");
	
	Запрос.УстановитьПараметр("НачГраница", Новый Граница(НачДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонГраница", Новый Граница(КонДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ОС", ОбъектОС);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Пробег;
	
КонецФункции

//-- Локализация

#КонецОбласти

#Область Заполнение

//++ Локализация

Процедура ЗаполнитьНаОснованииИнвентаризацииОСПриВводеНаОсновании(Объект, ДанныеЗаполнения)
	
	Объект.ДокументНаОсновании = Истина;
	
	МассивНомеровСтрок = Неопределено;
	СообщатьОбОшибках = Истина;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Объект.ДокументОснование = ДанныеЗаполнения.ДокументОснование;
		МассивНомеровСтрок = ДанныеЗаполнения.МассивНомеровСтрок;
		СообщатьОбОшибках = ДанныеЗаполнения.СообщатьОбОшибках;
	Иначе
		Объект.ДокументОснование = ДанныеЗаполнения;
	КонецЕсли;
	
	ЗаполнитьНаОснованииИнвентаризацииОС(Объект, СообщатьОбОшибках, МассивНомеровСтрок);
	
	ВнеоборотныеАктивыСлужебный.ЗаполнитьОтражениеВУчете(Объект, Объект.ОС.Выгрузить().ВыгрузитьКолонку("ОсновноеСредство"));
	
КонецПроцедуры

// Заполняет документ на основании инвентаризации ОС
//
// Параметры:
//  Объект				 - ДокументОбъект.СписаниеОС2_4	 - Документ, который надо заполнить.
//  СообщатьОбОшибках	 - Булево						 - Истина, если нужно сообщить об ошибках.
//  МассивНомеровСтрок	 - Массив						 - Строки инвентаризации.
//
Процедура ЗаполнитьНаОснованииИнвентаризацииОС(Объект, СообщатьОбОшибках, МассивНомеровСтрок = Неопределено)

	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = Документы.ИнвентаризацияОС.ДанныеЗаполненияНаОснованииИнвентаризации24(
							Объект.ДокументОснование, 
							Объект.Дата, 
							"Списание", 
							МассивНомеровСтрок);
							
	Если РезультатЗапроса.ТабличнаяЧасть = Неопределено Или РезультатЗапроса.ТабличнаяЧасть.Пустой() Тогда
		Если СообщатьОбОшибках Тогда
			ТекстОшибки = НСтр("ru='В документе %1 отсутствуют строки требующие создания списания'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Объект.ДокументОснование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ДокументОснование");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Реквизиты.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	Объект.Подразделение = Выборка.Местонахождение;
	
	Выборка = РезультатЗапроса.ТабличнаяЧасть.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.ОС.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

//++ Локализация

#Область ПроводкиРегУчета

Функция СписаниеПервоначальнойСтоимостиОСЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание первоначальной стоимости ОС \ Частичная ликвидация целевых средств в восстановительной стоимости ОС (Дт 01.09:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУЦФ КАК СуммаНУКт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРКт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация восстановительной стоимости""
	|		ИНАЧЕ ""Списание первоначальной стоимости ОС""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|	
	|ГДЕ
	|	(Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)
	|";
	
КонецФункции

Функция СписаниеНачисленнойАмортизации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации \ Частичная ликвидация начисленной амортизации (Дт Счет начисления амортизации:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияРегл 
	|	КОНЕЦ КАК Сумма,
	|	Строки.Амортизация КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация накопленной амортизации""
	|		ИНАЧЕ ""Списание начисленной амортизации""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|ГДЕ
	|	(Строки.Амортизация <> 0
	|		ИЛИ НЕ Строки.НачислятьИзнос 
	|			И (Строки.АмортизацияРегл <> 0
	|				ИЛИ Строки.АмортизацияНУ <> 0
	|				ИЛИ Строки.АмортизацияПР <> 0
	|				ИЛИ Строки.АмортизацияВР <> 0))
	|";
	
КонецФункции

Функция СписаниеНачисленнойАмортизацииЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации \  Частичная ликвидация целевых средств в начисленной амортизации(Дт Счет начисления амортизации:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУДт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРДт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация целевых средств в накопленной амортизации""
	|		ИНАЧЕ ""Списание начисленной амортизации""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|	
	|ГДЕ
	|	НЕ Строки.НачислятьИзнос
	|	И (Строки.АмортизацияЦФ <> 0
	|		ИЛИ Строки.АмортизацияНУЦФ <> 0
	|		ИЛИ Строки.АмортизацияПРЦФ <> 0
	|		ИЛИ Строки.АмортизацияВРЦФ <> 0)
	|";
	
КонецФункции

Функция СписаниеНачисленногоИзноса()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание начисленной амортизации \ Частичная ликвидация начисленной амортизации (Дт --- :: Кт СчетИзноса)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияРегл + Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИзносОсновныхСредств) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация начисленного износа""
	|		ИНАЧЕ ""Списание начисленного износа""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|ГДЕ
	|	Строки.НачислятьИзнос
	|	И (Строки.АмортизацияРегл <> 0
	|		ИЛИ Строки.АмортизацияЦФ <> 0)
	|";
	
КонецФункции

Функция СписаниеОстаточнойСтоимостиОСНаРасходы()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание остаточной стоимости ОС \ Частичная ликвидация остаточной стоимости ОС (Дт ...:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл + Строки.СтоимостьЦФ КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Строки.КорАналитикаРасходов // Объект строительства
	|	ИНАЧЕ
	|		Строки.КорСтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Строки.КорСтатьяРасходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|				ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|						ТОГДА ОбъектыСтроительства.СпособСтроительства
	|					ИНАЧЕ 
	|						ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|				КОНЕЦ
	|	ИНАЧЕ
	|		Строки.ОсновноеСредство
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ + Строки.СтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.СтоимостьПР + Строки.СтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.СтоимостьВР + Строки.СтоимостьВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ + Строки.СтоимостьНУЦФ КАК СуммаНУКт,
	|	Строки.СтоимостьПР + Строки.СтоимостьПРЦФ КАК СуммаПРКт,
	|	Строки.СтоимостьВР + Строки.СтоимостьВРЦФ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация остаточной стоимости ОС""
	|		ИНАЧЕ ""Списание остаточной стоимости ОС""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Строки.КорСтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = Строки.КорАналитикаРасходов
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС)
	|	И Строки.КорСтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0
	|		ИЛИ Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание остаточной стоимости ОС \ Частичная ликвидация остаточной стоимости ОС (Дт ...:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияРегл + Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеОперации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Строки.КорАналитикаРасходов // Объект строительства
	|	ИНАЧЕ
	|		Строки.КорСтатьяРасходов
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		Строки.КорСтатьяРасходов
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|				ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|						ТОГДА ОбъектыСтроительства.СпособСтроительства
	|					ИНАЧЕ 
	|						ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|				КОНЕЦ
	|	ИНАЧЕ
	|		Строки.ОсновноеСредство
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизацияНУ + Строки.АмортизацияНУЦФ КАК СуммаНУДт,
	|	Строки.АмортизацияПР + Строки.АмортизацияПРЦФ КАК СуммаПРДт,
	|	Строки.АмортизацияВР + Строки.АмортизацияВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизацияНУ + Строки.АмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.АмортизацияПР + Строки.АмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.АмортизацияВР + Строки.АмортизацияВРЦФ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация остаточной стоимости ОС""
	|		ИНАЧЕ ""Списание остаточной стоимости ОС""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Строки.КорСтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = Строки.КорАналитикаРасходов
	|ГДЕ
	|	Строки.НачислятьИзнос
	|	И Строки.КорСтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	И (Строки.АмортизацияРегл <> 0
	|		ИЛИ Строки.АмортизацияНУ <> 0
	|		ИЛИ Строки.АмортизацияПР <> 0
	|		ИЛИ Строки.АмортизацияВР <> 0
	|		ИЛИ Строки.АмортизацияЦФ <> 0
	|		ИЛИ Строки.АмортизацияНУЦФ <> 0
	|		ИЛИ Строки.АмортизацияПРЦФ <> 0
	|		ИЛИ Строки.АмортизацияВРЦФ <> 0)";

	
КонецФункции

Функция СписаниеОстаточнойСтоимостиОСНаСтатьиАктивовПассивов()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание остаточной стоимости ОС на прочие активы \ Частичная ликвидация остаточной стоимости ОС (Дт ...:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл + Строки.СтоимостьЦФ КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетУчета КАК СчетДт,
	|
	|	Операция.Субконто1 КАК СубконтоДт1,
	|	Операция.Субконто2 КАК СубконтоДт2,
	|	Операция.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ + Строки.СтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.СтоимостьПР + Строки.СтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.СтоимостьВР + Строки.СтоимостьВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ + Строки.СтоимостьНУЦФ КАК СуммаНУКт,
	|	Строки.СтоимостьПР + Строки.СтоимостьПРЦФ КАК СуммаПРКт,
	|	Строки.СтоимостьВР + Строки.СтоимостьВРЦФ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация остаточной стоимости ОС""
	|		ИНАЧЕ ""Списание остаточной стоимости ОС""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4 КАК Операция
	|	ПО Строки.Регистратор = Операция.Ссылка
	|	
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС)
	|	И Операция.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0
	|		ИЛИ Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание остаточной стоимости ОС на прочие активы \ Частичная ликвидация остаточной стоимости ОС (Дт ...:: Кт 01.09)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияРегл + Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Операция.СчетУчета КАК СчетДт,
	|
	|	Операция.Субконто1 КАК СубконтоДт1,
	|	Операция.Субконто2 КАК СубконтоДт2,
	|	Операция.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизацияНУ + Строки.АмортизацияНУЦФ КАК СуммаНУДт,
	|	Строки.АмортизацияПР + Строки.АмортизацияПРЦФ КАК СуммаПРДт,
	|	Строки.АмортизацияВР + Строки.АмортизацияВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизацияНУ + Строки.АмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.АмортизацияПР + Строки.АмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.АмортизацияВР + Строки.АмортизацияВРЦФ КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация остаточной стоимости ОС""
	|		ИНАЧЕ ""Списание остаточной стоимости ОС""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4 КАК Операция
	|	ПО Строки.Регистратор = Операция.Ссылка
	|	
	|ГДЕ
	|	Строки.НачислятьИзнос
	|	И Операция.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И (Строки.АмортизацияРегл <> 0
	|		ИЛИ Строки.АмортизацияНУ <> 0
	|		ИЛИ Строки.АмортизацияПР <> 0
	|		ИЛИ Строки.АмортизацияВР <> 0
	|		ИЛИ Строки.АмортизацияЦФ <> 0
	|		ИЛИ Строки.АмортизацияНУЦФ <> 0
	|		ИЛИ Строки.АмортизацияПРЦФ <> 0
	|		ИЛИ Строки.АмортизацияВРЦФ <> 0)";

	
КонецФункции

Функция СписаниеДоходыОтЦелевогоФинансирования()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Признание доходов от целевого финансирования (Дт 98 :: Кт 91)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыБудущихПериодовОтЦелевогоФинансированияОС) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходовЦФ КАК АналитикаУчетаКт,
	|	Строки.КорПодразделение КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.СтатьяДоходовЦФ КАК СубконтоКт1,
	|	Строки.АналитикаДоходовЦФ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУЦФ + Строки.СтоимостьПРЦФ + Строки.СтоимостьВРЦФ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	/////////////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	""Признание доходов от целевого финансирования при списании остаточной стоимости"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|	
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеСтоимостиОС)
	|	И (Строки.СтоимостьЦФ <> 0
	|		ИЛИ Строки.СтоимостьНУЦФ <> 0
	|		ИЛИ Строки.СтоимостьПРЦФ <> 0
	|		ИЛИ Строки.СтоимостьВРЦФ <> 0)
	|";
	
КонецФункции

Функция ВыбытиеАрендованногоОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Выбытие с забалансового учета арендуемых ОС (Дт ---:: Кт СчетЗабалансовогоУчета)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ЗалоговаяСтоимость КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Арендатор КАК СубконтоКт1,
	|	Строки.ОсновноеСредство КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выбытие с забалансового учета арендуемых ОС"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|
	|ГДЕ
	|	Строки.ЗалоговаяСтоимость <> 0
	|";
	
КонецФункции

Функция СписаниеРезерваДооценки()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание резерва дооценки (Дт 83.01 :: Кт 84.01)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР 
	|		КОГДА Строки.РезервПереоценкиСтоимостиРегл > 0
	|			ТОГДА Строки.РезервПереоценкиСтоимостиРегл - ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизацииРегл, 0)
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР 
	|		КОГДА Строки.РезервПереоценкиСтоимости > 0
	|			ТОГДА Строки.РезервПереоценкиСтоимости - ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизации, 0)
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибыльПодлежащаяРаспределению) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Списание резерва дооценки"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО ДокументыКОтражению.Ссылка = Строки.Регистратор
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|	ПО АмортизацияОС.Регистратор = Строки.Регистратор
	|		И АмортизацияОС.ОсновноеСредство = Строки.ОсновноеСредство
	|		И (АмортизацияОС.РезервПереоценкиАмортизацииРегл > 0
	|			ИЛИ АмортизацияОС.РезервПереоценкиАмортизации > 0)
	|ГДЕ
	|	Строки.РезервПереоценкиСтоимостиРегл > 0
	|		ИЛИ Строки.РезервПереоценкиСтоимости > 0
	|";
	
КонецФункции

Функция ТекстЗапросаВТАмортизацияОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица втАмортизацияОС
	|"+
	"ВЫБРАТЬ
	|	АмортизацияОС.Период,
	|	АмортизацияОС.Регистратор,
	|	АмортизацияОС.Организация,
	|	АмортизацияОС.ОсновноеСредство,
	|	АмортизацияОС.КорОрганизация КАК КорОрганизация,
	|	АмортизацияОС.Подразделение КАК Подразделение,
	|	АмортизацияОС.КорПодразделение КАК КорПодразделение,
	|	АмортизацияОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	АмортизацияОС.КорГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АмортизацияОС.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	АмортизацияОС.НомерСтроки,
	|	АмортизацияОС.Амортизация,
	|	АмортизацияОС.АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации,
	|	АмортизацияОС.ХозяйственнаяОперация,
	|	АмортизацияОС.КорСтатьяРасходов,
	|	АмортизацияОС.КорАналитикаРасходов,
	|	АмортизацияОС.НачислятьИзнос,
	|	Операция.ЧастичнаяЛиквидация
	|
	|ПОМЕСТИТЬ втАмортизацияОС
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|		ПО ДокументыКОтражению.Ссылка = АмортизацияОС.Регистратор
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4 КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка";
	
КонецФункции

Функция ТекстЗапросаВТСтоимостьОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица втСтоимостьОС
	|"+
	"ВЫБРАТЬ
	|	СтоимостьОС.Период,
	|	СтоимостьОС.Регистратор,
	|	СтоимостьОС.ОсновноеСредство,
	|	СтоимостьОС.Организация,
	|	СтоимостьОС.КорОрганизация КАК КорОрганизация,
	|	СтоимостьОС.Подразделение КАК Подразделение,
	|	СтоимостьОС.КорПодразделение КАК КорПодразделение,
	|	СтоимостьОС.Арендатор КАК Арендатор,
	|	СтоимостьОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СтоимостьОС.КорГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	СтоимостьОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтоимостьОС.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	СтоимостьОС.НомерСтроки,
	|	СтоимостьОС.Стоимость,
	|	СтоимостьОС.СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ,
	|	СтоимостьОС.ЗалоговаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимость,
	|	СтоимостьОС.РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл,
	|	СтоимостьОС.АмортизационнаяПремия,
	|	СтоимостьОС.ХозяйственнаяОперация,
	|	СтоимостьОС.КорСтатьяРасходов,
	|	СтоимостьОС.КорАналитикаРасходов,
	|	ПараметрыЦФ.СчетУчета КАК СчетУчетаЦФ,
	|	ПараметрыЦФ.СтатьяДоходов КАК СтатьяДоходовЦФ,
	|	ПараметрыЦФ.АналитикаДоходов КАК АналитикаДоходовЦФ,
	|	Операция.ЧастичнаяЛиквидация
	|
	|ПОМЕСТИТЬ втСтоимостьОС
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|		ПО ДокументыКОтражению.Ссылка = СтоимостьОС.Регистратор
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияОС КАК ПараметрыЦФ
	|		ПО СтоимостьОС.ОсновноеСредство = ПараметрыЦФ.ОсновноеСредство
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4 КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка";
	
КонецФункции

Функция СписаниеПервоначальнойСтоимостиОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание первоначальной стоимости ОС \ Частичная ликвидация восстановительной стоимости ОС (Дт 01.09:: Кт Счет учета ОС)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	
	|	ВЫБОР КОГДА Строки.ЧастичнаяЛиквидация
	|		ТОГДА ""Частичная ликвидация восстановительной стоимости""
	|		ИНАЧЕ ""Списание первоначальной стоимости ОС""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|	
	|ГДЕ
	|	(Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)
	|";
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ПолноеИмяОбъекта()

	Возврат "Документ.СписаниеОС2_4";

КонецФункции

#КонецОбласти

//-- Локализация

#КонецОбласти
