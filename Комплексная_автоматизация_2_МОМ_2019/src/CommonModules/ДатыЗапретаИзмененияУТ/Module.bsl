////////////////////////////////////////////////////////////////////////////////
// КОД ПРОЦЕДУР И ФУНКЦИЙ модуля ДатыЗапретаИзмененияПереопределяемый
// для поддержки библиотечного подхода разработки.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается из переопределяемого модуля.
// Позволяет изменить работу интерфейса при встраивании.
//
// Параметры:
//  НастройкиРаботыИнтерфейса - Структура - содержит свойство:
//   * ИспользоватьВнешнихПользователей - Булево - (начальное значение Ложь),
//     если установить Истина, тогда даты запрета можно будет настраивать для внешних пользователей.
//
Процедура НастройкаИнтерфейса(Знач НастройкиРаботыИнтерфейса) Экспорт
	
	НастройкиРаботыИнтерфейса.ИспользоватьВнешнихПользователей = Истина;
	
КонецПроцедуры

// Вызывается из переопределяемого модуля.
// Содержит описание таблиц и полей объектов для проверки запретов изменения данных.
//   Вызывается из процедуры ИзменениеЗапрещено общего модуля ДатыЗапретаИзменения,
//   используемой в подписке на событие ПередЗаписью объекта для проверки наличия
//   запретов и отказа от изменений запрещенного объекта.
//
// Параметры:
//  ИсточникиДанных - ТаблицаЗначений - с колонками:
//   * Таблица     - Строка - полное имя объекта метаданных,
//                   например, Метаданные.Документы.ПриходнаяНакладная.ПолноеИмя().
//   * ПолеДаты    - Строка - имя реквизита объекта или табличной части,
//                   например "Дата", "Товары.ДатаОтгрузки".
//   * Раздел      - Строка - имя предопределенного элемента
//                   "ПланВидовХарактеристикСсылка.РазделыДатЗапрета".
//   * ПолеОбъекта - Строка - имя реквизита объекта или реквизита табличной части,
//                   например "Организация", "Товары.Склад".
//
//  Для добавления строки имеется процедура ДобавитьСтроку в общем модуле ДатыЗапретаИзменения.
//
Процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(Знач ИсточникиДанных) Экспорт
	
	// Данные(Таблица, ПолеДаты, Раздел, ПолеОбъекта)
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачислениеИСписаниеБонусныхБаллов",    "Дата");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АвансовыйОтчет",                       "Дата", "АвансовыеОтчеты", "Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВзаимозачетЗадолженности",             "Дата", "ВзаимозачетыСписанияЗадолженности", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВзаимозачетЗадолженности",             "Дата", "ВзаимозачетыСписанияЗадолженности", "ОрганизацияКредитор");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВзаимозачетЗадолженности",             "Дата", "ВзаимозачетыСписанияЗадолженности", "КонтрагентДебитор");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВзаимозачетЗадолженности",             "Дата", "ВзаимозачетыСписанияЗадолженности", "КонтрагентКредитор");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеЗадолженности",                "Дата", "ВзаимозачетыСписанияЗадолженности", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеЗадолженности",                "Дата", "ВзаимозачетыСписанияЗадолженности", "Контрагент");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СверкаВзаиморасчетов",                 "Дата", "ВзаимозачетыСписанияЗадолженности", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СверкаВзаиморасчетов",                 "Дата", "ВзаимозачетыСписанияЗадолженности", "Контрагент");
	
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктВыполненныхРабот",                      "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктОРасхожденияхПослеОтгрузки",            "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктОРасхожденияхПослеПриемки",             "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВозвратТоваровОтКлиента",                  "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаРеализации",                  "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомиссионера",                        "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомиссионераОСписании",               "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетОРозничныхПродажах",                  "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РеализацияТоваровУслуг",                   "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РеализацияУслугПрочихАктивов",             "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыкупВозвратнойТарыКлиентом",              "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	//++ НЕ УТ
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыкупТоваровХранителем",                   "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПередачаТоваровХранителю",                 "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеТоваровОтХранителя",            "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеТоваровУХранителя",                "Дата", "ПродажиВозвратыОтКлиентов", "Организация");
	//-- НЕ УТ
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВозвратТоваровПоставщику",             "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаПриобретения",            "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомитенту",                       "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомитентуОСписании",              "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриобретениеТоваровУслуг",             "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриобретениеУслугПрочихАктивов",       "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеТоваров",                   "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СборкаТоваров",                        "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыкупВозвратнойТарыУПоставщика",       "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаНазначенияТоваров",       "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктОРасхожденияхПослеПеремещения",     "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеТоваровНаСклад",            "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ТаможеннаяДекларацияИмпорт",           "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	//++ НЕ УТ
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыкупПринятыхНаХранениеТоваров",       "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтгрузкаТоваровСХранения",             "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриемкаТоваровНаХранение",             "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеПринятыхНаХранениеТоваров",    "Дата", "ЗакупкиВозвратыПоставщикамПеремещенияСборки", "Организация");
	//-- НЕ УТ
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВнутреннееПотреблениеТоваров",         "Дата", "СписанияОприходованияТоваров", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОприходованиеИзлишковТоваров",         "Дата", "СписанияОприходованияТоваров", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПересортицаТоваров",                   "Дата", "СписанияОприходованияТоваров", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПорчаТоваров",                         "Дата", "СписанияОприходованияТоваров", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрочееОприходованиеТоваров",           "Дата", "СписанияОприходованияТоваров", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеНедостачТоваров",              "Дата", "СписанияОприходованияТоваров", "Организация");

	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВозвратТоваровМеждуОрганизациями",                        "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВозвратТоваровМеждуОрганизациями",                        "Дата", "РегламентныеОперации", "ОрганизацияПолучатель");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПоКомиссииМеждуОрганизациями",                       "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПоКомиссииМеждуОрганизациями",                       "Дата", "РегламентныеОперации", "Комиссионер");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании",              "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании",              "Дата", "РегламентныеОперации", "Комиссионер");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПередачаТоваровМеждуОрганизациями",                       "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПередачаТоваровМеждуОрганизациями",                       "Дата", "РегламентныеОперации", "ОрганизацияПолучатель");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеДоходовПоНаправлениямДеятельности", 		 "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеПрочихЗатрат",								 "Дата", "РегламентныеОперации", "Организация");	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеРасходовБудущихПериодов", "РаспределениеРасходов.Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасчетСебестоимостиТоваров",                              "Дата", "РегламентныеОперации", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВводОстатков",                                            "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СверкаНачальныхОстатковПоСкладу",                		 "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрочиеДоходыРасходы",                                     "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ДвижениеПрочихАктивовПассивов",                           "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачисленияКредитовИДепозитов",                            "Дата", "РегламентныеОперации", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасчетКурсовыхРазниц",                                    "Дата", "РегламентныеОперации", "Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетБанкаПоОперациямЭквайринга",      "Дата", "Банк", "БанковскийСчет");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеБезналичныхДенежныхСредств","ДатаПроведенияБанком", "Банк", "БанковскийСчет");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеБезналичныхДенежныхСредств",   "ДатаПроведенияБанком", "Банк", "БанковскийСчет");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОперацияПоПлатежнойКарте",                          "Дата", "Касса", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриходныйКассовыйОрдер",                            "Дата", "Касса", "Касса");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходныйКассовыйОрдер",                            "Дата", "Касса", "Касса");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ИнвентаризацияНаличныхДенежныхСредств",             "Дата", "Касса", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтражениеРасхожденийПриИнкассацииДенежныхСредств",  "Дата", "Касса", "Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОрдерНаОтражениеИзлишковТоваров",              "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОрдерНаОтражениеНедостачТоваров",              "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОрдерНаОтражениеПересортицыТоваров",           "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОрдерНаОтражениеПорчиТоваров",                 "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОрдерНаПеремещениеТоваров",                    "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтборРазмещениеТоваров",                       "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаПоОрдеруНаТовары",  			  "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаИзлишковНедостачПоТоварнымМестам","Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПересчетТоваров",                              "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриходныйОрдерНаТовары",                       "Дата", "СкладскиеОперации", "Склад");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходныйОрдерНаТовары",                       "ДатаОтгрузки", "СкладскиеОперации", "Склад");
	
	//++ НЕ УТ
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПереработчика",						"Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПередачаСырьяПереработчику",				"Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеОтПереработчика",				"Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВозвратСырьяОтПереработчика",			"Дата", "Производство", "Организация");
	
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыработкаСотрудников",					"Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктВыполненныхВнутреннихРабот",              "Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ДвижениеПродукцииИМатериалов",               "Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПроизводствоБезЗаказа",                      "Дата", "Производство", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеВозвратныхОтходов",             "Дата", "Производство", "Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеПроизводственныхЗатрат",	"Дата", "Производство", "Организация");
	//-- НЕ УТ
	
	// Планирование
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПродажПоКатегориям", "Дата",               "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПродажПоКатегориям", "НачалоПериода",      "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НормативРаспределенияПлановПродажПоКатегориям", "Дата", "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НормативРаспределенияПлановПродажПоКатегориям", "ДатаНачалаДействия", "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланЗакупок",            "Дата",               "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланОстатков",           "Дата",               "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланОстатков",           "НачалоПериода",      "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланЗакупок",            "НачалоПериода",      "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПродаж",             "Дата",               "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПродаж",             "НачалоПериода",      "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланСборкиРазборки",     "Дата",               "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланСборкиРазборки",     "НачалоПериода",      "Планирование", "");
	//++ НЕ УТ
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПроизводства",       "Дата",               "Планирование", "");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПроизводства",       "НачалоПериода",      "Планирование", "");
	//-- НЕ УТ
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрочиеДоходыРасходы", "ПрочиеРасходы.ДатаОтражения", "РегламентныеОперации", "ПрочиеРасходы.ДатаОтражения");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрочиеДоходыРасходы", "ПрочиеДоходы.ДатаОтражения", "РегламентныеОперации", "ПрочиеРасходы.ДатаОтражения");
	
	//++ НЕ УТ
	//Бюджетирование	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЭкземплярБюджета",    "НачалоПериода",      "Бюджетирование", "Сценарий");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.УстановкаЗначенийНефинансовыхПоказателей",    "НачалоПериода",      "Бюджетирование", "Сценарий");
	//-- НЕ УТ
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных);
	РегистрыСведений.ЗаданияКРасчетуСебестоимости.ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных);
	РегистрыСведений.ЗаданияКРаспределениюРасчетовСКлиентами.ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных);
	РегистрыСведений.ЗаданияКРаспределениюРасчетовСПоставщиками.ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных);
	
КонецПроцедуры

// Вызывается из переопределяемого модуля.
// Позволяет переопределить выполнение проверок запретов по произвольному условию.
//
// Параметры:
//  Объект       - СправочникОбъект,
//                 ДокументОбъект,
//                 ПланВидовХарактеристикОбъект,
//                 ПланСчетовОбъект,
//                 ПланВидовРасчетаОбъект,
//                 БизнесПроцессОбъект,
//                 ЗадачаОбъект,
//                 ПланОбменаОбъект - объект данных (ПередЗаписью или ПриЧтенииНаСервере).
//               - РегистрСведенийНаборЗаписей,
//                 РегистрНакопленияНаборЗаписей,
//                 РегистрБухгалтерииНаборЗаписей,
//                 РегистрРасчетаНаборЗаписей - набор записей (ПередЗаписью или ПриЧтенииНаСервере).
//
//                 Для документов в свойство ДополнительныеСвойства вставляется свойство РежимЗаписи,
//                 если вызов делается в процессе записи документа.
//  
//  ВыполнятьПроверку    		- Булево - когда Истина, тогда проверка изменения выполняется.
//                                Если установить Ложь, тогда проверка запрета изменения будет пропущена.
//
//  УзелПроверки 				- Неопределено - проверка запрета загрузки не выполняется.
//                              - ПланыОбменаСсылка - проверка запрета загрузки выполняется. Если
//                                установить Неопределено, проверка запрета загрузки будет пропущена.
//
//  ВерсияОбъекта               - Строка - начальное значение "". Проверяются обе версии объекта.
//                                Если установить "СтараяВерсия" или "НоваяВерсия", тогда будет
//                                выполнена проверка только старой или только новой версии объекта.
//
Процедура ПередПроверкойЗапретаИзменения(Объект, ВыполнятьПроверку, УзелПроверки, ВерсияОбъекта) Экспорт
	
	Если ОбщегоНазначения.ЭтоРегистр(Объект.Метаданные())
	 И Объект.Отбор.Найти("Регистратор") <> Неопределено Тогда
		ТипРегистратора = ТипЗнч(Объект.Отбор.Регистратор.Значение); // это набор записей документа-регистратора
	Иначе
		ТипРегистратора = Неопределено;
	КонецЕсли;
	
	ТипОбъекта = ТипЗнч(Объект);
		
	Если ТипОбъекта = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
	 ИЛИ ТипОбъекта = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
		
		// Отказ от проверки с учетом того, что ДатаПроведенияБанком, используемая в проверке, не указывается,
		// если документ не проведен банком, а указывается позже, после проведения документа банком.
		ПроведеноБанкомНоваяВерсия  = Объект.ПроведеноБанком;
		ПроведеноБанкомСтараяВерсия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.Ссылка, "ПроведеноБанком") = Истина;
		
		Если Не ПроведеноБанкомНоваяВерсия И Не ПроведеноБанкомСтараяВерсия Тогда
			ВыполнятьПроверку = Ложь;
			УзелПроверки = Неопределено;
			
		ИначеЕсли Не ПроведеноБанкомНоваяВерсия Тогда
			ВерсияОбъекта = "СтараяВерсия"; // Проверить только новую версию объекта.
			
		ИначеЕсли Не ПроведеноБанкомСтараяВерсия Тогда
			ВерсияОбъекта = "НоваяВерсия"; // Проверить только старую версию объекта.
		КонецЕсли;
		
	КонецЕсли;
	
	ЕстьСвойствоПроверкаДатыЗапретаИзменения = Ложь;
	ПроверитьСвойствоПроверкаДатыЗапретаИзменения(Объект, ВыполнятьПроверку, ЕстьСвойствоПроверкаДатыЗапретаИзменения);
	
	// Для распределения РБП проверка запрета изменений реализована в форме документа.
	Если НЕ ЕстьСвойствоПроверкаДатыЗапретаИзменения
	 И (ТипОбъекта = Тип("ДокументОбъект.РаспределениеРасходовБудущихПериодов")
	 ИЛИ ТипРегистратора = Тип("ДокументСсылка.РаспределениеРасходовБудущихПериодов")) Тогда
		ВыполнятьПроверку = Ложь;
	КонецЕсли;
	
	
	//++ НЕ УТ
	//Бюджетирование
	Если ТипОбъекта = Тип("ДокументОбъект.ЭкземплярБюджета") 
		ИЛИ ТипОбъекта = Тип("ДокументОбъект.УстановкаЗначенийНефинансовыхПоказателей") Тогда
			
		Если РольДоступна("РедактированиеДанныхБюджетированияВнеДатЗапрета") Тогда
			ВыполнятьПроверку = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТ
	
	// Движения заказов и заявок на расход ДС по регистрам расчетов проверять не нужно.
	Если ТипОбъекта = Тип("РегистрНакопленияНаборЗаписей.РасчетыСКлиентами")
	 И (ТипРегистратора = Тип("ДокументСсылка.ЗаказКлиента")
		) Тогда
		
		ВыполнятьПроверку = Ложь;
		
	ИначеЕсли ТипОбъекта = Тип("РегистрНакопленияНаборЗаписей.РасчетыСПоставщиками")
	 И (ТипРегистратора = Тип("ДокументСсылка.ЗаказПоставщику")
		 //++ НЕ УТ
		 ИЛИ ТипРегистратора = Тип("ДокументСсылка.ЗаказПереработчику")
		 //-- НЕ УТ
		) Тогда
		
		ВыполнятьПроверку = Ложь;
		
	ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
		
		ВыполнятьПроверку = Ложь;
		
	КонецЕсли;
	
	// Для отгрузки без перехода права собственности в закрытом периоде
	Если ТипОбъекта = Тип("ДокументОбъект.РеализацияТоваровУслуг")
		И Объект.ДополнительныеСвойства.Свойство("ИзменилосьТолькоСостояниеПереходаПраваСобственности")
		И Объект.ДополнительныеСвойства.ИзменилосьТолькоСостояниеПереходаПраваСобственности Тогда
		
		ВыполнятьПроверку = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура добавляет в дополнительные свойства параметр, отключающий проверку даты запрета изменений.
//
// Параметры:
//  Объект       - СправочникОбъект,
//                 ДокументОбъект,
//                 ПланВидовХарактеристикОбъект,
//                 ПланСчетовОбъект,
//                 ПланВидовРасчетаОбъект,
//                 БизнесПроцессОбъект,
//                 ЗадачаОбъект,
//                 ПланОбменаОбъект - объект данных (ПередЗаписью или ПриЧтенииНаСервере).
//                 РегистрСведенийНаборЗаписей,
//                 РегистрНакопленияНаборЗаписей,
//                 РегистрБухгалтерииНаборЗаписей.
//
Процедура ОтключитьПроверкуДатыЗапретаИзменения(Объект) Экспорт
	
	Объект.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
	
КонецПроцедуры

// Процедура добавляет в дополнительные свойства параметр, принудительно включающий проверку даты запрета изменений.
//
// Параметры:
//  Объект       - СправочникОбъект,
//                 ДокументОбъект,
//                 ПланВидовХарактеристикОбъект,
//                 ПланСчетовОбъект,
//                 ПланВидовРасчетаОбъект,
//                 БизнесПроцессОбъект,
//                 ЗадачаОбъект,
//                 ПланОбменаОбъект - объект данных (ПередЗаписью или ПриЧтенииНаСервере).
//                 РегистрСведенийНаборЗаписей,
//                 РегистрНакопленияНаборЗаписей,
//                 РегистрБухгалтерииНаборЗаписей.
//
Процедура ВключитьПроверкуДатыЗапретаИзменения(Объект) Экспорт
	
	Объект.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Истина);
	
КонецПроцедуры

Процедура ПроверитьСвойствоПроверкаДатыЗапретаИзменения(Объект, ВыполнятьПроверку, ЕстьСвойствоПроверкаДатыЗапретаИзменения)
	
	ЕстьСвойствоПроверкаДатыЗапретаИзменения = Объект.ДополнительныеСвойства.Свойство("ПроверкаДатыЗапретаИзменения");
	Если ЕстьСвойствоПроверкаДатыЗапретаИзменения Тогда
		ВыполнятьПроверку = Объект.ДополнительныеСвойства.ПроверкаДатыЗапретаИзменения;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
