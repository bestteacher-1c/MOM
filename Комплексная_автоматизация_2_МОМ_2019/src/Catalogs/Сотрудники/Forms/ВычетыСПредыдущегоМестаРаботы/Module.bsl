
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ГоловнаяОрганизация = Параметры.ГоловнаяОрганизация;
	ФизическоеЛицо      = Параметры.ФизическоеЛицо;
	
	Если Год = 0 Тогда
		Год = Год(ТекущаяДатаСеанса());
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(ВычетыПредыдущегоМестаРаботыНДФЛ.НалоговыйПериод) КАК НалоговыйПериод
		|ИЗ
		|	РегистрСведений.ВычетыПредыдущегоМестаРаботыНДФЛ КАК ВычетыПредыдущегоМестаРаботыНДФЛ
		|ГДЕ
		|	ВычетыПредыдущегоМестаРаботыНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
		|	И ВычетыПредыдущегоМестаРаботыНДФЛ.ГоловнаяОрганизация = &ГоловнаяОрганизация";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.НалоговыйПериод <> Null Тогда
				Год = Год(Выборка.НалоговыйПериод);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ФизическоеЛицо.Пустая() Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	УстановитьСписокДоступныхДоходов();
	ПрочитатьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГодПриИзменении(Элемент)
	ПрочитатьДанные();
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЗаписатьНаСервере();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСписокДоступныхДоходов()
	
	Доходы = ДоходыСГодовымВычетом(Дата(Год, 1, 1));
	
	Элементы.ВычетыКодДохода.СписокВыбора.Очистить();
	Для Каждого Доход Из Доходы Цикл
		Элементы.ВычетыКодДохода.СписокВыбора.Добавить(Доход.КодДохода, Доход.Код + ", " + Доход.Наименование);
	КонецЦикла;
	
	Элементы.ВычетыКодДохода.РежимВыбораИзСписка = Истина;
	
КонецПроцедуры

&НаСервере
Функция ДоходыСГодовымВычетом(НалоговыйПериод)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НалоговыйПериод", НалоговыйПериод);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВычетыПоДоходамНДФЛСрезПоследних.КодДохода КАК КодДохода,
	|	ВидыДоходовНДФЛ.Код КАК Код,
	|	ВидыДоходовНДФЛ.Наименование КАК Наименование
	|ИЗ
	|	РегистрСведений.ВычетыПоДоходамНДФЛ.СрезПоследних(&НалоговыйПериод, ГодовойВычет > 0) КАК ВычетыПоДоходамНДФЛСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДоходовНДФЛ КАК ВидыДоходовНДФЛ
	|		ПО ВычетыПоДоходамНДФЛСрезПоследних.КодДохода = ВидыДоходовНДФЛ.Ссылка";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Процедура ПрочитатьДанные()
	
	Вычеты.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("НалоговыйПериод", Дата(Год, 1, 1));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВычетыПредыдущегоМестаРаботыНДФЛ.КодДохода КАК КодДохода,
	|	ВычетыПредыдущегоМестаРаботыНДФЛ.СуммаВычета КАК СуммаВычета
	|ИЗ
	|	РегистрСведений.ВычетыПредыдущегоМестаРаботыНДФЛ КАК ВычетыПредыдущегоМестаРаботыНДФЛ
	|ГДЕ
	|	ВычетыПредыдущегоМестаРаботыНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
	|	И ВычетыПредыдущегоМестаРаботыНДФЛ.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И ВычетыПредыдущегоМестаРаботыНДФЛ.НалоговыйПериод = &НалоговыйПериод";
	Вычеты.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	НалоговыйПериод = Дата(Год, 1, 1);
	
	НаборЗаписей = РегистрыСведений.ВычетыПредыдущегоМестаРаботыНДФЛ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Отбор.ГоловнаяОрганизация.Установить(ГоловнаяОрганизация);
	НаборЗаписей.Отбор.НалоговыйПериод.Установить(НалоговыйПериод);
	
	Для Каждого СтрокаВычета Из Вычеты Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.ГоловнаяОрганизация = ГоловнаяОрганизация;
		Запись.НалоговыйПериод = НалоговыйПериод;
		Запись.ФизическоеЛицо = ФизическоеЛицо;
		Запись.КодДохода = СтрокаВычета.КодДохода;
		Запись.СуммаВычета = СтрокаВычета.СуммаВычета;
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
