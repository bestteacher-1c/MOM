#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриСозданииЧтенииНаСервере();
		Элементы.РазрешитьРедактированиеРеквизитовОбъекта.Доступность = Ложь;
	КонецЕсли;
	
	Если ТолькоПросмотр Или Элементы.РазрешитьРедактированиеРеквизитовОбъекта.Доступность Тогда
		Элементы.НастроитьОтражениеВРеглУчете.Доступность            = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеВзносов.Доступность     = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеЕНВД.Доступность        = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеВзносовЕНВД.Доступность = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеТравматизм.Доступность  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриСозданииЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипЗнч(Объект.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов")
		И Не ЗначениеЗаполнено(Объект.СчетУчета)Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан счет учета для отражения зарплаты на прочие пассивы.'"),
			,
			"НастроитьОтражениеВРеглУчете",
			,
			Отказ);
	КонецЕсли;
	
	Если Объект.РежимУчетаВзносов = 1 И АналитикаРасходовВзносовОбязательна И Не ЗначениеЗаполнено(Объект.АналитикаРасходовВзносов) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Аналитика расходов не заполнена.'"),
			,
			"АналитикаРасходовВзносов",
			,
			Отказ);
	КонецЕсли;
	
	Если Объект.РежимУчетаВзносов = 1 И ТипЗнч(Объект.СтатьяРасходовВзносов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов")
		И Не ЗначениеЗаполнено(Объект.СчетУчетаВзносов) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан счет учета для отражения взносов на прочие пассивы.'"),
			,
			"НастроитьОтражениеВРеглУчетеВзносов",
			,
			Отказ);
	КонецЕсли;
	
	Если Не Объект.ОсобыйСоответствуетОсновному Тогда
		
		Если ТипЗнч(Объект.СтатьяРасходовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов")
			И Не ЗначениеЗаполнено(Объект.СчетУчетаЕНВД)Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не указан счет учета для отражения зарплаты на прочие пассивы.'"),
				,
				"НастроитьОтражениеВРеглУчетеЕНВД",
				,
				Отказ);
		КонецЕсли;
		
		Если Объект.РежимУчетаВзносовЕНВД = 1 И АналитикаРасходовВзносовЕНВДОбязательна И Не ЗначениеЗаполнено(Объект.АналитикаРасходовВзносовЕНВД) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Аналитика расходов не заполнена.'"),
				,
				"АналитикаРасходовВзносовЕНВД",
				,
				Отказ);
			КонецЕсли;
			
		Если Объект.РежимУчетаВзносовЕНВД = 1 И ТипЗнч(Объект.СтатьяРасходовВзносовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов")
				И Не ЗначениеЗаполнено(Объект.СчетУчетаВзносовЕНВД) Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Не указан счет учета для отражения взносов на прочие пассивы.'"),
					,
					"НастроитьОтражениеВРеглУчетеВзносовЕНВД",
					,
					Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.РежимУчетаВзносовНаТравматизм = 1 И ТипЗнч(Объект.СтатьяРасходовТравматизм) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов")
		И Не ЗначениеЗаполнено(Объект.СчетУчетаТравматизм)Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не указан счет учета для отражения взносов на травматизм на прочие пассивы.'"),
			,
			"НастроитьОтражениеВРеглУчетеТравматизм",
			,
			Отказ);
	КонецЕсли;
	
	Если Объект.РежимУчетаВзносовНаТравматизм = 1 И АналитикаРасходовТравматизмОбязательна И Не ЗначениеЗаполнено(Объект.АналитикаРасходовТравматизм) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Аналитика расходов не заполнена.'"),
			,
			"АналитикаРасходовТравматизм",
			,
			Отказ);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПризнакиКонтроляАналитикиРасходов();
	
	НастроитьВидимостьАналитик();
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Если Элементы.РазрешитьРедактированиеРеквизитовОбъекта.Доступность Тогда
		Элементы.НастроитьОтражениеВРеглУчете.Доступность            = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеВзносов.Доступность     = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеЕНВД.Доступность        = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеВзносовЕНВД.Доступность = Ложь;
		Элементы.НастроитьОтражениеВРеглУчетеТравматизм.Доступность  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область НеЕНВД

&НаКлиенте
Процедура НастроитьОтражениеВРеглУчетеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодбираемыеАналитики = ПодбираемыеАналитики();
	
	ПараметрыОповещения = Новый Структура(
		"Постфикс, ПодбираемыеАналитики",
		"",
		ПодбираемыеАналитики);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	Параметрыформы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", СтруктураОтражения(""));
	ПараметрыФормы.Вставить("ПодбираемыеАналитики", ПодбираемыеАналитики);
	
	РеглУчетКлиент.ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассива(ЭтаФорма, ОписаниеОповещения, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтражениеВРеглУчетеВзносыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодбираемыеАналитики = ПодбираемыеАналитики(Истина);
	
	ПараметрыОповещения = Новый Структура(
		"Постфикс, ПодбираемыеАналитики",
		"Взносов",
		ПодбираемыеАналитики);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	Параметрыформы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", СтруктураОтражения("Взносов"));
	ПараметрыФормы.Вставить("ПодбираемыеАналитики", ПодбираемыеАналитики);
	
	РеглУчетКлиент.ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассива(ЭтаФорма, ОписаниеОповещения, Параметрыформы);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовПриИзменении(Элемент)
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходов,
		Объект.АналитикаРасходов,
		"АналитикаРасходов",
		Объект.АналитикаАктивовПассивов,
		"АналитикаАктивовПассивов",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("СтатьяРасходовВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Статья", Объект.СтатьяРасходов);
	ПараметрыФормы.Вставить("ПараметрыВыбора", Элемент.ПараметрыВыбора);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораСтатьи", ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СтатьяРасходов = Результат;
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходов,
		Объект.АналитикаРасходов,
		"АналитикаРасходов",
		Объект.АналитикаАктивовПассивов,
		"АналитикаАктивовПассивов",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если АналитикаРасходовЗаказРеализация Тогда
		ПродажиКлиент.НачалоВыбораАналитикиРасходов(Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Объект.АналитикаРасходов = ВыбранноеЗначение.АналитикаРасходов;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РежимАналитикиВзносовПриИзменении(Элемент)
	
	Объект.СтатьяРасходовВзносов = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка");
	Объект.АналитикаРасходовВзносов = Неопределено;
	Объект.АналитикаАктивовПассивовВзносов = Неопределено;
	АналитикаРасходовВзносовАвтоматически = Ложь;
	АналитикаРасходовВзносовОбязательна = Ложь;
	
	Элементы.ГруппаВзносыОбщаяАналитика.Видимость = Объект.РежимУчетаВзносов = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВзносовПриИзменении(Элемент)
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовВзносов,
		Объект.АналитикаРасходовВзносов,
		"АналитикаРасходовВзносов",
		Объект.АналитикаАктивовПассивовВзносов,
		"АналитикаАктивовПассивовВзносов",
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВзносовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("СтатьяРасходовВзносовВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Статья", Объект.СтатьяРасходовВзносов);
	ПараметрыФормы.Вставить("ПараметрыВыбора", Элемент.ПараметрыВыбора);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораСтатьи", ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВзносовВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СтатьяРасходовВзносов = Результат;
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовВзносов,
		Объект.АналитикаРасходовВзносов,
		"АналитикаРасходовВзносов",
		Объект.АналитикаАктивовПассивовВзносов,
		"АналитикаАктивовПассивовВзносов",
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если АналитикаРасходовВзносовЗаказРеализация Тогда
		ПродажиКлиент.НачалоВыбораАналитикиРасходов(Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		АналитикаРасходовВзносов = ВыбранноеЗначение.АналитикаРасходов;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ЕНВД

&НаКлиенте
Процедура НастроитьОтражениеВРеглУчетеЕНВДНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодбираемыеАналитики = ПодбираемыеАналитики();
	
	ПараметрыОповещения = Новый Структура(
		"Постфикс, ПодбираемыеАналитики",
		"ЕНВД",
		ПодбираемыеАналитики);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	Параметрыформы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", СтруктураОтражения("ЕНВД"));
	ПараметрыФормы.Вставить("ПодбираемыеАналитики", ПодбираемыеАналитики);
	
	РеглУчетКлиент.ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассива(ЭтаФорма, ОписаниеОповещения, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтражениеВРеглУчетеВзносыЕНВДНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодбираемыеАналитики = ПодбираемыеАналитики(Истина);
	
	ПараметрыОповещения = Новый Структура(
		"Постфикс, ПодбираемыеАналитики",
		"ВзносовЕНВД",
		ПодбираемыеАналитики);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	Параметрыформы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", СтруктураОтражения("ВзносовЕНВД"));
	ПараметрыФормы.Вставить("ПодбираемыеАналитики", ПодбираемыеАналитики);
	
	РеглУчетКлиент.ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассива(ЭтаФорма, ОписаниеОповещения, Параметрыформы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОсобыйСоответствуетОсновномуПриИзменении(Элемент)
	ОсобыйСоответствуетОсновномуПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовЕНВДПриИзменении(Элемент)
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовЕНВД,
		Объект.АналитикаРасходовЕНВД,
		"АналитикаРасходовЕНВД",
		Объект.АналитикаАктивовПассивовЕНВД,
		"АналитикаАктивовПассивовЕНВД",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовЕНВДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("СтатьяРасходовВыборЗавершениеЕНВД", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Статья", Объект.СтатьяРасходовЕНВД);
	ПараметрыФормы.Вставить("ПараметрыВыбора", Элемент.ПараметрыВыбора);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораСтатьи", ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВыборЗавершениеЕНВД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СтатьяРасходовЕНВД = Результат;
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовЕНВД,
		Объект.АналитикаРасходовЕНВД,
		"АналитикаРасходовЕНВД",
		Объект.АналитикаАктивовПассивовЕНВД,
		"АналитикаАктивовПассивовЕНВД",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовЕНВДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если АналитикаРасходовЕНВДЗаказРеализация Тогда
		ПродажиКлиент.НачалоВыбораАналитикиРасходов(Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовЕНВДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Объект.АналитикаРасходовЕНВД = ВыбранноеЗначение.АналитикаРасходов;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовЕНВДАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовЕНВДОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РежимАналитикиВзносовЕНВДПриИзменении(Элемент)
	
	Объект.СтатьяРасходовВзносовЕНВД = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка");
	Объект.АналитикаРасходовВзносовЕНВД = Неопределено;
	Объект.АналитикаАктивовПассивовВзносовЕНВД = Неопределено;
	АналитикаРасходовВзносовЕНВДАвтоматически = Ложь;
	АналитикаРасходовВзносовЕНВДОбязательна = Ложь;
	
	Элементы.ГруппаВзносыОбщаяАналитикаЕНВД.Видимость = Объект.РежимУчетаВзносовЕНВД = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВзносовЕНВДПриИзменении(Элемент)
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовВзносовЕНВД,
		Объект.АналитикаРасходовВзносовЕНВД,
		"АналитикаРасходовВзносовЕНВД",
		Объект.АналитикаАктивовПассивовВзносовЕНВД,
		"АналитикаАктивовПассивовВзносовЕНВД",
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВзносовЕНВДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("СтатьяРасходовВзносовВыборЗавершениеЕНВД", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Статья", Объект.СтатьяРасходовВзносовЕНВД);
	ПараметрыФормы.Вставить("ПараметрыВыбора", Элемент.ПараметрыВыбора);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораСтатьи", ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовВзносовВыборЗавершениеЕНВД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СтатьяРасходовВзносовЕНВД = Результат;
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовВзносовЕНВД,
		Объект.АналитикаРасходовВзносовЕНВД,
		"АналитикаРасходовВзносовЕНВД",
		Объект.АналитикаАктивовПассивовВзносовЕНВД,
		"АналитикаАктивовПассивовВзносовЕНВД",
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовЕНВДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если АналитикаРасходовВзносовЕНВДЗаказРеализация Тогда
		ПродажиКлиент.НачалоВыбораАналитикиРасходов(Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовЕНВДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Объект.АналитикаРасходовВзносовЕНВД = ВыбранноеЗначение.АналитикаРасходов;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовЕНВДАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовВзносовЕНВДОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Травматизм

&НаКлиенте
Процедура НастроитьОтражениеВРеглУчетеТравматизмНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодбираемыеАналитики = ПодбираемыеАналитики();
	
	ПараметрыОповещения = Новый Структура(
		"Постфикс, ПодбираемыеАналитики",
		"Травматизм",
		ПодбираемыеАналитики);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	Параметрыформы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", СтруктураОтражения("Травматизм"));
	ПараметрыФормы.Вставить("ПодбираемыеАналитики", ПодбираемыеАналитики);
	
	РеглУчетКлиент.ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассива(ЭтаФорма, ОписаниеОповещения, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовТравматизмПриИзменении(Элемент)
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовТравматизм,
		Объект.АналитикаРасходовТравматизм,
		"АналитикаРасходовТравматизм",
		Объект.АналитикаАктивовПассивовТравматизм,
		"АналитикаАктивовПассивовТравматизм",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовТравматизмНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("СтатьяРасходовТравматизмВыборЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Статья", Объект.СтатьяРасходовТравматизм);
	ПараметрыФормы.Вставить("ПараметрыВыбора", Элемент.ПараметрыВыбора);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораСтатьи", ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРасходовТравматизмВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СтатьяРасходовТравматизм = Результат;
	
	СтатьяРасходовПриИзмененииНаСервере(
		Объект.СтатьяРасходовТравматизм,
		Объект.АналитикаРасходовТравматизм,
		"АналитикаРасходовТравматизм",
		Объект.АналитикаАктивовПассивовТравматизм,
		"АналитикаАктивовПассивовТравматизм",
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовТравматизмНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если АналитикаРасходовЗаказРеализация Тогда
		ПродажиКлиент.НачалоВыбораАналитикиРасходов(Элемент, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовТравматизмОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Объект.АналитикаРасходов = ВыбранноеЗначение.АналитикаРасходов;
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовТравматизмАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АналитикаРасходовТравматизмОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ХарактерПриИзменении(Элемент)
	Объект.ОплатаСдельныхРабот = ХарактерНачисления = "РаспределяетсяНаПроизводство";
	НастроитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ОтдельнаяСтатьяКалькуляцииПриИзменении(Элемент)
	Элементы.СтатьяКалькуляции.Доступность = Объект.ОтдельнаяСтатьяКалькуляции;
КонецПроцедуры

&НаКлиенте
Процедура ОтдельнаяСтатьяКалькуляцииВзносовПриИзменении(Элемент)
	Элементы.СтатьяКалькуляцииВзносов.Доступность = Объект.ОтдельнаяСтатьяКалькуляцииВзносов;
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВзносыНаТравматизмНаОтдельнуюСтатьюПриИзменении(Элемент)
	Элементы.ГруппаСтатьиРасходовВзносовНаТравматизм.Доступность = Объект.РежимУчетаВзносовНаТравматизм;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкаОтраженияВРеглУчетеЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Постфикс = ПараметрыОповещения.Постфикс;
	
	Объект["СчетУчета" + Постфикс] = Результат.СчетУчета;
	Объект["Субконто1" + Постфикс] = Результат.Субконто1;
	Объект["Субконто2" + Постфикс] = Результат.Субконто2;
	Объект["Субконто3" + Постфикс] = Результат.Субконто3;
	ЭтаФорма["НастроитьОтражениеВРеглУчете" + Постфикс] = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(Результат, ПараметрыОповещения.ПодбираемыеАналитики);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		
		Модифицированность = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Объект", Объект.Ссылка);
		
		ОткрытьФорму("Справочник.СпособыОтраженияЗарплатыВБухУчете.Форма.РазблокированиеРеквизитов", 
			ПараметрыФормы,,,,,
			Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Реквизиты = ЗапретРедактированияРеквизитовОбъектовКлиент.Реквизиты(ЭтаФорма, , Ложь);
		Элементы.РазрешитьРедактированиеРеквизитовОбъекта.Доступность = Ложь;
		Элементы.НастроитьОтражениеВРеглУчете.Доступность             = Истина;
		Элементы.НастроитьОтражениеВРеглУчетеВзносов.Доступность      = Истина;
		Элементы.НастроитьОтражениеВРеглУчетеЕНВД.Доступность         = Истина;
		Элементы.НастроитьОтражениеВРеглУчетеВзносовЕНВД.Доступность  = Истина;
		Элементы.НастроитьОтражениеВРеглУчетеТравматизм.Доступность   = Истина;
		
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Реквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ПланыВидовХарактеристик.СтатьиРасходов.УстановитьУсловноеОформлениеАналитик(УсловноеОформление,
		"СтатьяРасходов, АналитикаРасходов, СтатьяРасходовВзносов, АналитикаРасходовВзносов");
		
	ПланыВидовХарактеристик.СтатьиРасходов.УстановитьУсловноеОформлениеАналитик(УсловноеОформление,
		"СтатьяРасходовЕНВД, АналитикаРасходовЕНВД, СтатьяРасходовВзносовЕНВД, АналитикаРасходовВзносовЕНВД");
	
	ПланыВидовХарактеристик.СтатьиРасходов.УстановитьУсловноеОформлениеАналитик(УсловноеОформление,
		"СтатьяРасходовТравматизм, АналитикаРасходовТравматизм");
	
	ПланыВидовХарактеристик.СтатьиРасходов.УстановитьУсловноеОформлениеАналитик(
		УсловноеОформление, Новый Структура("Взносы"));
		
	ПланыВидовХарактеристик.СтатьиРасходов.УстановитьУсловноеОформлениеАналитик(
		УсловноеОформление, Новый Структура("ВзносыЕНВД"));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	// сдельные
	
	Элементы.СтатьяКалькуляции.Доступность = Объект.ОтдельнаяСтатьяКалькуляции;
	Элементы.СтатьяКалькуляцииВзносов.Доступность = Объект.ОтдельнаяСтатьяКалькуляцииВзносов;
	Элементы.ГруппаСтатьиРасходовВзносовНаТравматизм.Доступность = Объект.РежимУчетаВзносовНаТравматизм;
	
	// постатейные
	
	Элементы.ГруппаВзносыОбщаяАналитика.Видимость = Объект.РежимУчетаВзносов = 1;
	
	Если Объект.ОсобыйСоответствуетОсновному Тогда
		Элементы.ОсобыйСоответствуетОсновномуСтраницы.ТекущаяСтраница = Элементы.ГруппаПустаяАналитикаЕНВД;
		Элементы.ГруппаНастройкиСтатейЕНВД.Доступность = Ложь;
	Иначе
		Элементы.ОсобыйСоответствуетОсновномуСтраницы.ТекущаяСтраница = Элементы.ГруппаОсновнаяАналитикаЕНВД;
		Элементы.ГруппаНастройкиСтатейЕНВД.Доступность = Истина;
	КонецЕсли;
	
	Элементы.ГруппаВзносыОбщаяАналитикаЕНВД.Видимость = Объект.РежимУчетаВзносовЕНВД = 1;
	
	СписокСтатей = СписокПараметровСтатейАналитик(ЭтаФорма);
	
	Для Каждого Элемент Из СписокСтатей Цикл
		ЗаполнитьПодсказкиВвода(Элемент.ИмяАналитикиРасходов, Элемент.ИмяАналитикиПассивов, ЭтаФорма[Элемент.ИмяАналитикиРасходов + "Автоматически"]);
	КонецЦикла;
	
	ПодбираемыеАналитики = ПодбираемыеАналитики();
	
	НастроитьОтражениеВРеглУчете = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(СтруктураОтражения(""), ПодбираемыеАналитики);
	НастроитьОтражениеВРеглУчетеЕНВД = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(СтруктураОтражения("ЕНВД"), ПодбираемыеАналитики);
	НастроитьОтражениеВРеглУчетеТравматизм = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(СтруктураОтражения("Травматизм"), ПодбираемыеАналитики);
	
	ПодбираемыеАналитики = ПодбираемыеАналитики(Истина);
	
	НастроитьОтражениеВРеглУчетеВзносов = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(СтруктураОтражения("Взносов"), ПодбираемыеАналитики);
	НастроитьОтражениеВРеглУчетеВзносовЕНВД = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(СтруктураОтражения("ВзносовЕНВД"), ПодбираемыеАналитики);
	
	ХарактерНачисления = ?(Объект.ОплатаСдельныхРабот, "РаспределяетсяНаПроизводство", "ОтражаетсяПоСтатьеРасходов");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьЭлементов()
	
	Элементы.СтраницаСдельные.Видимость = Объект.ОплатаСдельныхРабот;
	Элементы.СтраницаПостатейные.Видимость = Не Объект.ОплатаСдельныхРабот;
	Элементы.НаправлениеДеятельности.Видимость = Не Объект.ОплатаСдельныхРабот;
	Элементы.БазаРаспределенияПоВыработке.Видимость = Объект.ОплатаСдельныхРабот;
	
КонецПроцедуры

&НаСервере
Функция СтруктураОтражения(Постфикс, Заполнять = Истина)
	
	СтруктураЗаполнения = Новый Структура("СтатьяРасходов, АналитикаРасходов, АналитикаАктивовПассивов, СчетУчета, Субконто1, Субконто2, Субконто3",
		ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка());
	
	Если Не Заполнять Тогда
		Возврат СтруктураЗаполнения;
	КонецЕсли;
	
	СтруктураЗаполнения.СтатьяРасходов           = Объект["СтатьяРасходов" + Постфикс];
	СтруктураЗаполнения.АналитикаРасходов        = Объект["АналитикаРасходов" + Постфикс];
	СтруктураЗаполнения.АналитикаАктивовПассивов = Объект["АналитикаАктивовПассивов" + Постфикс];
	СтруктураЗаполнения.СчетУчета                = Объект["СчетУчета" + Постфикс];
	СтруктураЗаполнения.Субконто1                = Объект["Субконто1" + Постфикс];
	СтруктураЗаполнения.Субконто2                = Объект["Субконто2" + Постфикс];
	СтруктураЗаполнения.Субконто3                = Объект["Субконто3" + Постфикс];
		
	Если ТипЗнч(СтруктураЗаполнения.АналитикаРасходов) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СтруктураЗаполнения.Вставить("ФизическоеЛицо", СтруктураЗаполнения.АналитикаРасходов);
	ИначеЕсли ТипЗнч(СтруктураЗаполнения.АналитикаАктивовПассивов) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СтруктураЗаполнения.Вставить("ФизическоеЛицо", СтруктураЗаполнения.АналитикаАктивовПассивов);
	КонецЕсли;
	
	Возврат СтруктураЗаполнения;
	
КонецФункции

&НаСервере
Процедура НастроитьВидимостьАналитик()
	
	Элементы.АналитикаАктивовПассивов.Видимость  = ТипЗнч(Объект.СтатьяРасходов) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.АналитикаРасходов.Видимость         = ТипЗнч(Объект.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.ГруппаОтражениеВРеглУчете.Видимость = ТипЗнч(Объект.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов");
	
	Элементы.АналитикаАктивовПассивовВзносов.Видимость = ТипЗнч(Объект.СтатьяРасходовВзносов) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.АналитикаРасходовВзносов.Видимость        = ТипЗнч(Объект.СтатьяРасходовВзносов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.ГруппаОтражениеВРеглУчетеВзносы.Видимость = ТипЗнч(Объект.СтатьяРасходовВзносов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов");
	
	Элементы.АналитикаАктивовПассивовЕНВД.Видимость  = ТипЗнч(Объект.СтатьяРасходовЕНВД) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.АналитикаРасходовЕНВД.Видимость         = ТипЗнч(Объект.СтатьяРасходовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.ГруппаОтражениеВРеглУчетеЕНВД.Видимость = ТипЗнч(Объект.СтатьяРасходовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов");
	
	Элементы.АналитикаАктивовПассивовВзносовЕНВД.Видимость = ТипЗнч(Объект.СтатьяРасходовВзносовЕНВД) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.АналитикаРасходовВзносовЕНВД.Видимость        = ТипЗнч(Объект.СтатьяРасходовВзносовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.ГруппаОтражениеВРеглУчетеВзносыЕНВД.Видимость = ТипЗнч(Объект.СтатьяРасходовВзносовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов");
	
	Элементы.АналитикаАктивовПассивовТравматизм.Видимость = ТипЗнч(Объект.СтатьяРасходовТравматизм) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.АналитикаРасходовТравматизм.Видимость        = ТипЗнч(Объект.СтатьяРасходовТравматизм) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов");
	Элементы.ГруппаОтражениеВРеглУчетеТравматизм.Видимость = ТипЗнч(Объект.СтатьяРасходовТравматизм) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодсказкиВвода(ИмяЭлементаРасходов, ИмяЭлементаПассивов, ОтображатьПодсказку)
	
	Если ОтображатьПодсказку Тогда
		Элементы[ИмяЭлементаРасходов].ПодсказкаВвода = НСтр("ru = '<подбирается автоматически>'");
		Элементы[ИмяЭлементаПассивов].ПодсказкаВвода = НСтр("ru = '<подбирается автоматически>'");
	Иначе
		Элементы[ИмяЭлементаРасходов].ПодсказкаВвода = "";
		Элементы[ИмяЭлементаПассивов].ПодсказкаВвода = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	УстановитьПризнакиКонтроляАналитикиРасходов();
	
	НастроитьЭлементыФормы();
	
	НастроитьВидимостьАналитик();
	
	НастроитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакиКонтроляАналитикиРасходов(СтруктураСтатьиРасходов = Неопределено)
	
	ПодбираемыеАналитикиЗарплаты = ПодбираемыеАналитики();
	ПодбираемыеАналитикиВзносов  = ПодбираемыеАналитики(Истина);
	
	Если СтруктураСтатьиРасходов <> Неопределено Тогда
		
		МассивСтатей = Новый Массив;
		МассивСтатей.Добавить(СтруктураСтатьиРасходов);
		
	Иначе
		МассивСтатей = СписокПараметровСтатейАналитик(ЭтаФорма);
	КонецЕсли;
	
	Для Каждого Элемент Из МассивСтатей Цикл
		
		Если Не ЗначениеЗаполнено(Элемент.СтатьяРасходов) Тогда
			Продолжить;
		КонецЕсли;
		
		Автоматически = Ложь;
		
		Если Элемент.ВидыВзносовПодбираются Тогда
			
			Для Каждого Аналитика Из ПодбираемыеАналитикиВзносов Цикл
				Если Элемент.СтатьяРасходов.ТипЗначения.СодержитТип(Аналитика) Тогда
					Автоматически = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Для Каждого Аналитика Из ПодбираемыеАналитикиЗарплаты Цикл
				Если Элемент.СтатьяРасходов.ТипЗначения.СодержитТип(Аналитика) Тогда
					Автоматически = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		ЭтаФорма[Элемент.ИмяАналитикиРасходов + "Автоматически"] = Автоматически;
		
		Если Автоматически Тогда
			ЭтаФорма[Элемент.ИмяАналитикиРасходов + "Обязательна"] = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОсобыйСоответствуетОсновномуПриИзмененииНаСервере()
	
	Объект.СтатьяРасходовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	Объект.АналитикаРасходовЕНВД = Неопределено;
	Объект.АналитикаАктивовПассивовЕНВД = Неопределено;
	АналитикаРасходовЕНВДОбязательна = Ложь;
	АналитикаРасходовЕНВДАвтоматически = Ложь;
	
	Объект.СтатьяРасходовВзносовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	Объект.АналитикаРасходовВзносовЕНВД = Неопределено;
	Объект.АналитикаАктивовПассивовВзносовЕНВД = Неопределено;
	АналитикаРасходовВзносовЕНВДОбязательна = Ложь;
	АналитикаРасходовВзносовЕНВДАвтоматически = Ложь;

	Если Объект.ОсобыйСоответствуетОсновному Тогда
		Объект.РежимУчетаВзносовЕНВД = -1;
	Иначе
		Объект.РежимУчетаВзносовЕНВД = 0;
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокПараметровСтатейАналитик(Форма)
	
	Массив = Новый Массив;
	
	СтруктураСтатьи = СформироватьСтруктуруСтатьиРасходов(
		Форма.Объект.СтатьяРасходов, "АналитикаРасходов", "АналитикаАктивовПассивов", Ложь);
	Массив.Добавить(СтруктураСтатьи);
	
	СтруктураСтатьи = СформироватьСтруктуруСтатьиРасходов(
		Форма.Объект.СтатьяРасходовВзносов, "АналитикаРасходовВзносов", "АналитикаАктивовПассивовВзносов", Истина);
	Массив.Добавить(СтруктураСтатьи);
	
	СтруктураСтатьи = СформироватьСтруктуруСтатьиРасходов(
		Форма.Объект.СтатьяРасходовЕНВД, "АналитикаРасходовЕНВД", "АналитикаАктивовПассивовЕНВД", Ложь);
	Массив.Добавить(СтруктураСтатьи);
	
	СтруктураСтатьи = СформироватьСтруктуруСтатьиРасходов(
		Форма.Объект.СтатьяРасходовВзносовЕНВД, "АналитикаРасходовВзносовЕНВД", "АналитикаАктивовПассивовВзносовЕНВД", Истина);
	Массив.Добавить(СтруктураСтатьи);
	
	СтруктураСтатьи = СформироватьСтруктуруСтатьиРасходов(
		Форма.Объект.СтатьяРасходовТравматизм, "АналитикаРасходовТравматизм", "АналитикаАктивовПассивовТравматизм", Ложь);
	Массив.Добавить(СтруктураСтатьи);
	
	Возврат Массив;
	
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьСтруктуруСтатьиРасходов(СтатьяРасходов, ИмяАналитикиРасходов, ИмяАналитикиПассивов, ВидыВзносовПодбираются)
	
	СтруктураСтатьиРасходов = Новый Структура();
	СтруктураСтатьиРасходов.Вставить("СтатьяРасходов",         СтатьяРасходов);
	СтруктураСтатьиРасходов.Вставить("ИмяАналитикиРасходов",   ИмяАналитикиРасходов);
	СтруктураСтатьиРасходов.Вставить("ИмяАналитикиПассивов",   ИмяАналитикиПассивов);
	СтруктураСтатьиРасходов.Вставить("ВидыВзносовПодбираются", ВидыВзносовПодбираются);
	
	Возврат СтруктураСтатьиРасходов;
	
КонецФункции

&НаСервереБезКонтекста
Процедура АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст)
	
	ДанныеВыбора = Новый СписокЗначений;
	ПродажиСервер.ЗаполнитьДанныеВыбораАналитикиРасходов(ДанныеВыбора, Текст);
	
КонецПроцедуры

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура СтатьяРасходовПриИзмененииНаСервере(Статья, АналитикаРасходов, ИмяАналитикиРасходов, АналитикаПассивов, ИмяАналитикаПассивов, АвтоподборВидаВзноса)
	
	Если ТипЗнч(Статья) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
		
		ДоходыИРасходыСервер.СтатьяАктивовПассивовПриИзменении(Объект, Статья, АналитикаПассивов);
		
			АналитикаРасходов = Неопределено;
			
			ЭтаФорма[ИмяАналитикиРасходов + "Обязательна"] = Ложь;
			ЭтаФорма[ИмяАналитикиРасходов + "ЗаказРеализация"] = Ложь;
			
	Иначе
		
		ДоходыИРасходыСервер.СтатьяРасходовПриИзменении(Объект, Статья, АналитикаРасходов);
		
		ЭтаФорма[ИмяАналитикиРасходов + "Обязательна"] = 
			ЗначениеЗаполнено(Статья)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Статья, "КонтролироватьЗаполнениеАналитики");
		
		ЭтаФорма[ИмяАналитикиРасходов + "ЗаказРеализация"] = 
			ЗначениеЗаполнено(Статья)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Статья, "АналитикаРасходовЗаказРеализация");
		
		АналитикаАктивовПассивов = Неопределено;
		
	КонецЕсли;
	
	СтруктураСтатьиРасходов = СформироватьСтруктуруСтатьиРасходов(
		Статья, ИмяАналитикиРасходов, ИмяАналитикаПассивов, АвтоподборВидаВзноса);
	УстановитьПризнакиКонтроляАналитикиРасходов(СтруктураСтатьиРасходов);
	
	ЗаполнитьПодсказкиВвода(ИмяАналитикиРасходов, ИмяАналитикаПассивов, ЭтаФорма[ИмяАналитикиРасходов + "Автоматически"]);
	
	НастроитьВидимостьАналитик();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПодбираемыеАналитики(Взносы = Ложь)
	
	ПодбираемыеАналитики = Новый Массив;
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ОбъектыЭксплуатации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.Организации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	
	Если Взносы Тогда
		ПодбираемыеАналитики.Добавить(Тип("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	КонецЕсли;
	
	Возврат ПодбираемыеАналитики;
	
КонецФункции

#КонецОбласти

#КонецОбласти