#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ЗначениеПоУмолчанию(ВидАктива) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ГруппыФинансовогоУчетаВнеоборотныхАктивов.Ссылка
	|ИЗ
	|	Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов КАК ГруппыФинансовогоУчетаВнеоборотныхАктивов
	|ГДЕ
	|	ГруппыФинансовогоУчетаВнеоборотныхАктивов.ВидАктива = &ВидАктива
	|	И НЕ ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПометкаУдаления";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВидАктива", ВидАктива);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка();
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка();
	КонецЕсли; 

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет анализ всех неотраженных операций отражения внеоборотных активов,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	Параметры:
//		ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоСтоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчетаЦФ = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоСтоимостиЦФ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчетаАмортизации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчетаАмортизацииЦФ = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыбытиеВНА) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчетаВыбытия = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоВыбытию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДооценкиВНА) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчетаРезерваДооценки = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоРезервуДооценки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетЗабалансовогоУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|	И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоЗабалансовойСтоимости";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураВидовСчетовИРезультатаЗапроса = Новый Соответствие;
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.СтоимостьВНА, МассивРезультатов.Получить(0));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ, МассивРезультатов.Получить(1));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.АмортизацияВНА, МассивРезультатов.Получить(2));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ, МассивРезультатов.Получить(3));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ВыбытиеВНА, МассивРезультатов.Получить(4));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РезервДооценкиВНА, МассивРезультатов.Получить(5));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ЗабалансовыйУчетВНА, МассивРезультатов.Получить(6));

	ИмяСобытия = НСтр("ru = 'Анализ отражения внеоборотных активов, требующих настройки'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(СтруктураВидовСчетовИРезультатаЗапроса, ИмяСобытия);

КонецПроцедуры

Процедура ЗаполнитьСчетаПоУмолчанию(Объект) Экспорт

	Если Объект.ВидАктива = Перечисления.ВидыВнеоборотныхАктивов.ОсновноеСредство 
		ИЛИ Объект.ВидАктива = Перечисления.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество Тогда
		Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ОСвОрганизации;
		Объект.СчетУчетаЦФ = ПланыСчетов.Хозрасчетный.ОСвОрганизации_ЦФ;
		Объект.СчетУчетаАмортизации = ПланыСчетов.Хозрасчетный.АмортизацияОС_01;
		Объект.СчетУчетаАмортизацииЦФ = ПланыСчетов.Хозрасчетный.АмортизацияОС_01_ЦФ;
		Объект.СчетУчетаВыбытия = ПланыСчетов.Хозрасчетный.ВыбытиеОС;
		Объект.СчетУчетаРезерваДооценки = ПланыСчетов.Хозрасчетный.ПриростСтоимостиИмуществаПоПереоценке;
		Объект.СчетЗабалансовогоУчета = ПланыСчетов.Хозрасчетный.АрендованныеОсновныеСредства;
	ИначеЕсли Объект.ВидАктива = Перечисления.ВидыВнеоборотныхАктивов.НМА Тогда
		Объект.СчетУчета = ПланыСчетов.Хозрасчетный.НематериальныеАктивыОрганизации;
		Объект.СчетУчетаАмортизации = ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов;
		Объект.СчетУчетаЦФ = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаАмортизацииЦФ = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаВыбытия = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаРезерваДооценки = ПланыСчетов.Хозрасчетный.ПриростСтоимостиНМАПоПереоценке;
		Объект.СчетЗабалансовогоУчета = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	ИначеЕсли Объект.ВидАктива = Перечисления.ВидыВнеоборотныхАктивов.НИОКР Тогда
		Объект.СчетУчета = ПланыСчетов.Хозрасчетный.РасходыНаНИОКР;
		Объект.СчетУчетаЦФ = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаАмортизации = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаАмортизацииЦФ = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаВыбытия = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетУчетаРезерваДооценки = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		Объект.СчетЗабалансовогоУчета = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеРегистра.СтоимостьРегл <> 0 КАК ЕстьРасчетыПоСтоимости,
	|	ДанныеРегистра.СтоимостьЦФ <> 0 КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизации,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ЛОЖЬ КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ДанныеРегистра.СтоимостьРегл <> 0 КАК ЕстьРасчетыПоСтоимости,
	|	ДанныеРегистра.СтоимостьЦФ <> 0 КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизации,
	|	ЛОЖЬ КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ДанныеРегистра.Регистратор ССЫЛКА Документ.СписаниеОС2_4 КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ДанныеРегистра.ЗалоговаяСтоимость <> 0 КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимости,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ДанныеРегистра.АмортизацияРегл <> 0 КАК ЕстьРасчетыПоАмортизации,
	|	ДанныеРегистра.АмортизацияЦФ <> 0 КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ЛОЖЬ КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ИЗ
	|	РегистрНакопления.АмортизацияНМА КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимости,
	|	ЛОЖЬ КАК ЕстьРасчетыПоСтоимостиЦФ,
	|	ДанныеРегистра.АмортизацияРегл <> 0 КАК ЕстьРасчетыПоАмортизации,
	|	ДанныеРегистра.АмортизацияЦФ <> 0 КАК ЕстьРасчетыПоАмортизацииЦФ,
	|	ЛОЖЬ КАК ЕстьРасчетыПоВыбытию,
	|	ЛОЖЬ КАК ЕстьРасчетыПоРезервуДооценки,
	|	ЛОЖЬ КАК ЕстьРасчетыПоЗабалансовойСтоимости
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра";
	
	УсловиеВхожденияВРегистр = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляРегистра";
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеРегистра.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ";
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеРегистра.Организация В (&Организации)";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстСоединенияСТаблицейДокументов);
	Иначе
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыАнализатора.КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловийПоОрганизацииИПериоду);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	
	|	ТаблицаДвижений.ЕстьРасчетыПоСтоимости,
	|	ТаблицаДвижений.ЕстьРасчетыПоСтоимостиЦФ,
	|	ТаблицаДвижений.ЕстьРасчетыПоАмортизации,
	|	ТаблицаДвижений.ЕстьРасчетыПоАмортизацииЦФ,
	|	ТаблицаДвижений.ЕстьРасчетыПоВыбытию,
	|	ТаблицаДвижений.ЕстьРасчетыПоРезервуДооценки,
	|	ТаблицаДвижений.ЕстьРасчетыПоЗабалансовойСтоимости,
	|
	|	ЕСТЬNULL(АналитикаУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаЦФ, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчетаЦФ,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаАмортизации, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчетаАмортизации,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаАмортизацииЦФ, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчетаАмортизацииЦФ,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаВыбытия, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчетаВыбытия,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаРезерваДооценки, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчетаРезерваДооценки,
	|	ЕСТЬNULL(АналитикаУчета.СчетЗабалансовогоУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетЗабалансовогоУчета
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвижений КАК ТаблицаДвижений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов КАК АналитикаУчета
	|		ПО ТаблицаДвижений.ГруппаФинансовогоУчета = АналитикаУчета.Ссылка";
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

#КонецОбласти

#КонецЕсли