#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("ОбновитьИсториюРегистрацийВНалоговомОргане", Истина);
		ДополнительныеСвойства.Вставить("ОбновитьИсториюСамостоятельныхКлассификационныхЕдиниц", Истина);
	Иначе
		
		ПрежниеЗначение = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ОбособленноеПодразделение,Владелец,Родитель,РегистрацияВНалоговомОргане,СамостоятельнаяКлассификационнаяЕдиница");
		
		Если ПрежниеЗначение.Владелец <> Владелец Тогда
			ВызватьИсключение НСтр("ru='Нельзя менять организацию - владельца элемента справочника ""Подразделения""'");;
		КонецЕсли;
		
		Если ОбособленноеПодразделение <> ПрежниеЗначение.ОбособленноеПодразделение Тогда
			ДополнительныеСвойства.Вставить("ОбновитьИсториюРегистрацийВНалоговомОргане", Истина);
			ДополнительныеСвойства.Вставить("ОбновитьИсториюСамостоятельныхКлассификационныхЕдиниц", Истина);
		ИначеЕсли Родитель <> ПрежниеЗначение.Родитель Тогда
			
			Если ЗначениеЗаполнено(Родитель) Тогда
				РегистрацияВНалоговомОрганеВышестоящего = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "РегистрацияВНалоговомОргане");
				СКЕВышестоящего = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "СамостоятельнаяКлассификационнаяЕдиница");
			Иначе
				РегистрацияВНалоговомОрганеВышестоящего = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "РегистрацияВНалоговомОргане");
				СКЕВышестоящего = Неопределено;
			КонецЕсли;
			
			Если Не ОбособленноеПодразделение И ПрежниеЗначение.РегистрацияВНалоговомОргане <> РегистрацияВНалоговомОрганеВышестоящего Тогда
				ДополнительныеСвойства.Вставить("ОбновитьИсториюРегистрацийВНалоговомОргане", Истина);
			КонецЕсли;
			
			Если ПрежниеЗначение.СамостоятельнаяКлассификационнаяЕдиница <> СКЕВышестоящего Тогда
				ДополнительныеСвойства.Вставить("ОбновитьИсториюСамостоятельныхКлассификационныхЕдиниц", Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкаПорядкаЭлементов.ЗаполнитьЗначениеРеквизитаУпорядочивания(ЭтотОбъект, Отказ);
	
	РеквизитИерархическогоУпорядочивания = РеквизитИерархическогоУпорядочивания();
	Если РеквизитДопУпорядочиванияИерархического <> РеквизитИерархическогоУпорядочивания Тогда
		РеквизитДопУпорядочиванияИерархического = РеквизитИерархическогоУпорядочивания;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновитьИсториюРегистрацийВНалоговомОргане") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если ОбособленноеПодразделение Тогда
			
			Набор = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.СоздатьНаборЗаписей();
			Набор.Отбор.СтруктурнаяЕдиница.Установить(Ссылка);
			
			Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
				
				Запись = Набор.Добавить();
				Запись.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведенийСПериодомМесяц();
				Запись.СтруктурнаяЕдиница = Ссылка;
				Запись.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане;
				
			КонецЕсли;
			
			Набор.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			Набор.Записать();
			
		Иначе
			
			Если ЗначениеЗаполнено(Родитель) Тогда
				ВышестоящаяСтруктурнаяЕдиница = Родитель;
			Иначе
				ВышестоящаяСтруктурнаяЕдиница = Владелец;
			КонецЕсли;
			
			ПараметрыЗаполнения = Новый Соответствие;
			ПараметрыЗаполнения.Вставить(ВышестоящаяСтруктурнаяЕдиница, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка));
			
			РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.ОбновитьПодчиненныеСтруктурныеЕдиницы(ПараметрыЗаполнения);
			
		КонецЕсли; 
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли; 
	
	Если ДополнительныеСвойства.Свойство("ОбновитьИсториюСамостоятельныхКлассификационныхЕдиниц") Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Если ОбособленноеПодразделение Тогда
			
			Набор = РегистрыСведений.ИсторияСамостоятельныхКлассификационныхЕдиниц.СоздатьНаборЗаписей();
			Набор.Отбор.СтруктурнаяЕдиница.Установить(Ссылка);
			
			Если ЗначениеЗаполнено(СамостоятельнаяКлассификационнаяЕдиница) Тогда
				
				Запись = Набор.Добавить();
				Запись.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведенийСПериодомМесяц();
				Запись.СтруктурнаяЕдиница = Ссылка;
				Запись.СКЕ = СамостоятельнаяКлассификационнаяЕдиница;
				
			КонецЕсли;
			
			Набор.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			Набор.Записать();
			
		Иначе
			
			Если ЗначениеЗаполнено(Родитель) Тогда
				ВышестоящаяСтруктурнаяЕдиница = Родитель;
			КонецЕсли;
			
			ПараметрыЗаполнения = Новый Соответствие;
			ПараметрыЗаполнения.Вставить(ВышестоящаяСтруктурнаяЕдиница, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка));
			
			РегистрыСведений.ИсторияСамостоятельныхКлассификационныхЕдиниц.ОбновитьПодчиненныеСтруктурныеЕдиницы(ПараметрыЗаполнения);
			
		КонецЕсли; 
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	ОбновитьРеквизитИерархическогоУпорядочивания(Ссылка, РеквизитДопУпорядочиванияИерархического);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РеквизитИерархическогоУпорядочивания()
	
	РеквизитИерархическогоУпорядочивания = Формат(РеквизитДопУпорядочивания, "ЧЦ=5; ЧН=; ЧВН=; ЧГ=");
	ПроверяемыйРодитель = Родитель;
	Если ЗначениеЗаполнено(ПроверяемыйРодитель) Тогда
		ЗначенияРеквизитаРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПроверяемыйРодитель, "РеквизитДопУпорядочиванияИерархического");
		РеквизитИерархическогоУпорядочивания = ЗначенияРеквизитаРодителя + РеквизитИерархическогоУпорядочивания;
	КонецЕсли;
	
	Возврат РеквизитИерархическогоУпорядочивания;
	
КонецФункции

Процедура ОбновитьРеквизитИерархическогоУпорядочивания(ПодразделенияОрганизацийСсылка, РеквизитИерархическогоУпорядочивания)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Родитель = &ПодразделенияОрганизацийСсылка
	|	И НЕ ПодразделенияОрганизаций.РеквизитДопУпорядочиванияИерархического ПОДОБНО &РеквизитИерархическогоУпорядочивания";
	
	Запрос.УстановитьПараметр("ПодразделенияОрганизацийСсылка", ПодразделенияОрганизацийСсылка);
	Запрос.УстановитьПараметр("РеквизитИерархическогоУпорядочивания", РеквизитИерархическогоУпорядочивания + "%");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПодразделенияОрганизацийОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Попытка 
				ПодразделенияОрганизацийОбъект.Заблокировать();
			Исключение
				ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось изменить настройку упорядочивания для подразделения ""%1"" 
				|Возможно, подразделения редактируется другим пользователем'"),
				ПодразделенияОрганизацийОбъект.Наименование);
				ВызватьИсключение ТекстИсключенияЗаписи;
			КонецПопытки;
			ПодразделенияОрганизацийОбъект.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли