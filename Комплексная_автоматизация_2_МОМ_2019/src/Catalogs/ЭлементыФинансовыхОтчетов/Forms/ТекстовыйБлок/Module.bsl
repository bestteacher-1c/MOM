
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтаФорма);
	Заголовок = Параметры.ВидЭлемента;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	Элементы.ТолькоДляПечати.Видимость = Параметры.Свойство("ИспользоватьДляВводаПлана") 
		И Параметры.ИспользоватьДляВводаПлана;
	
	ПараметрыПодстановки = ПолучитьПараметрыПодстановкиОтчета();
	Если ЗначениеЗаполнено(Параметры.МодельБюджетирования) Тогда
		ДобавитьПараметрыПодстановкиПоВидуБюджета(ПараметрыПодстановки);
	КонецЕсли;
	ДобавитьКомандыПараметровПодстановки(ПараметрыПодстановки);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстовыйБлокПриИзменении(Элемент)
	
	Объект.НаименованиеДляПечати = СокрЛП(СтрПолучитьСтроку(Текст, 1));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ДополнительныеСвойства = Новый Структура("ПредставлениеТолькоДляПечати");
	ДополнительныеСвойства.ПредставлениеТолькоДляПечати = ?(ТолькоДляПечати, 
							НСтр("ru = 'Не выводится при заполнении бюджета'"), "");
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтаФорма, ДополнительныеСвойства);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКомандыПараметровПодстановки(ПараметрыПодстановки)
	
	ИмяДействияКоманды = "ДобавитьШаблонПодстановки";
	
	Для НомерПараметра = 1 По ПараметрыПодстановки.Количество() Цикл
		
		ИмяПараметраШаблона = "ДобавитьШаблон"+НомерПараметра;
		
		КомандаФормы = ЭтаФорма.Команды.Добавить(ИмяПараметраШаблона);
		КомандаФормы.Действие = ИмяДействияКоманды;
		
		НоваяКнопкаФормы = Элементы.Добавить(ИмяПараметраШаблона, Тип("КнопкаФормы"), Элементы.ДобавитьПараметр);
		НоваяКнопкаФормы.Заголовок = ПараметрыПодстановки[НомерПараметра-1];
		НоваяКнопкаФормы.ИмяКоманды = ИмяПараметраШаблона;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьПараметрыПодстановкиОтчета()
	
	МассивПараметров = Новый Массив;
	
	МассивПараметров.Добавить(НСтр("ru = 'Вид отчета'"));
	МассивПараметров.Добавить(НСтр("ru = 'Текущая дата и время'"));
	МассивПараметров.Добавить(НСтр("ru = 'Период отчетности'"));
	МассивПараметров.Добавить(НСтр("ru = 'Конечная дата периода отчета'"));
	// Однотипные параметры добавляемые в бюджетном отчете по своим правилам
	Если НЕ ЗначениеЗаполнено(Параметры.МодельБюджетирования) Тогда
		МассивПараметров.Добавить(НСтр("ru = 'Организация'"));
	КонецЕсли;
	
	Возврат МассивПараметров;
	
КонецФункции

&НаСервере
Функция ДобавитьПараметрыПодстановкиПоВидуБюджета(МассивПараметров)
	
	МассивПараметров.Добавить(НСтр("ru = 'Сценарий'"));
	
	МодельБюджетирования = Параметры.МодельБюджетирования;
	
	Если ЗначениеЗаполнено(МодельБюджетирования) Тогда
		РеквизитыМоделиБюджетирования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.МодельБюджетирования,
			Новый Структура("БюджетыПоОрганизациям,БюджетыПоПодразделениям"));
			
		Если РеквизитыМоделиБюджетирования.БюджетыПоОрганизациям Тогда
			МассивПараметров.Добавить(НСтр("ru = 'Организация'"));
		КонецЕсли;
		
		Если РеквизитыМоделиБюджетирования.БюджетыПоПодразделениям Тогда
			МассивПараметров.Добавить("Подразделение");
		КонецЕсли;
	КонецЕсли;
	
	ИспользоватьДляВводаПлана = Параметры.Свойство("ИспользоватьДляВводаПлана") И Параметры.ИспользоватьДляВводаПлана;
	Если ИспользоватьДляВводаПлана Тогда
		МассивПараметров.Добавить(НСтр("ru = 'Статус документа'"));
		МассивПараметров.Добавить(НСтр("ru = 'Ответственный'"));
	КонецЕсли;
	
	Если Параметры.Свойство("АдресСпискаАналитик") Тогда
		ТаблицаАналитикШапки = ПолучитьИзВременногоХранилища(Параметры.АдресСпискаАналитик);
		Для Каждого СтрокаТаблицы Из ТаблицаАналитикШапки Цикл
			МассивПараметров.Добавить(СтрокаТаблицы.ВидАналитики);
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивПараметров;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьШаблонПодстановки(Команда)
	 
	ТекстДляВставки = "["+ Элементы[Команда.Имя].Заголовок +"]";
	ВставитьТекстВЗаголовок(ТекстДляВставки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТекстВЗаголовок(ТекстДляВставки, Сдвиг = 0)
	
	СтрокаНач = 0;
	СтрокаКон = 0;
	КолонкаНач = 0;
	КолонкаКон = 0;
	
	Элементы.ТекстовыйБлок.ПолучитьГраницыВыделения(СтрокаНач, КолонкаНач, СтрокаКон, КолонкаКон);
	
	Если (КолонкаКон = КолонкаНач) И (КолонкаКон + СтрДлина(ТекстДляВставки)) > Элементы.ТекстовыйБлок.Ширина / 8 Тогда
		Элементы.ТекстовыйБлок.ВыделенныйТекст = "";
	КонецЕсли;
	
	Элементы.ТекстовыйБлок.ВыделенныйТекст = ТекстДляВставки;
	
	Если Не Сдвиг = 0 Тогда
		Элементы.ТекстовыйБлок.ПолучитьГраницыВыделения(СтрокаНач, КолонкаНач, СтрокаКон, КолонкаКон);
		Элементы.ТекстовыйБлок.УстановитьГраницыВыделения(СтрокаНач, КолонкаНач - Сдвиг, СтрокаКон, КолонкаКон - Сдвиг);
	КонецЕсли;
	ТекущийЭлемент = Элементы.ТекстовыйБлок;
	
КонецПроцедуры

#КонецОбласти

