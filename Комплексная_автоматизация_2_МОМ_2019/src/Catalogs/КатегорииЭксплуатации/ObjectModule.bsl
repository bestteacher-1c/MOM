#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		
		НепроверяемыеРеквизиты.Добавить("СпособПогашенияСтоимостиБУ");
		НепроверяемыеРеквизиты.Добавить("СпособПогашенияСтоимостиНУ");
		НепроверяемыеРеквизиты.Добавить("ЕдиницаИзмеренияНаработки");
		НепроверяемыеРеквизиты.Добавить("ОбъемНаработки");
		//++ НЕ УТ
	Иначе
		Если ИнвентарныйУчет=Истина И СпособПогашенияСтоимостиБУ = Перечисления.СпособыПогашенияСтоимостиТМЦ.ПоНаработке Тогда
			НепроверяемыеРеквизиты.Добавить("СрокЭксплуатации");
		Иначе
			НепроверяемыеРеквизиты.Добавить("ЕдиницаИзмеренияНаработки");
			НепроверяемыеРеквизиты.Добавить("ОбъемНаработки");
		КонецЕсли;
		//-- НЕ УТ
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Отказ ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//++ НЕ УТ
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		ВидыСчетаДляОчистки = Новый Массив;
		Если ЗначениеЗаполнено(СчетУчета) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.ТМЦВЭксплуатации);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетЗабалансовогоУчета) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом);
		КонецЕсли;
		Если ВидыСчетаДляОчистки.Количество() Тогда
			РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОчиститьПриЗаписиАналитикиУчета(Ссылка, ВидыСчетаДляОчистки);
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли