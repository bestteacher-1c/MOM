#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаПередЗаписью.Коэффициент КАК Коэффициент,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ТаблицаПередЗаписью.ДатаПринятияКУчетуУУ КАК ДатаПринятияКУчетуУУ,
	|	ТаблицаПередЗаписью.ДатаПринятияКУчетуБУ КАК ДатаПринятияКУчетуБУ,
	|	ТаблицаПередЗаписью.ПорядокУчетаНУ КАК ПорядокУчетаНУ
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАПередЗаписью
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМА КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период                КАК Период,
	|	Таблица.Организация           КАК Организация,
	|	Таблица.НематериальныйАктив   КАК НематериальныйАктив,
	|	Таблица.ОтражатьВРеглУчете    КАК ОтражатьВРеглУчете,
	|	Таблица.ОтражатьВУпрУчете     КАК ОтражатьВУпрУчете,
	|	&Регистратор                  КАК Документ
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период               КАК Период,
	|		ТаблицаПередЗаписью.Организация          КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив,
	|		ТаблицаПередЗаписью.ДатаПринятияКУчетуБУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВРеглУчете,
	|		ТаблицаПередЗаписью.ДатаПринятияКУчетуУУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВУпрУчете
	|	ИЗ
	|		ПервоначальныеСведенияНМАПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМА КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
	|			ПО ИСТИНА
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаНУ <> ТаблицаПередЗаписью.ПорядокУчетаНУ
	|				ИЛИ ТаблицаПриЗаписи.Коэффициент <> ТаблицаПередЗаписью.Коэффициент
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаПринятияКУчетуБУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаПринятияКУчетуБУ, МЕСЯЦ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаПринятияКУчетуУУ, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаПринятияКУчетуУУ, ДЕНЬ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаПринятияКУчетуУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив,
	|		ТаблицаПриЗаписи.ДатаПринятияКУчетуБУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВРеглУчете,
	|		ТаблицаПриЗаписи.ДатаПринятияКУчетуУУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВУпрУчете
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияНМА КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
	|			ПО ИСТИНА
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаНУ <> ТаблицаПередЗаписью.ПорядокУчетаНУ
	|				ИЛИ ТаблицаПриЗаписи.Коэффициент <> ТаблицаПередЗаписью.Коэффициент
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаПринятияКУчетуБУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаПринятияКУчетуБУ, МЕСЯЦ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И ТаблицаПриЗаписи.ДатаПринятияКУчетуУУ <> ТаблицаПередЗаписью.ДатаПринятияКУчетуУУ
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаПринятияКУчетуУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица";
	
	ВнеоборотныеАктивыЛокализация.ДополнитьТекстЗапросаПриЗаписиВРегистрПервоначальныеСведенияНМА(
		СтруктураВременныеТаблицы.МенеджерВременныхТаблиц, ТекстЗапроса);
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + "УНИЧТОЖИТЬ ПервоначальныеСведенияНМАПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПервоначальныеСведенияНМАИзменение", Выборка.Количество > 0);

	
КонецПроцедуры
 
#КонецОбласти

#КонецЕсли
