#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

Процедура СформироватьПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью(МенеджерВременныхТаблиц, Регистратор) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.ОсновноеСредство,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.ПервоначальнаяСтоимость,
	|	ТаблицаПередЗаписью.ГруппаОС,
	|	ТаблицаПередЗаписью.ПорядокПогашенияСтоимости,
	|	ТаблицаПередЗаписью.ДоговорЛизинга
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.Активность";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.4.9.12";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("436d2ebd-66fe-4ac9-b2e7-daefbb2aa880");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет регистр ""Первоначальные сведения ОС (бухгалтерский учет, версия 2.2)""
	|- заполняет новый ресурс Арендодатель'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеАрендованныхОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеПредметовЛизинга.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет";
	ИмяРегистра = "ПервоначальныеСведенияОСБухгалтерскийУчет";
	
	СписокДокументов = Новый Массив;
	СписокДокументов.Добавить("Документ.ПоступлениеАрендованныхОС");
	
	Для каждого ПолноеИмяДокумента Из СписокДокументов Цикл
		ИмяДокумента = СтрРазделить(ПолноеИмяДокумента, ".")[1];
		ТекстЗапросаМеханизмаПроведения = Документы[ИмяДокумента].АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра);
		Регистраторы = ОбновлениеИнформационнойБазыУТ.РегистраторыДляПерепроведения(
								ТекстЗапросаМеханизмаПроведения,
								ПолноеИмяРегистра,
								ПолноеИмяДокумента);
								
		ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	КонецЦикла; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК Данные
	|ГДЕ
	|	Данные.Регистратор ССЫЛКА Документ.ПоступлениеПредметовЛизинга
	|	И Данные.ДоговорЛизинга.ВариантУчетаИмущества = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаИмуществаПриЛизинге.ЗаБалансом)
	|	И Данные.Арендодатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	                                                                          
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ПоступлениеАрендованныхОС");
	Регистраторы.Добавить("Документ.ПоступлениеПредметовЛизинга");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы, Метаданные.РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.ПолноеИмя(), Параметры.Очередь);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли