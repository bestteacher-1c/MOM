#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Если СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Таблицы.Найти("ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью") <> Неопределено Тогда
		// При принятии к учету ОС регистр очищается, поэтому приходится сохранять данные заранее.
		Возврат;
	КонецЕсли; 
	
	РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СформироватьПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью(
		СтруктураВременныеТаблицы.МенеджерВременныхТаблиц, Отбор.Регистратор.Значение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСБухгалтерскийУчетИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимость <> ТаблицаПередЗаписью.ПервоначальнаяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.ГруппаОС <> ТаблицаПередЗаписью.ГруппаОС
	|				ИЛИ ТаблицаПриЗаписи.ПорядокПогашенияСтоимости <> ТаблицаПередЗаписью.ПорядокПогашенияСтоимости
	|				ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимость <> ТаблицаПередЗаписью.ПервоначальнаяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.ГруппаОС <> ТаблицаПередЗаписью.ГруппаОС
	|				ИЛИ ТаблицаПриЗаписи.ПорядокПогашенияСтоимости <> ТаблицаПередЗаписью.ПорядокПогашенияСтоимости
	|				ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСБухгалтерскийУчет_ЛизингИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.ДоговорЛизинга <> ТаблицаПередЗаписью.ДоговорЛизинга
	|				ИЛИ ТаблицаПриЗаписи.Период ЕСТЬ NULL)
	|			И ТаблицаПередЗаписью.ДоговорЛизинга <> ЗНАЧЕНИЕ(Справочник.ДоговорыЛизинга.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.ДоговорЛизинга <> ТаблицаПередЗаписью.ДоговорЛизинга
	|				ИЛИ ТаблицаПередЗаписью.Период ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.ДоговорЛизинга <> ЗНАЧЕНИЕ(Справочник.ДоговорыЛизинга.ПустаяСсылка)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПервоначальныеСведенияОСБухгалтерскийУчетПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Выборка.Следующий();
	СтруктураВременныеТаблицы.Вставить("ПервоначальныеСведенияОСБухгалтерскийУчетИзменение", Выборка.Количество > 0);
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Выборка.Следующий();
	СтруктураВременныеТаблицы.Вставить("ПервоначальныеСведенияОСБухгалтерскийУчет_ЛизингИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
