#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.СобытияОСОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.4.10.27";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e9aaf4d4-c7a7-415d-bd89-a3fe3863d803");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СобытияОСОрганизаций.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет движения регистра ""События ОС организаций""
	|- добавляет движения для документов ""Поступление арендованных ОС"", ""Выбытие арендованных ОС""
	|- исправляет движения для документов ""Модернизация ОС""'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ВыбытиеАрендованныхОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеАрендованныхОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.СтоимостьОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.СобытияОСОрганизаций.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.СобытияОСОрганизаций.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.СобытияОСОрганизаций";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПоступлениеАрендованныхОС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеАрендованныхОС КАК ПоступлениеАрендованныхОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
	|		ПО (СобытияОСОрганизаций.Регистратор = ПоступлениеАрендованныхОС.Ссылка)
	|ГДЕ
	|	ПоступлениеАрендованныхОС.Проведен
	|	И СобытияОСОрганизаций.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыбытиеАрендованныхОС.Ссылка
	|ИЗ
	|	Документ.ВыбытиеАрендованныхОС КАК ВыбытиеАрендованныхОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
	|		ПО (СобытияОСОрганизаций.Регистратор = ВыбытиеАрендованныхОС.Ссылка)
	|ГДЕ
	|	ВыбытиеАрендованныхОС.Проведен
	|	И СобытияОСОрганизаций.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтоимостьОС.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СобытияОСОрганизаций КАК СобытияОСОрганизаций
	|		ПО (СобытияОСОрганизаций.Регистратор = СтоимостьОС.Регистратор)
	|			И (СобытияОСОрганизаций.ОсновноеСредство = СтоимостьОС.ОсновноеСредство)
	|ГДЕ
	|	СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
	|	И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И СтоимостьОС.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.Регистратор,
	|	СтоимостьОС.ОсновноеСредство
	|
	|ИМЕЮЩИЕ
	|	СУММА(СтоимостьОС.СтоимостьРегл) <> МАКСИМУМ(ЕСТЬNULL(СобытияОСОрганизаций.СуммаЗатратБУ, 0))";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
								
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.СобытияОСОрганизаций";
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ПоступлениеАрендованныхОС");
	Регистраторы.Добавить("Документ.ВыбытиеАрендованныхОС");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
							Регистраторы, ПолноеИмяРегистра, Параметры.Очередь);
	
	//
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация КАК Организация,
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	ДанныеДокумента.Номер КАК НомерДокумента,
	|	СУММА(ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, ТаблицаОС.СтоимостьБУ - ТаблицаОС.СуммаЦелевыхСредств)) КАК СуммаЗатратБУ,
	|	СУММА(ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, ТаблицаОС.СтоимостьБУ)) КАК СуммаЗатратНУ
	|ИЗ
	|	Документ.МодернизацияОС2_4 КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МодернизацияОС2_4.ОС КАК ТаблицаОС
	|		ПО (ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|		ПО (СтоимостьОС.Регистратор = ТаблицаОС.Ссылка)
	|			И (СтоимостьОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|			И (СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|			И (СтоимостьОС.Активность)
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Регистратор
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Организация,
	|	ТаблицаОС.ОсновноеСредство,
	|	ДанныеДокумента.Номер";
	
	ЗапросСуммаЗатратБУ = Новый Запрос(ТекстЗапроса);
	ЗапросСуммаЗатратБУ.УстановитьПараметр("НазваниеДокумента", НСтр("ru='Модернизация ОС'"));
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(Параметры.Очередь, "Документ.МодернизацияОС2_4", ПолноеИмяРегистра);
	Пока Выборка.Следующий() Цикл
	
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СобытияОСОрганизаций.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СтоимостьОС.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СобытияОСОрганизаций.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			
			ЗапросСуммаЗатратБУ.УстановитьПараметр("Регистратор", Выборка.Регистратор);
			НаборЗаписей.Загрузить(ЗапросСуммаЗатратБУ.Выполнить().Выгрузить());
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор);
			КонецЕсли; 
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать записи регистра ""События ОС организаций"": %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.СобытияОСОрганизаций,
				Выборка.Регистратор,
				ТекстСообщения);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли