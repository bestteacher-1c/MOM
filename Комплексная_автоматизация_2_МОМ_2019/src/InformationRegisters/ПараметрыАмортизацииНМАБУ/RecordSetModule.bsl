#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаПередЗаписью.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ТаблицаПередЗаписью.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	ТаблицаПередЗаписью.КоэффициентБУ КАК КоэффициентБУ
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАБУПередЗаписью
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАБУ КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	ИСТИНА                      КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                        КАК ОтражатьВУпрУчете,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАБУИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период               КАК Период,
	|		ТаблицаПередЗаписью.Организация          КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		ПараметрыАмортизацииНМАБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииНМАБУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользованияБУ <> ТаблицаПередЗаписью.СрокПолезногоИспользованияБУ
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользованияНУ <> ТаблицаПередЗаписью.СрокПолезногоИспользованияНУ
	|				ИЛИ ТаблицаПриЗаписи.КоэффициентБУ <> ТаблицаПередЗаписью.КоэффициентБУ
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.ПараметрыАмортизацииНМАБУ КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользованияБУ <> ТаблицаПередЗаписью.СрокПолезногоИспользованияБУ
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользованияНУ <> ТаблицаПередЗаписью.СрокПолезногоИспользованияНУ
	|				ИЛИ ТаблицаПриЗаписи.КоэффициентБУ <> ТаблицаПередЗаписью.КоэффициентБУ
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПараметрыАмортизацииНМАБУПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПараметрыАмортизацииНМАБУИзменение", Выборка.Количество > 0);
	
	
КонецПроцедуры
 
#КонецОбласти

#КонецЕсли

