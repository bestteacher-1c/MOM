#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.ОбъектЭксплуатации,
	|	ТаблицаПередЗаписью.ПоказательНаработки,
	|	ТаблицаПередЗаписью.Значение,
	|	ТаблицаПередЗаписью.СреднесуточноеЗначение
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииПередЗаписью
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.Активность
	|	И ТаблицаПередЗаписью.ОбъектЭксплуатации ССЫЛКА Справочник.ОбъектыЭксплуатации";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		Или Не ДополнительныеСвойства.Свойство("ДляПроведения")
		Или Не ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	СписокЗапросов = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(Таблица.Период)  КАК Период,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период              КАК Период,
	|		ТаблицаПередЗаписью.ОбъектЭксплуатации  КАК ОсновноеСредство
	|	ИЗ
	|		НаработкиОбъектовЭксплуатацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОбъектЭксплуатации = ТаблицаПередЗаписью.ОбъектЭксплуатации
	|				И ТаблицаПриЗаписи.ПоказательНаработки = ТаблицаПередЗаписью.ПоказательНаработки
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				ИЛИ ТаблицаПриЗаписи.Значение <> ТаблицаПередЗаписью.Значение
	|				ИЛИ ТаблицаПриЗаписи.СреднесуточноеЗначение <> ТаблицаПередЗаписью.СреднесуточноеЗначение
	|				ИЛИ ТаблицаПриЗаписи.Период ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.ОбъектЭксплуатации
	|	ИЗ
	|		РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ НаработкиОбъектовЭксплуатацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОбъектЭксплуатации = ТаблицаПередЗаписью.ОбъектЭксплуатации
	|				И ТаблицаПриЗаписи.ПоказательНаработки = ТаблицаПередЗаписью.ПоказательНаработки
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, ДЕНЬ)
	|				ИЛИ ТаблицаПриЗаписи.Значение <> ТаблицаПередЗаписью.Значение
	|				ИЛИ ТаблицаПриЗаписи.СреднесуточноеЗначение <> ТаблицаПередЗаписью.СреднесуточноеЗначение
	|				ИЛИ ТаблицаПередЗаписью.Период ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ОсновноеСредство
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаНаработкиОбъектовЭксплуатацииИзменение();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(ПараметрыАмортизацииОСУУ.Период) КАК ПараметрыАмортизацииОСУУПериод,
		|	ТаблицаИзменение.Период КАК Период,
		|	ТаблицаИзменение.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУПоследнее
		|ИЗ
		|	НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации КАК ТаблицаИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|		ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ТаблицаИзменение.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУ.Период <= ТаблицаИзменение.Период)
		|			И (ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации В (
		|					ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции), 
		|					ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменение.ОсновноеСредство,
		|	ТаблицаИзменение.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменение.Период КАК Период,
		|	ТаблицаИзменение.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.Организация КАК Организация,
		|	ЛОЖЬ КАК ОтражатьВРеглУчете,
		|	ИСТИНА КАК ОтражатьВУпрУчете,
		|	&Регистратор КАК Документ
		|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииИзменение
		|ИЗ
		|	НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации КАК ТаблицаИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУПоследнее КАК ПараметрыАмортизацииОСУУПоследнее
		|		ПО (ТаблицаИзменение.ОсновноеСредство = ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство)
		|			И (ТаблицаИзменение.Период = ПараметрыАмортизацииОСУУПоследнее.Период)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|		ПО (ПараметрыАмортизацииОСУУ.ОсновноеСредство = ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУ.Период = ПараметрыАмортизацииОСУУПоследнее.ПараметрыАмортизацииОСУУПериод)
		|ГДЕ
		|	НЕ ПараметрыАмортизацииОСУУ.ОсновноеСредство ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПараметрыАмортизацииОСУУПоследнее
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатацииПередЗаписью";
		
	КонецЕсли; 
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[4].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("НаработкиОбъектовЭксплуатацииИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
