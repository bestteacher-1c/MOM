#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	//++ НЕ УТ
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьТаблицуПередЗаписьюДляФормированияЗаданий();
	//-- НЕ УТ
	
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	// Заполним ресурсы регистра, кэшируемые из справочника учетной политики.
	КэшПараметровПоОрганизациям = Новый Соответствие;
	
	Для Каждого ТекСтр Из ЭтотОбъект Цикл
		
		ПараметрыУчетнойПолитики = Неопределено;
		ДанныеКэша = КэшПараметровПоОрганизациям.Получить(ТекСтр.Организация);
		
		Если ДанныеКэша <> Неопределено Тогда
			
			ПараметрыУчетнойПолитики = ДанныеКэша.Получить(ТекСтр.УчетнаяПолитика);
			
			Если ПараметрыУчетнойПолитики = Неопределено Тогда
				
				ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.СформироватьКэшируемыеПараметрыУчетнойПолитики(
					ТекСтр.УчетнаяПолитика,
					ТекСтр.Организация);
				
				КэшПараметровПоОрганизациям[ТекСтр.Организация].Вставить(ТекСтр.УчетнаяПолитика, ПараметрыУчетнойПолитики);
				
			КонецЕсли;
			
		Иначе
			
			КэшПараметровПоОрганизациям.Вставить(
				ТекСтр.Организация,
				Новый Соответствие);
				
			ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.СформироватьКэшируемыеПараметрыУчетнойПолитики(
				ТекСтр.УчетнаяПолитика,
				ТекСтр.Организация);
				
			КэшПараметровПоОрганизациям[ТекСтр.Организация].Вставить(ТекСтр.УчетнаяПолитика, ПараметрыУчетнойПолитики);
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ТекСтр, ПараметрыУчетнойПолитики);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	//++ НЕ УТ
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьЗаданияПриИзмененииУчетнойПолитики();
	
	//-- НЕ УТ
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УточняемыеДанные.Период КАК Период,
	|	УточняемыеДанные.Организация КАК Организация,
	|	УточняемыеДанные.УчетнаяПолитика КАК УчетнаяПолитика,
	|	УточняемыеДанные.ПлательщикЕНВД КАК ПлательщикЕНВД,
	|	УточняемыеДанные.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	УточняемыеДанные.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	УточняемыеДанные.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	УточняемыеДанные.ВедетсяУчетПостоянныхИВременныхРазниц КАК ВедетсяУчетПостоянныхИВременныхРазниц
	|ПОМЕСТИТЬ ВТНовыеДанные
	|ИЗ
	|	&Данные КАК УточняемыеДанные
	|";
	
	Запрос.УстановитьПараметр("Данные", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить(); 
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НовыеДанные.Период КАК Период,
	|	Организации.Ссылка КАК Организация,
	|	НовыеДанные.УчетнаяПолитика КАК УчетнаяПолитика,
	|	НовыеДанные.ПлательщикЕНВД КАК ПлательщикЕНВД,
	|	НовыеДанные.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	НовыеДанные.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	НовыеДанные.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	НовыеДанные.ВедетсяУчетПостоянныхИВременныхРазниц
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыеДанные КАК НовыеДанные
	|		ПО Организации.Ссылка.ГоловнаяОрганизация = НовыеДанные.Организация
	|ГДЕ
	|	Организации.Ссылка <> Организации.ГоловнаяОрганизация
	|	И Организации.ГоловнаяОрганизация В
	|			(ВЫБРАТЬ
	|				Организации.Организация
	|			ИЗ
	|				ВТНовыеДанные КАК Организации
	|			ГДЕ
	|				Организации.Организация = Организации.Организация.ГоловнаяОрганизация)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),Выборка);
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//++ НЕ УТ

Процедура СформироватьТаблицуПередЗаписьюДляФормированияЗаданий()

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц_ЛизингИзменение", Новый МенеджерВременныхТаблиц);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПриЗаписи.Период           КАК Период,
	|	ТаблицаПриЗаписи.Организация      КАК Организация,
	|	ТаблицаПриЗаписи.УчетнаяПолитика  КАК УчетнаяПолитика
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций_ЛизингИзменение_ПередЗаписью
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК ТаблицаПриЗаписи
	|ГДЕ
	|	ТаблицаПриЗаписи.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи
	|	//ОтборОрганизация//
	|	//ОтборПериод//
	|";
	
	Если Отбор.Организация.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
							"//ОтборОрганизация//", 
							"И ТаблицаПриЗаписи.Организация = &Организация");
	КонецЕсли;
	
	Если Отбор.Период.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
							"//ОтборПериод//", 
							"И ТаблицаПриЗаписи.Период = &Период");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц_ЛизингИзменение;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация.Значение);
	Запрос.УстановитьПараметр("Период", Отбор.Период.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СформироватьЗаданияПриИзмененииУчетнойПолитики()

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = Новый Структура("МенеджерВременныхТаблиц", ДополнительныеСвойства.МенеджерВременныхТаблиц_ЛизингИзменение);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период        КАК Период,
	|	ТаблицаПередЗаписью.Организация   КАК Организация,
	|	НЕОПРЕДЕЛЕНО                      КАК Документ
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций_ЛизингИзменение
	|ИЗ
	|	УчетнаяПолитикаОрганизаций_ЛизингИзменение_ПередЗаписью КАК ТаблицаПередЗаписью
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК ТаблицаПриЗаписи
	|		ПО ТаблицаПриЗаписи.Период = ТаблицаПередЗаписью.Период
	|			И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаПередЗаписью.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи, ЛОЖЬ)
	|	И НЕ ЕСТЬNULL(ТаблицаПриЗаписи.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи, ЛОЖЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаПриЗаписи.Период           КАК Период,
	|	ТаблицаПриЗаписи.Организация      КАК Организация,
	|	НЕОПРЕДЕЛЕНО                      КАК Документ
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК ТаблицаПриЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций_ЛизингИзменение_ПередЗаписью КАК ТаблицаПередЗаписью
	|		ПО ТаблицаПриЗаписи.Период = ТаблицаПередЗаписью.Период
	|			И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаПриЗаписи.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи, ЛОЖЬ)
	|	И НЕ ЕСТЬNULL(ТаблицаПередЗаписью.УчетнаяПолитика.ВключатьВСоставНалоговыхРасходовЛизинговыеПлатежи, ЛОЖЬ)
	|	//ОтборОрганизация//
	|	//ОтборПериод//
	|";
	
	Если Отбор.Организация.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
							"//ОтборОрганизация//", 
							"И ТаблицаПриЗаписи.Организация = &Организация");
	КонецЕсли;
	
	Если Отбор.Период.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
							"//ОтборПериод//", 
							"И ТаблицаПриЗаписи.Период = &Период");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация.Значение);
	Запрос.УстановитьПараметр("Период", Отбор.Период.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("УчетнаяПолитикаОрганизаций_ЛизингИзменение", Выборка.Количество > 0);
	
	ВнеоборотныеАктивыЛокализация.СформироватьЗаданиеПризнаниеВНУЛизинговыхПлатежей(СтруктураВременныеТаблицы, Истина, Истина);
	
КонецПроцедуры

//-- НЕ УТ
#КонецОбласти

#КонецЕсли
