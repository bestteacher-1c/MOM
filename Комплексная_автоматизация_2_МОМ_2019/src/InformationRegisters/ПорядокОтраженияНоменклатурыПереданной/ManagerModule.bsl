#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает результат выполнения анализатора отражения номенклатуры переданной, с настроенными счетами.
//	
//	Параметры:
//		МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям;
//		Период - СтандартныйПериод - период с датой начала и окончания анализа.
//	ВозвращаемоеЗначение:
//		РезультатЗапроса - список всех операций, удовлетворяющий входным параметрам с настроенными для них счетами.
//
Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ПараметрыАнализатора = Новый Структура("МассивОрганизаций, НачалоПериода, КонецПериода", МассивОрганизаций, Период.ДатаНачала, Период.ДатаОкончания);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияНоменклатуры.Организация КАК Организация,
	|	ДвиженияНоменклатуры.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	
	|	ДвиженияНоменклатуры.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаПередачиНаКомиссию
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаПередачиНаКомиссию
	|	КОНЕЦ КАК СчетУчетаПередачиНаКомиссию,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаПередачиНаКомиссию, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаПередачиНаКомиссиюПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет)  <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет)  <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНДСПриПродаже
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже
	|	КОНЕЦ КАК СчетУчетаНДСПриПродаже,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет)  <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ПорядокОтражения.СтатьяДоходовОСНО
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяДоходовОСНО
	|	КОНЕЦ КАК СтатьяДоходовОСНО,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовОСНО, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяДоходовОСНОПоУмолчанию
	|ИЗ
	|	ДвиженияНоменклатуры КАК ДвиженияНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияНоменклатурыПереданной КАК ПорядокОтражения
	|	ПО
	|		ДвиженияНоменклатуры.Организация = ПорядокОтражения.Организация
	|		И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|	ПО
	|		ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокОтражения.Организация КАК Организация,
	|	ПорядокОтражения.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаПередачиНаКомиссию
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаПередачиНаКомиссию
	|	КОНЕЦ КАК СчетУчетаПередачиНаКомиссию,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаПередачиНаКомиссию, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаПередачиНаКомиссиюПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет)  <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет)  <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНДСПриПродаже
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже
	|	КОНЕЦ КАК СчетУчетаНДСПриПродаже,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет)  <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ПорядокОтражения.СтатьяДоходовОСНО
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяДоходовОСНО
	|	КОНЕЦ КАК СтатьяДоходовОСНО,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовОСНО, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяДоходовОСНОПоУмолчанию
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияНоменклатурыПереданной КАК ПорядокОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДвиженияНоменклатуры КАК ДвиженияНоменклатуры
	|	ПО
	|		ПорядокОтражения.Организация = ДвиженияНоменклатуры.Организация
	|		И ПорядокОтражения.ГруппаФинансовогоУчета = ДвиженияНоменклатуры.ГруппаФинансовогоУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|	ПО
	|		ПорядокОтражения.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|
	|ГДЕ
	|	ПорядокОтражения.Организация В (&МассивОрганизаций)
	|	И ДвиженияНоменклатуры.Организация ЕСТЬ NULL
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Организация Возр,
	|	ГруппаФинансовогоУчета ВОЗР
	|";
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтатья", ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ДанныеРегистра.ВидЗапасов
	|ПОМЕСТИТЬ ДвиженияРегистраСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	КлючиПартнеров.Организация,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	НЕ ДанныеРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути)) КАК ЕстьПродажаСобственныхТоваров,
	|	ДанныеРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути)) КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ПОМЕСТИТЬ ДвиженияРегистраВыручки
	|ИЗ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиПартнеров
	|		ПО КлючиПартнеров.КлючАналитики = ДанныеРегистра.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	И (ДанныеРегистра.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|		ИЛИ ДанныеРегистра.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПередачаТоваровПереработчику,
	|	ЛОЖЬ КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ДанныеРегистра
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.Ссылка = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДанныеРегистра.ВидЗапасов
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ВидыЗапасовСпр.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасовСпр.Организация КАК Организация,
	|	ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ИСТИНА КАК ЕстьПередачаТоваровПереработчику,
	|	ЛОЖЬ КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ИЗ РегистрНакопления.ТоварыПереданныеПереработчику КАК ДанныеРегистра
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.Ссылка = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДанныеРегистра.ВидЗапасов
	|ГДЕ
	|	&ОрганизацияИзСправочника_УсловиеДляРегистра
	|	И ВидыЗапасовСпр.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраВыручки.Организация,
	|	ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ДвиженияРегистраВыручки.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПередачаТоваровПереработчику,
	|	ДвиженияРегистраВыручки.ЕстьЗатратыНаПриобретениеТМЦ КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ИЗ ДвиженияРегистраВыручки КАК ДвиженияРегистраВыручки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = ДвиженияРегистраВыручки.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраВыручки.ВидЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраСебестоимости.Организация,
	|	ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ТОГДА НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПередачаТоваровПереработчику,
	|	ИСТИНА КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ИЗ ДвиженияРегистраСебестоимости КАК ДвиженияРегистраСебестоимости
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО НоменклатураСпр.Ссылка = ДвиженияРегистраСебестоимости.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраСебестоимости.ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСобственныхТоваров) КАК ЕстьПродажаСобственныхТоваров,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПередачаТоваровПереработчику) КАК ЕстьПередачаТоваровПереработчику,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьЗатратыНаПриобретениеТМЦ) КАК ЕстьЗатратыНаПриобретениеТМЦ
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраВыручки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки";
	
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета",
		ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
		
	УсловиеВхожденияВРегистр = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляРегистра";
	УсловиеВхожденияВРегистрССоединениемПоОрганизации = "ГДЕ" + Символы.ПС + Символы.Таб + "&ОрганизацияИзРегистраСведений_УсловиеДляРегистра";
	УсловиеВхожденияВСправочникССоединениемПоОрганизации = "ГДЕ" + Символы.ПС + Символы.Таб + "&ОрганизацияИзСправочника_УсловиеДляРегистра";
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеРегистра.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ";
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеРегистра.Организация В (&Организации)";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра.Организация", "КлючиПартнеров.Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистрССоединениемПоОрганизации, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "КлючиПартнеров", "ВидыЗапасовСпр");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВСправочникССоединениемПоОрганизации, ТекстУсловий);
	Иначе
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыАнализатора.КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра.Организация", "КлючиПартнеров.Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистрССоединениемПоОрганизации, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "КлючиПартнеров", "ВидыЗапасовСпр");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВСправочникССоединениемПоОрганизации, ТекстУсловий);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДвиженияНоменклатуры.Организация КАК Организация,
	|	ДвиженияНоменклатуры.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ДвиженияНоменклатуры.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	ДвиженияНоменклатуры.ЕстьЗатратыНаПриобретениеТМЦ КАК ЕстьЗатратыНаПриобретениеТМЦ,
	|	ДвиженияНоменклатуры.ЕстьПередачаТоваровПереработчику КАК ЕстьПередачаТоваровПереработчику,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчетаПередачиНаКомиссию, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаПередачиНаКомиссию, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаПередачиНаКомиссию КОНЕЦ КАК СчетУчетаПередачиНаКомиссию,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчетаВыручкиОтПродаж, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВыручкиОтПродаж, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаВыручкиОтПродаж КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчетаСебестоимостиПродаж, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаСебестоимостиПродаж, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаСебестоимостиПродаж КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчетаНДСПриПродаже, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСПриПродаже, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаНДСПриПродаже КОНЕЦ КАК СчетУчетаНДСПриПродаже,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаЗатратыНаПриобретение, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчетаЗатратыНаПриобретение, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаЗатратыНаПриобретение, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаЗатратыНаПриобретение КОНЕЦ КАК СчетУчетаЗатратыНаПриобретение,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиВПереработку, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчетаПередачиВПереработку, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаПередачиВПереработку, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаПередачиВПереработку КОНЕЦ КАК СчетУчетаПередачиВПереработку,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СтатьяДоходовОСНО, ЕСТЬNULL(ОбщаяНастройка.СтатьяДоходовОСНО, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)))
	|		ИНАЧЕ ПорядокОтражения.СтатьяДоходовОСНО КОНЕЦ КАК СтатьяДоходовОСНО,
	|
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаПередачиНаКомиссию, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаПередачиНаКомиссию, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет)
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СчетУчетаПередачиНаКомиссию, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаПередачиНаКомиссию, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиНаКомиссию, &ПустойСчет) = &ПустойСчет КАК СчетУчетаПередачиНаКомиссиюПоУмолчанию,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаВыручкиОтПродаж, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВыручкиОтПродаж, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет)
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СчетУчетаВыручкиОтПродаж, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВыручкиОтПродаж, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаСебестоимостиПродаж, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаСебестоимостиПродаж, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет)
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СчетУчетаСебестоимостиПродаж, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаСебестоимостиПродаж, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаНДСПриПродаже, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСПриПродаже, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет)
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СчетУчетаНДСПриПродаже, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСПриПродаже, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаЗатратыНаПриобретение, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаЗатратыНаПриобретение, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаЗатратыНаПриобретение, &ПустойСчет)
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СчетУчетаЗатратыНаПриобретение, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаЗатратыНаПриобретение, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаЗатратыНаПриобретение, &ПустойСчет) = &ПустойСчет КАК СчетУчетаЗатратыНаПриобретениеПоУмолчанию,
	|	ЕСТЬNULL(АналитикаУчета.СчетУчетаПередачиВПереработку, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаПередачиВПереработку, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиВПереработку, &ПустойСчет)
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СчетУчетаПередачиВПереработку, ЕСТЬNULL(ОбщаяНастройка.СчетУчетаПередачиВПереработку, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаПередачиВПереработку, &ПустойСчет) = &ПустойСчет КАК СчетУчетаПередачиВПереработкуПоУмолчанию,
	|	ЕСТЬNULL(АналитикаУчета.СтатьяДоходовОСНО, ЕСТЬNULL(ОбщаяНастройка.СтатьяДоходовОСНО, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)))
	|		= ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка))
	|		ИЛИ ЕСТЬNULL(АналитикаУчета.СтатьяДоходовОСНО, ЕСТЬNULL(ОбщаяНастройка.СтатьяДоходовОСНО, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка))) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|		И ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовОСНО, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка))
	|		= ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходовОСНОПоУмолчанию
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвижений КАК ДвиженияНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНоменклатурыПереданной КАК ПорядокОтражения
	|		ПО ДвиженияНоменклатуры.Организация = ПорядокОтражения.Организация
	|			И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК АналитикаУчета
	|		ПО ДвиженияНоменклатуры.ГруппаФинансовогоУчета = АналитикаУчета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНоменклатурыПереданной КАК ОбщаяНастройка
	|		ПО ОбщаяНастройка.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И ОбщаяНастройка.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Выполняет анализ всех неотраженных операций отражения номенклатуры переданной,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	Параметры:
//		ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПередачаНаКомиссию) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиНаКомиссию = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродажКомиссионера) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриПродажеКомиссионера) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СебестоимостьПродажКомиссионера) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьЗатратыНаПриобретениеТМЦ
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаЗатратыНаПриобретение = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПередачаВПереработку) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьПередачаТоваровПереработчику
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиВПереработку = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураВидовСчетовИРезультатаЗапроса = Новый Соответствие;
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ПередачаНаКомиссию, МассивРезультатов.Получить(0));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродажКомиссионера, МассивРезультатов.Получить(1));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.НДСПриПродажеКомиссионера, МассивРезультатов.Получить(2));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродажКомиссионера, МассивРезультатов.Получить(3));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ, МассивРезультатов.Получить(4));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ПередачаВПереработку, МассивРезультатов.Получить(5));
	
	ИмяСобытия = НСтр("ru = 'Анализ порядка отражения номенклатуры контрагентов, требующего настройки'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(СтруктураВидовСчетовИРезультатаЗапроса, ИмяСобытия);

КонецПроцедуры

// Выполняет анализ операций отражения номенклатуры переданной, в зависимости от параметров и дополняет исходную таблицу
// операций новыми строками.
//	
//	Параметры:
//		ПараметрыАнализа - Структура - содержит настройки анализатора со следующими свойствами:
//			* РежимАнализа - Строка - принимает значения вида "ПоНеотраженным" и "ЗаПериод". Причем если не задана, значение по умолчанию "ЗаПериод".
//			* КонецПериода - Дата - по эту дату анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* НачалоПериода - Дата - начиная с этой даты анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//		АдресХранилища - Строка - адрес сохранения результата работы метода (ТаблицаЗначений).
//
Процедура ДополнитьТаблицуНаОснованииАнализаОпераций(ПараметрыАнализа, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализа);
	 
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ПараметрыАнализа.ЗаполняемаяТаблица);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретение,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработку,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовОСНО,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссиюПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретениеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработкуПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовОСНОПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаПередачиНаКомиссию,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаЗатратыНаПриобретение,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаПередачиВПереработку,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ПОМЕСТИТЬ ТекущаяТаблицаСчетовУчета
	|ИЗ
	|	&Таблица КАК ТекущаяТаблицаСчетовУчета;
	|
	|///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета КАК ГруппаФинансовогоУчета,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссию, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиНаКомиссию) КАК СчетУчетаПередачиНаКомиссию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж) КАК СчетУчетаВыручкиОтПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж) КАК СчетУчетаСебестоимостиПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже) КАК СчетУчетаНДСПриПродаже,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретение, ДвиженияНоменклатурыСоСчетами.СчетУчетаЗатратыНаПриобретение) КАК СчетУчетаЗатратыНаПриобретение,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработку, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиВПереработку) КАК СчетУчетаПередачиВПереработку,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СтатьяДоходовОСНО, ДвиженияНоменклатурыСоСчетами.СтатьяДоходовОСНО) КАК СтатьяДоходовОСНО,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссиюПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиНаКомиссиюПоУмолчанию) КАК СчетУчетаПередачиНаКомиссиюПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродажПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродажПоУмолчанию) КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродажПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродажПоУмолчанию) КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродажеПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродажеПоУмолчанию) КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретениеПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаЗатратыНаПриобретениеПоУмолчанию) КАК СчетУчетаЗатратыНаПриобретениеПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработкуПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиВПереработкуПоУмолчанию) КАК СчетУчетаПередачиВПереработкуПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СтатьяДоходовОСНОПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СтатьяДоходовОСНОПоУмолчанию) КАК СтатьяДоходовОСНОПоУмолчанию,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссию, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиНаКомиссию)
	|		= ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК ТребуетсяНастройкаСчетУчетаПередачиНаКомиссию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров КАК ТребуетсяНастройкаСчетУчетаВыручкиОтПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров КАК ТребуетсяНастройкаСчетУчетаНДСПриПродаже,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров КАК ТребуетсяНастройкаСчетУчетаСебестоимостиПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретение, ДвиженияНоменклатурыСоСчетами.СчетУчетаЗатратыНаПриобретение) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьЗатратыНаПриобретениеТМЦ КАК ТребуетсяНастройкаСчетУчетаЗатратыНаПриобретение,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработку, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиВПереработку) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПередачаТоваровПереработчику КАК ТребуетсяНастройкаСчетУчетаПередачиВПереработку,
	|
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссию, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиНаКомиссию)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретение, ДвиженияНоменклатурыСоСчетами.СчетУчетаЗатратыНаПриобретение) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьЗатратыНаПриобретениеТМЦ) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработку, ДвиженияНоменклатурыСоСчетами.СчетУчетаПередачиВПереработку) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПередачаТоваровПереработчику) КАК ТребуетсяНастройка,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.ИзмененныеДанные, ""СчетУчетаПередачиНаКомиссию, СчетУчетаВыручкиОтПродаж, СчетУчетаНДСПриПродаже, СчетУчетаСебестоимостиПродаж, СчетУчетаЗатратыНаПриобретение, СчетУчетаПередачиВПереработку"") КАК ИзмененныеДанные
	|
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|	ПО
	|		ДвиженияНоменклатурыСоСчетами.Организация = ТекущаяТаблицаСчетовУчета.Организация
	|		И ДвиженияНоменклатурыСоСчетами.АналитикаУчета = ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретение,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработку,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовОСНО,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиНаКомиссиюПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаЗатратыНаПриобретениеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПередачиВПереработкуПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовОСНОПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаПередачиНаКомиссию,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаЗатратыНаПриобретение,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаПередачиВПереработку,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|ИЗ
	|	ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|	ПО
	|		ТекущаяТаблицаСчетовУчета.Организация = ДвиженияНоменклатурыСоСчетами.Организация
	|		И ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета = ДвиженияНоменклатурыСоСчетами.АналитикаУчета
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация ЕСТЬ NULL";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Новый Структура("СчетаУчетаНоменклатурыПереданной", Таблица), АдресХранилища);
	
КонецПроцедуры

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ИзмененныеДанные <> """"
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНастроек.Организация КАК Организация,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета = ТаблицаНастроек.ГруппаФинансовогоУчета
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация, ГруппаФинансовогоУчета, ИзмененныеДанные");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецЕсли