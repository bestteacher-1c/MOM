#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ТаблицаПередЗаписью.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ТаблицаПередЗаписью.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ТаблицаПередЗаписью.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ТаблицаПередЗаписью.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ТаблицаПередЗаписью.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ТаблицаПередЗаписью.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ТаблицаПередЗаписью.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСПередЗаписью
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период             КАК Период,
	|	Таблица.Организация        КАК Организация,
	|	Таблица.ОсновноеСредство   КАК ОсновноеСредство,
	|	Таблица.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	Таблица.ОтражатьВУпрУчете  КАК ОтражатьВУпрУчете,
	|	&Регистратор               КАК Документ
	|ПОМЕСТИТЬ ПервоначальныеСведенияОСИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство,
	|		ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВРеглУчете,
	|		ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВУпрУчете
	|	ИЗ
	|		ПервоначальныеСведенияОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
	|			ПО ИСТИНА
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаБУ <> ТаблицаПередЗаписью.ПорядокУчетаБУ
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаНУ <> ТаблицаПередЗаписью.ПорядокУчетаНУ
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаУУ <> ТаблицаПередЗаписью.ПорядокУчетаУУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииНУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ
	|				ИЛИ ТаблицаПриЗаписи.СтоимостьДо2002 <> ТаблицаПередЗаписью.СтоимостьДо2002
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2002 <> ТаблицаПередЗаписью.АмортизацияДо2002
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2009 <> ТаблицаПередЗаписью.АмортизацияДо2009
	|				ИЛИ ТаблицаПриЗаписи.ФактическийСрокИспользованияДо2009 <> ТаблицаПередЗаписью.ФактическийСрокИспользованияДо2009
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, ДЕНЬ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, ДЕНЬ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.ВариантПримененияЦелевогоФинансирования <> ТаблицаПередЗаписью.ВариантПримененияЦелевогоФинансирования)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство,
	|		ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюБУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВРеглУчете,
	|		ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ <> ДАТАВРЕМЯ(1, 1, 1) КАК ОтражатьВУпрУчете
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияОС КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОСПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|			ЛЕВОЕ СОЕДИНЕНИЕ Константа.ПорядокУчетаВНАВУпрУчете КАК ПорядокУчетаВНАВУпрУчете
	|			ПО ИСТИНА
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаБУ <> ТаблицаПередЗаписью.ПорядокУчетаБУ
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаНУ <> ТаблицаПередЗаписью.ПорядокУчетаНУ
	|				ИЛИ ТаблицаПриЗаписи.ПорядокУчетаУУ <> ТаблицаПередЗаписью.ПорядокУчетаУУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииБУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииБУ
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизацииНУ <> ТаблицаПередЗаписью.МетодНачисленияАмортизацииНУ
	|				ИЛИ ТаблицаПриЗаписи.СтоимостьДо2002 <> ТаблицаПередЗаписью.СтоимостьДо2002
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2002 <> ТаблицаПередЗаписью.АмортизацияДо2002
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2009 <> ТаблицаПередЗаписью.АмортизацияДо2009
	|				ИЛИ ТаблицаПриЗаписи.ФактическийСрокИспользованияДо2009 <> ТаблицаПередЗаписью.ФактическийСрокИспользованияДо2009
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ)
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ) 
	|					И ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ <> ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ
	|				ИЛИ ПорядокУчетаВНАВУпрУчете.Значение = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл) 
	|					И НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.ВариантПримененияЦелевогоФинансирования <> ТаблицаПередЗаписью.ВариантПримененияЦелевогоФинансирования)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица";
	
	ВнеоборотныеАктивыЛокализация.ДополнитьТекстЗапросаПриЗаписиВРегистрПервоначальныеСведенияОС(
		СтруктураВременныеТаблицы.МенеджерВременныхТаблиц, ТекстЗапроса);
		
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + "УНИЧТОЖИТЬ ПервоначальныеСведенияОСПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПервоначальныеСведенияОСИзменение", Выборка.Количество > 0);
	
КонецПроцедуры
 
#КонецОбласти

#КонецЕсли
