#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияНМАОрганизаций.Период               КАК Период,
	|	СостоянияНМАОрганизаций.Регистратор          КАК Регистратор,
	|	СостоянияНМАОрганизаций.Организация          КАК Организация,
	|	СостоянияНМАОрганизаций.НематериальныйАктив  КАК НематериальныйАктив,
	|	СостоянияНМАОрганизаций.Состояние            КАК Состояние
	|ПОМЕСТИТЬ СостоянияНМАОрганизацийПередЗаписью
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|
	|ГДЕ
	|	СостоянияНМАОрганизаций.Регистратор = &Регистратор
	|";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	                      
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Период       КАК Период,
	|	Таблица.Регистратор  КАК Регистратор,
	|	Таблица.Организация  КАК Организация
	|ПОМЕСТИТЬ ИзмененениеСостоянияНМАПринятиеКУчету
	|ИЗ
	|	(ВЫБРАТЬ
	|		СостоянияНМАОрганизаций.Период      КАК Период,
	|		СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|		СостоянияНМАОрганизаций.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)
	|				ТОГДА 1
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|				ТОГДА 2
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				ТОГДА 3
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|				ТОГДА 4
	|		КОНЕЦ КАК Состояние
	|	ИЗ
	|		СостоянияНМАОрганизацийПередЗаписью КАК СостоянияНМАОрганизаций
	|	ГДЕ
	|		СостоянияНМАОрганизаций.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СостоянияНМАОрганизаций.Период      КАК Период,
	|		СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|		СостоянияНМАОрганизаций.Организация КАК Организация,
	|		-(ВЫБОР
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)
	|				ТОГДА 1
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|				ТОГДА 2
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				ТОГДА 3
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|				ТОГДА 4
	|		КОНЕЦ) КАК Состояние
	|	ИЗ
	|		РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|	ГДЕ
	|		СостоянияНМАОрганизаций.Регистратор = &Регистратор
	|		И СостоянияНМАОрганизаций.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА) Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Состояние) <> 0
	|;
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Период,
	|	Таблица.Регистратор                  КАК Документ,
	|	Таблица.НематериальныйАктив          КАК НематериальныйАктив,
	|	Таблица.Организация                  КАК Организация
	|ПОМЕСТИТЬ СостоянияНМАОрганизацийИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		СостоянияНМАОрганизаций.Период      КАК Период,
	|		СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|		СостоянияНМАОрганизаций.Организация КАК Организация,
	|		СостоянияНМАОрганизаций.НематериальныйАктив КАК НематериальныйАктив,
	|		ВЫБОР
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)
	|				ТОГДА 1
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|				ТОГДА 2
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				ТОГДА 3
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|				ТОГДА 4
	|		КОНЕЦ КАК Состояние
	|	ИЗ
	|		СостоянияНМАОрганизацийПередЗаписью КАК СостоянияНМАОрганизаций
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СостоянияНМАОрганизаций.Период      КАК Период,
	|		СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|		СостоянияНМАОрганизаций.Организация КАК Организация,
	|		СостоянияНМАОрганизаций.НематериальныйАктив КАК НематериальныйАктив,
	|		-(ВЫБОР
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)
	|				ТОГДА 1
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|				ТОГДА 2
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				ТОГДА 3
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|				ТОГДА 4
	|		КОНЕЦ) КАК Состояние
	|	ИЗ
	|		РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|	ГДЕ
	|		СостоянияНМАОрганизаций.Регистратор = &Регистратор) Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|	Таблица.Регистратор,
	|	Таблица.НематериальныйАктив,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Состояние) <> 0
	|;
	|
	|///////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СостоянияНМАОрганизацийПередЗаписью";
	
	МассивЗапросовЗаданийПоОперациям = Новый Массив;
	МассивЗапросовЗаданийПоОперациям.Добавить(
		УчетНДСУП.ТекстЗапросаФормированияЗаданийПриПринятииКУчетуИлиМодернизацииВНА("ИзмененениеСостоянияНМАПринятиеКУчету"));
	ТекстЗапросаЗаданийКЗакрытиюМесяца = ЗакрытиеМесяцаСервер.ТекстЗапросЗаданийКЗакрытиюМесяца("СостоянияНМАОрганизаций", МассивЗапросовЗаданийПоОперациям);
	
	ТекстЗапроса = ТекстЗапроса
			+ ОбщегоНазначения.РазделительПакетаЗапросов()
			+  ТекстЗапросаЗаданийКЗакрытиюМесяца;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		
	Выборка = Запрос.ВыполнитьПакет()[1].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("СостоянияНМАОрганизацийИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
