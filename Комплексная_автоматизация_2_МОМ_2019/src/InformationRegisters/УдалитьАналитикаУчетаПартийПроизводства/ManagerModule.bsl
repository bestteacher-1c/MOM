#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция получает элемент справочника - ключ аналитики учета партий производства.
//
// Параметры:
//	ПараметрыАналитики - Выборка, Структура - содержит структуры со свойствами:
//		* Номенклатура - СправочникСсылка.Номенклатура - Номенклатура
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - Характеристика номенклатуры
//		* Спецификация - СправочникСсылка.РесурсныеСпецификации - Спецификация
//		* Назначение - СправочникСсылка.Назначения - Назначение.
//
// Возвращаемое значение:
//	СправочникСсылка.УдалитьКлючиАналитикиУчетаПартийПроизводства - Созданный элемент справочника.
//
Функция ЗначениеКлючаАналитики(ПараметрыАналитики) Экспорт
	
	НаборЗаписей = ПолучитьНаборЗаписей(ПараметрыАналитики);
	
	Если НаборЗаписей <> Неопределено Тогда
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0
			И Не ЗначениеЗаполнено(НаборЗаписей[0].КлючАналитики) Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли;
	
		Если НаборЗаписей.Количество() > 0
			И ЗначениеЗаполнено(НаборЗаписей[0].КлючАналитики) Тогда
			Результат = НаборЗаписей[0].КлючАналитики;
		Иначе
			Результат = СоздатьКлючАналитики(ПараметрыАналитики);
		КонецЕсли;
	
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

// Функция получает элемент справочника - ключ аналитики учета партий производства.
//
// Параметры:
//	ПараметрыАналитики (обязательный) - Выборка, Структура - содержит структуры со свойствами:
//		* Номенклатура - СправочникСсылка.Номенклатура - Номенклатура
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - Характеристика номенклатуры
//		* Спецификация - СправочникСсылка.РесурсныеСпецификации - Спецификация
//		* Назначение - СправочникСсылка.Назначения - Назначение.
//
// Возвращаемое значение:
//	СправочникСсылка.УдалитьКлючиАналитикиУчетаПартийПроизводства - Созданный элемент справочника.
//
Функция СоздатьКлючАналитики(ПараметрыАналитики, ЗаписьПриОбновленииИБ = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = ПолучитьНаборЗаписей(ПараметрыАналитики);
	
	Если НаборЗаписей <> Неопределено Тогда
		
		НачатьТранзакцию();
		
		Попытка
			// Создание нового ключа аналитики.
			СтрокаНабора = НаборЗаписей[0];
			
			СправочникОбъект = Справочники.УдалитьКлючиАналитикиУчетаПартийПроизводства.СоздатьЭлемент();
			СправочникОбъект.Наименование = ПолучитьПолноеНаименованиеКлючаАналитики(СтрокаНабора);
			ЗаполнитьЗначенияСвойств(СправочникОбъект, СтрокаНабора, "Номенклатура, Характеристика, Спецификация, Назначение");
			Если ЗаписьПриОбновленииИБ Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
			Иначе
				СправочникОбъект.Записать();
			КонецЕсли;

			Результат = СправочникОбъект.Ссылка;

			СтрокаНабора.КлючАналитики = Результат;
			
			Если ЗаписьПриОбновленииИБ Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Ложь, Истина);
			Иначе
				НаборЗаписей.Записать(Ложь);
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			// во время инициализации ключа, данный ключ уже был создан в ИБ другим сеансом.
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда // запись не создана из-за ошибки заполнения полей.
				КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание нового ключа аналитики учета партий производства'", КодОсновногоЯзыка),
										УровеньЖурналаРегистрации.Ошибка,,,
										ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ВызватьИсключение;
			Иначе
				Результат = НаборЗаписей[0].КлючАналитики;
			КонецЕсли;
		КонецПопытки;

		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

// Заполняет поле АналитикаУчетаПартийПроизводства в коллекции, содержащей "Номенклатура, Характеристика, Спецификация, Назначение".
// Параметры:
//
//	Коллекция (обязательный) - ТаблицаЗначений - обязательные поля:
//		* АналитикаУчетаПартийПроизводства - СправочникСсылка.УдалитьКлючиАналитикиУчетаПартийПроизводства - АналитикаУчетаПартийПроизводства.
//
//	ИменаПолей (необязательный) - Структура - содержит реальные имена полей коллекции для получения и формирования аналитики.
//		Имеет структуру {АналитикаУчетаПартийПроизводства [, Номенклатура, Характеристика, Спецификация, Назначение]},
//			все ключи определения полей, за исключением поля самой аналитики - необязательны.
//		по умолчанию заполняется методом ИменаПолейКоллекцииПоУмолчанию(...)
//			("АналитикаУчетаПартийПроизводства, Номенклатура, Характеристика, Спецификация, Назначение",
//			"АналитикаУчетаПартийПроизводства", "", "", "", "")
//
Процедура ЗаполнитьВКоллекции(Коллекция, ИменаПолей = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИменаПолей = Неопределено Тогда
		ИменаПолей = ИменаПолейКоллекцииПоУмолчанию();
	КонецЕсли;

	Запрос = Новый Запрос(ТекстЗначенияКлючейАналитикиВКоллекции(ИменаПолей));
	Запрос.УстановитьПараметр("Коллекция", Коллекция);
	УстановитьПараметрИзмерение(Запрос, ИменаПолей, "Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	УстановитьПараметрИзмерение(Запрос, ИменаПолей, "Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	УстановитьПараметрИзмерение(Запрос, ИменаПолей, "Спецификация", Справочники.РесурсныеСпецификации.ПустаяСсылка());
	УстановитьПараметрИзмерение(Запрос, ИменаПолей, "Назначение", Справочники.Назначения.ПустаяСсылка());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.АналитикаУчетаПартийПроизводства) Тогда
			КлючАналитики = ЗначениеКлючаАналитики(Выборка)
		Иначе
			КлючАналитики = Выборка.АналитикаУчетаПартийПроизводства;
		КонецЕсли;
		
		Коллекция[Выборка.Индекс][ИменаПолей.АналитикаУчетаПартийПроизводства] = КлючАналитики;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру полей выбора информации из коллекции для формирования аналитики учета партий производства.
//
// Возвращаемое значение:
//	Структура - содержит реальные имена полей коллекции для получения и формирования аналитики.
//		Имеет структуру {АналитикаУчетаПартийПроизводства [, Номенклатура, Характеристика, Спецификация, Назначение]},
//			все ключи определения полей, за исключением поля самой аналитики - необязательны.
//		по умолчанию заполняется методом ИменаПолейКоллекцииПоУмолчанию(...)
//			("АналитикаУчетаПартийПроизводства, Номенклатура, Характеристика, Спецификация, Назначение",
//			"АналитикаУчетаПартийПроизводства", "", "", "", "")
//
Функция ИменаПолейКоллекцииПоУмолчанию() Экспорт
	
	ИменаПолей = Новый Структура();
	ИменаПолей.Вставить("АналитикаУчетаПартийПроизводства", "УдалитьАналитикаУчетаПартийПроизводства");
	ИменаПолей.Вставить("Номенклатура", "");
	ИменаПолей.Вставить("Характеристика", "");
	ИменаПолей.Вставить("Спецификация", "");
	ИменаПолей.Вставить("Назначение", "");
	
	Возврат ИменаПолей;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Функция ПолучитьНаборЗаписей(ПараметрыАналитики)
	
	// В параметрах аналитики могут быть не все свойства
	СтруктураАналитики = Новый Структура("Номенклатура, Характеристика, Спецификация, Назначение");
	ЗаполнитьЗначенияСвойств(СтруктураАналитики, ПараметрыАналитики);
	Если НЕ ЗначениеЗаполнено(СтруктураАналитики.Номенклатура)
		И НЕ ЗначениеЗаполнено(СтруктураАналитики.Характеристика)
		И НЕ ЗначениеЗаполнено(СтруктураАналитики.Спецификация)
		И НЕ ЗначениеЗаполнено(СтруктураАналитики.Назначение) Тогда
		Возврат Неопределено
	Иначе
		НаборЗаписей = РегистрыСведений.УдалитьАналитикаУчетаПартийПроизводства.СоздатьНаборЗаписей();
		
		Для Каждого КлючЗначение Из СтруктураАналитики Цикл
			НаборЗаписей.Отбор[КлючЗначение.Ключ].Установить(КлючЗначение.Значение);
		КонецЦикла;
		
		НоваяСтрока = НаборЗаписей.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураАналитики, "Номенклатура, Характеристика, Спецификация, Назначение");
		Возврат НаборЗаписей;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьПолноеНаименованиеКлючаАналитики(МенеджерЗаписи)

	ПолноеНаименование =
		?(ЗначениеЗаполнено(МенеджерЗаписи.Номенклатура), СокрЛП(МенеджерЗаписи.Номенклатура) + "; ", "")
		+ ?(ЗначениеЗаполнено(МенеджерЗаписи.Характеристика), СокрЛП(МенеджерЗаписи.Характеристика) + "; ", "")
		+ ?(ЗначениеЗаполнено(МенеджерЗаписи.Спецификация), СокрЛП(МенеджерЗаписи.Спецификация) + "; ", "")
		+ ?(ЗначениеЗаполнено(МенеджерЗаписи.Назначение), СокрЛП(МенеджерЗаписи.Назначение) + "; ", "");
	
	ПустоеНаименование = НСтр("ru = '<Запись с пустыми ключами>'");
	ПолноеНаименование = ?(ЗначениеЗаполнено(ПолноеНаименование), ПолноеНаименование, ПустоеНаименование);
	
	Возврат ПолноеНаименование;

КонецФункции

#КонецОбласти

#Область ЗаполнениеКлючейАналитикиВКоллекции

Функция ТекстЗначенияКлючейАналитикиВКоллекции(ИменаПолей)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Коллекция.НомерСтроки - 1	КАК Индекс,
		|	&ПолеАналитика				КАК АналитикаУчетаПартийПроизводства,
		|	ВЫБОР
		|		КОГДА &ПолеНоменклатура В (ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|								НЕОПРЕДЕЛЕНО)
		|			ТОГДА &Номенклатура
		|		ИНАЧЕ &ПолеНоменклатура
		|	КОНЕЦ						КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА &ПолеХарактеристика В (ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|								НЕОПРЕДЕЛЕНО)
		|			ТОГДА &Характеристика
		|		ИНАЧЕ &ПолеХарактеристика
		|	КОНЕЦ						КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ПолеСпецификация В (ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка),
		|								НЕОПРЕДЕЛЕНО)
		|			ТОГДА &Спецификация
		|		ИНАЧЕ &ПолеСпецификация
		|	КОНЕЦ						КАК Спецификация,
		|	ВЫБОР
		|		КОГДА &ПолеНазначение В (ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка),
		|								НЕОПРЕДЕЛЕНО)
		|			ТОГДА &Назначение
		|		ИНАЧЕ &ПолеНазначение
		|	КОНЕЦ						КАК Назначение
		|ПОМЕСТИТЬ Коллекция
		|ИЗ
		|	&Коллекция КАК Коллекция
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Спецификация,
		|	Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Коллекция.Индекс			КАК Индекс,
		|	Аналитика.КлючАналитики		КАК АналитикаУчетаПартийПроизводства,
		|	Коллекция.Номенклатура		КАК Номенклатура,
		|	Коллекция.Характеристика	КАК Характеристика,
		|	Коллекция.Спецификация		КАК Спецификация,
		|	Коллекция.Назначение		КАК Назначение
		|ПОМЕСТИТЬ втАналитики
		|ИЗ
		|	Коллекция КАК Коллекция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьАналитикаУчетаПартийПроизводства КАК Аналитика
		|		ПО (Аналитика.Номенклатура = Коллекция.Номенклатура)
		|			И (Аналитика.Характеристика = Коллекция.Характеристика)
		|			И (Аналитика.Спецификация = Коллекция.Спецификация)
		|			И (Аналитика.Назначение = Коллекция.Назначение)
		|ГДЕ
		|	Аналитика.КлючАналитики ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Коллекция.Индекс			КАК Индекс,
		|	Аналитика.КлючАналитики		КАК АналитикаУчетаПартийПроизводства,
		|	Коллекция.Номенклатура		КАК Номенклатура,
		|	Коллекция.Характеристика	КАК Характеристика,
		|	Коллекция.Спецификация		КАК Спецификация,
		|	Коллекция.Назначение		КАК Назначение
		|ИЗ
		|	Коллекция КАК Коллекция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьАналитикаУчетаПартийПроизводства КАК Аналитика
		|		ПО (Аналитика.Номенклатура = Коллекция.Номенклатура)
		|			И (Аналитика.Характеристика = Коллекция.Характеристика)
		|			И (Аналитика.Спецификация = Коллекция.Спецификация)
		|			И (Аналитика.Назначение = Коллекция.Назначение)
		|ГДЕ
		|	НЕ Аналитика.КлючАналитики = Коллекция.АналитикаУчетаПартийПроизводства
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Коллекция.Индекс			КАК Индекс,
		|	Аналитика.КлючАналитики		КАК АналитикаУчетаПартийПроизводства,
		|	Коллекция.Номенклатура		КАК Номенклатура,
		|	Коллекция.Характеристика	КАК Характеристика,
		|	Коллекция.Спецификация		КАК Спецификация,
		|	Коллекция.Назначение		КАК Назначение
		|ИЗ
		|	Коллекция КАК Коллекция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьАналитикаУчетаПартийПроизводства КАК Аналитика
		|		ПО (Аналитика.Номенклатура = Коллекция.Номенклатура)
		|			И (Аналитика.Характеристика = Коллекция.Характеристика)
		|			И (Аналитика.Спецификация = Коллекция.Спецификация)
		|			И (Аналитика.Назначение = Коллекция.Назначение)
		|ГДЕ
		|	Аналитика.КлючАналитики = ЗНАЧЕНИЕ(Справочник.УдалитьКлючиАналитикиУчетаПартийПроизводства.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втАналитики.Индекс			КАК Индекс,
		|	втАналитики.АналитикаУчетаПартийПроизводства КАК АналитикаУчетаПартийПроизводства,
		|	втАналитики.Номенклатура	КАК Номенклатура,
		|	втАналитики.Характеристика	КАК Характеристика,
		|	втАналитики.Спецификация	КАК Спецификация,
		|	втАналитики.Назначение		КАК Назначение
		|ИЗ
		|	втАналитики КАК втАналитики
		|
		|СГРУППИРОВАТЬ ПО
		|	втАналитики.Индекс,
		|	втАналитики.Спецификация,
		|	втАналитики.АналитикаУчетаПартийПроизводства,
		|	втАналитики.Характеристика,
		|	втАналитики.Назначение,
		|	втАналитики.Номенклатура";
	
	// заменим в тексте запроса подставляемые поля из структуры ИменаПолей
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеАналитика", "Коллекция." + ИменаПолей.АналитикаУчетаПартийПроизводства);
	
	// Поля могут быть не заданы
	
	ПолеУказывается = ИменаПолей.Свойство("Номенклатура")
					И ЗначениеЗаполнено(ИменаПолей.Номенклатура)
					И ТипЗнч(ИменаПолей.Номенклатура) = Тип("Строка");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеНоменклатура",
								?(ПолеУказывается,
									"Коллекция." + ИменаПолей.Номенклатура,
									"ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)"));
	
	ПолеУказывается = ИменаПолей.Свойство("Характеристика")
					И ЗначениеЗаполнено(ИменаПолей.Характеристика)
					И ТипЗнч(ИменаПолей.Характеристика) = Тип("Строка");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеХарактеристика",
								?(ПолеУказывается,
									"Коллекция." + ИменаПолей.Характеристика,
									"ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)"));
	
	ПолеУказывается = ИменаПолей.Свойство("Спецификация")
					И ЗначениеЗаполнено(ИменаПолей.Спецификация)
					И ТипЗнч(ИменаПолей.Спецификация) = Тип("Строка");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСпецификация",
								?(ПолеУказывается,
									"Коллекция." + ИменаПолей.Спецификация,
									"ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)"));
	
	ПолеУказывается = ИменаПолей.Свойство("Назначение")
					И ЗначениеЗаполнено(ИменаПолей.Назначение)
					И ТипЗнч(ИменаПолей.Назначение) = Тип("Строка");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеНазначение",
								?(ПолеУказывается,
									"Коллекция." + ИменаПолей.Назначение,
									"ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрИзмерение(Запрос, ИменаПолей, Параметр, ЗначениеПоУмолчанию)
	
	Если ИменаПолей.Свойство(Параметр)
		И ЗначениеЗаполнено(ИменаПолей[Параметр])
		И Не ТипЗнч(ИменаПолей[Параметр]) = Тип("Строка") Тогда
		Запрос.УстановитьПараметр(Параметр, ИменаПолей[Параметр]);
	Иначе
		Запрос.УстановитьПараметр(Параметр, ЗначениеПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли