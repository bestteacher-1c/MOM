#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4")
		ИЛИ ВнеоборотныеАктивы.ДвиженияЗаписываютсяПриРасчетеСтоимостиВНА(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
		ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц));
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.ПериодРасчета           КАК ПериодРасчета,
	|	ТаблицаПередЗаписью.Организация             КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство        КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.СуммаНалога             КАК СуммаНалога,
	|	ТаблицаПередЗаписью.СуммаНалогаКУплате      КАК СуммаНалогаКУплате,
	|	ТаблицаПередЗаписью.СуммаАвансовыхПлатежей  КАК СуммаАвансовыхПлатежей
	|ПОМЕСТИТЬ РасчетНалогаНаИмуществоПередЗаписью
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4") 
		ИЛИ ВнеоборотныеАктивы.ДвиженияЗаписываютсяПриРасчетеСтоимостиВНА(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период                        КАК Период,
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)  КАК НачалоМесяца,
	|	КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ)   КАК КонецМесяца,
	|	Таблица.Организация                   КАК Организация,
	|	Таблица.ОсновноеСредство              КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_ИзменныеДанные
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.ПериодРасчета     КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		РасчетНалогаНаИмуществоПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасчетНалогаНаИмущество КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.ПериодРасчета ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалога <> ТаблицаПередЗаписью.СуммаНалога
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалогаКУплате <> ТаблицаПередЗаписью.СуммаНалогаКУплате
	|				ИЛИ ТаблицаПриЗаписи.СуммаАвансовыхПлатежей <> ТаблицаПередЗаписью.СуммаАвансовыхПлатежей
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ПериодРасчета, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ПериодРасчета, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.ПериодРасчета,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.РасчетНалогаНаИмущество КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ РасчетНалогаНаИмуществоПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.ПериодРасчета ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалога <> ТаблицаПередЗаписью.СуммаНалога
	|				ИЛИ ТаблицаПриЗаписи.СуммаНалогаКУплате <> ТаблицаПередЗаписью.СуммаНалогаКУплате
	|				ИЛИ ТаблицаПриЗаписи.СуммаАвансовыхПлатежей <> ТаблицаПередЗаписью.СуммаАвансовыхПлатежей
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.ПериодРасчета, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.ПериодРасчета, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИзменныеДанные.Период           КАК Месяц,
	|	ИзменныеДанные.Организация      КАК Организация,
	|	ИзменныеДанные.ОсновноеСредство КАК ОбъектУчета,
	|	&Регистратор                    КАК Документ
	|ИЗ
	|	ВТ_ИзменныеДанные КАК ИзменныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ДанныеКРасчету
	|		ПО ДанныеКРасчету.Организация = ИзменныеДанные.Организация
	|			И ДанныеКРасчету.ОсновноеСредство = ИзменныеДанные.ОсновноеСредство
	|			И ДанныеКРасчету.Период >= ИзменныеДанные.НачалоМесяца
	|			И ДанныеКРасчету.Период <= ИзменныеДанные.КонецМесяца
	|			И ДанныеКРасчету.Активность
	|			И ДанныеКРасчету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДанныеКРасчету.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИзменныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РасчетНалогаНаИмуществоПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	РегистрыСведений.ЗаданияКРасчетуСтоимостиВНА.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
