#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает результат выполнения анализатора отражения расходов с настроенными счетами.
//	
//	Параметры:
//		МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям;
//		Период - СтандартныйПериод - период с датой начала и окончания анализа.
//	ВозвращаемоеЗначение:
//		РезультатЗапроса - список всех операций, удовлетворяющий входным параметрам с настроенными для них счетами.
//
Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ПараметрыАнализатора = Новый Структура("МассивОрганизаций, НачалоПериода, КонецПериода", МассивОрганизаций, Период.ДатаНачала, Период.ДатаОкончания);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов
	|ПОМЕСТИТЬ ПрочиеРасходы
	|ИЗ
	|	ТаблицаДвижений КАК ПрочиеРасходы
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрочиеРасходы.Организация КАК Организация,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
	|	ПрочиеРасходы.СтатьяРасходов.ВидЦенностиНДС КАК ВидЦенностиНДС,
	|	ПрочиеРасходы.СтатьяРасходов.ВидРасходов КАК ВидРасходов,
	|	ПрочиеРасходы.СтатьяРасходов.ВидПрочихДоходовИРасходов КАК ВидПрочихРасходов,
	|	ПрочиеРасходы.СтатьяРасходов.КосвенныеЗатратыНУ КАК КосвенныеЗатратыНУ,
	|	ПрочиеРасходы.СтатьяРасходов.ВидДеятельностиРасходов КАК ВидДеятельностиРасходов,
	|	ИСТИНА КАК ЕстьРасходы,
	|	
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияОСНО, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетСписанияОСНО
	|		ИНАЧЕ ПрочиеРасходы.СтатьяРасходов.СчетСписанияОСНО
	|	КОНЕЦ КАК СчетСписанияОСНО,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияОСНО, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СчетСписанияОСНОПоУмолчанию,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|				И ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПрочиеРасходы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходовНеСписываются)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И НЕ ПрочиеРасходы.СтатьяРасходов.КосвенныеЗатратыНУ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасходыСписываютсяНаОСНО,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияЕНВД, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетСписанияЕНВД
	|		ИНАЧЕ ПрочиеРасходы.СтатьяРасходов.СчетСписанияЕНВД
	|	КОНЕЦ КАК СчетСписанияЕНВД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияЕНВД, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СчетСписанияЕНВДПоУмолчанию,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|				ИЛИ НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПрочиеРасходы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходовНеСписываются)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И НЕ ПрочиеРасходы.СтатьяРасходов.КосвенныеЗатратыНУ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасходыСписываютсяНаЕНВД,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчета
	|		ИНАЧЕ ПрочиеРасходы.СтатьяРасходов.СчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) = &ПустойСчет
	|				И ПрочиеРасходы.СтатьяРасходов.СчетУчета <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаПоУмолчанию,
	|	ЕСТЬNULL(ПорядокОтражения.ОтражениеВУСН, НЕОПРЕДЕЛЕНО) КАК ОтражениеВУСН
	|ИЗ
	|	ПрочиеРасходы КАК ПрочиеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|		ПО ПрочиеРасходы.Организация = ПорядокОтражения.Организация
	|			И ПрочиеРасходы.Подразделение = ПорядокОтражения.Подразделение
	|			И ПрочиеРасходы.СтатьяРасходов = ПорядокОтражения.СтатьяРасходов
	|			И (ПорядокОтражения.Документ = НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК УчетнаяПолитикаОрганизаций
	|		ПО ПрочиеРасходы.Организация = УчетнаяПолитикаОрганизаций.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокОтражения.Организация КАК Организация,
	|	ПорядокОтражения.Подразделение КАК Подразделение,
	|	ПорядокОтражения.СтатьяРасходов КАК СтатьяРасходов,
	|	ПорядокОтражения.СтатьяРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
	|	ПорядокОтражения.СтатьяРасходов.ВидЦенностиНДС КАК ВидЦенностиНДС,
	|	ПорядокОтражения.СтатьяРасходов.ВидРасходов КАК ВидРасходов,
	|	ПорядокОтражения.СтатьяРасходов.ВидПрочихДоходовИРасходов КАК ВидПрочихРасходов,
	|	ПорядокОтражения.СтатьяРасходов.КосвенныеЗатратыНУ КАК КосвенныеЗатратыНУ,
	|	ПорядокОтражения.СтатьяРасходов.ВидДеятельностиРасходов КАК ВидДеятельностиРасходов,
	|	ЛОЖЬ ЕстьРасходы,
	|	
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияОСНО, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетСписанияОСНО
	|		ИНАЧЕ ПорядокОтражения.СтатьяРасходов.СчетСписанияОСНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияОСНО, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПорядокОтражения.СтатьяРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|				И ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПорядокОтражения.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПорядокОтражения.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходовНеСписываются)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПорядокОтражения.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И НЕ ПорядокОтражения.СтатьяРасходов.КосвенныеЗатратыНУ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияЕНВД, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетСписанияЕНВД
	|		ИНАЧЕ ПорядокОтражения.СтатьяРасходов.СчетСписанияЕНВД
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияЕНВД, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПорядокОтражения.СтатьяРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|				ИЛИ НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПорядокОтражения.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПорядокОтражения.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходовНеСписываются)
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПорядокОтражения.СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И НЕ ПорядокОтражения.СтатьяРасходов.КосвенныеЗатратыНУ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ РасходыСписываютсяНаЕНВД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчета
	|		ИНАЧЕ ПорядокОтражения.СтатьяРасходов.СчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) = &ПустойСчет
	|				И ПорядокОтражения.СтатьяРасходов.СчетУчета <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаПоУмолчанию,
	|	ПорядокОтражения.ОтражениеВУСН
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПрочиеРасходы КАК ПрочиеРасходы
	|		ПО ПорядокОтражения.Организация = ПрочиеРасходы.Организация
	|			И ПорядокОтражения.Подразделение = ПрочиеРасходы.Подразделение
	|			И ПорядокОтражения.СтатьяРасходов = ПрочиеРасходы.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК УчетнаяПолитикаОрганизаций
	|		ПО ПорядокОтражения.Организация = УчетнаяПолитикаОрганизаций.Организация
	|ГДЕ
	|	ПрочиеРасходы.Организация ЕСТЬ NULL
	|	И ПорядокОтражения.Организация В (&МассивОрганизаций)
	|	И ПорядокОтражения.Документ = НЕОПРЕДЕЛЕНО
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СтатьяРасходов";
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	ВариантыРаспределенияРасходовНеСписываются = Новый Массив;
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы);
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов);
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	Запрос.УстановитьПараметр("ВариантыРаспределенияРасходовНеСписываются", ВариантыРаспределенияРасходовНеСписываются);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Функция ВременнаяТаблицаСтатейАмортизацииКОтражению(МассивОрганизаций, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив,
	|	СчетаБухгалтерскогоУчетаНМА.СчетУчета,
	|	ПараметрыЦелевогоФинансированияНМА.СчетУчета КАК СчетУчетаЦФ,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансированияНМА.ПрименениеЦелевогоФинансирования, ЛОЖЬ) КАК ПрименениеЦелевогоФинансирования
	|ПОМЕСТИТЬ втСчетаБухгалтерскогоУчетаНМА
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СчетаБухгалтерскогоУчетаНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(&ДатаОкончания, ) КАК ПараметрыЦелевогоФинансированияНМА
	|		ПО СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ПараметрыЦелевогоФинансированияНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ЕСТЬNULL(ДанныеСчетУчета.Подразделение, ЕСТЬNULL(ДанныеСчетУчетаЦФ.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	втСчетаБухгалтерскогоУчетаНМА КАК втСчетаБухгалтерскогоУчетаНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ДатаОкончания,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.СчетУчета
	|					ИЗ
	|						втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|				Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчета
	|		ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчета.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ДатаОкончания,
	|				Счет В
	|					(ВЫБРАТЬ
	|						Т.СчетУчетаЦФ
	|					ИЗ
	|						втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|				Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчетаЦФ
	|		ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчетаЦФ.Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)) КАК Организация,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеНМА.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиАмортизацииКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО (ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка)
	|			И (ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И (СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК МестонахождениеНМА
	|		ПО СпособыОтраженияРасходов.НематериальныйАктив = МестонахождениеНМА.НематериальныйАктив
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)),
	|	ВЫБОР
	|		КОГДА СтатьиРасходовПоИзменениюПараметров.Ссылка ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
	|	КОНЕЦ,
	|	ЕСТЬNULL(СтатьиРасходовПоИзменениюПараметров.Ссылка, ПринятиеКУчетуОС.СтатьяРасходовАмортизационнойПремии)
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В (&МассивОрганизаций)
	|					И Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС) КАК ПервоначальныеСведения
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ПО (ПервоначальныеСведения.Регистратор = ПринятиеКУчетуОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО (ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка)
	|			И (ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И (СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходовПоИзменениюПараметров
	|		ПО (ОтражениеАмортизационныхРасходов.СтатьяРасходовАмортизационнойПремии = СтатьиРасходовПоИзменениюПараметров.Ссылка)
	|ГДЕ
	|	ПринятиеКУчетуОС.ВключитьАмортизационнуюПремиюВСоставРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов)
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО (ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка)
	|			И (ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И (СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втСчетаБухгалтерскогоУчетаНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втМестонахожденияНМА";

	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
		
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	КонецПериода = Неопределено;
	ПараметрыАнализатора.Свойство("КонецПериода", КонецПериода);
	Если Не ЗначениеЗаполнено(КонецПериода) Тогда
		ОграничениеПериода = Неопределено;
		Если Не ПараметрыАнализатора.Свойство("ДатаОкончанияПериода", ОграничениеПериода) Тогда
			ОграничениеПериода = Новый СтандартнаяДатаНачала();
		КонецЕсли;
		КонецПериода = Обработки.НастройкаОтраженияДокументовВРеглУчете.ПериодКОтражениюВРеглУчете(ПараметрыАнализатора.МассивОрганизаций, ОграничениеПериода).ДатаОкончания;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаСтатейАмортизацииКОтражению(ПараметрыАнализатора.МассивОрганизаций, КонецПериода);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиПрочихОпераций.Ссылка
	|ПОМЕСТИТЬ СтатьиПрочихОпераций
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиПрочихОпераций
	|ГДЕ
	|	СтатьиПрочихОпераций.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И СтатьиПрочихОпераций.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ДанныеРегистра.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов), ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.СтатьяРасходов
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО ДанныеДокумента.СтатьяРасходов = СтатьиПрочихОпераций.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|	И ДанныеДокумента.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ТаблицаРаспределениеРасходов.СтатьяРасходов
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК ТаблицаРаспределениеРасходов
	|		ПО ДанныеДокумента.Ссылка = ТаблицаРаспределениеРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (ТаблицаРаспределениеРасходов.СтатьяРасходов = СтатьиПрочихОпераций.Ссылка)
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|	ЕСТЬNULL(ВЫРАЗИТЬ(ТаблицаНачисления.СтатьяДоходовРасходов КАК ПланВидовХарактеристик.СтатьиРасходов), ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
	|ИЗ
	|	Документ.НачисленияКредитовИДепозитов КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачисленияКредитовИДепозитов.Начисления КАК ТаблицаНачисления
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНачисления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (ТаблицаНачисления.СтатьяДоходовРасходов = СтатьиПрочихОпераций.Ссылка)
	|ГДЕ
	|	&УсловиеДляДокумента
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.СтатьяДоходовРасходов
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО (Статьи.Ссылка = ДанныеРегистра.СтатьяДоходовРасходов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО (СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяДоходовРасходов)
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втСтатьиАмортизацииКОтражению.Организация,
	|	втСтатьиАмортизацииКОтражению.Подразделение,
	|	втСтатьиАмортизацииКОтражению.СтатьяРасходов
	|ИЗ
	|	втСтатьиАмортизацииКОтражению КАК втСтатьиАмортизацииКОтражению";

	УсловиеВхожденияВРегистр = "ГДЕ
	|	&УсловиеДляРегистра
	|	";
	УсловиеВхожденияВДокумент = "ГДЕ
	|	&УсловиеДляДокумента
	|	";
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеРегистра.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ
	|			";
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеРегистра.Организация В (&Организации)
	|	";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Регистратор", "Ссылка");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
	Иначе
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Период", "Дата");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		ТекстУсловий = ТекстУсловий + "И ДанныеДокумента.Проведен" + Символы.ПС + Символы.Таб;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
	КонецЕсли;
		
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений, МассивОрганизаций)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	ВариантыРаспределенияРасходовНеСписываются = Новый Массив;
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы);
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов);
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
	ВариантыРаспределенияРасходовНеСписываются.Добавить(Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	Запрос.УстановитьПараметр("ВариантыРаспределенияРасходовНеСписываются", ВариантыРаспределенияРасходовНеСписываются);
	Запрос.УстановитьПараметр("Счета90и91", РеглУчетВыборкиСерверПовтИсп.Счета90и91().ВыгрузитьЗначения());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.Подразделение КАК Подразделение,
	|	ТаблицаДвижений.СтатьяРасходов КАК СтатьяРасходов,
	|	
	|	ВЫБОР
	|		КОГДА АналитикаУчета.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|				И ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|			ТОГДА ЛОЖЬ
	|		КОГДА АналитикаУчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА АналитикаУчета.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходовНеСписываются)
	|			ТОГДА ЛОЖЬ
	|		КОГДА АналитикаУчета.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|				И НЕ АналитикаУчета.КосвенныеЗатратыНУ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасходыСписываютсяНаОСНО,
	|	ВЫБОР
	|		КОГДА АналитикаУчета.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|				ИЛИ НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|			ТОГДА ЛОЖЬ
	|		КОГДА АналитикаУчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА АналитикаУчета.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходовНеСписываются)
	|			ТОГДА ЛОЖЬ
	|		КОГДА АналитикаУчета.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|				И НЕ АналитикаУчета.КосвенныеЗатратыНУ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасходыСписываютсяНаЕНВД,
	|	ЕСТЬNULL(ПорядокОтражения.СчетУчета, ЕСТЬNULL(АналитикаУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))) В (&Счета90и91) КАК СчетУчетаВ90и91,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))
	|		ИНАЧЕ ПорядокОтражения.СчетУчета КОНЕЦ КАК СчетУчета,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияОСНО, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетСписанияОСНО, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))
	|		ИНАЧЕ ПорядокОтражения.СчетСписанияОСНО КОНЕЦ КАК СчетСписанияОСНО,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетСписанияЕНВД, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(АналитикаУчета.СчетСписанияЕНВД, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))
	|		ИНАЧЕ ПорядокОтражения.СчетСписанияЕНВД КОНЕЦ КАК СчетСписанияЕНВД,
	|
	|	ЕСТЬNULL(ПорядокОтражения.СчетУчета, ЕСТЬNULL(АналитикаУчета.СчетУчета,
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))) = АналитикаУчета.СчетУчета КАК СчетУчетаПоУмолчанию,
	|	ЕСТЬNULL(ПорядокОтражения.СчетСписанияОСНО, ЕСТЬNULL(АналитикаУчета.СчетСписанияОСНО,
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))) = АналитикаУчета.СчетСписанияОСНО КАК СчетСписанияОСНОПоУмолчанию,
	|	ЕСТЬNULL(ПорядокОтражения.СчетСписанияЕНВД, ЕСТЬNULL(АналитикаУчета.СчетСписанияЕНВД,
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))) = АналитикаУчета.СчетСписанияЕНВД КАК СчетСписанияЕНВДПоУмолчанию
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвижений КАК ТаблицаДвижений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|		ПО ТаблицаДвижений.Организация = ПорядокОтражения.Организация
	|			И ТаблицаДвижений.Подразделение = ПорядокОтражения.Подразделение
	|			И ТаблицаДвижений.СтатьяРасходов = ПорядокОтражения.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК АналитикаУчета
	|		ПО ТаблицаДвижений.СтатьяРасходов = АналитикаУчета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(, Организация В (&МассивОрганизаций)) КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаДвижений.Организация = УчетнаяПолитикаОрганизаций.Организация";
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Выполняет анализ всех неотраженных операций отражения расходов,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	Параметры:
//		ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* ДатаОкончанияПериода - СтандартнаяДатаНачала - конец периода определяется на основании этой даты
//				(см. метод "Обработки.НастройкаОтраженияДокументовВРеглУчете.ПериодКОтражениюВРеглУчете");
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
		
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений, ПараметрыАнализатора.МассивОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.Подразделение КАК МестоУчета,
	|	ТаблицаДвиженийСоСчетами.СтатьяРасходов КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.Подразделение КАК МестоУчета,
	|	ТаблицаДвиженийСоСчетами.СтатьяРасходов КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовОСНО) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.РасходыСписываютсяНаОСНО
	|	И НЕ ТаблицаДвиженийСоСчетами.СчетУчетаВ90и91
	|	И ТаблицаДвиженийСоСчетами.СчетСписанияОСНО = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.Подразделение КАК МестоУчета,
	|	ТаблицаДвиженийСоСчетами.СтатьяРасходов КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.РасходыСписываютсяНаЕНВД
	|	И НЕ ТаблицаДвиженийСоСчетами.СчетУчетаВ90и91
	|	И ТаблицаДвиженийСоСчетами.СчетСписанияЕНВД = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураВидовСчетовИРезультатаЗапроса = Новый Соответствие;
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.Расходы, МассивРезультатов.Получить(0));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.СписаниеРасходовОСНО, МассивРезультатов.Получить(1));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД, МассивРезультатов.Получить(2));
	
	ИмяСобытия = НСтр("ru = 'Анализ порядка отражения прочих расходов, требующего настройки'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(СтруктураВидовСчетовИРезультатаЗапроса, ИмяСобытия);

КонецПроцедуры

// Выполняет анализ операций отражения расходов, в зависимости от параметров и дополняет исходную таблицу операций
// новыми строками.
//	
//	Параметры:
//		ПараметрыАнализа - Структура - содержит настройки анализатора со следующими свойствами:
//			* РежимАнализа - Строка - принимает значения вида "ПоНеотраженным" и "ЗаПериод". Причем если не задана, значение по умолчанию "ЗаПериод".
//			* КонецПериода - Дата - по эту дату анализируются операции, причем если режим анализа "ПоНеотраженным", то актуальна только для
//				формирования статей амортизации к отражению;
//			* ДатаОкончанияПериода - СтандартнаяДатаНачала - если конец периода не задан, конец периода определяется на основании этой даты
//				(см. метод "Обработки.НастройкаОтраженияДокументовВРеглУчете.ПериодКОтражениюВРеглУчете");
//			* НачалоПериода - Дата - начиная с этой даты анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//		АдресХранилища - Строка - адрес сохранения результата работы метода (ТаблицаЗначений).
//
Процедура ДополнитьТаблицуНаОснованииАнализаОпераций(ПараметрыАнализа, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализа);
	 
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений, ПараметрыАнализа.МассивОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ПараметрыАнализа.ЗаполняемаяТаблица);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.Подразделение,
	|	ВЫРАЗИТЬ(ТекущаяТаблицаСчетовУчета.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов) КАК СтатьяРасходов,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчета,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияОСНО,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВД,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияОСНОПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВДПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчета,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетСписанияОСНО,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетСписанияЕНВД,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ПОМЕСТИТЬ ТекущаяТаблицаСчетовУчета
	|ИЗ
	|	&Таблица КАК ТекущаяТаблицаСчетовУчета;
	|
	|///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.Подразделение,
	|	ТаблицаДвиженийСоСчетами.СтатьяРасходов,
	|	ТаблицаДвиженийСоСчетами.СтатьяРасходов.ВидЦенностиНДС КАК ВидЦенностиНДС,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчета, ТаблицаДвиженийСоСчетами.СчетУчета) КАК СчетУчета,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияОСНО, ТаблицаДвиженийСоСчетами.СчетСписанияОСНО) КАК СчетСписанияОСНО,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВД, ТаблицаДвиженийСоСчетами.СчетСписанияЕНВД) КАК СчетСписанияЕНВД,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаПоУмолчанию) КАК СчетУчетаПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияОСНОПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетСписанияОСНОПоУмолчанию) КАК СчетСписанияОСНОПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВДПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетСписанияЕНВДПоУмолчанию) КАК СчетСписанияЕНВДПоУмолчанию,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчета, ТаблицаДвиженийСоСчетами.СчетУчета) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК ТребуетсяНастройкаСчетУчета,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияОСНО, ТаблицаДвиженийСоСчетами.СчетСписанияОСНО) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.РасходыСписываютсяНаОСНО И НЕ ТаблицаДвиженийСоСчетами.СчетУчетаВ90и91 КАК ТребуетсяНастройкаСчетСписанияОСНО,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВД, ТаблицаДвиженийСоСчетами.СчетСписанияЕНВД) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.РасходыСписываютсяНаЕНВД И НЕ ТаблицаДвиженийСоСчетами.СчетУчетаВ90и91 КАК ТребуетсяНастройкаСчетСписанияЕНВД,
	|	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчета, ТаблицаДвиженийСоСчетами.СчетУчета) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияОСНО, ТаблицаДвиженийСоСчетами.СчетСписанияОСНО) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.РасходыСписываютсяНаОСНО И НЕ ТаблицаДвиженийСоСчетами.СчетУчетаВ90и91 ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВД, ТаблицаДвиженийСоСчетами.СчетСписанияЕНВД) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.РасходыСписываютсяНаЕНВД И НЕ ТаблицаДвиженийСоСчетами.СчетУчетаВ90и91 КАК ТребуетсяНастройка,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.ИзмененныеДанные, ""СчетУчета, СчетСписанияОСНО, СчетСписанияЕНВД"") КАК ИзмененныеДанные
	|
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|	ПО
	|		ТаблицаДвиженийСоСчетами.Организация = ТекущаяТаблицаСчетовУчета.Организация
	|		И ТаблицаДвиженийСоСчетами.Подразделение = ТекущаяТаблицаСчетовУчета.Подразделение
	|		И ТаблицаДвиженийСоСчетами.СтатьяРасходов = ТекущаяТаблицаСчетовУчета.СтатьяРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.Подразделение,
	|	ТекущаяТаблицаСчетовУчета.СтатьяРасходов,
	|	ТекущаяТаблицаСчетовУчета.СтатьяРасходов.ВидЦенностиНДС,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчета,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияОСНО,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВД,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияОСНОПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетСписанияЕНВДПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчета,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетСписанияОСНО,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетСписанияЕНВД,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ИЗ
	|	ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|	ПО
	|		ТекущаяТаблицаСчетовУчета.Организация = ТаблицаДвиженийСоСчетами.Организация
	|		И ТаблицаДвиженийСоСчетами.Подразделение = ТекущаяТаблицаСчетовУчета.Подразделение
	|		И ТаблицаДвиженийСоСчетами.СтатьяРасходов = ТекущаяТаблицаСчетовУчета.СтатьяРасходов
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.Организация ЕСТЬ NULL";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Новый Структура("СчетаУчетаРасходов", Таблица), АдресХранилища);
	
КонецПроцедуры

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ИсходнаяТаблица.СтатьяРасходов КАК СтатьяРасходов
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ИзмененныеДанные <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СтатьяРасходов
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, Подразделение, СтатьяРасходов) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.Подразделение,
	|				ТаблицаНастроек.СтатьяРасходов
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		ДанныеРегистра.Организация = ТаблицаНастроек.Организация
	|		И ДанныеРегистра.Подразделение = ТаблицаНастроек.Подразделение
	|		И ДанныеРегистра.СтатьяРасходовСписания = ТаблицаНастроек.СтатьяРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|ГДЕ
	|	(ДанныеРегистра.Период <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, Подразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.Подразделение
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|			ГДЕ
	|				ТаблицаНастроек.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация, Подразделение, СтатьяРасходов, ИзмененныеДанные");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак наличия ошибок заполнения настроек отражения амортизации НМА в регламентированном учете.
//
// Параметры:
// 		МассивОрганизаций - Массив, СправочникСсылка.Организации - Массив или ссылка на элемент справочника организаций, по которым выполняется проверка
// 		Период - Дата - Период отражения амортизации.
//
// Возвращаемое значение:
// 		Булево - Истина, если имеются ошибки заполенния настроек отражения расходов.
//
Функция ЕстьОшибкиЗаполненияРасходовПоАмортизацииНМА(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрОрганизации = Неопределено;
	Если ТипЗнч(МассивОрганизаций) = Тип("Массив") Тогда
		ПараметрОрганизации = МассивОрганизаций;
	Иначе
		ПараметрОрганизации = Новый Массив;
		ПараметрОрганизации.Добавить(МассивОрганизаций);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрОрганизации);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив,
	|	СчетаБухгалтерскогоУчетаНМА.СчетУчета,
	|	ПараметрыЦелевогоФинансированияНМА.СчетУчета КАК СчетУчетаЦФ,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансированияНМА.ПрименениеЦелевогоФинансирования, ЛОЖЬ) КАК ПрименениеЦелевогоФинансирования
	|ПОМЕСТИТЬ втСчетаБухгалтерскогоУчетаНМА
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СчетаБухгалтерскогоУчетаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(&НачалоМесяца, ) КАК ПараметрыЦелевогоФинансированияНМА
	|	ПО СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ПараметрыЦелевогоФинансированияНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ЕСТЬNULL(ДанныеСчетУчета.Подразделение, ЕСТЬNULL(ДанныеСчетУчетаЦФ.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	втСчетаБухгалтерскогоУчетаНМА КАК втСчетаБухгалтерскогоУчетаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоМесяца,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.СчетУчета
	|				ИЗ
	|					втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчета
	|	ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчета.Субконто1
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоМесяца,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.СчетУчетаЦФ
	|				ИЗ
	|					втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчетаЦФ
	|	ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчетаЦФ.Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)) КАК Организация,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеНМА.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиНМАКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК МестонахождениеНМА
	|		ПО СпособыОтраженияРасходов.НематериальныйАктив = МестонахождениеНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	втСтатьиНМАКОтражению.СтатьяРасходов
	|ИЗ
	|	втСтатьиНМАКОтражению КАК втСтатьиНМАКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|		ПО втСтатьиНМАКОтражению.Организация = ПорядокОтраженияРасходов.Организация
	|			И втСтатьиНМАКОтражению.Подразделение = ПорядокОтраженияРасходов.Подразделение
	|			И втСтатьиНМАКОтражению.СтатьяРасходов = ПорядокОтраженияРасходов.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО втСтатьиНМАКОтражению.СтатьяРасходов.СчетУчета = Хозрасчетный.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, Хозрасчетный.Ссылка) ЕСТЬ NULL ";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает признак наличия ошибок заполнения настроек отражения амортизации ОС в регламентированном учете.
//
// Параметры:
// 		МассивОрганизаций - Массив, СправочникСсылка.Организации - Массив или ссылка на элемент справочника организаций, по которым выполняется проверка
// 		Период - Дата - Период отражения амортизации.
//
// Возвращаемое значение:
// 		Булево - Истина, если имеются ошибки заполенния настроек отражения расходов.
//
Функция ЕстьОшибкиЗаполненияРасходовПоАмортизацииОС(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрОрганизации = Неопределено;
	Если ТипЗнч(МассивОрганизаций) = Тип("Массив") Тогда
		ПараметрОрганизации = МассивОрганизаций;
	Иначе
		ПараметрОрганизации = Новый Массив;
		ПараметрОрганизации.Добавить(МассивОрганизаций);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрОрганизации);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)) КАК Организация,
	|	ВЫБОР КОГДА СтатьиРасходовПоИзменениюПараметров.Ссылка ЕСТЬ NULL
	|		ТОГДА ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)))
	|	КОНЕЦ КАК Подразделение,
	|	ЕСТЬNULL(СтатьиРасходовПоИзменениюПараметров.Ссылка, ПринятиеКУчетуОС.СтатьяРасходовАмортизационнойПремии) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиОсновныхСредствКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&НачалоМесяца,
	|				Организация В (&МассивОрганизаций)
	|					И Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС) КАК ПервоначальныеСведения
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ПО (ПервоначальныеСведения.Регистратор = ПринятиеКУчетуОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходовПоИзменениюПараметров
	|		ПО ОтражениеАмортизационныхРасходов.СтатьяРасходовАмортизационнойПремии = СтатьиРасходовПоИзменениюПараметров.Ссылка
	|ГДЕ
	|	ПринятиеКУчетуОС.ВключитьАмортизационнуюПремиюВСоставРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов)
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|ГДЕ
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	втСтатьиОсновныхСредствКОтражению.СтатьяРасходов
	|ИЗ
	|	втСтатьиОсновныхСредствКОтражению КАК втСтатьиОсновныхСредствКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|		ПО втСтатьиОсновныхСредствКОтражению.Организация = ПорядокОтраженияРасходов.Организация
	|			И втСтатьиОсновныхСредствКОтражению.Подразделение = ПорядокОтраженияРасходов.Подразделение
	|			И втСтатьиОсновныхСредствКОтражению.СтатьяРасходов = ПорядокОтраженияРасходов.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ПО втСтатьиОсновныхСредствКОтражению.СтатьяРасходов.СчетУчета = Хозрасчетный.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, Хозрасчетный.Ссылка) ЕСТЬ NULL
	|		ИЛИ ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			И ЕСТЬNULL(Хозрасчетный.Ссылка, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает настройки отражения статьи расходов в регламентированном учете. Если записей в регистре нет - будут
// получаться данные по умолчанию из статьи расходов.
//
// Параметры:
//		Организация - СправочникСсылка.Организации - ссылка на элемент справочника организаций, по которой получается настройка регистра;
//		СтатьяРасходов - ПланВидовХарактеристик.СтатьиРасходов - статья расходов, для которой получаются настройки, в случае отсутствия записей в регистре - настройки будут получаться непосредственно из статьи;
//		Подразделение - СправочникСсылка.СтруктураПредприятия, СправочникСсылка.Партнеры - если не задано - настройки будут искаться для пустого подразделения.
//
// Возвращаемое значение:
//		Структура - идентична структуре ресурсов регистра.
//
Функция НастройкаОтраженияСтатьиРасходов(Организация, СтатьяРасходов, Подразделение = Неопределено) Экспорт
	
	НастройкаОтражения = НоваяНастройкаОтраженияСтатьиРасходов();	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорядокОтраженияРасходов.СтатьяРасходов,
	|	ПорядокОтраженияРасходов.СчетУчета,
	|	ПорядокОтраженияРасходов.ОтражениеВУСН,
	|	ПорядокОтраженияРасходов.СпособНастройкиСчетовУчета,
	|	ПорядокОтраженияРасходов.СчетУчетаНУ,
	|	ПорядокОтраженияРасходов.СтатьяРасходовНУ,
	|	ПорядокОтраженияРасходов.ВидСубконто,
	|	ПорядокОтраженияРасходов.ВидСубконтоНУ,
	|	ПорядокОтраженияРасходов.СчетСписанияОСНО,
	|	ПорядокОтраженияРасходов.СчетСписанияЕНВД,
	|	1 КАК Приоритет
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|ГДЕ
	|	ПорядокОтраженияРасходов.Организация = &Организация
	|	И ПорядокОтраженияРасходов.СтатьяРасходов = &СтатьяРасходов
	|	И ПорядокОтраженияРасходов.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтатьиРасходов.Ссылка,
	|	СтатьиРасходов.СчетУчета,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.ВидДеятельностиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиРасходов.ОсновнаяДеятельность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются)
	|		КОГДА СтатьиРасходов.ВидДеятельностиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиРасходов.ПрочаяДеятельность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
	|		КОГДА СтатьиРасходов.ВидДеятельностиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Распределяются)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыНастройкиСчетовУчета.ОдинаковоДляВсехДокументов),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|		КОГДА ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов)
	|		КОГДА ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	СтатьиРасходов.СчетСписанияОСНО,
	|	СтатьиРасходов.СчетСписанияЕНВД,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат)
	|			ТОГДА 2
	|		КОГДА ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов)
	|			ТОГДА 3
	|		КОГДА ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|		ПО СтатьиРасходов.СчетУчета = ХозрасчетныйВидыСубконто.Ссылка
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НастройкаОтражения, Выборка);
	КонецЕсли;
	
	Возврат НастройкаОтражения;
	
КонецФункции

Функция РезультатЗапросаПоНастройкамОтраженияВУчетеДетально(
	Организация,
	Подразделение,
	СтатьяРасходов,
	ДатаНачала,
	ДатаОкончания,
	ПоказыватьОтраженныеВУчете
	) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов
	|
	|ПОМЕСТИТЬ ПрочиеРасходы
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор,
	|		Организация = &Организация
	|		И Подразделение = &Подразделение
	|		И СтатьяРасходов = &СтатьяРасходов
	|	) КАК ДанныеРегистра
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументов
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументов.Регистратор
	|		И (ОтражениеДокументов.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|			ИЛИ &ПоказыватьОтраженныеВУчете)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрочиеРасходы.Документ КАК Документ,
	|	ПрочиеРасходы.Документ.Дата КАК Дата,
	|	ПрочиеРасходы.Документ.Номер КАК Номер,
	|	ТИПЗНАЧЕНИЯ(ПрочиеРасходы.Документ) КАК Тип,
	|	ИСТИНА КАК ЕстьРасходы,
	|
	|	ЕСТЬNULL(ПорядокОтражения.СчетУчета, Неопределено) КАК СчетУчета,
	|	ЕСТЬNULL(ПорядокОтражения.СчетУчетаНУ, Неопределено) КАК СчетУчетаНУ,
	|	ЕСТЬNULL(ПорядокОтражения.СтатьяРасходовНУ, Неопределено) КАК СтатьяРасходовНУ,
	|	ЕСТЬNULL(ПорядокОтражения.ОтражениеВУСН, Неопределено) КАК ОтражениеВУСН
	|ИЗ
	|	ПрочиеРасходы КАК ПрочиеРасходы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|	ПО
	|		&Организация = ПорядокОтражения.Организация
	|		И &Подразделение = ПорядокОтражения.Подразделение
	|		И &СтатьяРасходов = ПорядокОтражения.СтатьяРасходов
	|		И ПрочиеРасходы.Документ = ПорядокОтражения.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата Возр,
	|	Документ
	|");
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	Запрос.УстановитьПараметр("ПоказыватьОтраженныеВУчете", ПоказыватьОтраженныеВУчете);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Функция НоваяНастройкаОтраженияСтатьиРасходов()
	
	СтруктураВозврата = Новый Структура;
	
	СтруктураВозврата.Вставить("СтатьяРасходов");
	СтруктураВозврата.Вставить("СчетУчета");
	СтруктураВозврата.Вставить("ОтражениеВУСН");
	СтруктураВозврата.Вставить("СпособНастройкиСчетовУчета");
	СтруктураВозврата.Вставить("СчетУчетаНУ");
	СтруктураВозврата.Вставить("СтатьяРасходовНУ");
	СтруктураВозврата.Вставить("ВидСубконто");
	СтруктураВозврата.Вставить("ВидСубконтоНУ");
	СтруктураВозврата.Вставить("СчетСписанияОСНО");
	СтруктураВозврата.Вставить("СчетСписанияЕНВД");
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#КонецЕсли