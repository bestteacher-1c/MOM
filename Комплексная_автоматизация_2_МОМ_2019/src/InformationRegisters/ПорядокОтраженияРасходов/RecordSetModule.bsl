#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не ЗначениеЗаполнено(Запись.Документ)
		 И Запись.Документ <> Неопределено Тогда
			Запись.Документ = Неопределено;
		КонецЕсли;
		
		Если Запись.СпособНастройкиСчетовУчета.Пустая() Тогда
			Запись.СпособНастройкиСчетовУчета = Перечисления.СпособыНастройкиСчетовУчета.ОдинаковоДляВсехДокументов;
		КонецЕсли;
		
		ВидыСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные;
		
		Если ЗначениеЗаполнено(Запись.СчетУчета) Тогда
			
			Если Не Запись.СчетУчета.ВидыСубконто.Найти(ВидыСубконто.СтатьиЗатрат) = Неопределено Тогда
				Запись.ВидСубконто = ВидыСубконто.СтатьиЗатрат;
				
			ИначеЕсли Не Запись.СчетУчета.ВидыСубконто.Найти(ВидыСубконто.РасходыБудущихПериодов) = Неопределено Тогда
				Запись.ВидСубконто = ВидыСубконто.РасходыБудущихПериодов;
				
			ИначеЕсли Не Запись.СчетУчета.ВидыСубконто.Найти(ВидыСубконто.ПрочиеДоходыИРасходы) = Неопределено Тогда
				Запись.ВидСубконто = ВидыСубконто.ПрочиеДоходыИРасходы;
				
			Иначе
				Запись.ВидСубконто = Неопределено;
				
			КонецЕсли;
			
		Иначе
			Запись.ВидСубконто = Неопределено;
			
		КонецЕсли;
		
		Если Запись.СпособНастройкиСчетовУчета = Перечисления.СпособыНастройкиСчетовУчета.ОдинаковоДляВсехДокументов Тогда
			Если ЗначениеЗаполнено(Запись.СчетУчетаНУ) Тогда
				Запись.СчетУчетаНУ = Неопределено;
			КонецЕсли;
			Если ЗначениеЗаполнено(Запись.СтатьяРасходовНУ) Тогда
				Запись.СтатьяРасходовНУ = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.СчетУчетаНУ) Тогда
			
			Если Не Запись.СчетУчетаНУ.ВидыСубконто.Найти(ВидыСубконто.СтатьиЗатрат) = Неопределено Тогда
				Запись.ВидСубконтоНУ = ВидыСубконто.СтатьиЗатрат;
				
			ИначеЕсли Не Запись.СчетУчетаНУ.ВидыСубконто.Найти(ВидыСубконто.РасходыБудущихПериодов) = Неопределено Тогда
				Запись.ВидСубконтоНУ = ВидыСубконто.РасходыБудущихПериодов;
				
			ИначеЕсли Не Запись.СчетУчетаНУ.ВидыСубконто.Найти(ВидыСубконто.ПрочиеДоходыИРасходы) = Неопределено Тогда
				Запись.ВидСубконтоНУ = ВидыСубконто.ПрочиеДоходыИРасходы;
				
			Иначе
				Запись.ВидСубконтоНУ = Неопределено;
				
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(Запись.СтатьяРасходовНУ) Тогда
			Запись.ВидСубконтоНУ = ВидыСубконто.РасходыБудущихПериодов;
			
		Иначе
			Запись.ВидСубконтоНУ = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если Отказ ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокОтраженияРасходов.Организация,
	|	ПорядокОтраженияРасходов.СтатьяРасходов,
	|	ПорядокОтраженияРасходов.Подразделение,
	|	ПорядокОтраженияРасходов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		И ПорядокОтраженияРасходов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И ПорядокОтраженияРасходов.Подразделение = НЕОПРЕДЕЛЕНО КАК ЭтоОбщаяНастройка,
	|	ПорядокОтраженияРасходов.СчетУчета,
	|	ПорядокОтраженияРасходов.СчетСписанияОСНО,
	|	ПорядокОтраженияРасходов.СчетСписанияЕНВД
	|ПОМЕСТИТЬ ТаблицаНовыхДанных
	|ИЗ
	|	&ТаблицаНовыхДанных КАК ПорядокОтраженияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Подразделение КАК МестоУчета,
	|	ТаблицаНовыхДанных.СтатьяРасходов КАК АналитикаУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчета
	|ПОМЕСТИТЬ ДанныеЗаполненныхСчетовРегистра
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Подразделение,
	|	ТаблицаНовыхДанных.СтатьяРасходов,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовОСНО)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетСписанияОСНО <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Подразделение,
	|	ТаблицаНовыхДанных.СтатьяРасходов,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетСписанияЕНВД <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	Запрос.УстановитьПараметр("ТаблицаНовыхДанных", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить();
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОчиститьПриЗаписиРегистра(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли