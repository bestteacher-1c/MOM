#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ГруппаФинансовогоУчета") Тогда
		ГруппаФинансовогоУчета = Параметры.ГруппаФинансовогоУчета;
	КонецЕсли;
	
	Если Параметры.Свойство("Расчеты") Тогда
		СтруктураРасчетов = Параметры.Расчеты;
	КонецЕсли;
	
	Если СтруктураРасчетов = Неопределено И ЗначениеЗаполнено(ГруппаФинансовогоУчета) Тогда
		ПоляРасчетов = "РасчетыСКлиентами,РасчетыСПоставщиками,РасчетыСКомиссионерами,РасчетыСКомитентами,РасчетыСКредиторами,РасчетыСДебиторами,РасчетыСЛизингодателями";
		СтруктураРасчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ГруппаФинансовогоУчета, ПоляРасчетов);
	КонецЕсли;
	
	УправлениеФормой();
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список, "ГруппаФинансовогоУчета", ГруппаФинансовогоУчета, ЗначениеЗаполнено(ГруппаФинансовогоУчета));
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГруппаФинансовогоУчетаПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список, "ГруппаФинансовогоУчета", ГруппаФинансовогоУчета, ЗначениеЗаполнено(ГруппаФинансовогоУчета))
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	КоличествоУказанныхВидовРасчетов = 0;
	Для каждого ВидРасчетов Из СтруктураРасчетов Цикл
		КоличествоУказанныхВидовРасчетов = КоличествоУказанныхВидовРасчетов + ?(ВидРасчетов.Значение, 1, 0);
	КонецЦикла;
	
	Если КоличествоУказанныхВидовРасчетов <> 1 Тогда
		Элементы.ГруппаРасчетыСКомиссионерами.Видимость = Ложь;
		Элементы.ГруппаРасчетыСКомитентами.Видимость = Ложь;
		Элементы.ГруппаРасчетыСКредиторами.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов");
		Элементы.ГруппаРасчетыСДебиторами.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов");
		Элементы.ГруппаРасчетыСЛизингодателями.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьЛизинг");
	Иначе
		Для каждого ВидРасчетов Из СтруктураРасчетов Цикл
			ГруппаВидаРасчетов = Элементы.Найти("Группа" + ВидРасчетов.Ключ);
			Если ГруппаВидаРасчетов <> Неопределено Тогда
				ГруппаВидаРасчетов.Видимость = ВидРасчетов.Значение;
				ГруппаВидаРасчетов.ОтображатьВШапке = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти
