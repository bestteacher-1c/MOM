#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.НематериальныйАктив,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.СтатьяРасходов,
	|	ТаблицаПередЗаписью.АналитикаРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходовЗаданДокументом,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходов,
	|	ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаПередЗаписью.ОрганизацияПолучательРасходов,
	|	ТаблицаПередЗаписью.СчетПередачиРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетПередЗаписью
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовЗаданДокументом <> ТаблицаПередЗаписью.СпособОтраженияРасходовЗаданДокументом
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СчетПередачиРасходов <> ТаблицаПередЗаписью.СчетПередачиРасходов
	|				ИЛИ ТаблицаПриЗаписи.НематериальныйАктив ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовЗаданДокументом <> ТаблицаПередЗаписью.СпособОтраженияРасходовЗаданДокументом
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СчетПередачиРасходов <> ТаблицаПередЗаписью.СчетПередачиРасходов
	|				ИЛИ ТаблицаПередЗаписью.НематериальныйАктив ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
