
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не ЗначениеЗаполнено(Запись.СчетУчетаНДСПриЗакупке) Тогда
			Если Запись.СчетУчетаНаСкладе = ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке
			 ИЛИ Запись.СчетУчетаНаСкладе = ПланыСчетов.Хозрасчетный.КомпонентыОсновныхСредств Тогда
				Запись.СчетУчетаНДСПриЗакупке = ПланыСчетов.Хозрасчетный.НДСприПриобретенииОсновныхСредств;
			Иначе
				Запись.СчетУчетаНДСПриЗакупке = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если Отказ ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокОтраженияНоменклатуры.Организация,
	|	ПорядокОтраженияНоменклатуры.Склад,
	|	ПорядокОтраженияНоменклатуры.ГруппаФинансовогоУчета,
	|	ПорядокОтраженияНоменклатуры.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|		И ПорядокОтраженияНоменклатуры.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И ПорядокОтраженияНоменклатуры.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК ЭтоОбщаяНастройка,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаНаСкладе,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаВыручкиОтПродаж,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаСебестоимостиПродаж,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаВыручкиОтПродажЕНВД,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаСебестоимостиПродажЕНВД,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаНДСПриПродаже,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаВПути,
	|	ПорядокОтраженияНоменклатуры.СчетУчетаНДСВПути
	|ПОМЕСТИТЬ ТаблицаНовыхДанных
	|ИЗ
	|	&ТаблицаНовыхДанных КАК ПорядокОтраженияНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад КАК МестоУчета,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчета
	|ПОМЕСТИТЬ ДанныеЗаполненныхСчетовРегистра
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаНаСкладе <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаВыручкиОтПродаж <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СебестоимостьПродаж)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаСебестоимостиПродаж <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаВыручкиОтПродажЕНВД <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СебестоимостьПродажЕНВД)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаСебестоимостиПродажЕНВД <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриПродаже)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаНДСПриПродаже <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаВПути <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	ТаблицаНовыхДанных.Склад,
	|	ТаблицаНовыхДанных.ГруппаФинансовогоУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриОтгрузкеБезПереходаПраваСобственности)
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчетаНДСВПути <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	Запрос.УстановитьПараметр("ТаблицаНовыхДанных", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить();
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОчиститьПриЗаписиРегистра(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли