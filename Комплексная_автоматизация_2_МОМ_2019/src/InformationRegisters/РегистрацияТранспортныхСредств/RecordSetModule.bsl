#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.ВключатьВНалоговуюБазу КАК ВключатьВНалоговуюБазу,
	|	ТаблицаПередЗаписью.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	ТаблицаПередЗаписью.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	ТаблицаПередЗаписью.Марка КАК Марка,
	|	ТаблицаПередЗаписью.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
	|	ТаблицаПередЗаписью.НалоговыйОрган КАК НалоговыйОрган,
	|	ТаблицаПередЗаписью.КодПоОКТМО КАК КодПоОКТМО,
	|	ТаблицаПередЗаписью.КодПоОКАТО КАК КодПоОКАТО,
	|	ТаблицаПередЗаписью.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	ТаблицаПередЗаписью.НалоговаяБаза КАК НалоговаяБаза,
	|	ТаблицаПередЗаписью.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	ТаблицаПередЗаписью.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ТаблицаПередЗаписью.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	ТаблицаПередЗаписью.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	ТаблицаПередЗаписью.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	ТаблицаПередЗаписью.ПроцентУменьшения КАК ПроцентУменьшения,
	|	ТаблицаПередЗаписью.СуммаУменьшения КАК СуммаУменьшения,
	|	ТаблицаПередЗаписью.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	ТаблицаПередЗаписью.ЭкологическийКласс КАК ЭкологическийКласс,
	|	ТаблицаПередЗаписью.ОбщаяСобственность КАК ОбщаяСобственность,
	|	ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ТаблицаПередЗаписью.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	ТаблицаПередЗаписью.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ТаблицаПередЗаписью.ВидЗаписи КАК ВидЗаписи,
	|	ТаблицаПередЗаписью.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ РегистрацияТранспортныхСредствПередЗаписью
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период            КАК Период,
	|	Таблица.Организация       КАК Организация,
	|	Таблица.ОсновноеСредство  КАК ОсновноеСредство,
	|	ИСТИНА                    КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                      КАК ОтражатьВУпрУчете,
	|	&Регистратор              КАК Документ
	|ПОМЕСТИТЬ РегистрацияТранспортныхСредствИзменение
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ) КАК Период,
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		РегистрацияТранспортныхСредствПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК ТаблицаПриЗаписи
	|			ПО (ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация)
	|				И (ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство)
	|				И (ТаблицаПриЗаписи.Регистратор = &Регистратор)
	|	ГДЕ
	|		(ТаблицаПередЗаписью.ВключатьВНалоговуюБазу <> ЕСТЬNULL(ТаблицаПриЗаписи.ВключатьВНалоговуюБазу, НЕОПРЕДЕЛЕНО)
	|			ИЛИ ТаблицаПередЗаписью.РегистрационныйЗнак <> ТаблицаПриЗаписи.РегистрационныйЗнак
	|			ИЛИ ТаблицаПередЗаписью.ИдентификационныйНомер <> ТаблицаПриЗаписи.ИдентификационныйНомер
	|			ИЛИ ТаблицаПередЗаписью.Марка <> ТаблицаПриЗаписи.Марка
	|			ИЛИ ТаблицаПередЗаписью.ПостановкаНаУчетВНалоговомОргане <> ТаблицаПриЗаписи.ПостановкаНаУчетВНалоговомОргане
	|			ИЛИ ТаблицаПередЗаписью.НалоговыйОрган <> ТаблицаПриЗаписи.НалоговыйОрган
	|			ИЛИ ТаблицаПередЗаписью.КодПоОКТМО <> ТаблицаПриЗаписи.КодПоОКТМО
	|			ИЛИ ТаблицаПередЗаписью.КодПоОКАТО <> ТаблицаПриЗаписи.КодПоОКАТО
	|			ИЛИ ТаблицаПередЗаписью.КодВидаТранспортногоСредства <> ТаблицаПриЗаписи.КодВидаТранспортногоСредства
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяБаза <> ТаблицаПриЗаписи.НалоговаяБаза
	|			ИЛИ ТаблицаПередЗаписью.ЕдиницаИзмеренияНалоговойБазы <> ТаблицаПриЗаписи.ЕдиницаИзмеренияНалоговойБазы
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяСтавка <> ТаблицаПриЗаписи.НалоговаяСтавка
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяЛьгота <> ТаблицаПриЗаписи.НалоговаяЛьгота
	|			ИЛИ ТаблицаПередЗаписью.КодНалоговойЛьготы <> ТаблицаПриЗаписи.КодНалоговойЛьготы
	|			ИЛИ ТаблицаПередЗаписью.ЛьготнаяСтавка <> ТаблицаПриЗаписи.ЛьготнаяСтавка
	|			ИЛИ ТаблицаПередЗаписью.ПроцентУменьшения <> ТаблицаПриЗаписи.ПроцентУменьшения
	|			ИЛИ ТаблицаПередЗаписью.СуммаУменьшения <> ТаблицаПриЗаписи.СуммаУменьшения
	|			ИЛИ ТаблицаПередЗаписью.РегиональныйКодЛьготы <> ТаблицаПриЗаписи.РегиональныйКодЛьготы
	|			ИЛИ ТаблицаПередЗаписью.ЭкологическийКласс <> ТаблицаПриЗаписи.ЭкологическийКласс
	|			ИЛИ ТаблицаПередЗаписью.ОбщаяСобственность <> ТаблицаПриЗаписи.ОбщаяСобственность
	|			ИЛИ ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЧислитель <> ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЧислитель
	|			ИЛИ ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЗнаменатель <> ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель
	|			ИЛИ ТаблицаПередЗаписью.ПовышающийКоэффициент <> ТаблицаПриЗаписи.ПовышающийКоэффициент
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяСтавкаЗависитОтГодаВыпускаТС <> ТаблицаПриЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|			ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|			ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|		И ТаблицаПередЗаписью.Организация.ЮридическоеФизическоеЛицо <> ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ),
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияТранспортныхСредствПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|	ГДЕ
	|		(ТаблицаПриЗаписи.ВключатьВНалоговуюБазу <> ЕСТЬNULL(ТаблицаПередЗаписью.ВключатьВНалоговуюБазу, НЕОПРЕДЕЛЕНО)
	|			ИЛИ ТаблицаПередЗаписью.РегистрационныйЗнак <> ТаблицаПриЗаписи.РегистрационныйЗнак
	|			ИЛИ ТаблицаПередЗаписью.ИдентификационныйНомер <> ТаблицаПриЗаписи.ИдентификационныйНомер
	|			ИЛИ ТаблицаПередЗаписью.Марка <> ТаблицаПриЗаписи.Марка
	|			ИЛИ ТаблицаПередЗаписью.ПостановкаНаУчетВНалоговомОргане <> ТаблицаПриЗаписи.ПостановкаНаУчетВНалоговомОргане
	|			ИЛИ ТаблицаПередЗаписью.НалоговыйОрган <> ТаблицаПриЗаписи.НалоговыйОрган
	|			ИЛИ ТаблицаПередЗаписью.КодПоОКТМО <> ТаблицаПриЗаписи.КодПоОКТМО
	|			ИЛИ ТаблицаПередЗаписью.КодПоОКАТО <> ТаблицаПриЗаписи.КодПоОКАТО
	|			ИЛИ ТаблицаПередЗаписью.КодВидаТранспортногоСредства <> ТаблицаПриЗаписи.КодВидаТранспортногоСредства
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяБаза <> ТаблицаПриЗаписи.НалоговаяБаза
	|			ИЛИ ТаблицаПередЗаписью.ЕдиницаИзмеренияНалоговойБазы <> ТаблицаПриЗаписи.ЕдиницаИзмеренияНалоговойБазы
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяСтавка <> ТаблицаПриЗаписи.НалоговаяСтавка
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяЛьгота <> ТаблицаПриЗаписи.НалоговаяЛьгота
	|			ИЛИ ТаблицаПередЗаписью.КодНалоговойЛьготы <> ТаблицаПриЗаписи.КодНалоговойЛьготы
	|			ИЛИ ТаблицаПередЗаписью.ЛьготнаяСтавка <> ТаблицаПриЗаписи.ЛьготнаяСтавка
	|			ИЛИ ТаблицаПередЗаписью.ПроцентУменьшения <> ТаблицаПриЗаписи.ПроцентУменьшения
	|			ИЛИ ТаблицаПередЗаписью.СуммаУменьшения <> ТаблицаПриЗаписи.СуммаУменьшения
	|			ИЛИ ТаблицаПередЗаписью.РегиональныйКодЛьготы <> ТаблицаПриЗаписи.РегиональныйКодЛьготы
	|			ИЛИ ТаблицаПередЗаписью.ЭкологическийКласс <> ТаблицаПриЗаписи.ЭкологическийКласс
	|			ИЛИ ТаблицаПередЗаписью.ОбщаяСобственность <> ТаблицаПриЗаписи.ОбщаяСобственность
	|			ИЛИ ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЧислитель <> ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЧислитель
	|			ИЛИ ТаблицаПередЗаписью.ДоляВПравеОбщейСобственностиЗнаменатель <> ТаблицаПриЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель
	|			ИЛИ ТаблицаПередЗаписью.ПовышающийКоэффициент <> ТаблицаПриЗаписи.ПовышающийКоэффициент
	|			ИЛИ ТаблицаПередЗаписью.НалоговаяСтавкаЗависитОтГодаВыпускаТС <> ТаблицаПриЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|			ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|			ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|		И ТаблицаПриЗаписи.Организация.ЮридическоеФизическоеЛицо <> ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрацияТранспортныхСредствПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("РегистрацияТранспортныхСредствИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли