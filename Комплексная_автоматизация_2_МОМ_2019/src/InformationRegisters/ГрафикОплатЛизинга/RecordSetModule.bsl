#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	УслугаПоЛизингуИтог = 0; ЗачетОбеспечительногоПлатежаИтог = 0; ВыкупПредметаЛизингаИтог = 0;
	Для каждого СтрокаНабора Из ЭтотОбъект Цикл
		
		УслугаПоЛизингуИтог = УслугаПоЛизингуИтог + СтрокаНабора.УслугаПоЛизингу;
		СтрокаНабора.УслугаПоЛизингуИтог = УслугаПоЛизингуИтог;
		
		ЗачетОбеспечительногоПлатежаИтог = ЗачетОбеспечительногоПлатежаИтог + СтрокаНабора.ЗачетОбеспечительногоПлатежа;
		СтрокаНабора.ЗачетОбеспечительногоПлатежаИтог = ЗачетОбеспечительногоПлатежаИтог;
		
		ВыкупПредметаЛизингаИтог = ВыкупПредметаЛизингаИтог + СтрокаНабора.ВыкупПредметаЛизинга;
		СтрокаНабора.ВыкупПредметаЛизингаИтог = ВыкупПредметаЛизингаИтог;
	КонецЦикла;
	
	СФормироватьТаблицуОбъектовОплаты();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоФинансовымИнструментам(
		ДополнительныеСвойства.ТаблицаОбъектовОплаты.ВыгрузитьКолонку("ОбъектОплаты"));
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
			ЗаполнитьЗначенияСвойств(СтрокаНабора,ДанныеЗаполнения);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует таблицу заказов, которые были раньше в движениях и которые сейчас будут записаны.
//
Процедура СФормироватьТаблицуОбъектовОплаты()
	
	Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.ВариантГрафика.Значение, "Владелец");
	
	ТаблицаОбъектовОплаты = Новый ТаблицаЗначений;
	ТаблицаОбъектовОплаты.Колонки.Добавить("ОбъектОплаты");
	ТаблицаОбъектовОплаты.Колонки.Добавить("ВариантГрафика");
	НоваяСтрока = ТаблицаОбъектовОплаты.Добавить();
	НоваяСтрока.ОбъектОплаты = Договор;
	НоваяСтрока.ВариантГрафика = Отбор.ВариантГрафика.Значение;
	
	ДополнительныеСвойства.Вставить("ТаблицаОбъектовОплаты", ТаблицаОбъектовОплаты);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли