#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.НематериальныйАктив,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.ПервоначальнаяСтоимость,
	|	ТаблицаПередЗаписью.НачислятьАмортизацию,
	|	ТаблицаПередЗаписью.СпособНачисленияАмортизации,
	|	ТаблицаПередЗаписью.СрокПолезногоИспользования,
	|	ТаблицаПередЗаписью.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаПередЗаписью.Коэффициент,
	|	ТаблицаПередЗаписью.РезервПереоценкиСтоимости,
	|	ТаблицаПередЗаписью.РезервПереоценкиАмортизации
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАБухгалтерскийУчетПередЗаписью
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАБухгалтерскийУчетИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		ПервоначальныеСведенияНМАБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.Организация <> ТаблицаПередЗаписью.Организация
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимость <> ТаблицаПередЗаписью.ПервоначальнаяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизацию <> ТаблицаПередЗаписью.НачислятьАмортизацию
	|				ИЛИ ТаблицаПриЗаписи.СпособНачисленияАмортизации <> ТаблицаПередЗаписью.СпособНачисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользования <> ТаблицаПередЗаписью.СрокПолезногоИспользования
	|				ИЛИ ТаблицаПриЗаписи.ОбъемПродукцииРаботДляВычисленияАмортизации <> ТаблицаПередЗаписью.ОбъемПродукцииРаботДляВычисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.Коэффициент <> ТаблицаПередЗаписью.Коэффициент
	|				ИЛИ ТаблицаПриЗаписи.РезервПереоценкиСтоимости <> ТаблицаПередЗаписью.РезервПереоценкиСтоимости
	|				ИЛИ ТаблицаПриЗаписи.РезервПереоценкиАмортизации <> ТаблицаПередЗаписью.РезервПереоценкиАмортизации
	|				ИЛИ ТаблицаПриЗаписи.НематериальныйАктив ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАБухгалтерскийУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.Организация <> ТаблицаПередЗаписью.Организация
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимость <> ТаблицаПередЗаписью.ПервоначальнаяСтоимость
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизацию <> ТаблицаПередЗаписью.НачислятьАмортизацию
	|				ИЛИ ТаблицаПриЗаписи.СпособНачисленияАмортизации <> ТаблицаПередЗаписью.СпособНачисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользования <> ТаблицаПередЗаписью.СрокПолезногоИспользования
	|				ИЛИ ТаблицаПриЗаписи.ОбъемПродукцииРаботДляВычисленияАмортизации <> ТаблицаПередЗаписью.ОбъемПродукцииРаботДляВычисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.Коэффициент <> ТаблицаПередЗаписью.Коэффициент
	|				ИЛИ ТаблицаПриЗаписи.РезервПереоценкиСтоимости <> ТаблицаПередЗаписью.РезервПереоценкиСтоимости
	|				ИЛИ ТаблицаПриЗаписи.РезервПереоценкиАмортизации <> ТаблицаПередЗаписью.РезервПереоценкиАмортизации
	|				ИЛИ ТаблицаПередЗаписью.НематериальныйАктив ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПервоначальныеСведенияНМАБухгалтерскийУчетПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПервоначальныеСведенияНМАБухгалтерскийУчетИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
