#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение КАК МестоПроизводства
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Подразделение = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ИНАЧЕ ДанныеРегистра.Подразделение
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И НЕ ДанныеРегистра.Подразделение ССЫЛКА Справочник.Партнеры
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ВЫБОР
	|		КОГДА  ДанныеРегистра.АналитикаУчетаНоменклатуры.ТипМестаХранения В (ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение),ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад), ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер))
	|			ТОГДА ДанныеРегистра.АналитикаУчетаНоменклатуры.МестоХранения
	|		ИНАЧЕ ЕСТЬNULL(ДанныеРегистра.АналитикаУчетаНоменклатуры.МестоХранения.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Партнер
	|ИЗ
	|	Документ.ПоступлениеОтПереработчика КАК ДанныеДокумента
	|ГДЕ
	|	&УсловиеДляДокумента";
	
	УсловиеВхожденияВРегистр = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляРегистра";
	УсловиеВхожденияВДокумент = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляДокумента";
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеРегистра.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ";
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеРегистра.Организация В (&Организации)";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Регистратор", "Ссылка");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
	Иначе
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыАнализатора.КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Период", "Дата");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		ТекстУсловий = ТекстУсловий + Символы.ПС + Символы.Таб + "И ДанныеДокумента.Проведен";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.МестоПроизводства КАК МестоПроизводства,
	|
	|	ЕСТЬNULL(ПорядокОтражения.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвижений КАК ТаблицаДвижений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияПроизводства КАК ПорядокОтражения
	|		ПО ТаблицаДвижений.Организация = ПорядокОтражения.Организация
	|			И ТаблицаДвижений.МестоПроизводства = ПорядокОтражения.МестоПроизводства";
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Выполняет анализ всех неотраженных операций отражения производства,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	Параметры:
//		ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.МестоПроизводства КАК МестоУчета,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить();
	
	СтруктураВидовСчетовИРезультатаЗапроса = Новый Соответствие;
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.Производство, Результат);
	
	ИмяСобытия = НСтр("ru = 'Анализ порядка отражения производства, требующего настройки'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(СтруктураВидовСчетовИРезультатаЗапроса, ИмяСобытия);
	
КонецПроцедуры

// Выполняет анализ операций отражения производства, в зависимости от параметров и дополняет исходную таблицу операций
// новыми строками.
//	
//	Параметры:
//		ПараметрыАнализа - Структура - содержит настройки анализатора со следующими свойствами:
//			* РежимАнализа - Строка - принимает значения вида "ПоНеотраженным" и "ЗаПериод". Причем если не задана, значение по умолчанию "ЗаПериод".
//			* КонецПериода - Дата - по эту дату анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* НачалоПериода - Дата - начиная с этой даты анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//		АдресХранилища - Строка - адрес сохранения результата работы метода (ТаблицаЗначений).
//
Процедура ДополнитьТаблицуНаОснованииАнализаОпераций(ПараметрыАнализа, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализа);		
	 
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ПараметрыАнализа.ЗаполняемаяТаблица);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.МестоПроизводства,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ПОМЕСТИТЬ ТекущаяТаблицаСчетовУчета
	|ИЗ
	|	&Таблица КАК ТекущаяТаблицаСчетовУчета;
	|
	|///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.МестоПроизводства,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчета, ТаблицаДвиженийСоСчетами.СчетУчета) КАК СчетУчета,
	|	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчета, ТаблицаДвиженийСоСчетами.СчетУчета) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК ТребуетсяНастройка,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.ИзмененныеДанные, ""СчетУчета"") КАК ИзмененныеДанные
	|
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|	ПО
	|		ТаблицаДвиженийСоСчетами.Организация = ТекущаяТаблицаСчетовУчета.Организация
	|		И ТаблицаДвиженийСоСчетами.МестоПроизводства = ТекущаяТаблицаСчетовУчета.МестоПроизводства
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.МестоПроизводства,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|ИЗ
	|	ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|	ПО
	|		ТекущаяТаблицаСчетовУчета.Организация = ТаблицаДвиженийСоСчетами.Организация
	|		И ТекущаяТаблицаСчетовУчета.МестоПроизводства = ТаблицаДвиженийСоСчетами.МестоПроизводства
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.Организация ЕСТЬ NULL";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Новый Структура("СчетаУчетаПроизводства", Таблица), АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли