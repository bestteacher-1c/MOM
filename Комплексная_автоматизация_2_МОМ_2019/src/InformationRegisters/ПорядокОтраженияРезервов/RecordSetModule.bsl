#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если Отказ ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПорядокОтраженияРезервов.Организация,
	|	ПорядокОтраженияРезервов.ВидРезервов,
	|	ПорядокОтраженияРезервов.ВидРезервов = ЗНАЧЕНИЕ(Справочник.Резервы.ПустаяСсылка)
	|		И ПорядокОтраженияРезервов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ЭтоОбщаяНастройка,
	|	ПорядокОтраженияРезервов.СчетУчета
	|ПОМЕСТИТЬ ТаблицаНовыхДанных
	|ИЗ
	|	&ТаблицаНовыхДанных КАК ПорядокОтраженияРезервов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНовыхДанных.Организация,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчета,
	|	ТаблицаНовыхДанных.ВидРезервов КАК АналитикаУчета,
	|	ТаблицаНовыхДанных.ЭтоОбщаяНастройка КАК ЭтоОбщаяНастройка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Резервы) КАК ВидСчета
	|ПОМЕСТИТЬ ДанныеЗаполненныхСчетовРегистра
	|ИЗ
	|	ТаблицаНовыхДанных КАК ТаблицаНовыхДанных
	|ГДЕ
	|	ТаблицаНовыхДанных.СчетУчета <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	Запрос.УстановитьПараметр("ТаблицаНовыхДанных", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить();
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОчиститьПриЗаписиРегистра(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
