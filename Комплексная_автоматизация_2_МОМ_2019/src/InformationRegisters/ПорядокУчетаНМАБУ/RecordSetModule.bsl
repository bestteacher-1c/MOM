#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ТаблицаПередЗаписью.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаПередЗаписью.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаПередЗаписью.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходов КАК СпособОтраженияРасходов
	|ПОМЕСТИТЬ ПорядокУчетаНМАБУПередЗаписью
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период              КАК Период,
	|	Таблица.Организация         КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	ИСТИНА                      КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                        КАК ОтражатьВУпрУчете,
	|	&Регистратор                КАК Документ
	|ПОМЕСТИТЬ ПорядокУчетаНМАБУИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период               КАК Период,
	|		ТаблицаПередЗаписью.Организация          КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		ПорядокУчетаНМАБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаНМАБУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаНМАБУ КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПередЗаписью.Организация       КАК Организация,
	|	ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|ПОМЕСТИТЬ ПорядокУчетаНМАБУ_НачислятьАмортизацию
	|ИЗ
	|	ПорядокУчетаНМАБУПередЗаписью КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ ИЛИ ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПриЗаписи.Организация,
	|	ТаблицаПриЗаписи.НематериальныйАктив
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАБУ КАК ТаблицаПриЗаписи
	|ГДЕ
	|	ТаблицаПриЗаписи.Регистратор = &Регистратор
	|	И (ТаблицаПриЗаписи.НачислятьАмортизациюБУ
	|		ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаНМАБУПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПорядокУчетаНМАБУИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

