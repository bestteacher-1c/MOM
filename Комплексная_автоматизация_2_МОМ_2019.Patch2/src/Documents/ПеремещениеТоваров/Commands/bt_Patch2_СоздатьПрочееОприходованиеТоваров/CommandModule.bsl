
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	
	НадоСоздаватьПрочееОприходованиеТоваров = Ложь;
	
	ПрочееОприходованиеТоваров = ПолучитьСсылкуНаПрочееОприходованиеТоваров(ПараметрКоманды, НадоСоздаватьПрочееОприходованиеТоваров);
	
	Если НадоСоздаватьПрочееОприходованиеТоваров = Истина Тогда
		
		ЗаполнитьПрочееОприходованиеТоваровНаОснованииПеремещенияТоваров(ПараметрКоманды, ПрочееОприходованиеТоваров);
		
	КонецЕсли;
	
	ОткрытьФорму("Документ.ПрочееОприходованиеТоваров.Форма.ФормаДокумента", 
	Новый Структура("Ключ", ПрочееОприходованиеТоваров));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСсылкуНаПрочееОприходованиеТоваров(ПеремещениеТоваров, НадоСоздаватьПрочееОприходованиеТоваров)
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеСведения.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|ГДЕ
	|	ДополнительныеСведения.Объект = &Объект
	|	И ДополнительныеСведения.Свойство.Имя = &Имя";
	
	Запрос.УстановитьПараметр("Имя", "ПрочееОприходованиеЗаСчетДоходов_0852c9e0cf9248c7a85888425341592d");
	Запрос.УстановитьПараметр("Объект", ПеремещениеТоваров);
	                                                         
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		УИД = Новый УникальныйИдентификатор(ВыборкаДетальныеЗаписи.Значение);
		СсылкаНаПрочееОприходованиеТоваров = Документы.ПрочееОприходованиеТоваров.ПолучитьСсылку(УИД);
		
		
		Если СсылкаНаПрочееОприходованиеТоваров.ПолучитьОбъект() = Неопределено Тогда
			
			НадоСоздаватьПрочееОприходованиеТоваров = Истина;
			
			СсылкаНаПрочееОприходованиеТоваров = Документы.ПрочееОприходованиеТоваров.ПустаяСсылка();
			
			МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = ПеремещениеТоваров;
			МенеджерЗаписи.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПрочееОприходованиеЗаСчетДоходов_0852c9e0cf9248c7a85888425341592d");
			МенеджерЗаписи.Значение = "";
			
			Попытка
				
				МенеджерЗаписи.Записать();
				
			Исключение
				
				Сообщить(ОписаниеОшибки());
				Сообщить("Удалите вручную строку из свойства ""Прочее оприходование за счет доходов""");
				
			КонецПопытки;
			
		КонецЕсли;
		
	Иначе
		
		СсылкаНаПрочееОприходованиеТоваров = Документы.ПрочееОприходованиеТоваров.ПустаяСсылка();
		НадоСоздаватьПрочееОприходованиеТоваров = Истина;
		
	КонецЕсли;
	
	Возврат СсылкаНаПрочееОприходованиеТоваров;
	
КонецФункции // ПолучитьСсылкуНаПриход()

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ЗаполнитьПрочееОприходованиеТоваровНаОснованииПеремещенияТоваров(ПеремещениеТоваров, ПрочееОприходованиеТоваров)
	
	
	ПрочееОприходованиеТоваровОбъект = Документы.ПрочееОприходованиеТоваров.СоздатьДокумент();
	
	
	Запрос = Новый Запрос;    
	Запрос.УстановитьПараметр("ПеремещениеТоваров", ПеремещениеТоваров);
	Запрос.УстановитьПараметр("Период", ПеремещениеТоваров.Дата);
	Запрос.УстановитьПараметр("ВидЦены", Справочники.ВидыЦен.ПолучитьСсылку(
		Новый УникальныйИдентификатор("929648fa-6cd2-4c92-a892-7234a9e5ace1")));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеТоваров.Организация КАК Организация,
	|	ПеремещениеТоваров.Подразделение КАК Подразделение,
	|	ПеремещениеТоваров.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПеремещениеТоваров.Дата КАК Дата,
	|	ПеремещениеТоваров.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ПеремещениеТоваров.Сделка КАК Сделка,
	|	ПеремещениеТоваров.Руководитель КАК Руководитель,
	|	ПеремещениеТоваров.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ПеремещениеТоваров.ВидыЗапасовУказаныВручную КАК ВидыЗапасовУказаныВручную,
	|	ПеремещениеТоваров.СкладОтправитель КАК Склад
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ПеремещениеТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеТоваровТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ПеремещениеТоваровТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПеремещениеТоваровТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ПеремещениеТоваровТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ПеремещениеТоваровТовары.Количество КАК Количество,
	|	ПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
	|	ЕСТЬNULL(ПеремещениеТоваровТовары.Упаковка.Числитель, 1) КАК УпаковкаЧислитель,
	|	ЕСТЬNULL(ПеремещениеТоваровТовары.Упаковка.Знаменатель, 1) КАК УпаковкаЗнаменатель,
	|	ПеремещениеТоваровТовары.Серия КАК Серия,
	|	ПеремещениеТоваровТовары.Назначение КАК Назначение
	|ПОМЕСТИТЬ ВТТоварыДокумента
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка = &ПеремещениеТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
	|	ЦеныНоменклатурыСрезПоследних.Цена / ВЫБОР
	|		КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка.Числитель ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Упаковка.Числитель
	|	КОНЕЦ * ВЫБОР
	|		КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка.Знаменатель ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Упаковка.Знаменатель
	|	КОНЕЦ КАК ЦенаПоВидуЦен,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦены.ЦенаВключаетНДС КАК ВключаетНДС,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура.СтавкаНДС КАК НоменклатураСтавкаНДС,
	|	ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена / ВЫБОР
	|			КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка.Числитель ЕСТЬ NULL
	|				ТОГДА 1
	|			ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Упаковка.Числитель
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка.Знаменатель ЕСТЬ NULL
	|				ТОГДА 1
	|			ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Упаковка.Знаменатель
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ЦеныНоменклатурыСрезПоследних.ВидЦены.ЦенаВключаетНДС = ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(перечисление.СтавкиНДС.НДС18)
	|							ТОГДА 100 / 118
	|						КОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(перечисление.СтавкиНДС.НДС10)
	|							ТОГДА 100 / 110
	|						КОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(перечисление.СтавкиНДС.НДС20)
	|							ТОГДА 100 / 120
	|						ИНАЧЕ 1
	|					КОНЕЦ
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена
	|ПОМЕСТИТЬ ВТЦеныНоменклатуры
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&Период,
	|			ВидЦены = &ВидЦены
	|				И (Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						ВТТоварыДокумента.Номенклатура КАК Номенклатура,
	|						ВТТоварыДокумента.Характеристика КАК Характеристика
	|					ИЗ
	|						ВТТоварыДокумента КАК ВТТоварыДокумента)) КАК ЦеныНоменклатурыСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ВТТоварыДокумента.Характеристика КАК Характеристика,
	|	ВТТоварыДокумента.Серия КАК Серия,
	|	ВТТоварыДокумента.Назначение КАК Назначение,
	|	ВТТоварыДокумента.Упаковка КАК Упаковка,
	|	ВТТоварыДокумента.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВТТоварыДокумента.Количество КАК Количество,
	|	ВТТоварыДокумента.НоменклатураЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
	|	ВТТоварыДокумента.УпаковкаЧислитель КАК УпаковкаЧислитель,
	|	ВТТоварыДокумента.УпаковкаЗнаменатель КАК УпаковкаЗнаменатель,
	|	ВТЦеныНоменклатуры.Цена КАК Цена,
	|	ВТТоварыДокумента.Количество * ВТЦеныНоменклатуры.Цена КАК Сумма
	|ИЗ
	|	ВТТоварыДокумента КАК ВТТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦеныНоменклатуры КАК ВТЦеныНоменклатуры
	|		ПО (ВТТоварыДокумента.Номенклатура = ВТЦеныНоменклатуры.Номенклатура)
	|			И (ВТТоварыДокумента.Характеристика = ВТЦеныНоменклатуры.Характеристика)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = МассивРезультатов.Получить(3).Выбрать();
	
	Статья = ПланыВидовХарактеристик.СтатьиДоходов.ПолучитьСсылку(
		Новый УникальныйИдентификатор("4cdaa672-1001-11e9-9592-9367f7e3dc6e"));
	
	Пока ВыборкаДетальныеЗаписи.Следующий() = Истина Цикл
		
		НоваяСтрока = ПрочееОприходованиеТоваровОбъект.Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
		
		НоваяСтрока.СтатьяРасходовДоходов = Статья;
		
	КонецЦикла;
	  
	ВыборкаДетальныеЗаписи = МассивРезультатов.Получить(0).Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ПрочееОприходованиеТоваровОбъект.Дата = ВыборкаДетальныеЗаписи.Дата;
	ПрочееОприходованиеТоваровОбъект.Организация = Справочники.Организации.ПолучитьСсылку(
		Новый УникальныйИдентификатор("1ad79775-ac7e-4b60-909c-1c9582a3fa2e"));
	ПрочееОприходованиеТоваровОбъект.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
	ПрочееОприходованиеТоваровОбъект.Склад = ВыборкаДетальныеЗаписи.Склад;
	
	
	ПрочееОприходованиеТоваровОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОприходованиеЗаСчетДоходов;
	
	ПрочееОприходованиеТоваровОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Попытка
		
		ПрочееОприходованиеТоваровОбъект.Записать();
		
		
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат;
		
	КонецПопытки;
		
	ПрочееОприходованиеТоваров = ПрочееОприходованиеТоваровОбъект.Ссылка;
	
	МенеджерЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = ПеремещениеТоваров;
	МенеджерЗаписи.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПрочееОприходованиеЗаСчетДоходов_0852c9e0cf9248c7a85888425341592d");
	МенеджерЗаписи.Значение = ПрочееОприходованиеТоваров.УникальныйИдентификатор();
	
	Попытка
		
		МенеджерЗаписи.Записать();
		
	Исключение
		
		Сообщить(ОписаниеОшибки());
		
	КонецПопытки;
	
КонецПроцедуры


